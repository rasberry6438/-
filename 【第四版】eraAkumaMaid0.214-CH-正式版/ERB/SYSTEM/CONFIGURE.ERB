;Configの呼出と素質の自動調整
;ARGSに"角色定制関連"or "系统＆事件相関"が入っているとそれだけを叫出来
@SET_CONFIGURE, ARGS
#DIM LCOUNT
#DIM MEMO_FLAG, 10
#DIM MEMO_GOUI
#DIMS PRINT_CONFIGTEXT

REDRAW 0
MEMO_FLAG:0 = FLAG:陰毛
MEMO_FLAG:1 = FLAG:巨乳補正
MEMO_FLAG:2 = CONFIG("ＶＡ拡張の手動ＬＶＵＰ")
MEMO_FLAG:3 = CONFIG("危険日生性交の合意")
MEMO_FLAG:4 = CONFIG("胎内精子で妊娠")

SELECTCASE ARGS
CASE "角色定制関連"
	CALL SETCONFIG, "角色定制関連"
CASE "系统＆事件相関"
	CALL SETCONFIG, "系统＆事件相関"
CASEELSE
	CALL SET_CONFIGURE_MAIN
ENDSELECT

;陰毛の状態を変更した時
IF FLAG:陰毛 && FLAG:陰毛 != MEMO_FLAG:0
	FOR LCOUNT, 1, CHARANUM
		CALL SETFLAG, "陰毛と腋毛", LCOUNT
	NEXT
ENDIF
;巨乳の罩杯の定義を変化した時
IF FLAG:巨乳補正 != MEMO_FLAG:1
	CALL SETFLAG, "胸圍測りなおし"
	FOR LCOUNT, 1, CHARANUM
		CALL CHECK_3SIZE, LCOUNT
	NEXT
ENDIF
;ＶＡ拡張の自動ＬＶＵＰに戻したなら早速ＬＶＵＰ
IF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0 && MEMO_FLAG:2
	FOR LCOUNT, 1, CHARANUM
		ABL:LCOUNT:Ｖ拡張 = CALCF("Ｖ拡張の上限", LCOUNT)
		ABL:LCOUNT:Ａ拡張 = CALCF("Ａ拡張の上限", LCOUNT)
	NEXT
ENDIF
;危険日生性交の合意条件を変更した場合
IF CONFIG("危険日生性交の合意") != MEMO_FLAG:3
	;これがONだと合意の文が出てしまうので一時的にOFF
	FLAG:VERUP格子 = 0
	PRINT_CONFIGTEXT = 
	FOR LCOUNT, 2, CHARANUM
		MEMO_GOUI = COND("合意：危険日生性交", LCOUNT)
		CALL DEL_GOUI, "危険日生性交", LCOUNT
		CALL CHECK_GOUI, "汎用", LCOUNT
		SIF COND("合意：危険日生性交", LCOUNT) != MEMO_GOUI
			PRINT_CONFIGTEXT += @"%CALLNAME:LCOUNT%と"
	NEXT
	IF PRINT_CONFIGTEXT != ""
		PRINT_CONFIGTEXT = %SUBSTRING(PRINT_CONFIGTEXT, 0, STRLENS(PRINT_CONFIGTEXT) - 2)%
		IF CONFIG("危険日生性交の合意")
			CALL PRINT_STRW, @"%CALLNAME:MASTER%从%PRINT_CONFIGTEXT%獲得了_イエロー_[危険日生性交]_的合意。"
		ELSE
			CALL PRINT_STRW, @"%CALLNAME:MASTER%从%PRINT_CONFIGTEXT%失去了_イエロー_[危険日生性交]_的合意。"
		ENDIF
	ENDIF
	FLAG:VERUP格子 = 1
ENDIF
;胎内精子で妊娠が無効变成了ら胎内精子をゼロに
IF CONFIG("胎内精子で妊娠") == 0 && MEMO_FLAG:4
	FOR LCOUNT, 1, CHARANUM
		BASE:LCOUNT:胎内精液量 += BASE:LCOUNT:胎内精子量
		BASE:LCOUNT:胎内精子量 = 0
		SIF BASE:LCOUNT:胎児 == 0 && BASE:LCOUNT:受精 == 0
			BASE:LCOUNT:精液の優勢 = 0
	NEXT
;胎内精子で妊娠が有効变成了際に危険日生性交合意没有かつ避妊中でなく創口貼を装備していた場合に胎内精子をゼロに
ELSEIF CONFIG("胎内精子で妊娠") && MEMO_FLAG:4 == 0
	FOR LCOUNT, 1, CHARANUM
		IF CFLAG:LCOUNT:創口貼装着 && COND("合意：危険日生性交", LCOUNT) == 0 && COND("避妊不要", LCOUNT) == 0 && BASE:LCOUNT:胎児 == 0 && BASE:LCOUNT:受精 == 0
			BASE:LCOUNT:胎内精液量 += BASE:LCOUNT:胎内精子量
			BASE:LCOUNT:胎内精子量 = 0
			BASE:LCOUNT:精液の優勢 = 0
		ENDIF
	NEXT
ENDIF

REDRAW 1


@SET_CONFIGURE_MAIN
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT

PRINTL 
PRINTFORML 現在有以下相関設定。请選択想要変更的内容
PRINTL 
PRINTFORML  [ 0] 角色定制相関的設定
PRINTFORML  [ 1] 系统与活动相関的設定
PRINTFORML  [ 2] 着装的設定
CALL PRINT_STRL, @" [ 3] 颜绘动画的ON/OFF　現在：_黄色_\@ CONFIG("颜绘动画ＯＦＦ") ? OFF # ON \@"
CALL PRINT_STRL, @" [ 4] 在房间中最左边展示%CALLNAME:MASTER%的颜绘　現在：_黄色_\@ CONFIG("MASTER颜绘") ? 〇 # × \@"

PRINTFORML  [10] 保存Config
;グローバル変数の読み込み
LOADGLOBAL
SIF GLOBAL:Config保存の日時 > 0
	PRINTFORML  [20] 读取Config（記録日:{GLOBAL:Config保存の日時/10000}年{(GLOBAL:Config保存の日時%10000)/100}月{GLOBAL:Config保存の日時%100}日）


;ここ以下はオープニング終わるまでは封印
IF FLAG:オープニング == 0
	SIF CHARANUM <= 3
		SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  [40] 重新排列
	RESETCOLOR
	PRINTL 
	PRINTFORML  [50] 顔绘测试
	PRINTFORML  [51] 淫紋画像测试
	PRINTL 
	IF TARGET > MASTER
		IF CFLAG:颜绘 == 0
			PRINTFORML  [60] 設定[%CSTR:称号%]%NAME:TARGET%的顔绘并显示
		ELSE
			PRINTFORML  [60] 変更[%CSTR:称号%]%NAME:TARGET%的顔绘
		ENDIF
		SIF MARK:淫紋 && CEVENTS("淫紋") == ""
			PRINTFORML  [61] [%CSTR:称号%]%NAME:TARGET%的淫紋类型設定(注意！只能设定一次！！)
		PRINTL 
	ENDIF

	IF CFLAG:MASTER:颜绘
		PRINTFORML  [70] %NAME:MASTER%颜绘変更(想要设定衣服的话也请按这里)
		PRINTFORML  [71] 停止表示%NAME:MASTER%颜绘
	ELSE
		PRINTFORML  [70] %NAME:MASTER%颜绘設定
	ENDIF
	PRINTL 
ENDIF

PRINTFORML [100] 返回

$INPUT_LOOP
INPUT
CHOICE = RESULT

SELECTCASE CHOICE
CASE 40 TO 99
	SIF FLAG:オープニング
		GOTO INPUT_LOOP
ENDSELECT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 0
	CALL SETCONFIG, "角色定制関連"
CASE 1
	CALL SETCONFIG, "系统＆事件相関"
CASE 2
	CALL SETCONFIG, "着装設定"
CASE 3
	INVERTBIT FLAG:颜绘动画, 0
CASE 4
	INVERTBIT FLAG:MASTER颜绘, 0
CASE 10
	CALL SAVE_CONFIGURE
CASE 20
	SIF GLOBAL:Config保存の日時 > 0
		CALL LOAD_CONFIGURE
CASE 40
	IF CHARANUM <= 3
		PRINTFORMW 没有可以排列的角色
	ELSE
		CALL CHARA_SORT
	ENDIF
CASE 50
	CALL SIMULATE_MAIDFACE
CASE 51
	CALL SIMULATE_INMON
CASE 60
	SIF TARGET > MASTER
		CALL SIMULATE_MAIDFACE, TARGET
CASE 61
	IF TARGET > MASTER && MARK:淫紋 && CEVENTS("淫紋") == ""
		PRINTFORML ※現段階是LV{MARK:淫紋}、可以通过其他选择来确认淫纹画数增加时的形状※
		CALL SIMULATE_INMON, TARGET
		PRINTFORMW %CALLNAME:TARGET%的淫紋变成了%CONDS("淫紋の種類")%类型
	ENDIF
CASE 70
	CALL SIMULATE_MAIDFACE, MASTER
CASE 71
	CFLAG:MASTER:颜绘 = 0
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@CONFIG(ARGS)
#FUNCTION
SELECTCASE ARGS
CASE "主画面颜绘ＯＦＦ"
	RETURNF GETBIT(FLAG:画像ONOFF, 0)
CASE "メイン画面CIPＯＦＦ"
	RETURNF GETBIT(FLAG:画像ONOFF, 1)

CASE "永久過濾", "颜绘勧誘"
	RETURNF FLAG:ARGS
CASE "颜绘动画ＯＮ"
	SIF FLAG:颜绘动画
		RETURNF 1
CASE "颜绘动画ＯＦＦ"
	SIF FLAG:颜绘动画 == 0
		RETURNF 1
CASE "颜绘动画動作中"
	SIF FLAG:自動進行停止
		RETURNF 0
	SIF ANIME_CHARA:0 == 0
		RETURNF 0
	SIF FLAG:颜绘动画
		RETURNF 1
CASE "颜绘サイズ"
	RETURNF FLAG:颜绘サイズ
CASE "MASTER颜绘"
	RETURNF FLAG:MASTER颜绘
CASE "自动调戏"
	RETURNF FLAG:自动调戏
CASE "自动调戏ＯＮ"
	SIF FLAG:自动调戏 != 999
		RETURNF 1
CASE "自动调戏ＯＦＦ"
	SIF FLAG:自动调戏 == 999
		RETURNF 1
CASE "自动调戏動作中"
	SIF FLAG:自動進行停止
		RETURNF 0
	SIF TARGET <= 1
		RETURNF 0
	SIF CFLAG:睡眠 || CFLAG:刚睡醒
		RETURNF 0
	SIF FLAG:自动调戏 != 999
		RETURNF 1
CASE "partnerの自動変更"
	RETURNF FLAG:partnerの自動変更
CASE "能力表示で３围閲覧"
	RETURNF GETBIT(FLAG:能力画面, 0)
CASE "能力表示でABLUP"
	RETURNF GETBIT(FLAG:能力画面, 1)
CASE "partner変更で分析"
	RETURNF GETBIT(FLAG:対象変更画面, 0)
CASE "助手への指示"
	RETURNF GETBIT(FLAG:助手画面, 0)

CASE "着装設定"
	RETURNF FLAG:換装
CASE "服はお気に入りから選択"
	RETURNF GETBIT(FLAG:換装, 0)
CASE "禁止自動脱衣"
	RETURNF GETBIT(FLAG:換装, 1)
CASE "在家里脱鞋"
	RETURNF GETBIT(FLAG:換装, 2)
CASE "胸罩を合わせる"
	RETURNF GETBIT(FLAG:換装, 3)
CASE "女仆帽"
	RETURNF GETBIT(FLAG:換装, 4)

CASE "装扮：一覧"
	SIF FLAG:換装画面 == 0
		RETURNF 1
CASE "装扮：角色衣装"
	RETURNF GETBIT(FLAG:換装画面, 0)
CASE "装扮：Cosplay"
	RETURNF GETBIT(FLAG:換装画面, 1)
CASE "装扮：制服"
	RETURNF GETBIT(FLAG:換装画面, 2)
CASE "装扮：体操服"
	RETURNF GETBIT(FLAG:換装画面, 3)
CASE "装扮：泳装"
	RETURNF GETBIT(FLAG:換装画面, 4)

CASE "陰毛と腋毛", "陰毛"
	RETURNF FLAG:陰毛
CASE "腋毛"
	RETURNF GETBIT(FLAG:陰毛, 10)
CASE "素質的固定与除外"
	RETURNF FLAG:素質的固定与除外
;默认はOFF
CASE "性器年齢"
	RETURNF FLAG:性器年齢
CASE "使い込まれ素質の自動獲得"
	RETURNF FLAG:使い込まれ素質の自動獲得
CASE "允许女性为你以外的其他人受孕"
	RETURNF FLAG:允许女性为你以外的其他人受孕


;調教过滤器関連
CASE "Ｖ調教过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 0)
CASE "Ａ調教过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 1)
CASE "尿道調教过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 2)
CASE "逆挿入过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 3)
CASE "準備不足过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 4)
CASE "道具調教过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 5)
CASE "ハード調教过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 6)
CASE "消耗品过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 7)
CASE "実行判定过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 8)
CASE "極厚避孕套"
	RETURNF GETBIT(FLAG:調教过滤器, 9)
CASE "立位"
	SIF TEQUIP:推倒
		RETURNF 0
	RETURNF GETBIT(FLAG:調教过滤器, 10)

CASE "脱掉鞋子过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 15)
CASE "脱掉胖次过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 16)
;これは默认でON状態
CASE "豊穣过滤器"
	SIF GETBIT(FLAG:調教过滤器, 17) == 0
		RETURNF 1

CASE "指令过滤器"
	RETURNF GETBIT(FLAG:調教过滤器, 20)

CASE "指令过滤器：全指令表示"
	SIF CONFIG("指令过滤器") == 0
		RETURNF 0
	RETURNF GETBIT(FLAG:調教过滤器, 30)



CASE "妊娠設定：受精確率表示ＯＦＦ"
	RETURNF GETBIT(FLAG:妊娠設定, 0)

CASE "恍惚状態"
	RETURNF FLAG:恍惚状態
CASE "恍惚耐性"
	RETURNF FLAG:恍惚耐性

CASE "自動挿入解除"
	RETURNF FLAG:自動挿入解除
CASE "自動挿入解除OFF"
	SIF FLAG:自動挿入解除 == 1
		RETURNF 1
CASE "自動挿入解除選択肢"
	SIF FLAG:自動挿入解除 == 2
		RETURNF 1
CASE "委身于對方的警告"
	RETURNF FLAG:委身于對方的警告
CASE "委身于對方的警告表示"
	IF FLAG:委身于對方的警告 == 0
		RETURNF 1
	ELSEIF FLAG:委身于對方的警告 == 1
		IF TARGET:1
			SIF CHOSECOM(286, TARGET) == 0 && CHOSECOM(286, TARGET:1) == 0
				RETURNF 1
		ELSE
			SIF CHOSECOM(286, TARGET) == 0
				RETURNF 1
		ENDIF
	ENDIF
CASE "對男性PLAYER的胸奉仕"
	RETURNF FLAG:對男性PLAYER的胸奉仕

CASE "夏天的運動汗流浃背"
	RETURNF FLAG:夏天的運動汗流浃背
CASE "包茎阴茎的称呼"
	RETURNF FLAG:包茎阴茎的称呼
CASE "９９９インデント"
	RETURNF FLAG:９９９インデント
CASE "９９９インデント：左寄せ"
	SIF FLAG:９９９インデント == 0
		RETURNF 1
CASE "９９９インデント：中央"
	SIF FLAG:９９９インデント == 1
		RETURNF 1
CASE "９９９インデント：右寄せ"
	SIF FLAG:９９９インデント == 2
		RETURNF 1
CASE "９９９インデント補正"
	SELECTCASE FLAG:９９９インデント
	CASE 0
		RETURNF 0
	CASE 1
		RETURNF NUM("折り返し文字数") - 60
	CASE 2
		RETURNF NUM("折り返し文字数") - 30
	ENDSELECT

CASE "乱性"
	RETURNF FLAG:乱性
CASE "部員間Ｈ"
	SIF FLAG:乱性 >= 1
		RETURNF 1
CASE "路人Ｈ"
	SIF FLAG:乱性 >= 2
		RETURNF 1

CASE "危険日生性交の合意"
	RETURNF FLAG:危険日生性交の合意

CASE "胎内精子で妊娠"
	RETURNF FLAG:胎内精子で妊娠

CASE "巨乳補正"
	RETURNF FLAG:巨乳補正
CASE "巨乳補正：Ｆ"
	SIF FLAG:巨乳補正 == 0
		RETURNF 1
CASE "巨乳補正：Ｇ"
	SIF FLAG:巨乳補正 == 1
		RETURNF 1
CASE "巨乳補正：Ｈ"
	SIF FLAG:巨乳補正 == 2
		RETURNF 1

CASE "恋慕と淫乱の任意獲得"
	RETURNF GETBIT(FLAG:手動取得, 0)
CASE "ＶＡ拡張の手動ＬＶＵＰ"
	RETURNF GETBIT(FLAG:手動取得, 1)

CASE "乳頭奸"
	RETURNF FLAG:乳頭奸
CASE "精液浣腸"
	RETURNF FLAG:精液浣腸

CASE "气味の表記変更"
	RETURNF FLAG:气味
CASE "性騷擾禁止"
	RETURNF FLAG:性騷擾禁止
CASE "性騷擾許可"
	SIF FLAG:性騷擾禁止 == 0
		RETURNF 1
CASE "制限時間"
	RETURNF FLAG:制限時間
CASE "射精部位の自動選択"
	RETURNF FLAG:射精部位の自動選択
CASE "髪型詳細"
	RETURNF FLAG:髪型詳細
CASE "Ｖ射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 3)
CASE "Ａ射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 4)
CASE "尿道射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 19)
CASE "Ｂ射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 18)
CASE "口射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 1)
CASE "手射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 2)
CASE "胸射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 5)
CASE "脚射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 6)
CASE "自慰射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 20)
CASE "其他射精の自動選択"
	RETURNF GETBIT(FLAG:射精部位の自動選択, 0)
CASE "全射精部位の自動選択"
	SIF FLAG:射精部位の自動選択 == 1p0 + 1p1 + 1p2 + 1p3 + 1p4 + 1p5 + 1p6 + 1p18 + 1p19 + 1p20
		RETURNF 1

CASE "乳頭の詳細設定"
	RETURNF FLAG:乳頭
CASE "乳頭肥大"
	RETURNF GETBIT(FLAG:乳頭, 0)
CASE "陥没乳頭"
	RETURNF GETBIT(FLAG:乳頭, 1)
CASE "巨乳輪"
	RETURNF GETBIT(FLAG:乳頭, 2)

CASE "陰核の詳細設定"
	RETURNF FLAG:陰核
CASE "陰核肥大"
	RETURNF GETBIT(FLAG:陰核, 0)
CASE "陰核包茎"
	RETURNF GETBIT(FLAG:陰核, 1)

CASE "常時挿入自動解除"
	RETURNF FLAG:常時挿入自動解除
CASE "嘔吐発生"
	RETURNF FLAG:嘔吐発生
CASE "Ｈ前廁所"
	RETURNF FLAG:Ｈ前廁所
CASE "助手同行"
	RETURNF FLAG:助手同行
CASE "裆部颜色"
	RETURNF FLAG:裆部颜色
CASE "裆部颜色：胖次の色と同じ"
	SIF FLAG:裆部颜色 == 0
		RETURNF 1
CASE "裆部颜色：白"
	SIF FLAG:裆部颜色 == 1
		RETURNF 1
CASE "裆部颜色：稍微帯些变化"
	SIF FLAG:裆部颜色 == 2
		RETURNF 1

;以下は項目ONOFFボタン。角色メイク中は例外的に全部表示させる
CASE "項目：搾精排行"
	RETURNF GETBIT(FLAG:項目OFF, 0)
CASE "項目：助手的任务指示"
	RETURNF GETBIT(FLAG:項目OFF, 1)
CASE "項目：角色介绍"
	RETURNF GETBIT(FLAG:項目OFF, 2)
CASE "項目：身嗜み"
	RETURNF GETBIT(FLAG:項目OFF, 3)
CASE "項目：服装詳細"
	RETURNF GETBIT(FLAG:項目OFF, 4)
CASE "項目：合意"
	RETURNF GETBIT(FLAG:項目OFF, 5)
CASE "項目：性癖・弱点"
	RETURNF GETBIT(FLAG:項目OFF, 6)
CASE "項目：自室の状態"
	RETURNF GETBIT(FLAG:項目OFF, 7)
CASE "項目：属性与外表"
	RETURNF GETBIT(FLAG:項目OFF, 8)
CASE "項目：宝珠"
	RETURNF GETBIT(FLAG:項目OFF, 9)
CASE "項目：経験"
	RETURNF GETBIT(FLAG:項目OFF, 10)
CASE "項目：胖次收藏"
	RETURNF GETBIT(FLAG:項目OFF, 11)
CASE "項目：能力"
	RETURNF GETBIT(FLAG:項目OFF, 12)
CASE "項目：刻印"
	RETURNF GETBIT(FLAG:項目OFF, 13)
CASE "項目：素質"
	RETURNF GETBIT(FLAG:項目OFF, 14)
CASE "項目：家族構成"
	RETURNF GETBIT(FLAG:項目OFF, 15)
CASE "項目：相性"
	RETURNF GETBIT(FLAG:項目OFF, 16)
CASE "項目：性処理"
	RETURNF GETBIT(FLAG:項目OFF, 17)
CASE "項目：射精排行"
	RETURNF GETBIT(FLAG:項目OFF, 18)
CASE "項目：所持道具一覧"
	RETURNF GETBIT(FLAG:項目OFF, 19)
CASE "項目：特記事項"
	RETURNF GETBIT(FLAG:項目OFF, 20)
CASE "項目：特別経験詳細"
	RETURNF GETBIT(FLAG:項目OFF, 21)
CASE "項目：特別経験"
	RETURNF GETBIT(FLAG:項目OFF, 22)
CASE "項目：平常的装扮"
	RETURNF GETBIT(FLAG:項目OFF, 23)
CASE "項目：出産の記録"
	RETURNF GETBIT(FLAG:項目OFF, 24)

ENDSELECT
RETURNF 0

@SETCONFIG, ARGS
#DIM LCOUNT
;MEMO_LINECOUNT:1は"角色定制関連"、"系统＆事件相関"、"主人の好み"、"你の紹介文"、"着装設定"でのみ使用
#DIM MEMO_LINECOUNT, 2
#DIM NEXT_LINE
#DIM CHOICE
#DIM IS_DELSELECT
#DIM MEMO, 10
#DIMS MEMOS, 10
;ここは基本的里一直REDRAW 0
REDRAW 0

MEMO_LINECOUNT = LINECOUNT
PRINTL 
SELECTCASE ARGS
CASE "角色定制関連"
	MEMO_LINECOUNT:1 = LINECOUNT - 1

	PRINTFORM  [ 0] \@ CONFIG("巨乳補正") ? ○ # －\@ 巨乳の上限の設定：
	SETCOLOR DEF_COLOR("暗黄色")
	IF CONFIG("巨乳補正：Ｈ")
		PRINTL Ｈ
	ELSEIF CONFIG("巨乳補正：Ｇ")
		PRINTL Ｇ
	ELSE
		PRINTL Ｆ
	ENDIF
	RESETCOLOR
	PRINTFORM  [ 1] \@ CONFIG("乳頭の詳細設定") ? ○ # －\@ 乳頭の詳細設定
	IF CONFIG("乳頭の詳細設定")
		PRINTFORM ：
		SETCOLOR DEF_COLOR("暗黄色")
		PRINTFORM \@ CONFIG("乳頭肥大") ? ○ # ×\@ 乳頭肥大 
		PRINTFORM \@ CONFIG("陥没乳頭") ? ○ # ×\@ 陥没乳頭 
		PRINTFORM \@ CONFIG("巨乳輪") ? ○ # ×\@ 巨乳輪
		RESETCOLOR
	ENDIF
	PRINTL 
	PRINTFORM  [ 2] \@ CONFIG("陰核の詳細設定") ? ○ # －\@ 陰核の詳細設定
	IF CONFIG("陰核の詳細設定")
		PRINTFORM ：
		SETCOLOR DEF_COLOR("暗黄色")
		PRINTFORM \@ CONFIG("陰核肥大") ? ○ # ×\@ 陰核肥大 
		PRINTFORM \@ CONFIG("陰核包茎") ? ○ # ×\@ 陰核包茎 
		RESETCOLOR
	ENDIF
	PRINTL 
	PRINTFORM  [ 3] \@ CONFIG("陰毛と腋毛") ? ○ # －\@ 陰毛与%TEXTS("腋")%毛の設定：
	SETCOLOR DEF_COLOR("暗黄色")
	IF CONFIG("陰毛と腋毛")
		IF GETBIT(FLAG:陰毛, 4)
			PRINT 誰も生えない
		ELSEIF GETBIT(FLAG:陰毛, 3)
			PRINT 只有男性才会长
		ELSEIF GETBIT(FLAG:陰毛, 2)
			PRINT 只有姐姐和男性才会长
		ELSEIF GETBIT(FLAG:陰毛, 1)
			PRINT 小孩以外都会长
		ELSEIF GETBIT(FLAG:陰毛, 0)
			PRINT 大家都长
		ENDIF
		PRINTFORML ＆%TEXTS("腋")%毛\@ CONFIG("腋毛") ? 有些 # 没有 \@
	ELSE
		PRINTL 不显示
	ENDIF
	RESETCOLOR

	PRINTFORM  [ 4] \@ CONFIG("性器年齢") ? ○ # －\@ 性器年齢の設定：
	SETCOLOR DEF_COLOR("暗黄色")
	IF CONFIG("性器年齢")
		PRINTL 有
	ELSE
		PRINTL 无
	ENDIF
	RESETCOLOR

	PRINTFORM  [ 5] \@ CONFIG("素質的固定与除外") ? ○ # －\@ 素質的固定与除外の設定：
	SETCOLOR DEF_COLOR("暗黄色")
	IF CONFIG("素質的固定与除外")
		PRINTL 毎次都初期化
	ELSE
		PRINTL 保存完毕
	ENDIF
	RESETCOLOR
  	PRINTFORML  [ 6] \@ CONFIG("永久過濾") ? 〇 #  － \@ 未完成度较高的角色将不会在勧誘画面之类的界面上表示

	;グローバル変数の読み込み
	LOADGLOBAL
	IF GLOBAL > 0 && FLAG:VER_Config != GLOBAL
		PRINTFORML  [ 7] Config設定（記録日:{GLOBAL/10000}年{(GLOBAL%10000)/100}月{GLOBAL%100}日）的读取
		PRINTFORML [100] 这様就好
		CALL INPUT_SELECT, 8, 100
	ELSE
		PRINTFORML [100] 这様就好
		CALL INPUT_SELECT, 8, 100
	ENDIF
	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0
		CALL SETCONFIG, "巨乳補正"
	CASE 1
		CALL SETCONFIG, "乳頭の詳細設定"
	CASE 2
		CALL SETCONFIG, "陰核の詳細設定"
	CASE 3
		CALL SETCONFIG, "陰毛と腋毛"
	CASE 4
		CALL SETCONFIG, "性器年齢"
	CASE 5
		CALL SETCONFIG, "素質的固定与除外"
	CASE 6
		CALL SETCONFIG, "永久過濾"
	CASE 7
		CALL LOAD_CONFIGURE
	ENDSELECT
	ARGS = 角色定制関連
	CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
	RESTART
CASE "系统＆事件相関"
	MEMO_LINECOUNT:1 = LINECOUNT - 1

	PRINTFORML  [ 0] \@ CONFIG("恋慕と淫乱の任意獲得") ? ○ # －\@ 恋慕・淫乱の任意獲得
	PRINTFORML  [ 1] \@ CONFIG("ＶＡ拡張の手動ＬＶＵＰ") ? ○ # －\@ 手動进行ＶＡ拡張ＬＶの変化
	PRINTFORML  [ 2] \@ CONFIG("性騷擾禁止") ? ○ # －\@ 禁止主人的无選択肢的性骚扰
	PRINTFORML  [ 3] \@ CONFIG("妊娠設定：受精確率表示ＯＦＦ") ? ○ # －\@ 中出時的受精確率表示のＯＦＦ
	PRINTFORML  [ 4] \@ CONFIG("恍惚状態") ? ○ # －\@ 恍惚状態時随对方摆弄
	PRINTFORML  [ 5] \@ CONFIG("制限時間") ? ○ # －\@ 被推倒時的可选项设置時间限制
	PRINTFORM  [ 6] \@ CONFIG("气味の表記変更") ? ○ # －\@ 「气味」的单词的表示変更。現在是：
	CALL PRINTL_COLORTEXT, TEXTS("气味"), DEF_COLOR("黄色")
	PRINTFORML  [ 7] \@ CONFIG("乳頭奸") ? ○ # －\@ 可以乳頭奸
	PRINTFORML  [ 8] \@ CONFIG("精液浣腸") ? ○ # －\@ 在菊花里大量中出的话可以起到浣腸的効果
	PRINTFORM  [ 9] \@ CONFIG("射精部位の自動選択") ? ○ # －\@ 射精時、自動选择就这様射精
	IF CONFIG("射精部位の自動選択")
		PRINT :
		SETCOLOR DEF_COLOR("黄色")
		IF CONFIG("全射精部位の自動選択")
			PRINT ＡＬＬ
		ELSE
			SIF CONFIG("Ｖ射精の自動選択")
				PRINT Ｖ
			SIF CONFIG("Ａ射精の自動選択")
				PRINT Ａ
			SIF CONFIG("尿道射精の自動選択")
				PRINT 尿道
			SIF CONFIG("Ｂ射精の自動選択")
				PRINT 乳内
			SIF CONFIG("口射精の自動選択")
				PRINT 口
			SIF CONFIG("手射精の自動選択")
				PRINT 手
			SIF CONFIG("胸射精の自動選択")
				PRINT 胸
			SIF CONFIG("脚射精の自動選択")
				PRINT 脚
			SIF CONFIG("自慰射精の自動選択")
				PRINT 自慰
			SIF CONFIG("其他射精の自動選択")
				PRINT 其他
		ENDIF
		RESETCOLOR
	ENDIF
	PRINTL 
	PRINTFORM  [10] \@ CONFIG("恍惚耐性") ? ○ # －\@ 恍惚時
	SELECTCASE CONFIG("恍惚耐性")
	CASE 2
		PRINTFORML 自動忍耐
	CASE 1
		PRINTFORML 出现選択肢
	CASEELSE
		PRINTFORML 有時忍耐有時出现選択肢
	ENDSELECT
	PRINTFORML  [11] \@ CONFIG("自動挿入解除") ? ○ # －\@ 射精後是否会以概率解除自動挿入：\@ CONFIG("自動挿入解除選択肢") ? 以選択肢方式 # 不拔出 \@
	PRINTFORML  [12] \@ CONFIG("夏天的運動汗流浃背") ? ○ # －\@ 夏天時、在身体运動后的情况下、性器会有恥垢
	PRINTFORML  [13] \@ CONFIG("包茎阴茎的称呼") ? ○ # －\@ 在地文中改变包茎阴茎的称呼
	PRINTFORM  [14] \@ CONFIG("自动调戏") ? ○ # －\@ 時間経過時会有人自行动作的是否执行or间隔時間変更：
	SELECTCASE CONFIG("自动调戏")
	CASE 0
		CALL PRINT_STRL, "黄色_20秒一次"
	CASE 1 TO 59
		CALL PRINT_STRL, @"黄色_{CONFIG("自动调戏")}秒一次"
	CASE 60, 120, 180, 240, 300, 360, 420, 480, 540, 600
		CALL PRINT_STR, @"黄色_{CONFIG("自动调戏")/60}分一次"
	CASE 60 TO 600
		CALL PRINT_STRL, @"黄色_{CONFIG("自动调戏")/60}分{CONFIG("自动调戏")%60}秒一回"
	CASE 999
		CALL PRINT_STRL, "黄色_停止中"
	ENDSELECT
	PRINTFORM  [15] \@ CONFIG("９９９インデント") ? ○ # －\@ 在选择了"打发時间"等推进時間的指令後変更選択肢的場所：
	IF CONFIG("９９９インデント：右寄せ")
		CALL PRINT_STRL, "黄色_右侧"
	ELSEIF CONFIG("９９９インデント：中央")
		CALL PRINT_STRL, "黄色_中央"
	ELSE
		CALL PRINT_STRL, "黄色_左侧"
	ENDIF
	PRINTFORM  [16] \@ CONFIG("乱性") ? ○ # －\@ 没有[贞操重视]＆有[淫乱]＆欲求不満的时候、和%CALLNAME:MASTER%以外的人做Ｈ的事情：
	IF CONFIG("乱性") == 0
		CALL PRINT_STRL, "不允許"
	ELSEIF CONFIG("路人Ｈ")
		CALL PRINT_STRL, "黄色_和誰都可以"
	ELSEIF CONFIG("部員間Ｈ")
		CALL PRINT_STRL, "黄色_如果是和部員的话可以"
	ENDIF
	PRINTFORML  [17] \@ CONFIG("危険日生性交の合意") ? ○ # －\@ 如果獲得恋慕＆淫乱就可以得到[危険日生性交]的合意
	PRINTFORML  [18] \@ CONFIG("胎内精子で妊娠") ? ○ # －\@ 膣内射精之后如果在体内残留精子的话有可能会妊娠
	PRINTFORML  [19] \@ CONFIG("使い込まれ素質の自動獲得") ? ○ # －\@ 自動獲得[縦深菊穴]之类的使用熟练系的素質
	PRINTFORML  [20] \@ CONFIG("partnerの自動変更") ? ○ # －\@ 随时间经过偶尔会发生一些小事件、当选择露出破绽时会自动变更%TEXTS("対象の名称")%
	PRINTFORM  [21] \@ CONFIG("委身于對方的警告") ? ○ # －\@ 指令"委身于對方"選択時の注意書き：
	SELECTCASE CONFIG("委身于對方的警告")
	CASE 0
		CALL PRINT_STRL, "黄色_一直顯示"
	CASE 1
		CALL PRINT_STRL, "黄色_Ｈ時只顯示一次る"
	CASEELSE
		CALL PRINT_STRL, "黄色_不顯示"
	ENDSELECT

	PRINTFORM  [22] \@ CONFIG("對男性PLAYER的胸奉仕") ? ○ # －\@ "胸爱抚奉侍"等对PLAYER执行的胸奉仕指令即使是在PLAYER是男性的情况下也可以実行：
	SELECTCASE CONFIG("對男性PLAYER的胸奉仕")
	CASE 0
		CALL PRINT_STRL, "黄色_しない"
	CASE 1
		CALL PRINT_STRL, "黄色_伪娘の時のみ"
	CASEELSE
		CALL PRINT_STRL, "黄色_常にする"
	ENDSELECT
	PRINTFORML  [23] \@ CONFIG("常時挿入自動解除") ? ○ # －\@ 身嗜管理時拜託的時常插入玩具將在Ｈ開始時自動解除
	PRINTFORML  [24] \@ CONFIG("嘔吐発生") ? ○ # －\@ 強制口交將有可能發生嘔吐事件
	PRINTFORML  [25] \@ CONFIG("Ｈ前廁所") ? ○ # －\@ Ｈ前%CALLNAME:MASTER%会自己去一下厕所
	PRINTFORML  [26] \@ CONFIG("助手同行") ? ○ # －\@ 場所移動的时候%TEXTS("助手の名称")%同伴将会自动跟随

	PRINTFORML  [27] \@ CONFIG("主画面颜绘ＯＦＦ") ? ○ # －\@ 将在主界面的颜绘表示变成ＯＦＦ
	PRINTFORML  [28] \@ CONFIG("メイン画面CIPＯＦＦ") ? ○ # －\@ 将在主界面的下半身画像表示变成ＯＦＦ
	IF FIND_NO("日向")
		CALL PRINT_STRL, @"灰色_ [29] \@ CONFIG("允许女性为你以外的其他人受孕") ? ○ # －\@ 允许女性为你以外的其他人受孕(※日向在场的情况下固定为〇※)"
	ELSE
		PRINTFORML  [29] \@ CONFIG("允许女性为你以外的其他人受孕") ? ○ # －\@ 允许女性为你以外的其他人受孕
	ENDIF


	PRINTFORML [100] 这様就好
	CALL INPUT_SELECT, 30, 100
	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0
		CALL SETCONFIG, "恋慕と淫乱の任意獲得"
	CASE 1
		CALL SETCONFIG, "ＶＡ拡張の手動ＬＶＵＰ"
	CASE 2
		CALL SETCONFIG, "性騷擾禁止"
	CASE 3
		CALL SETCONFIG, "妊娠設定：受精確率表示ＯＦＦ"
	CASE 4
		CALL SETCONFIG, "恍惚状態"
	CASE 5
		CALL SETCONFIG, "制限時間"
	CASE 6
		CALL SETCONFIG, "气味の表記変更"
	CASE 7
		CALL SETCONFIG, "乳頭奸"
	CASE 8
		CALL SETCONFIG, "精液浣腸"
	CASE 9
		CALL SETCONFIG, "射精部位の自動選択"
	CASE 10
		CALL SETCONFIG, "恍惚耐性"
	CASE 11
		CALL SETCONFIG, "自動挿入解除"
	CASE 12
		CALL SETCONFIG, "夏天的運動汗流浃背"
	CASE 13
		CALL SETCONFIG, "包茎阴茎的称呼"
	CASE 14
		CALL SETCONFIG, "自动调戏"
	CASE 15
		CALL SETCONFIG, "９９９インデント"
	CASE 16
		CALL SETCONFIG, "乱性"
	CASE 17
		CALL SETCONFIG, "危険日生性交の合意"
	CASE 18
		CALL SETCONFIG, "胎内精子で妊娠"
	CASE 19
		CALL SETCONFIG, "使い込まれ素質の自動獲得"
	CASE 20
		CALL SETCONFIG, "partnerの自動変更"
	CASE 21
		CALL SETCONFIG, "委身于對方的警告"
	CASE 22
		CALL SETCONFIG, "對男性PLAYER的胸奉仕"
	CASE 23
		CALL SETCONFIG, "常時挿入自動解除"
	CASE 24
		CALL SETCONFIG, "嘔吐発生"
	CASE 25
		CALL SETCONFIG, "Ｈ前廁所"
	CASE 26
		CALL SETCONFIG, "助手同行"
	CASE 27
		CALL SETCONFIG, "主画面颜绘ＯＦＦ"
	CASE 28
		CALL SETCONFIG, "メイン画面CIPＯＦＦ"
	CASE 29
		SIF FIND_NO("日向") == 0
			CALL SETCONFIG, "允许女性为你以外的其他人受孕"
	;CASE 
	;	CALL SETCONFIG, "髪型詳細"
	ENDSELECT
	ARGS = 系统＆事件相関
	CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
	RESTART

CASE "着装設定"
	MEMO_LINECOUNT:1 = LINECOUNT - 1

	PRINTFORML 请選択想要変更的内容
	PRINTL 
	PRINTFORML  [ 0] \@ CONFIG("禁止自動脱衣") ? ○ # － \@ 禁止自動脱衣
	PRINTFORML  [ 1] \@ CONFIG("在家里脱鞋") ? ○ # － \@ 在自己家里会脱掉靴子
	PRINTFORML  [ 2] \@ CONFIG("胸罩を合わせる") ? ○ # － \@ 選択内裤時会自动同時选择胸罩
	PRINTFORML  [ 3] \@ CONFIG("女仆帽") ? ○ # － \@ 女仆頭饰换成女仆帽
	PRINTFORM  [ 4] \@ CONFIG("裆部颜色") ? ○ # － \@ 胖次的裆部颜色选择　現在是：%TEXTS("裆部颜色")%
	PRINTL 

	PRINTFORML [100] 返回

	CALL INPUT_SELECT, 5, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 4
		CALL SETCONFIG, "裆部颜色"
	CASEELSE
		INVERTBIT FLAG:換装, RESULT+1
	ENDSELECT

	FOR LCOUNT, 1, CHARANUM
		SIF CHECK_SHIRT("女仆装", LCOUNT) == 0
			CONTINUE
		IF CONFIG("女仆帽")
			SIF CHECK_OTHER("礼服頭花", LCOUNT)
				CALL SET_OTHER, "女仆帽", LCOUNT
		ELSE
			SIF CHECK_OTHER("女仆帽", LCOUNT)
				CALL SET_OTHER, "礼服頭花", LCOUNT
		ENDIF
	NEXT

	ARGS = 着装設定
	CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
	RESTART
CASE "裆部颜色"
	PRINTFORML  [ 0] 和胖次同颜色(默认)
	PRINTFORML  [ 1] 白！
	PRINTFORML  [ 2] 稍微帯些变化

	CALL INPUT_SELECT, 3

	FLAG:裆部颜色 = RESULT

CASE "主人の好み"
	MEMO_LINECOUNT:1 = LINECOUNT - 1

	PRINTFORML 请选择想要変更的东西
	PRINTFORM  [ 0] 身嗜 … 
	CALL PRINT_MIDASHINAMI_SHORT, MASTER
	PRINTL 

	PRINTFORM  [ 1] 　弱点 … 
	IF CSTR:MASTER:弱点 == ""
		PRINTFORM 没有
	ELSEIF COND("全部弱点", MASTER)
		PRINT [全部弱点] 
	ELSE
		VARSET LOCALS
		SPLIT CSTR:MASTER:弱点, "/", LOCALS
		FOR LCOUNT, 0, NUM("弱点")
			SIF LOCALS:LCOUNT == ""
				CONTINUE
			PRINTFORM [%SUBSTRING(LOCALS:LCOUNT, 0, STRLENS(LOCALS:LCOUNT) - 6)%]
		NEXT
	ENDIF
	PRINTL 

	PRINTFORM  [ 2] 　性癖 … 
	IF CSTR:MASTER:性癖 == ""
		PRINTFORM 没有
	ELSEIF STRCOUNTS(CSTR:MASTER:性癖, "/") >= 10
		PRINTFORM 有的是
	ELSE
		VARSET LOCALS
		SPLIT CSTR:MASTER:性癖, "/", LOCALS
		FOR LCOUNT, 0, NUM("属性")
			SIF LOCALS:LCOUNT == ""
				CONTINUE
			PRINTFORM [%LOCALS:LCOUNT%]
		NEXT
	ENDIF
	PRINTL 
	PRINTFORML  [ 3] 　名前 … %NAME:MASTER%(%CSTR:MASTER:ふりがな%) を変更する
	PRINTFORM  [ 4] 穿環 … 
	IF TALENT:MASTER:陰蒂環
		IF CEVENT("陰蒂環", MASTER)
			CALL PRINT_STR, @"[\@ PENIS(MASTER) ? 阴茎 # 陰核 \@_黄色_(%CEVENTS("陰蒂環", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[\@ PENIS(MASTER) ? 阴茎 # 陰核 \@]"
		ENDIF
	ENDIF
	IF TALENT:MASTER:陰唇環
		IF CEVENT("陰唇環", MASTER)
			CALL PRINT_STR, @"[陰唇_黄色_(%CEVENTS("陰唇環", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[陰唇]"
		ENDIF
	ENDIF
	IF TALENT:MASTER:恥丘穿環
		IF CEVENT("恥丘穿環", MASTER)
			CALL PRINT_STR, @"[恥丘_黄色_(%CEVENTS("恥丘穿環", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[恥丘]"
		ENDIF
	ENDIF
	IF TALENT:MASTER:尿道穿環
		IF CEVENT("尿道穿環", MASTER)
			CALL PRINT_STR, @"[尿道_黄色_(%CEVENTS("尿道穿環", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[尿道]"
		ENDIF
	ENDIF
	IF TALENT:MASTER:乳环
		IF CEVENT("乳环", MASTER)
			CALL PRINT_STR, @"[乳頭_黄色_(%CEVENTS("乳环", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[乳頭]"
		ENDIF
	ENDIF
	IF TALENT:MASTER:肚脐环
		IF CEVENT("肚脐环", MASTER)
			CALL PRINT_STR, @"[肚臍_黄色_(%CEVENTS("肚脐环", MASTER)%)_]"
		ELSE
			CALL PRINT_STR, @"[肚臍]"
		ENDIF
	ENDIF
	SIF COND("身体穿环数", MASTER) == 0
		PRINTFORM 没有
	PRINTL 
	IF CONFIG("陰毛")

	ENDIF

	PRINTFORML  [ 5] 　紹介 … 改变%NAME:MASTER%(%CSTR:MASTER:ふりがな%) 的介绍

	PRINTFORML  [90] 颜绘 … \@ COND("颜绘表示中", MASTER) ? 表示中 # 没有 \@
	IF COND("颜绘表示中", MASTER)
		PRINTFORML  [99] 颜绘表示停止
		PRINTFORML [100] 这様就好
		CALL INPUT_SELECT, 6, 100, 90, 99
	ELSE
		PRINTFORML 
		PRINTFORML [100] 这様就好
		CALL INPUT_SELECT, 6, 100, 90
	ENDIF

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0
		CALL SET_MIDASHINAMI, MASTER
	CASE 1
		CALL SETCONFIG, "弱点"
	CASE 2
		CALL SETCONFIG, "性癖"
	CASE 3
		CALL NAME_CONVERT, MASTER
	CASE 4
		CALL SETFLAG, "衣装の記録", MASTER
		CALL SET_PIERCE, MASTER
		CALL SETFLAG, "衣装記録からの復元", MASTER
	CASE 5
		CALL SETCONFIG, "自我介绍文"
	CASE 90
		CALL SIMULATE_MAIDFACE, MASTER
		RETURN 1
	CASE 99
		CFLAG:MASTER:颜绘 = 0
	ENDSELECT
	ARGS = 主人の好み
	CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
	RESTART
CASE "性癖"
	PRINTL 请設定性癖。从以下选项里选你喜欢的就好
	SIF FLAG:オープニング
		PRINTL 之后还可以再変更、所以决定不了的话这个也是可以回頭再说的
	CALL PRINT_STR, "其他角色的□ 属性 □欄已选择的性癖会以_イエロー_这个颜色_显示出来_L_"
	VARSET LOCALS
	FOR LCOUNT, 0, NUM("属性")
		MEMOS = %NAMES("属性", LCOUNT)%\/
		PRINTFORMLC 　[{LCOUNT, 2}] \@ STRCOUNT(CSTR:MASTER:性癖, MEMOS) ? ○ # － \@ [%NAMES("属性", LCOUNT)%]

		SIF LCOUNT%3 == 2
			PRINTL 
	NEXT
	PRINTL 
	PRINTL  [ 99] 全部解除
	PRINTL  [100] 这様就好
	CALL INPUT_SELECT, NUM("属性"), 99, 100

	CHOICE = RESULT

	SELECTCASE CHOICE
	CASE 100
		REDRAW 1
		RETURN 0
	CASE 99
		CSTR:MASTER:性癖 = 
	CASEELSE
		MEMOS = %NAMES("属性", CHOICE)%\/
		IF STRCOUNT(CSTR:MASTER:性癖, MEMOS)
			VARSET LOCALS
			SPLIT CSTR:MASTER:性癖, "/", LOCALS
			CSTR:MASTER:性癖 = 
			FOR LCOUNT, 0, NUM("属性")
				SELECTCASE LOCALS:LCOUNT
				CASE "", NAMES("属性", CHOICE)
					CONTINUE
				ENDSELECT
				CSTR:MASTER:性癖 = %CSTR:MASTER:性癖%%LOCALS:LCOUNT%\/
			NEXT
		ELSE
			CSTR:MASTER:性癖 = %CSTR:MASTER:性癖%%NAMES("属性", CHOICE)%\/
		ENDIF

	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE "弱点"
	PRINTL 请設定弱点。从以下选项里选你喜欢的就好
	SIF FLAG:オープニング
		PRINTL 之后还可以再変更、所以决定不了的话回頭再说也是可以的
	VARSET LOCALS
	FOR LCOUNT, 0, NUM("弱点")
		PRINTFORMLC 　[{LCOUNT, 2}] \@ COND(NAMES("弱点", LCOUNT), MASTER) ? ○ # － \@ [%NAMES("弱点", LCOUNT)%]
		SIF LCOUNT%3 == 2
			PRINTL 
	NEXT
	PRINTL 
	IF COND("全部弱点", MASTER)
		PRINTL   [99] 全部解除
	ELSE
		PRINTL   [99] 全部選択
	ENDIF
	PRINTL  [100] 这様就好
	CALL INPUT_SELECT, NUM("弱点"), 99, 100

	CHOICE = RESULT

	SELECTCASE CHOICE
	CASE 100
		REDRAW 1
		RETURN 0
	CASE 99
		IF COND("全部弱点", MASTER)
			CSTR:MASTER:弱点 = 
		ELSE
			CSTR:MASTER:弱点 = 
			FOR LCOUNT, 0, NUM("弱点")
				CSTR:MASTER:弱点 = %CSTR:MASTER:弱点%%NAMES("弱点", LCOUNT)%\/
			NEXT
		ENDIF
	CASEELSE
		IF COND(NAMES("弱点", CHOICE), MASTER)
			VARSET LOCALS
			SPLIT CSTR:MASTER:弱点, "/", LOCALS
			CSTR:MASTER:弱点 = 
			FOR LCOUNT, 0, NUM("弱点")
				SELECTCASE LOCALS:LCOUNT
				CASE "", NAMES("弱点", CHOICE)
					CONTINUE
				ENDSELECT
				CSTR:MASTER:弱点 = %CSTR:MASTER:弱点%%LOCALS:LCOUNT%\/
			NEXT
		ELSE
			CSTR:MASTER:弱点 = %CSTR:MASTER:弱点%%NAMES("弱点", CHOICE)%\/
		ENDIF
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE "自我介绍文"
	;メモ
	MEMO_LINECOUNT:1 = LINECOUNT
	LOCALS = %SAVESTR:自我介绍文%
	IS_DELSELECT = 0

	$YOUR_INTRODUCTION_MAIN

	;MEMOSにSAVESTR:自我介绍文を切り分ける
	VARSET MEMOS
	SPLIT LOCALS, "#改行", MEMOS

	;表示紹介文を入力させる行を表示
	IF !IS_DELSELECT
		PRINTL 设定自我介绍。没有文字的行将会被自动消除。想要显示空行时请输入一个空格。
	ELSE
		PRINTL 请选择想要删除的行
	ENDIF
	PRINTL
	FOR LCOUNT, 0, 10
		HTML_PRINT @"<button value='{LCOUNT}'><u>%TEXT_LJ(MEMOS:LCOUNT, 120)%</u></button>"
	NEXT

	IF !IS_DELSELECT
		HTML_PRINT "<p align='right'><button value='88'>[88]删除模式</button>　　　<button value='99'>[99]回到变更之前的状态</button>　　　<button value='100'>[100]确定</button>　　</p>"
	ELSE
		HTML_PRINT "<p align='right'><button value='88'><font color='red'>[88]删除模式</font></button>　　　<button value='99'>[99]回到变更之前的状态</button>　　　<button value='100'>[100]确定</button>　　</p>"
	ENDIF
	;どの行を変更させるかの入力
	INPUT
	CHOICE = RESULT

	;見栄えがいいように入力された文字を消す
	CLEARLINE 1

	;100以外はここでループさせる
	SELECTCASE CHOICE
	CASE 0 TO 9
		IF !IS_DELSELECT
			;ここは内容の書き換え

			;変更する内容を入力するように指示
			PRINTL 请输入半角120个（全角60个）以内的文字。
			PRINTL 什么都不输入的话不会发生任何变化
			CALL INPUTS_SELECT, 120

			CLEARLINE 1
			IF RESULTS != ""
				;入力された内容に書き換え
				MEMOS:CHOICE = %RESULTS%
			ELSE
				;空の場合はここでもどる
				CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
				GOTO YOUR_INTRODUCTION_MAIN
			ENDIF
		ELSE
			;削除モードでは対象を消す
			MEMOS:CHOICE =
		ENDIF

		LOCALS =
		FOR LCOUNT, 0, 10
			LOCALS += MEMOS:LCOUNT
			SIF MEMOS:LCOUNT != "" && LCOUNT < 9
				LOCALS += "\#改行"
		NEXT

		CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
		GOTO YOUR_INTRODUCTION_MAIN
	CASE 88
		;削除モード切替
		INVERTBIT IS_DELSELECT, 0

		CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
		GOTO YOUR_INTRODUCTION_MAIN

	CASE 99
		;変更前に戻す
		PRINTL 将会回到变更之前的状态。可以吗？
		PRINTL [0] 是
		PRINTL [1] 否
		CALL INPUT_SELECT, 2
		IF RESULT == 0
			CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
			RESTART
		ELSEIF RESULT == 1
			CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
			GOTO YOUR_INTRODUCTION_MAIN
		ENDIF
	CASE 100
		;ここは終了処理
	CASEELSE
		;入力をループさせる
		CLEARLINE LINECOUNT - MEMO_LINECOUNT:1
		GOTO YOUR_INTRODUCTION_MAIN
	ENDSELECT

	SAVESTR:自我介绍文 = %LOCALS%
	VARSET LOCALS
	VARSET MEMOS

	REDRAW 1
	RETURN 0
CASE "陰毛と腋毛"
	PRINTFORML 陰毛和%TEXTS("腋")%毛的設定。从外表年齢来指定是否会长
	PRINTFORML 不过、会长%TEXTS("腋")%毛只能限定在会长陰毛的角色之中
	PRINTFORML （注：OFF的话、全部的陰毛＆%TEXTS("腋")%毛flag都会初期化！）
	PRINTL 
	PRINTFORML  [ 0] \@ GETBIT(FLAG:陰毛, 0) ? ○ # － \@ 大家都长
	PRINTFORML  [ 1] \@ GETBIT(FLAG:陰毛, 1) ? ○ # － \@ 兒童：×　少女：○　大姐姐：○　大人：○
	PRINTFORML  [ 2] \@ GETBIT(FLAG:陰毛, 2) ? ○ # － \@ 兒童：×　少女：×　大姐姐：○　大人：○
	PRINTFORML  [ 3] \@ GETBIT(FLAG:陰毛, 3) ? ○ # － \@ 兒童：×　少女：×　大姐姐：×　大人：○
	PRINTFORML  [ 4] \@ GETBIT(FLAG:陰毛, 4) ? ○ # － \@ 誰都不长
	PRINTFORML  [ 5] \@ FLAG:陰毛 == 0 ? ○ # － \@ 陰毛的設定本身调为OFF(不会再提到陰毛了)
	SIF FLAG:陰毛 == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  [10] \@ CONFIG("腋毛") ? ○ # － \@ %TEXTS("腋")%毛ＯＮ
	RESETCOLOR
	PRINTFORML [100] 返回

	CALL INPUT_SELECT, 6, 10, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 5
		LOCAL = 0
		FOR LCOUNT, 1, CHARANUM
			SIF BASE:LCOUNT:陰毛 == 0
				CONTINUE
			LOCAL = 1
			BREAK
		NEXT
		IF LOCAL
			PRINTFORML 真的要把陰毛与%TEXTS("腋")%毛的状態初始化吗？
			PRINTFORML [ 0] 是
			PRINTFORML [ 1] 果然還是放棄
			CALL INPUT_SELECT, 2
			SIF RESULT == 1
				RESTART
		ENDIF
		PRINTFORMW 陰毛与%TEXTS("腋")%毛的設定调为OFF了
		FLAG:陰毛 = 0
		CALL SETFLAG, "陰毛と腋毛の設定初期化"
		RETURN 0
	CASE 0 TO 4
		CLEARBIT FLAG:陰毛, 0
		CLEARBIT FLAG:陰毛, 1
		CLEARBIT FLAG:陰毛, 2
		CLEARBIT FLAG:陰毛, 3
		CLEARBIT FLAG:陰毛, 4
		SETBIT FLAG:陰毛, RESULT
	CASE 10
		SIF FLAG:陰毛
			INVERTBIT FLAG:陰毛, 10
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE "使い込まれ素質の自動獲得"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORMW [縦深菊穴]之类的会根据経験而自動獲得
	ELSE
		PRINTFORMW 手動獲得的地点在LVUP画面の[90] 使用経験値獲得素質 请去那里吧
	ENDIF

CASE "性器年齢"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORMW 外見年齢和性器年齢的値例如[黑木耳],[美穴],[色素沉淀]等等設定现已启用
	ELSE
		PRINTFORMW 决定不提及性器外表的详細
	ENDIF

CASE "乱性"
	CALL PRINT_SELECT, @"只会诱惑%CALLNAME:MASTER%一个人来作为Ｈ的对象(默认)\/如果是関系好的部員的话也会做Ｈ的事情\/也会和%CALLNAME:MASTER%不认识的人一起悄悄地做Ｈ的事情"
	FLAG:乱性 = RESULT

	SELECTCASE FLAG:乱性
	CASE 0
		PRINTFORMW 不会和%CALLNAME:MASTER%以外之外的人Ｈ。[有男朋友]与[人妻]属性的除外。
	CASE 1
		PRINTFORML 仅限部員同士之间、可能会出现一起做Ｈ的事情的情况。
		PRINTFORMW [有男朋友]与[人妻]属性的除外。
	CASE 2
		PRINTFORML 也会和%CALLNAME:MASTER%不认识的人一起悄悄地做Ｈ的事情。
		PRINTFORMW 不过游戏本体里、目前还没有写相関的事件。
	ENDSELECT
CASE "自動挿入解除"
	PRINTFORML 中出後自動选择拔出阴茎
	CALL PRINT_SELECT, "自動拔出(默认)/不拔出/在要拔出的時候出现選択肢"
	FLAG:自動挿入解除 = RESULT

	SELECTCASE RESULT
	CASE 0
		PRINTFORMW 根据射精量自動解除挿入。
	CASE 1
		PRINTFORMW 不会自動解除挿入。但戴套時例外。
	CASE 2
		PRINTFORMW 将要挿入解除時会出现選択肢。但戴套時例外。
	ENDSELECT

CASE "危険日生性交の合意"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORMW 尽管本来必須要[相思相愛]才行、但是在[恋慕]或[淫乱]的情况下也会獲得该合意。
	ELSE
		PRINTFORMW 獲得危険日生性交的合意必須要相思相愛了。
	ENDIF
CASE "包茎阴茎的称呼"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORMW 包茎的对象的话会有包茎酱之类的表述。
	ELSE
		PRINTFORMW 不会过分指摘包茎了。
	ENDIF

CASE "夏天的運動汗流浃背"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORML 夏天時、在身体运動后的情况下、会出现有恥垢
		PRINTFORMW 请注意那些呈现流汗状態的人。
	ELSE
		PRINTFORMW 一直都是干净漂亮的状態。
	ENDIF
CASE "partnerの自動変更"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTFORMW %CALLNAME:MASTER%进入放置状态的话%TEXTS("対象の名称")%偶尔会发生变化。
	ELSE
		PRINTFORMW 停止%TEXTS("対象の名称")%自動変更。
	ENDIF
CASE "自动调戏"
	PRINTFORM 現在
	SELECTCASE CONFIG("自动调戏")
	CASE 0
		CALL PRINT_STR, "的自动调戏頻度为_黄色_20秒一次"
	CASE 1 TO 59
		CALL PRINT_STR, @"的自动调戏頻度为_黄色_{CONFIG("自动调戏")}秒一次"
	CASE 60, 120, 180, 240, 300, 360, 420, 480, 540, 600
		CALL PRINT_STR, @"的自动调戏頻度为_黄色_{CONFIG("自动调戏")/60}分一次"
	CASE 60 TO 600
		CALL PRINT_STR, @"的自动调戏頻度为_黄色_{CONFIG("自动调戏")/60}分{CONFIG("自动调戏")%60}秒一次"
	CASE 999
		CALL PRINT_STR, "自动调戏_黄色_停止中"
	ENDSELECT
	PRINTFORML 。
	PRINTFORML 要怎么设定？直接输入[5]～[600]、就可以設定为相应的秒数
	PRINTFORML [  0] 返回默认(20秒)
	PRINTFORML [ 10] 变更为10秒
	PRINTFORML [ 30] 变更为30秒
	PRINTFORML [ 60] 变更为１分
	PRINTFORML [180] 变更为３分
	PRINTFORML [999] 停止

	INPUT

	SELECTCASE RESULT
	CASE 0, 5 TO 600, 999
	CASEELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDSELECT

	FLAG:自动调戏 = RESULT

	SELECTCASE FLAG:自动调戏
	CASE 0
		CALL PRINT_STRW, "变回默认設定为(_黄色_20秒左右_)了。"
	CASE 1 TO 59
		CALL PRINT_STRW, @"自动调戏頻度为_黄色_{CONFIG("自动调戏")}秒_了。"
	CASE 60, 120, 180, 240, 300, 360, 420, 480, 540, 600
		CALL PRINT_STRW, @"自动调戏頻度为_黄色_{CONFIG("自动调戏")/60}分_了。"
	CASE 1 TO 600
		CALL PRINT_STRW, @"自动调戏頻度为_黄色_{CONFIG("自动调戏")/60}分{CONFIG("自动调戏")%60}秒_了。"
	CASE 999
		CALL PRINT_STRW, @"自动调戏_停止_了。"
	ENDSELECT
	SIF FLAG:自动调戏 != 999
		PRINTFORMW 选择画面右下的[{DAILY_ポーズ}]pause 时会暂时停止自动调戏。
CASE "９９９インデント"
	PRINTFORML 选择"打发時间"之类的什么都不做单纯的推進時間的指令後的選択肢的場所要在哪里？
	PRINTFORML [0]左侧(默认)%BL(NUM("折り返し文字数") - 60 - 17)%[1]中央%BL(60 - 30 - 7)%[2]右侧
	CALL INPUT_SELECT, 3

	FLAG:９９９インデント = RESULT

	SELECTCASE FLAG:９９９インデント
	CASE 0
		PRINTFORMW 变为左侧(默认)了
	CASE 1
		PRINTFORMW %BL(CONFIG("９９９インデント補正") )%中央表示了
	CASE 2
		PRINTFORMW %BL(CONFIG("９９９インデント補正") )%右侧表示了
	ENDSELECT

CASE "恍惚状態"
	PRINTL 　恍惚状態時会随对方摆弄吗？
	PRINTL 　（具体的说、变得恍惚了的话、会由对方随性选择指令）
	PRINTL [0] 是
	PRINTL [1] 否
	CALL INPUT_SELECT, 2
	IF RESULT == 0
		PRINTW 请注意不要被榨干了
		FLAG:恍惚状態 = 1
	ELSEIF RESULT == 1
		FLAG:恍惚状態 = 0
	ENDIF
CASE "恍惚耐性"
	PRINTFORML  [ 0] 委身于恍惚感(默认)
	PRINTFORML  [ 1] 出现支付精力来维持意識的選択肢
	PRINTFORML  [ 2] 不出现選択肢直接默认支付精力来维持意识

	CALL INPUT_SELECT, 3

	FLAG:恍惚耐性 = RESULT
CASE "巨乳補正"
	PRINTFORML 请設定巨乳的上限。詳細地設定一下。
	PRINTFORML  [ 0] \@ CONFIG("巨乳補正：Ｆ") ? ○ # － \@ Ｅ～Ｆ罩杯为巨乳
	PRINTFORML  [ 1] \@ CONFIG("巨乳補正：Ｇ") ? ○ # － \@ Ｅ～Ｇ罩杯为巨乳
	PRINTFORML  [ 2] \@ CONFIG("巨乳補正：Ｈ") ? ○ # － \@ Ｅ～Ｈ罩杯为巨乳
	RESETCOLOR
	PRINTFORML [100] 返回

	CALL INPUT_SELECT, 3, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0 TO 2
		FLAG:巨乳補正 = RESULT
	ENDSELECT

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE "妊娠設定：受精確率表示ＯＦＦ"
	INVERTBIT FLAG:妊娠設定, 0
	IF CONFIG(ARGS)
		PRINTFORMW 现在不知道受精確率了
	ELSE
		PRINTFORMW 现在显示受精確率了
	ENDIF
CASE "恋慕と淫乱の任意獲得"
	INVERTBIT FLAG:手動取得, 0
	IF CONFIG(ARGS)
		PRINTFORMW 满足条件的時候可以在能力値UP的画面里獲得
	ELSE
		PRINTFORMW 爱情就像龙卷风！
	ENDIF
CASE "ＶＡ拡張の手動ＬＶＵＰ"
	INVERTBIT FLAG:手動取得, 1
	IF CONFIG(ARGS)
		PRINTFORMW 满足条件的時候可以在能力値UP的画面里上昇
	ELSE
		PRINTFORMW 可以擅自扩张了
	ENDIF

CASE "乳頭奸"
	INVERTBIT FLAG:乳頭奸, 0
	IF CONFIG(ARGS)
		PRINTW 胸部在巨乳以上的人如果变成淫乳了的话、就可以乳頭奸了
	ELSE
		PRINTW 乳頭不会出现可以挿入的穴了。这才普通嘛
	ENDIF
CASE "精液浣腸"
	INVERTBIT FLAG:精液浣腸, 0
	IF CONFIG(ARGS)
		PRINTL 虽然不至于到浣腸的地步、但是对积攒了排泄物的人来说如果被中出的刺激太大了的话…
		PRINTW 果然还是应该在肛交前把肚子里清理干净比较好吧
	ELSE
		PRINTW 精液在中出時是没有排泄促進作用的
	ENDIF
CASE "制限時間"
	INVERTBIT FLAG:制限時間, 0
	IF CONFIG(ARGS)
		PRINTW 刺激一点也不错
	ELSE
		PRINTW 这様的话容易被推倒的日子也不用担心了
	ENDIF
CASE "射精部位の自動選択"
	PRINTFORML 请选择不出现選択肢、直接就在对应部位射精的PLAY
	PRINTL 
	PRINTFORML  [ 0] \@ CONFIG("Ｖ射精の自動選択") ? ○ # － \@ Ｖ插入
	PRINTFORML  [ 1] \@ CONFIG("Ａ射精の自動選択") ? ○ # － \@ Ａ插入
	PRINTFORML  [ 2] \@ CONFIG("尿道射精の自動選択") ? ○ # － \@ 尿道插入
	PRINTFORML  [ 3] \@ CONFIG("Ｂ射精の自動選択") ? ○ # － \@ 乳頭奸
	PRINTFORML  [ 4] \@ CONFIG("口射精の自動選択") ? ○ # － \@ 口交之类的使用对方的嘴的PLAY
	PRINTFORML  [ 5] \@ CONFIG("手射精の自動選択") ? ○ # － \@ 手交之类的使用对方的手的PLAY
	PRINTFORML  [ 6] \@ CONFIG("胸射精の自動選択") ? ○ # － \@ 乳交之类的使用对方的胸的PLAY
	PRINTFORML  [ 7] \@ CONFIG("脚射精の自動選択") ? ○ # － \@ 足交之类的使用对方的足的PLAY
	PRINTFORML  [ 8] \@ CONFIG("自慰射精の自動選択") ? ○ # － \@ 自己自慰時
	PRINTFORML  [ 9] \@ CONFIG("其他射精の自動選択") ? ○ # － \@ 其他
	PRINTFORML  [90] 全部ＯＮ
	PRINTFORML  [91] 全部ＯＦＦ
	PRINTFORML [100] 返回

	CALL INPUT_SELECT, 10, 90, 91, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 90
		FLAG:射精部位の自動選択 = SETBITS(FLAG:射精部位の自動選択, 0, 6)
		FLAG:射精部位の自動選択 = SETBITS(FLAG:射精部位の自動選択, 18, 20)
	CASE 91
		FLAG:射精部位の自動選択 = 0
	;TCVAR:PLAY区分メインに対応させる
	CASE 0
		INVERTBIT FLAG:射精部位の自動選択, 3
	CASE 1
		INVERTBIT FLAG:射精部位の自動選択, 4
	CASE 2
		INVERTBIT FLAG:射精部位の自動選択, 19
	CASE 3
		INVERTBIT FLAG:射精部位の自動選択, 18
	CASE 4
		INVERTBIT FLAG:射精部位の自動選択, 1
	CASE 5
		INVERTBIT FLAG:射精部位の自動選択, 2
	CASE 6
		INVERTBIT FLAG:射精部位の自動選択, 5
	CASE 7
		INVERTBIT FLAG:射精部位の自動選択, 6
	CASE 8
		INVERTBIT FLAG:射精部位の自動選択, 20
	CASE 9
		INVERTBIT FLAG:射精部位の自動選択, 0
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE "气味の表記変更"
	IF FLAG:气味 < 100
		PRINTFORML 这里可以修改文本中出现的「气味」这个单词、改变成玩家喜欢的那个（中文里没有这种语法区别、所以该功能请无视）
		PRINTFORM 具体的には、
		CALL PRINTL_COLORTEXT, "一般には良いとも不快とも断定しづらいがクセになるようなにおい", DEF_COLOR("黄色")
		CALL PRINT_COLORTEXT, "……例えば、好きな相手の性臭や体臭です、", DEF_COLOR("黄色")
		PRINTFORML の表記を選んでいただきます
		PRINTFORML （ただし、髪の气味、料理の气味などの一般に良い香りのものは「气味」で固定、
		PRINTFORMW 　頭皮の臭い、排泄物の臭いなどの一般に良くない香りの物は「臭い」で固定します）
		PRINTFORML 
	ENDIF
	PRINTFORML それでは以下から選択してください
	PRINTL 
	PRINTFORML [ 0] 气味
	PRINTFORML [ 1] 臭い
	PRINTFORML [ 2] におい
	PRINTFORML [ 3] ニオイ

	CALL INPUT_SELECT, 4

	FLAG:气味 = RESULT

	PRINTFORMW %TEXTS("气味")%を選択しました。以下が例文です
	PRINTL 
	PRINTFORMW 胖次越しの阴部の%TEXTS("气味")%……
	PRINTFORMW じっとりと滲んだ汗の%TEXTS("气味")%……
	PRINTFORMW 火照った身体から漂うエッチな%TEXTS("气味")%……
	PRINTFORMW 膣口をきゅっと締めたり、ぽかぁと開いたりさせて胎内の%TEXTS("气味")%を漂わせる……
	PRINTFORMW 鼻先に突き付けられた阴茎の%TEXTS("气味")%……
	PRINTFORMW 汗混じりの淫靡な%TEXTS("气味")%……
	PRINTFORMW 着古した你の恤の%TEXTS("气味")%……
	PRINTL 
	PRINTFORML ……%TEXTS("气味")%でいいですか？
	PRINTFORML [ 0] 是
	PRINTFORML [ 1] 考え直す

	CALL INPUT_SELECT, 2

	IF RESULT == 0
		PRINTFORMW それではお楽しみください
	ELSE
		FLAG:气味 = 100
		RESTART
	ENDIF
CASE "性騷擾禁止"
	INVERTBIT FLAG:性騷擾禁止, 0
	IF CONFIG(ARGS)
		PRINTFORML 尽可能避免无選択肢的性骚扰…不过若有遗漏也请原谅
		PRINTFORMW （基本的来说就是単純減少了会触发的事件）
	ELSE
		PRINTFORMW %CALLNAME:MASTER%会在無選択肢的情况下手滑了
	ENDIF

CASE "乳頭の詳細設定"
	CALL PRINT_STR, "乳頭的詳細情况…具体的来说、可以调整_黄色_乳頭肥大_和_黄色_陥没乳頭_和_黄色_巨乳輪_的存在与否_L"
	PRINTFORML ON就是〇、OFF的话就是×
	PRINTL
	PRINTFORML  [ 0] \@ CONFIG("乳頭肥大") ? ○ # ×\@ 乳頭肥大
	PRINTFORML  [ 1] \@ CONFIG("陥没乳頭") ? ○ # ×\@ 陥没乳頭
	PRINTFORML  [ 2] \@ CONFIG("巨乳輪") ? ○ # ×\@ 巨乳輪
	PRINTFORML [100] 这様就好

	CALL INPUT_SELECT, 3, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0 TO 3
		INVERTBIT FLAG:乳頭, RESULT
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE "陰核の詳細設定"
	CALL PRINT_STR, "陰核的詳細情况…具体的来说、可以调整_黄色_陰核肥大_与_黄色_陰核包茎_的存在与否_L"
	PRINTFORML ON就是〇、OFF的话就是×
	PRINTL
	PRINTFORML  [ 0] \@ CONFIG("陰核肥大") ? ○ # ×\@ 陰核肥大
	PRINTFORML  [ 1] \@ CONFIG("陰核包茎") ? ○ # ×\@ 陰核包茎
	PRINTFORML [100] 这様就好

	CALL INPUT_SELECT, 2, 100

	SELECTCASE RESULT
	CASE 100
		RETURN 0
	CASE 0 TO 2
		INVERTBIT FLAG:陰核, RESULT
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE "素質的固定与除外"
	INVERTBIT FLAG:ARGS, 0

CASE "胎内精子で妊娠"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTL 胎内残留精子的话可以妊娠了。
		PRINTW 请注意不要不小心意外怀孕。
	ELSE
		PRINTW 胎内就算残留精子也不会妊娠了。
	ENDIF
CASE "委身于對方的警告"
	PRINTFORM  是否顯示選擇委身于對方時出現的警告？（現在：
	SELECTCASE CONFIG("委身于對方的警告")
	CASE 0
		CALL PRINT_STR, "黄色_一直顯示"
	CASE 1
		CALL PRINT_STR, "黄色_Ｈ時只顯示一次"
	CASEELSE
		CALL PRINT_STR, "黄色_不顯示"
	ENDSELECT
	PRINTFORML ）
	PRINTFORML  [ 0] 一直顯示(默認)
	PRINTFORML  [ 1] Ｈ時只顯示一次る
	PRINTFORML  [ 2] 不顯示

	CALL INPUT_SELECT, 3

	FLAG:委身于對方的警告 = RESULT

CASE "對男性PLAYER的胸奉仕"
	PRINTL 打算如何變更呢？
	PRINTFORML  [ 0] 禁止(默認)
	PRINTFORML  [ 1] 僅在男娘時ＯＫ
	PRINTFORML  [ 2] 許可

	CALL INPUT_SELECT, 3

	FLAG:對男性PLAYER的胸奉仕 = RESULT
CASE "常時挿入自動解除"
	INVERTBIT FLAG:ARGS, 0
CASE "嘔吐発生"
	INVERTBIT FLAG:ARGS, 0
	IF CONFIG(ARGS)
		PRINTL 連續実行強制口交、開口口枷、深喉口交時実行強制口交等容易發生嘔吐。
		PRINTW 對於沒有[嘔吐反射耐性]的角色請不要頻繁使用強制口交指令。
		PRINTW 但是如果插入的陰莖比較短小就不用擔心這樣的問題。
	ELSE
		PRINTW 不會發生嘔吐事件。
	ENDIF
CASE "主画面颜绘ＯＦＦ"
	INVERTBIT FLAG:画像ONOFF, 0
CASE "メイン画面CIPＯＦＦ"
	INVERTBIT FLAG:画像ONOFF, 1

CASE "Ｈ前廁所", "助手同行", "永久過濾", "允许女性为你以外的其他人受孕"
	INVERTBIT FLAG:ARGS, 0
ENDSELECT


;ARGの名前自体を変更する。基本的には角色メイクの時のみ選択可能
@NAME_CONVERT, ARG

PRINTFORML 変更%CALLNAME:ARG%的名字
PRINTFORML 请输入在全角８文字(半角的话就是16文字)以内的名字
PRINTFORML 什么都不输入的话就不会変更。
CALL INPUTS_SELECT, 16

PRINTL 
SELECTCASE RESULTS
CASE "", NAME:ARG
	PRINTFORMW 没有変更
CASEELSE
	NAME:ARG = %RESULTS%
	SIF ARG != MASTER
		CALLNAME:ARG = %NAME:ARG%
	PRINTFORMW 名字変更为%RESULTS%
	
	PRINTL 
	PRINTFORML 接着、请确定这个第二人称的假名。
	PRINTFORML （什么都不输入的话%RESULTS%就会变成假名）
	PRINTL 
	PRINTFORML 请输入在全角20文字(半角的话就是40文字)以内的名字

	CALL INPUTS_SELECT, 40

	PRINTL 
	IF RESULTS != ""
		CSTR:ARG:ふりがな = %RESULTS%
	ELSE
		CSTR:ARG:ふりがな = %NAME:ARG%
	ENDIF
	PRINTFORMW 名字为%NAME:ARG%而假名名字为%CSTR:ARG:ふりがな%
ENDSELECT


;ARGからの呼ばれ方を変更する
@NAME_CONVERT_SP, ARG
#DIMS MEMO_CNAME
#DIMS MEMO_CNAME_KANA

PRINTFORML %CALLNAME:ARG%、会在和%CALLNAME:MASTER%说话時変更第二人称的称呼
PRINTFORML 请输入在全角10文字(半角的话就是20文字)以内的名字
PRINTFORML 以及、如果想在自己的名字后面被以 様 来称呼的话就请以"NAME_MASTER様"的様子进行输入
PRINTFORML 什么都不输入的话就不会変更。
CALL INPUTS_SELECT, 20

PRINTL 
SELECTCASE RESULTS
CASE "", CEVENTS("二人称主人", ARG)
	PRINTFORMW 没有変更
CASEELSE

	MEMO_CNAME = 二人称主人：%RESULTS%
	MEMO_CNAME_KANA = 二人称主人かな：%RESULTS%

	;呼び名フラグ重置
	CALL DEL_CEVENT_GROUP_EX, "二人称主人", ARG
	CALL SET_CEVENT, MEMO_CNAME, ARG
	PRINTFORMW 和%CALLNAME:MASTER%说话時的第二人称変更为%CNAMET(ARG, "二人称")%了
	PRINTL 
	PRINTFORML 接着、请确定这个第二人称的假名。
	PRINTFORML （什么都不输入的话%CNAMET(ARG, "二人称")%就会变成假名）
	PRINTL 
	PRINTFORML 请输入在全角20文字(半角的话就是40文字)以内的名字

	CALL INPUTS_SELECT, 40

	PRINTL 
	SIF RESULTS != ""
		MEMO_CNAME_KANA = 二人称主人かな：%RESULTS%

	CALL SET_CEVENT, MEMO_CNAME_KANA, ARG

	PRINTFORML 和%CALLNAME:MASTER%说话時的第二人称変更为%CNAMET(ARG, "二人称")%、
	PRINTFORMW 以及其假名変更为%CNAMET(ARG, "二人称", "かな")%了
ENDSELECT

;ARGのあだ名を変更する
@NICKNAME_CONVERT, ARG
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
PRINTL 
;すでにあだ名がある？
IF NICKNAME:ARG != ""
	PRINTFORML 変更%CALLNAME:ARG%的爱称（現在为%NICKNAME:ARG%）
ELSE
	PRINTFORML 为%CALLNAME:ARG%取爱称
ENDIF
PRINTFORML 请输入在全角10文字(半角的话就是20文字)以内的名字

IF NICKNAME:ARG != ""
	PRINTFORML 什么都不输入的话就不会変更
	CALL PRINT_STRL, "以及、不想要爱称的话就请输入_黄色_あだ名を無くす_"
ELSE
	PRINTFORML 什么都不输入的话、就会取消取爱称
ENDIF
CALL INPUTS_SELECT, 20

PRINTL 
SELECTCASE RESULTS
CASE ""
	IF NICKNAME:ARG != ""
		PRINTFORMW 没有変更
	ELSE
		PRINTFORMW 取消取爱称了
	ENDIF
CASE "あだ名を無くす"
	IF NICKNAME:ARG != ""
		NICKNAME:ARG = 
		PRINTFORMW %CALLNAME:ARG%的爱称被取消了
	ELSE
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
CASEELSE
	NICKNAME:ARG = %RESULTS%
	PRINTFORMW %CALLNAME:ARG%的爱称、变成了%NICKNAME:ARG%
ENDSELECT

