@SAVEGAME_EX, ARG
#DIMS SAVEINFO_EX
#DIM CHOICE
#DIM MEMO_DATANO
#DIM MEMO_LINECOUNT
#DIM MEMO_REDRAW

MEMO_REDRAW = CURRENTREDRAW()
MEMO_LINECOUNT = LINECOUNT
PRINTL 

$REDRAW_SAVEDATA
REDRAW 0

SIF LASTLOAD_NO >= 0
	CALL PRINT_STRL, @"現在正在游玩_黄色_{LASTLOAD_NO, 2}番_的数据哟。"
PRINTL 要存到几号位里呢？
CALL PRINT_SAVEDATA, ARG, "SAVE"

PRINTL [999] 返回

$INPUT_DATANUM
INPUT

SELECTCASE RESULT
CASE 999
	;バージョン格子をON
	FLAG:VERUP格子 = 1
	REDRAW MEMO_REDRAW
	RETURN 1
CASE 201 TO (200 + (NUM_NORMALSAVE()/NUM_SHOWSAVE() ) )
	ARG = RESULT - 201
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
;PLAYヤーが保存可能なのは通常保存
CASE 0 TO (NUM_NORMALSAVE() - 1)
	CHOICE = RESULT
	CHKDATA CHOICE
	;元のSAVEGAMEではRESULT == 0のとき以外は確認をとらない
	IF RESULT == 0
		PRINTL 已经有存档在这里了。要覆盖吗？
		PRINTLC [0] 是
		PRINTLC [1] 否
		PRINTL 

		$YESNO_INPUT_LOOP
		INPUT

		IF RESULT == 1
			GOTO REDRAW_SAVEDATA
		ELSEIF RESULT != 0
			CLEARLINE 1
			REUSELASTLINE 無効な値です
			GOTO YESNO_INPUT_LOOP
		ENDIF
	ENDIF

	;バージョン格子を初期化
	FLAG:VERUP格子 = 0
	FLAG:加载時台詞 = 0
	REDRAW MEMO_REDRAW

	SAVEINFO_EX = %GETTIMES_CUSTOM()% %SAVEINFO_MESSAGE()%
	SAVEDATA CHOICE, SAVEINFO_EX

	LOADGLOBAL
	GLOBAL:最新ＳＬ番号 = CHOICE
	SAVEGLOBAL

	FLAG:VERUP格子 = 1
	FLAG:加载時台詞 = 1
	RETURN 1
ENDSELECT

CLEARLINE 1
;REUSELASTLINE 無効な値です
GOTO INPUT_DATANUM

@LOADGAME_EX, ARG
#DIM CHOICE
#DIM MEMO_LINECOUNT
#DIM MEMO_REDRAW

MEMO_REDRAW = CURRENTREDRAW()
MEMO_LINECOUNT = LINECOUNT
PRINTL 

$REDRAW_SAVEDATA
REDRAW 0

SIF LASTLOAD_NO >= 0
	CALL PRINT_STRL, @"現在加载的是_黄色_{LASTLOAD_NO, 2}番_的数据哟。"
PRINTPLAINFORM 加載哪一個存檔？下部は直近の保存データで、[{NUM_NORMALSAVE()}]～[{NUM_NORMALSAVE() + NUM_AUTOSAVE() - 1}]はオート保存です
PRINTL 
CALL PRINT_SAVEDATA, ARG, "LOAD"
PRINTL [999] 返回

$INPUT_DATANUM
INPUT

SELECTCASE RESULT
CASE 999
	REDRAW MEMO_REDRAW
	RETURN 1
CASE 201 TO (201 + (NUM_NORMALSAVE()/NUM_SHOWSAVE() ) + 1)
	ARG = RESULT - 201
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
;加载可能なのは通常保存＆オート保存
CASE 0 TO (NUM_NORMALSAVE() + NUM_AUTOSAVE() - 1)
	CHOICE = RESULT
	CHKDATA CHOICE
	;RESULT == 0なら加载可能
	IF RESULT != 0
		PRINTVL CHOICE
		PRINTL 没有数据
		GOTO REDRAW_SAVEDATA
	ENDIF
	REDRAW MEMO_REDRAW

	LOADGLOBAL
	GLOBAL:最新ＳＬ番号 = CHOICE
	SAVEGLOBAL

	LOADDATA CHOICE

	RETURN 1
ENDSELECT

CLEARLINE 1
;REUSELASTLINE 無効な値です
GOTO INPUT_DATANUM


;１页あたりの保存データ表示数を指定する関数。現在は20
@NUM_SHOWSAVE()
#FUNCTION
RETURNF 20

;通常保存数の数。現在は100
@NUM_NORMALSAVE()
#FUNCTION
RETURNF 100

;オート保存数の数。現在は20
@NUM_AUTOSAVE()
#FUNCTION
RETURNF 20


;-------------------------------------------------
;ARGには現在的表示页、ARGSにはSAVEorLOADが入っている
;-------------------------------------------------
@PRINT_SAVEDATA, ARG, ARGS
;通常保存を表示する
CALL PRINT_SAVEDATA_NORMAL, ARG, ARGS
PRINTL 

;页切り替えタブの表示
CALL PRINT_SAVEDATA_TAB, ARG, ARGS
PRINTL 

;直近の保存データを5個表示する
CALL PRINT_SAVEDATA_LATEST, 5
PRINTL 
;直近オート保存を2個表示する
CALL PRINT_SAVEDATA_LATESTAUTO, 2
PRINTL 
RETURN

;-------------------------------------------------
;通常保存データの表示
;-------------------------------------------------
@PRINT_SAVEDATA_NORMAL, ARG, ARGS
#DIM LCOUNT

FOR LCOUNT, 0, NUM_SHOWSAVE()
	CHKDATA LCOUNT + ARG*NUM_SHOWSAVE()
	SIF LASTLOAD_NO == LCOUNT + ARG*NUM_SHOWSAVE()
		SETCOLOR DEF_COLOR("黄色")

	SELECTCASE LCOUNT + ARG*NUM_SHOWSAVE()
	CASE IS >= 100
		PRINTFORML [{LCOUNT + ARG*NUM_SHOWSAVE(), 2}] %RESULTS%
	CASEELSE
		PRINTFORML  [{LCOUNT + ARG*NUM_SHOWSAVE(), 2}] %RESULTS%
	ENDSELECT

	RESETCOLOR
NEXT


;-------------------------------------------------
;直近保存データの表示。ARG個だけ表示する
;-------------------------------------------------
@PRINT_SAVEDATA_LATEST, ARG
#DIM LCOUNT
;対応した番号の保存データを記録した時間
#DIM DYNAMIC VALUE_SAVETIME, 100
;保存データを時系列順に並べた際の番号。最新保存が5番ならNO_SAVEDATA:0 = 5となる
#DIM DYNAMIC NO_SAVETIME, 100

;直近の保存データをARG個表示させるために領帯ムスタンプ判定(オート保存は除く)
FOR LCOUNT, 0, NUM_NORMALSAVE()
	;まず記録した時間を見る
	CHKDATA LCOUNT
	NO_SAVETIME:LCOUNT = LCOUNT
	VALUE_SAVETIME:LCOUNT = POWER(2, 62) - REGET_TIME(RESULTS)
NEXT

;昇順でソートしかできないので2の62乗から値を引く形式とする
ARRAYMSORT VALUE_SAVETIME, NO_SAVETIME

CALL DRAWLINES, "NOBUTTON_---□ 直近の保存データ([100]～はオート保存) □"
SETCOLOR DEF_COLOR("緑")
FOR LCOUNT, 0, ARG
	CHKDATA NO_SAVETIME:LCOUNT
	SIF RESULT
		BREAK
	SIF LASTLOAD_NO == NO_SAVETIME:LCOUNT
		SETCOLOR DEF_COLOR("黄色")

	SELECTCASE NO_SAVETIME:LCOUNT
	CASE IS >= 100
		PRINTFORML [{NO_SAVETIME:LCOUNT, 2}] %RESULTS%
	CASEELSE
		PRINTFORML  [{NO_SAVETIME:LCOUNT, 2}] %RESULTS%
	ENDSELECT

	SETCOLOR DEF_COLOR("水色")
NEXT
RESETCOLOR


;-------------------------------------------------
;最新オート保存の表示。ARG個だけ表示する
;-------------------------------------------------
@PRINT_SAVEDATA_LATESTAUTO, ARG
#DIM LCOUNT
;対応した番号の保存データを記録した時間
#DIM DYNAMIC VALUE_SAVETIME, 20
;保存データを時系列順に並べた際の番号。最新保存が5番ならNO_SAVEDATA:0 = 5となる
#DIM DYNAMIC NO_SAVETIME, 20

;直近のオート保存をARG個表示させるために領帯ムスタンプ判定(通常保存は除く)
FOR LCOUNT, 0, NUM_AUTOSAVE()
	CHKDATA NUM_NORMALSAVE() + LCOUNT
	NO_SAVETIME:LCOUNT = NUM_NORMALSAVE() + LCOUNT
	VALUE_SAVETIME:LCOUNT = POWER(2, 62) - REGET_TIME(RESULTS)
NEXT

;昇順でソートしかできないので2の62乗から値を引く形式とする
ARRAYMSORT VALUE_SAVETIME, NO_SAVETIME

;CALL DRAWLINES, "---□ オート保存 □"
;SETCOLOR DEF_COLOR("水色")
FOR LCOUNT, 0, ARG
	CHKDATA NO_SAVETIME:LCOUNT
	SIF RESULT
		BREAK
	PRINTFORML [{NO_SAVETIME:LCOUNT, 2}] %RESULTS%

NEXT
;色を変えた場合はコメントアウトを取下
;RESETCOLOR


;-------------------------------------------------
;页切り替えタブの表示
;-------------------------------------------------
@PRINT_SAVEDATA_TAB, ARG, ARGS
#DIM LCOUNT
#DIMS TEXT_PAGESELECT

TEXT_PAGESELECT = 
FOR LCOUNT, 0, (NUM_NORMALSAVE()/NUM_SHOWSAVE() ) + 1
	;保存画面ではオート保存は見せない
	SIF ARGS == "SAVE" && LCOUNT == (NUM_NORMALSAVE()/NUM_SHOWSAVE() )
		CONTINUE

	SELECTCASE LCOUNT
	CASE ARG
		TEXT_PAGESELECT += @"_灰色_NOBUTTON_ [×]"
	CASEELSE
		TEXT_PAGESELECT += @"[{201+LCOUNT}]"
	ENDSELECT

	SELECTCASE LCOUNT
	CASE (NUM_NORMALSAVE()/NUM_SHOWSAVE() )
		TEXT_PAGESELECT += @"自动保存表示 "
	CASEELSE
		TEXT_PAGESELECT += @"{NUM_SHOWSAVE()*LCOUNT}～{NUM_SHOWSAVE()*(LCOUNT+1)-1} "
	ENDSELECT

	SELECTCASE LCOUNT
	CASE ARG
		TEXT_PAGESELECT += @"_"
	ENDSELECT
NEXT
CALL PRINT_STRL, TEXT_PAGESELECT


;保存データの文字列ARGSから記録された年月日時間に対応した数字を返させる
;値が大きいほど新しい
@REGET_TIME(ARGS)
#FUNCTION
#DIM LCOUNT
#DIM NUM_ZERO

LOCALS = 
NUM_ZERO = 0
FOR LCOUNT, 0, 100
	IF ISNUMERIC(SUBSTRINGU(ARGS, LCOUNT, 1) )
		LOCALS += @"%SUBSTRINGU(ARGS, LCOUNT, 1)%"
	ELSE
		SELECTCASE SUBSTRINGU(ARGS, LCOUNT, 1)
		CASE "朝"
			LOCALS += "0"
			BREAK
		CASE "昼"
			LOCALS += "1"
			BREAK
		CASE "夕"
			LOCALS += "2"
			BREAK
		CASE "夜"
			LOCALS += "3"
			BREAK
		CASE " "
			LOCALS += "0"
			NUM_ZERO += 1
			SIF NUM_ZERO >= 5
				BREAK
		ENDSELECT
	ENDIF
NEXT
RETURNF TOINT(LOCALS)


@GETTIMES_CUSTOM()
#FUNCTIONS
#DIMS TIMEINFO
;カスタム時間表示の例
;年。下二桁のみに簡略化…つまり２桁固定でいい
TIMEINFO = {(GETTIME() / 10000000000000) % 100}
;月
LOCAL = (GETTIME() / 100000000000) % 100
TIMEINFO = %TIMEINFO%/\@ LOCAL < 10 ? 0 # \@{LOCAL}
;日
LOCAL = (GETTIME() / 1000000000) % 100
TIMEINFO = %TIMEINFO%/\@ LOCAL < 10 ? 0 # \@{LOCAL}
;時
LOCAL = ((GETTIME() / 10000000) % 100) % 24
TIMEINFO = %TIMEINFO% \@ LOCAL < 10 ? 0 # \@{LOCAL}
;分
LOCAL = (GETTIME() / 100000) % 100
TIMEINFO = %TIMEINFO%:\@ LOCAL < 10 ? 0 # \@{LOCAL}

RETURNF TIMEINFO

@SAVEINFO_MESSAGE()
#FUNCTIONS
#DIMS SAVEINFO
#DIM LCOUNT
#DIM CHOICE

SAVEINFO = {DAY, 3}日%TEXTS("季節")%(%TEXTS("曜日")%)%TEXTS("時間")%

IF TARGET
	SAVEINFO += @" %TEXT_LJ(NAME:TARGET, 16)%"
	IF TALENT:相思相愛
		SAVEINFO += "相"
	ELSEIF TALENT:恋慕
		SAVEINFO += "恋"
	ELSE
		SAVEINFO += "--"
	ENDIF
	IF TALENT:淫乱
		SAVEINFO += "淫"
	ELSE
		SAVEINFO += "--"
	ENDIF
	IF CFLAG:睡眠
		SAVEINFO += "眠"
	ELSE
		SAVEINFO += "--"
	ENDIF
ENDIF

SAVEINFO += @" {CHARANUM-1}人"
IF CHARANUM-1 >= 3
	SAVEINFO += "("
	;優先順位上位6名を表示する
	FOR LCOUNT, 1, 8
		CHOICE = FINDCHARA(CFLAG:優先順位, LCOUNT, 2)
		SIF CHOICE <= 0
			BREAK
		SELECTCASE LCOUNT
		CASE 7
			SAVEINFO += ",…"
			BREAK
		CASE IS > 1
			SAVEINFO += ","
		ENDSELECT

		SAVEINFO += @"%CONDS("短縮名", CHOICE)%"
	NEXT
	SAVEINFO += ")"
ENDIF

SAVEINFO = %TEXT_LJ(SAVEINFO, NUM("折り返し文字数") - 5 - 9 - 7 - 7 - 6 - 2)% %VERS_eraAkumaMaid()%

SIF FLAG:周回数
	SAVEINFO += @" {FLAG:周回数 + 1}周目"

RETURNF SAVEINFO

@TITLE_LOADGAME
;标题画面の加载でも独自の加载画面を使う場合、@TITLE_LOADGAMEを定義する
;@TITLE_LOADGAMEが定義されていると、標準の加载画面の代わりにTITLE_LOADGAMEが呼ばれる。
CALL LOADGAME_EX
RETURN
;RETURNすると标题へ返回。

;オート保存の処理を独自に行う場合、@SYSTEM_AUTOSAVEを定義する
;@SYSTEM_AUTOSAVEが定義されていると、標準のオート保存の代わりにSYSTEM_AUTOSAVEが呼ばれる。
;(@SYSTEM_AUTOSAVEを定義し什麼也不做でRETURNすればオート保存を禁止できる。その場合はLOADDATA_EXから99番のデータに関する記述を削除しておくと親切)
@SYSTEM_AUTOSAVE
#DIM LCOUNT
#DIMS SAVEINFO_EX
;対応した番号の保存データの、記録された時間
;NUM_NORMALSAVE() + NUM_AUTOSAVE()と同じ数必要
#DIM VALUE_SAVETIME, 120
;この番号にオート保存する。NUM_NORMALSAVE()番に近く、最も古い番号を選ぶ。
#DIM CHOICE

VARSET VALUE_SAVETIME

;初期番号は通常保存数の番号(例えばこれが100ならオート保存は100番から割り振られる)
CHOICE = NUM_NORMALSAVE()

FOR LCOUNT, NUM_NORMALSAVE(), NUM_NORMALSAVE() + NUM_AUTOSAVE()
	SIF LCOUNT == LASTLOAD_NO
		CONTINUE

	CHKDATA LCOUNT
	VALUE_SAVETIME:LCOUNT = REGET_TIME(RESULTS)

	SIF VALUE_SAVETIME:LCOUNT < VALUE_SAVETIME:CHOICE
		CHOICE = LCOUNT
NEXT

SAVEINFO_EX = %GETTIMES_CUSTOM()% %SAVEINFO_MESSAGE()%
SAVEDATA CHOICE, SAVEINFO_EX

@EVENTLOAD
#PRI

