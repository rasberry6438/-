;-------------------------------------------------------------------------------------------------
;助手に性欲処理を頼む
;台詞については TRYCALLFORM KOJO_GUEST_K{NO:ASSI}, LOCALS, ASSI で呼び、無い場合には地の文で補填
;おおまかなフラグについてはCEVENTの"助手で性処理："グループを使用しても良い。これは毎ターン初期化される
;得た経験などについてはEXP_GAINで参照できる
;ただし、主人の経験を参照する場合には他の助手相手のPLAYで経験が増えていることがあるため注意が必要
;一回に付き助手経験が+1されるため、EXP_GAIN("助手経験", ASSI)でこのターンに何回相手したかが分かる（現在は１回で終了）
;同様にEXP_GAINで増えた経験を参照可能
;前回やったことについてはPREVCOMで参照可能。
;阴茎が汚れてるかどうかは CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢")
;-------------------------------------------------------------------------------------------------
@PLAY_WITH_ASSI
#DIM LCOUNT
#DIM MEMO, 10
#DIM MEMO_LINECOUNT
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIMS EVENTNAME
#DIMS MEMO_CLO

;おあずけフラグがたっている？
IF CEVENT("助手で性処理：おあずけ", ASSI)
	;口上があるかどうかを行数で格子
	MEMO_LINECOUNT = LINECOUNT

	CALL KOJO_GUEST, "助手で性処理：おあずけ", ASSI

	;台詞が無いなら汎用イベント（台詞は極力没有で地の文のみとする）
	SIF LINECOUNT == MEMO_LINECOUNT
		PRINTFORMW 続きは%CALLNAME:ASSI%的房間でしましょう
	RETURN 0
ENDIF

;SIMULATE_TRAINを使うためにASSIをTARGETにする
;-------------------------------------------------------------------------------------------------
MEMO_TARGET = TARGET
MEMO_ASSI = ASSI
TARGET = ASSI

CALL SIMULATE_TRAIN_FIRST

TARGET = MEMO_TARGET
ASSI = MEMO_ASSI
;-------------------------------------------------------------------------------------------------

;選択回数によるローテーションにはRANDF(10, EXP_GAIN("助手経験", ASSI) )を用いる：保留
;前回と違う人or時間帯ごとの初回なら各種フラグ初期化
IF ASSI != COND("前Ｔ対象") || EXP_GAIN("助手経験", ASSI) == 0
	PREVCOM = -1
	VARSET TEQUIP:ASSI:0, 0, 0, 100
	VARSET TEQUIP:MASTER:0, 0, 0, 100
	CALL DEL_CEVENT_GROUP, "助手で性処理", ASSI
ENDIF

;着ている服を記憶
MEMO_CLO = 
FOR LCOUNT, 40, 50
	MEMO_CLO = %MEMO_CLO%{EQUIP:ASSI:LCOUNT},%CSTR:ASSI:LCOUNT%,
NEXT
;確認用
;PRINTFORMW %MEMO_CLO%

;１ターン限定のフラグを消去
CALL DEL_CEVENT_GROUP, "助手で性処理", ASSI
SELECTCOM:5 = -1

;指定が無いなら、やることは口交or舐陰
IF PENIS(MASTER) && RANDIF(1, TALENT:MASTER:男性 == 0)
	SELECTCOM = 91
ELSE
	SELECTCOM = 98
ENDIF

PRINTL 

;口上があるかどうかを行数で格子
MEMO_LINECOUNT = LINECOUNT

;内部で好きにイベントとSELECTCOMを決定
CALL KOJO_GUEST, "助手で性処理", ASSI

;台詞が無いなら汎用イベント（台詞は極力没有で地の文のみとする）
IF LINECOUNT == MEMO_LINECOUNT
	SELECTCASE SELECTCOM
	;口交
	CASE 91
		;六九式
		IF ABL:ASSI:施虐属性 && ABL:MASTER:受虐属性 && CHECK_CLO("胖次露出", ASSI) + CHECK_CLO("Ｖ露出", ASSI) && RAND:3 == 0
			;面部騎乗
			IF MAX(ABL:ASSI:施虐属性, ABL:MASTER:受虐属性) >= 3 && RAND:2
				IF CHECK_CLO("Ｖ露出", ASSI)
					PRINTFORMW 当%CALLNAME:MASTER%按照命令躺下后、%CALLNAME:ASSI%弯下腰将裸露出来的阴部压在了脸上！
				ELSEIF RAND:2
					PRINTFORMW 当%CALLNAME:MASTER%按照命令躺下后、%CALLNAME:ASSI%弯下腰坐在了脸上！
				ELSE
					IF RAND:2
						PRINTFORMW %CALLNAME:ASSI%将内衣脱下丢到一边、放下腰坐在了%CALLNAME:MASTER%的脸上！
					ELSE
						PRINTFORMW 当%CALLNAME:MASTER%按照命令躺下后、%CALLNAME:ASSI%脱掉了内衣放下腰坐在了脸上！
					ENDIF
					CALL SET_PANTIES, "", ASSI
					CALL SET_CEVENT, "助手で性処理：胖次下ろし", ASSI
				ENDIF
				TEQUIP:ASSI:面部騎乗 = 1
				TEQUIP:ASSI:六九式 = 91
				TEQUIP:MASTER:六九式 = 1
			ELSE
				PRINTFORMW %CALLNAME:ASSI%用69的姿势压在%CALLNAME:MASTER%的身上、无防备地露出股间的同时用舌頭责弄着阴茎！
				TEQUIP:ASSI:六九式 = 91
				;悪戯の内容を決める
				IF CHECK_CLO("Ｖ露出", ASSI) == 0
					IF RAND:2
						TEQUIP:MASTER:六九式 = 7
					ELSE
						TEQUIP:MASTER:六九式 = 1
					ENDIF
				ELSEIF RAND:7 == 0 && CALCF("尿道径", ASSI) > SIZE("指の直径", MASTER)
					TEQUIP:MASTER:六九式 = 15
				ELSEIF RAND:6 == 0
					TEQUIP:MASTER:六九式 = 14
				ELSEIF RAND:5 == 0
					TEQUIP:MASTER:六九式 = 13
				ELSEIF RAND:4 == 0
					TEQUIP:MASTER:六九式 = 8
				ELSEIF RAND:3 == 0
					TEQUIP:MASTER:六九式 = 7
				ELSEIF RAND:2
					TEQUIP:MASTER:六九式 = 2
				ELSE
					TEQUIP:MASTER:六九式 = 1
				ENDIF
			ENDIF

			SELECTCOM = 99
		ELSE
			;服を脱いでの奉仕。胖次を脱掉or全裸
			IF ABL:ASSI:露出癖 >= 3 && CHECK_CLO("裸体", ASSI) == 0 && EQUIP:ASSI:胖次 && RAND:3 == 0
				IF EQUIP:ASSI:胖次 == 0 || (ABL:ASSI:露出癖 >= 5 && RAND:2)
					PRINTFORMW %CALLNAME:ASSI%脱掉了所有的衣物、整齐地整理好放在一边之后在%CALLNAME:MASTER%的面前跪下用舌頭奉侍起阴茎
					CALL SETFLAG, "扒成裸体", ASSI
					CALL SET_CEVENT, "助手で性処理：裸体", ASSI
				ELSE
					PRINTFORMW 将胖次脱掉一半挂在腿上的%CALLNAME:ASSI%は、%TEXTR("熟练地/自然地")%取出%CALLNAME:MASTER%的阴茎将嘴唇贴了上去
					CALL SET_PANTIES, "", ASSI
					CALL SET_CEVENT, "助手で性処理：胖次下ろし", ASSI
				ENDIF
			;深喉
			ELSEIF CHECK("喉姦", ASSI) >= 2 && SIZE("阴茎", MASTER) && RAND:2
				PRINTFORMW %CALLNAME:ASSI%跪在%CALLNAME:MASTER%股间前、将坚硬地屹立在眼前的阴茎直到根部为止%TEXTR("一口气/整根/全部")%含入口中
				TEQUIP:ASSI:深喉 = MASTER
			ELSE
				PRINTFORM %CALLNAME:ASSI%は
				IF RAND:3 == 0
					PRINTFORM 跪在%CALLNAME:MASTER%的股间前
				ELSEIF RAND:2
					PRINTFORM 用自然的动作将%CALLNAME:MASTER%的
				ELSE
					PRINTFORM 一边用舌頭舔舐着一边将%CALLNAME:MASTER%的
				ENDIF
				PRINTFORM 阴茎取出来、
				;包茎
				IF TALENT:MASTER:包茎 && RAND:3 == 0
					;真性包茎
					IF COND("真性包茎", MASTER)
						PRINTFORMW 咬住覆盖着的包皮%TEXTR("用舌頭顶在尖端上旋转/从微微打開的包皮中用舌頭进行按摩")%
					;假性包茎
					ELSE
						PRINTFORMW 将多余的皮肤剥下之后%TEXTR("用嘴含住了龟頭/用舌頭舔舐龟頭/亲吻了龟頭")%
					ENDIF
				ELSEIF TALENT:ASSI:恋慕 && RAND:2
					PRINTFORMW 怜爱地%TEXTR("亲吻/用脸颊磨蹭")%之后放入口中
				ELSE
					PRINTFORMW %TEXTR("用舌頭舔/舔舐")%的同时可爱地向上看着这边侍奉着
				ENDIF
			ENDIF
		ENDIF
	CASEELSE
		SWAP TARGET, ASSI
		TRYCALLFORM TRAIN_MESSAGE_COM{SELECTCOM}
		SWAP TARGET, ASSI
	ENDSELECT
ENDIF


;SIMULATE_TRAINを使うためにASSIをTARGETにする
;-------------------------------------------------------------------------------------------------
MEMO_TARGET = TARGET
MEMO_ASSI = ASSI
TARGET = ASSI

	;主人の射精や助手の絶頂のカウントに使用
	MEMO:0 = EXP:MASTER:射精経験
	MEMO:1 = EXP:絶頂経験

	;回数はまだ調整していない
	FOR LCOUNT, 0, 10
		CALL SIMULATE_TRAIN_COM, SELECTCOM, 1
		SIF SELECTCOM:5 >= 0
			CALL SIMULATE_TRAIN_COM, SELECTCOM:5, 1
		;基本はMASTERが射精するまで
		SIF EXP:MASTER:射精経験 > MEMO:0
			BREAK
	NEXT

	;経験や珠などの獲得量計算
	CALL SIMULATE_TRAIN_CALC

	;結果発表
	CALL SIMULATE_TRAIN_SHOW

TARGET = MEMO_TARGET
ASSI = MEMO_ASSI
;-------------------------------------------------------------------------------------------------

SIF CEVENT("助手で性処理：地の文スキップ", ASSI)
	GOTO AFTER_TEXT_1

IF TEQUIP:ASSI:面部騎乗
	IF RANDF(4) == 0 && ABL:ASSI:施虐属性 >= 3
		PRINTFORMW %CALLNAME:MASTER%被%CALLNAME:ASSI%的面部騎乗闻嗅着酸臭味的同时、就这样吮吸着阴部射精了…
	ELSEIF CHECK_CLO("胖次露出", ASSI)
		IF RANDF(2) == 0
			PRINTFORMW %CALLNAME:MASTER%隔着胖次闻嗅着%CALLNAME:ASSI%的阴部可以说是%TEXTS("气味")%的气味的同时、就这样吮吸着阴部射精了…
		ELSE
			PRINTFORMW %CALLNAME:MASTER%一边亲吻着%CALLNAME:ASSI%的被%TEXTS("气味")%的气味浸染的胖次的同时、就这样吮吸着阴部射精了…
		ENDIF
	ELSE
		IF RANDF(3) == 0
			PRINTFORMW %CALLNAME:MASTER%被%CALLNAME:ASSI%的阴部塞满了口鼻、就这样吮吸着阴部射精了…
		ELSEIF RANDF(3) == 1
			PRINTFORMW %CALLNAME:MASTER%的脸被%CALLNAME:ASSI%阴部摩擦的同时、就这样吮吸着阴部射精了…
		ELSE
			PRINTFORMW %CALLNAME:MASTER%被%CALLNAME:ASSI%的股间侵犯的同时、就这样吮吸着阴部射精了…
		ENDIF
	ENDIF
ELSEIF TEQUIP:ASSI:深喉
	PRINTFORMW %CALLNAME:MASTER%的阴茎被化为了性器的%CALLNAME:ASSI%的嘴所捕食、无法反抗地被榨取出了精液…
;以下はSELECTCOMのみの分岐
ELSE
	SELECTCASE SELECTCOM
	CASE 91
		PRINTFORMW %CALLNAME:MASTER%在跪坐在股间用嘴进处理的%CALLNAME:ASSI%喉中、大量地射精了…
	CASE 92
		PRINTFORMW %CALLNAME:MASTER%在用嘴亲吻着龟頭用胸部撸动肉棒的%CALLNAME:ASSI%的口中、大量地射精了…
	CASE 99
		PRINTFORM %CALLNAME:MASTER%
		SELECTCASE TEQUIP:MASTER:六九式
		CASE 1
			IF CHECK_CLO("Ｖ露出", ASSI)
				PRINTFORM 把%CALLNAME:ASSI%的%TEXTR("阴部的各个角落全部来回舔舐/阴部上用脸来回摩擦/用嘴玩弄着阴核")%的同时
			ELSE
				PRINTFORM 把%CALLNAME:ASSI%的%TEXTR("的阴部隔着内衣享受/内裤放在脸上深深地闻嗅/的淫核作为重点进行攻击将内衣弄得湿乎乎的")%的同时
			ENDIF
		CASE 2
			PRINTFORM 用%TEXTR("手指按摩着/手指玩弄着")%%CALLNAME:ASSI%的后穴的同时
		CASE 7
			IF CHECK_CLO("Ｖ露出", ASSI)
				LOCALS = 闻嗅%TEXTS("气味")%
				PRINTFORM 打開%CALLNAME:ASSI%的小穴%TEXTR("又合上进行着玩弄/进行观察/插入手指检查着里面的情况/进行检查", LOCALS)%的同时
			ELSE
				PRINTFORM 将%CALLNAME:ASSI%的%TEXTR("小穴隔着内衣玩弄着/的胖次拉扯着玩弄着/的淫核作为重点进行攻击将内衣弄得湿乎乎的")%的同时
			ENDIF
		CASE 8
			PRINTFORM 将手指插入%CALLNAME:ASSI%的小穴中%TEXTR("咕哩咕哩地戳弄/打開又闭上/執拗地刮擦膣壁")%的同时
		CASE 13
			LOCALS = 享受着散发着热气的性器的%TEXTS("气味")%
			PRINTFORM 打開%CALLNAME:ASSI%的小穴%TEXTR("又合上进行着玩弄/进行观察/插入手指检查着里面的情况/进行检查", LOCALS)%的同时
		CASE 14
			PRINTFORM 打開%CALLNAME:ASSI%的后穴%TEXTR("观察每个角落/确认着色素沉淀的情况/进行检查")%的同时
		CASE 15
			PRINTFORM 将手指插入%CALLNAME:ASSI%的尿道进进出出地玩弄的同时
		ENDSELECT
		PRINTFORMW 、明明是在吮吸阴部还是放任让精液吐了出来…
	CASE 140
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:ASSI%の頭を掴んで勢いよく腰を前後させ、腰を突き出して咽頭を擦りあげながら喉奥に充分射精した…
	CASEELSE
		CALL SAMEN_SHOOT_MESSAGE, ASSI
	ENDSELECT
ENDIF

$AFTER_TEXT_1
CALL DEL_CEVENT, "助手で性処理：地の文スキップ", ASSI

;口上があるかどうかを行数で格子
MEMO_LINECOUNT = LINECOUNT

CALL KOJO_GUEST, "助手で性処理：事後", ASSI

;台詞が無いなら汎用台詞
IF LINECOUNT == MEMO_LINECOUNT
	;特に口上が用意されていないならおあずけフラグＯＮ
	CALL SET_CEVENT, "助手で性処理：おあずけ", ASSI
ENDIF

SIF CEVENT("助手で性処理：地の文スキップ", ASSI)
	GOTO AFTER_TEXT_2




$AFTER_TEXT_2
CALL DEL_CEVENT, "助手で性処理：地の文スキップ", ASSI

EXP:ASSI:助手経験 += 1
;欲求も少々満たされます
CALL CALC, "欲求不満解消", ASSI, 10

;着衣の乱れを直す
CALL SETFLAG, "衣装全脱ぎ", ASSI

VARSET LOCALS
SPLIT MEMO_CLO, ",", LOCALS

FOR LCOUNT, 0, 10
	EQUIP:ASSI:(LCOUNT + 40) = TOINT(LOCALS:(LCOUNT*2) )
	CSTR:ASSI:(LCOUNT + 40) = %LOCALS:(LCOUNT*2 + 1)%
NEXT

RETURN 1



