;-------------------------------------------------
;性処理関連が複雑化してきたのでまとめる
;-------------------------------------------------
;@CANTRAINの条件付きver
;ARGSにはTRAINNAMEが入る。性欲解消(你がpartnerの性処理に回る)の際にはTRAINNAMEの前に"性欲解消"と付ける。例)ARGS = 性欲解消正常位
;具現については[本人が具現の使い手]＆[兩面通吃]の場合に限り性処理でも使用可としていたがバグに見えるっぽいのでオミット
;-------------------------------------------------
@CANSS(ARGS, ARG)
#FUNCTION
#DIM IS_REV
;竿役
#DIM ACTOR_USEP

SIF ARG < 0 || ARGS == ""
	RETURNF 0
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

ACTOR_USEP = 0
IS_REV = 0

;性欲解消？
IF STRCOUNT(ARGS, "性欲解消")
	ARGS = %SUBSTRINGU(ARGS, 4, -1)%
	IS_REV = 1
ENDIF

;CANTRAINを満たしている事が前提なので処女の場合は無視して良い
SIF CANTRAIN(ARGS, ARG) == 0
	RETURNF 0

;性別条件などがあるなら見る。性欲解消は該当箇所の感覚３ＬＶが目安
SELECTCASE ARGS
CASE "舐陰"
	SIF TALENT:ARG:男性
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ｃ感覚 < 3 || TALENT:ARG:Ｃ性向 + TALENT:ARG:淫核 + COND("陰核肥大", ARG) == 0
			RETURNF 0
	ENDIF
CASE "手淫", "乳交"
	ACTOR_USEP = PLAYER
CASE "口交"
	ACTOR_USEP = PLAYER
	IF IS_REV
		SIF ABL:ARG:精液中毒 < 3 || COND("精飲弱点", ARG) + TALENT:ARG:气味弱点 == 0
			RETURNF 0
	ENDIF
CASE "正常位", "後背位", "屈曲位", "対面座位", "背面座位", "騎乗位"
	ACTOR_USEP = PLAYER
	SIF TALENT:ARG:男性
		RETURNF 0
	SIF ABL:ARG:Ｖ拡張 < SIZE("阴茎", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ｖ感覚 < 3 || COND("Ｖ熟達", ARG) == 0
			RETURNF 0
	ENDIF
CASE "肛門正常位", "肛門後背位", "肛門屈曲位", "肛門対面座位", "肛門背面座位", "肛門騎乗位"
	ACTOR_USEP = PLAYER
	SIF ABL:ARG:Ａ拡張 < SIZE("阴茎", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ａ感覚 < 3 || COND("Ａ熟達", ARG) == 0 || TALENT:ARG:Ａ性向 + TALENT:ARG:尻穴狂い + TALENT:ARG:男性 == 0
			RETURNF 0
	ENDIF
CASE "尿道正常位", "尿道後背位", "尿道屈曲位", "尿道対面座位", "尿道背面座位", "尿道騎乗位"
	ACTOR_USEP = PLAYER
	SIF ABL:ARG:尿道拡張 < SIZE("阴茎", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:尿道感覚 < 3 || COND("尿道熟達", ARG) == 0 || TALENT:ARG:尿道性感 + TALENT:ARG:尿道狂 == 0
			RETURNF 0
	ENDIF
CASE "乳頭奸"
	ACTOR_USEP = PLAYER
	SIF COND("Ｂ熟達", ARG) == 0
		RETURNF 0
CASE "奉仕舐陰"
	SIF TALENT:PLAYER:男性
		RETURNF 0
CASE "舐陰莖"
	ACTOR_USEP = ARG
	IF IS_REV
		SIF ABL:ARG:Ｃ感覚 < 3 || TALENT:ARG:Ｃ性向 + TALENT:ARG:淫核 + TALENT:ARG:男性 == 0
			RETURNF 0
	ENDIF
CASE "正常位奉仕", "後背位奉仕", "騎乗位侵犯"
	ACTOR_USEP = ARG
	SIF TALENT:PLAYER:男性
		RETURNF 0
	IF IS_REV
		SIF TALENT:ARG:Ｃ性向 + TALENT:ARG:男性 == 0
			RETURNF 0
	ELSE
		SIF COND("Ｖ熟達", PLAYER) == 0
			RETURNF 0
	ENDIF
CASE "Ａ正常位奉仕", "Ａ後背位奉仕", "Ａ騎乗位侵犯"
	ACTOR_USEP = ARG
	IF IS_REV
		SIF TALENT:ARG:Ｃ性向 + TALENT:ARG:男性 == 0
			RETURNF 0
	ELSE
		SIF COND("Ａ熟達", PLAYER) == 0
			RETURNF 0
	ENDIF

;それ以外は性処理指令ではないのでダメ
CASEELSE
	RETURNF 0
ENDSELECT

;要竿役なら条件を満たすか格子
IF ACTOR_USEP
	;SIF PENIS(ACTOR_USEP) || (TALENT:ACTOR_USEP:具現 && TALENT:ACTOR_USEP:兩面通吃)
	SIF PENIS(ACTOR_USEP)
		RETURNF 1
	RETURNF 0
ENDIF

RETURNF 1

