;-------------------------------------------------
;ABL:露出癖 のLVUP処理
;-------------------------------------------------
@ABLUP4
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP4
;何もできない場合
IF RESULT == 0 && ABL:露出癖 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
SETCOLOR DEF_COLOR("黄色")
;羞恥快楽経験と露出経験による軽減
SELECTCASE EXP:露出経験 + EXP:羞恥快楽経験
CASE IS >= EXP_LV(ABL:露出癖 + 4)
	PRINTFORML ※與之關聯的經驗非常的多、要求的寶珠減少80％※
CASE IS >= EXP_LV(ABL:露出癖 + 3)
	PRINTFORML ※與之關聯經驗很多、要求的寶珠減少50％※
CASE IS >= EXP_LV(ABL:露出癖 + 2)
	PRINTFORML ※與之關聯經驗較多、要求的寶珠減少20％※
;著しく足りていない
CASE IS < EXP_LV(ABL:露出癖 + 1)
	PRINTFORML ※與之關聯經驗太少、要求的寶珠增加3倍※
ENDSELECT
RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:露出癖 += 1
		PRINTFORMW 露出癖等級提升到LV{ABL:露出癖}了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP4
SIF RESULT
	RESTART



;ABL:露出癖のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP4, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:露出癖
CASE 0
	JUEL:0:恥情 = 1000
CASE 1 TO 4
	JUEL:0:恥情 = 1000 + ABL:露出癖 * 1500
CASE 5 TO 9
	JUEL:0:恥情 = 7000 + (ABL:露出癖 - 4) * 4000
CASEELSE
	JUEL:0:恥情 = 30000 + (ABL:露出癖 - 10) * 20000
ENDSELECT

SIF TALENT:清楚
	TIMES JUEL:0:恥情, 1.50
SIF TALENT:倒錯的
	TIMES JUEL:0:恥情, 0.75
SIF TALENT:喜歡引人注目
	TIMES JUEL:0:恥情, 0.50

;羞恥快楽経験と露出経験による軽減
SELECTCASE EXP:露出経験 + EXP:羞恥快楽経験
CASE IS >= EXP_LV(ABL:露出癖 + 4)
	TIMES JUEL:0:恥情, 0.20
CASE IS >= EXP_LV(ABL:露出癖 + 3)
	TIMES JUEL:0:恥情, 0.50
CASE IS >= EXP_LV(ABL:露出癖 + 2)
	TIMES JUEL:0:恥情, 0.80
;著しく足りていない
CASE IS < EXP_LV(ABL:露出癖 + 1)
	TIMES JUEL:0:恥情, 3.00
ENDSELECT

;最大LV未満か？
SIF ABL:露出癖 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:恥情 && JUEL:恥情 >= JUEL:0:恥情
	RETURN 1
RETURN 0

