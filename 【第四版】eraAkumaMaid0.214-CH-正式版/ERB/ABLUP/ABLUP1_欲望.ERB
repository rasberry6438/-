;-------------------------------------------------
;ABL:欲望 のLVUP処理
;-------------------------------------------------
@ABLUP1
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10
#DIM ADDCOND_RESULT

;必要な珠などの値を取得
CALL IS_ABLUP1
;何もできない場合
IF RESULT == 0 && ABL:欲望 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR
VARSET NUM_JUEL
NUM_CHOICES = 0
IF ABL:欲望 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;淫乱に目覚める
IF TALENT:淫乱 == 0 && COND("恋慕と淫乱の自動獲得可能") == 0

	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("淫乱")
	ADDCOND_RESULT = RESULT

	;ダメだった場合
	IF STR:実行条件 != ""
		CALL PRINT_STRL, @"灰色_ [×] 淫乱に目覚める(%STR:実行条件%)"
	ELSEIF COND("淫乱取得可能") || ADDCOND_RESULT
		PRINTFORML  [10] 覺醒[淫乱]
		SET_CHOICE = 10
	ELSE
		CALL PRINT_STRL, "灰色_ [×] 覺醒[淫乱](要:欲望5LV、自慰中毒或精液中毒的其中一个达到LV３、今回の絶頂数10)"
	ENDIF
ENDIF
PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 10
	CALL SETFLAG, "淫乱"
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:欲望 += 1
		PRINTFORMW 欲望变成了LV{ABL:欲望}。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP1
SIF RESULT
	RESTART



;ABL:欲望のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
;なお淫乱の獲得もここに含める
@IS_ABLUP1, ARGS
#DIM ADDCOND_RESULT
CALL VARSET_ABLUP
SELECTCASE ABL:欲望
CASE 0
	JUEL:0:欲情 = 100
CASE 1 TO 4
	JUEL:0:欲情 = ABL:欲望 * 1000
CASE 5 TO 9
	JUEL:0:欲情 = 4000 + (ABL:欲望 - 4) * 5000
CASEELSE
	JUEL:0:欲情 = 30000 + (ABL:欲望 - 10) * 20000
ENDSELECT

;淫乱が獲得できる？
IF TALENT:淫乱 == 0 && COND("恋慕と淫乱の自動獲得可能") == 0 && ARGS != "ＬＶ限定"
	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("淫乱")
	ADDCOND_RESULT = RESULT
	SIF STR:実行条件 == "" && (COND("淫乱取得可能") || ADDCOND_RESULT)
		RETURN 1
ENDIF
;最大LV未満か？
SIF ABL:欲望 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:欲情 && JUEL:欲情 >= JUEL:0:欲情
	RETURN 1
RETURN 0
