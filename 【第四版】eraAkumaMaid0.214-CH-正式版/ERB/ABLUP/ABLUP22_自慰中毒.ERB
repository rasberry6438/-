;-------------------------------------------------
;ABL:自慰中毒 のLVUP処理
;-------------------------------------------------
@ABLUP22
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP22
;何もできない場合
IF RESULT == 0 && ABL:自慰中毒 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
SETCOLOR DEF_COLOR("黄色")
;自慰経験による軽減
SELECTCASE EXP:自慰経験
CASE IS >= EXP_LV(ABL:自慰中毒 + 5)
	PRINTFORML ※自慰經驗非常的多、要求的寶珠減少80％※
CASE IS >= EXP_LV(ABL:自慰中毒 + 4)
	PRINTFORML ※自慰經驗很多、要求的寶珠減少50％※
CASE IS >= EXP_LV(ABL:自慰中毒 + 3)
	PRINTFORML ※自慰經驗較多、要求的寶珠減少20％※
;ある程度の経験が必要
CASE IS < EXP_LV(ABL:自慰中毒 + 1)
	PRINTFORML ※自慰經驗太少、自慰中毒的LV是無法提升的※
	SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  [×] 自慰経験 {EXP:自慰経験, 8}/{EXP_LV(ABL:自慰中毒 + 1)}
;著しく足りていない
CASE IS < EXP_LV(ABL:自慰中毒 + 2)
	PRINTFORML ※自慰經驗太少、要求的寶珠增加3倍※
ENDSELECT
RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

;特殊な選択肢
SET_CHOICE = 0

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:自慰中毒 += 1
		PRINTFORMW 自慰中毒等級提升到{ABL:自慰中毒}LV了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP22
SIF RESULT
	RESTART



;ABL:自慰中毒のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP22, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:自慰中毒
CASE 0
	JUEL:0:欲情 = 2000
CASE 1
	JUEL:0:欲情 = 5000
CASE 2
	JUEL:0:欲情 = 10000
CASE 3
	JUEL:0:欲情 = 20000
CASE 4
	JUEL:0:欲情 = 30000
CASEELSE
	JUEL:0:欲情 = 40000 + (ABL:自慰中毒 - 5) * 30000
ENDSELECT

SIF TALENT:真面目 || TALENT:自制心
	TIMES JUEL:0:欲情, 2.00
SIF TALENT:容易中毒
	TIMES JUEL:0:欲情, 0.50

;自慰経験による軽減
SELECTCASE EXP:自慰経験
CASE IS >= EXP_LV(ABL:自慰中毒 + 5)
	TIMES JUEL:0:欲情, 0.20
CASE IS >= EXP_LV(ABL:自慰中毒 + 4)
	TIMES JUEL:0:欲情, 0.50
CASE IS >= EXP_LV(ABL:自慰中毒 + 3)
	TIMES JUEL:0:欲情, 0.80
;ある程度の自慰経験が必要
CASE IS < EXP_LV(ABL:自慰中毒 + 1)
	JUEL:0:欲情 = 0
;著しく足りていない
CASE IS < EXP_LV(ABL:自慰中毒 + 2)
	TIMES JUEL:0:欲情, 3.00
ENDSELECT

JUEL:0:恥情 = JUEL:0:欲情 * 3 / 2

;最大LV未満か？
SIF ABL:自慰中毒 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:恥情 && JUEL:恥情 >= JUEL:0:恥情
	RETURN 1
SIF JUEL:0:欲情 && JUEL:欲情 >= JUEL:0:欲情
	RETURN 1
RETURN 0

