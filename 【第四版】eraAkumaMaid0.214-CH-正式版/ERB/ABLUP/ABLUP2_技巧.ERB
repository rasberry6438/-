;-------------------------------------------------
;ABL:技巧 のLVUP処理
;-------------------------------------------------
@ABLUP2
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP2
;何もできない場合
IF RESULT == 0 && ABL:技巧 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
SETCOLOR DEF_COLOR("黄色")
;口交経験～舐陰経験の合計によるEXPLVによる軽減
SELECTCASE GET_EXP_LV(EXP:接吻経験 + (EXP:口交経験+EXP:手淫経験+EXP:足交経験+EXP:腰靈活経験+EXP:乳交経験+EXP:玩弄Ａ経験+EXP:舐陰経験)*5) - ABL:技巧*2
CASE IS >= 12
	PRINTFORML ※與技巧關聯的經驗的合計總數顯著的多、必要的寶珠減少80％※
CASE IS >= 9
	PRINTFORML ※與技巧關聯的經驗的合計總數非常的多、必要的寶珠減少60％※
CASE IS >= 6
	PRINTFORML ※與技巧關聯的經驗的合計總數很多、必要的寶珠減少40％※
CASE IS >= 3
	PRINTFORML ※與技巧關聯的經驗的合計總數較多、必要的寶珠減少20％※
ENDSELECT
RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:技巧 += 1
		PRINTFORMW 技巧等級提升到LV{ABL:技巧}了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP2
SIF RESULT
	RESTART



;ABL:技巧のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP2, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:技巧
CASE 0
	JUEL:0:習得 = 100
CASE 1
	JUEL:0:習得 = 1000
CASE 2 TO 9
	JUEL:0:習得 = 1000 + (ABL:技巧 - 1)* 3000
CASEELSE
	JUEL:0:習得 = 30000 + (ABL:技巧 - 10)* 20000
ENDSELECT

;口交経験～舐陰経験の合計によるEXPLVによる軽減
SELECTCASE GET_EXP_LV(EXP:接吻経験 + (EXP:口交経験+EXP:手淫経験+EXP:足交経験+EXP:腰靈活経験+EXP:乳交経験+EXP:玩弄Ａ経験+EXP:舐陰経験)*5) - ABL:技巧*2
CASE IS >= 12
	TIMES JUEL:0:習得, 0.20
CASE IS >= 9
	TIMES JUEL:0:習得, 0.40
CASE IS >= 6
	TIMES JUEL:0:習得, 0.60
CASE IS >= 3
	TIMES JUEL:0:習得, 0.80
ENDSELECT

;最大LV未満か？
SIF ABL:技巧 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:習得 && JUEL:習得 >= JUEL:0:習得
	RETURN 1
RETURN 0

