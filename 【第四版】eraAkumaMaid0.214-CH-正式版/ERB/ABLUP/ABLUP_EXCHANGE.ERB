;ARGは１度に交換する単位。初期は１万
@JUEL_TO_EXP, ARG
#DIM LCOUNT
#DIM MEMO_EXP
#DIM MEMO_JUEL, 16

;まずは経験値と各種珠の量を記録
MEMO_EXP = EXP:経験値
FOR LCOUNT, 0, 16
	MEMO_JUEL:LCOUNT = JUEL:LCOUNT
NEXT

CALL JUEL_TO_EXP_CALC, ARG, JUEL:0, JUEL:1, JUEL:2, JUEL:3, JUEL:4, JUEL:5, JUEL:6, JUEL:7, JUEL:8, JUEL:9, JUEL:10, JUEL:11, JUEL:12, JUEL:13, JUEL:14, JUEL:15

;やり直す？
IF RESULT == 99
	EXP:経験値 = MEMO_EXP
	FOR LCOUNT, 0, 16
		JUEL:LCOUNT = MEMO_JUEL:LCOUNT
	NEXT
	RESTART
ENDIF

;交換単位は最小１万。ARG < 0ならば全部交換
@JUEL_TO_EXP_CALC, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11, ARG:12, ARG:13, ARG:14, ARG:15, ARG:16
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

PRINTFORML 
PRINTFORML 在这里用各種宝珠１万個可以交換経験値10Ｐ
PRINTFORML 因为并不是很划算的比例、所以请只在宝珠有剩余的情况下使用
PRINTFORML 
PRINTFORM 交換後的経験値是{EXP:経験値}Ｐ、
IF ARG < 0
	PRINTFORML 選択的宝珠会全部交換。
	PRINTFORML 要交換哪一个呢？
ELSE
	PRINTFORML 単位是{ARG/10000}万。要交換哪一个呢？
	PRINTFORML （交換単位不足的以１万为基础单位进行交換）
ENDIF

FOR LCOUNT, 0, 16
	SIF PALAMNAME:LCOUNT == "" || PALAMNAME:LCOUNT == "反感"
		CONTINUE

	SELECTCASE ARG:(LCOUNT+1)
	CASE IS >= 10000
		PRINTFORM  [{LCOUNT, 2}] 
	CASEELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORM  [×] 
	ENDSELECT

	PRINTFORM %PALAMNAME:LCOUNT%の珠:{ARG:(LCOUNT+1), 8}
	SIF JUEL:LCOUNT != ARG:(LCOUNT+1)
		PRINTFORM  → {JUEL:LCOUNT, 8} {(ARG:(LCOUNT+1) - JUEL:LCOUNT)/1000, 5}Ｐ get
	PRINTL 
	RESETCOLOR
NEXT

SELECTCASE ARG
CASE 10000 TO 1000000
	PRINTFORML  [90] 交換単位增加{ARG/1000}万
CASE 10000000
	PRINTFORML  [90] 全部宝珠都交換成経験値
CASEELSE
	PRINTFORML  [90] 交換単位回到１万
ENDSELECT

PRINTFORML  [99] 重置交換状況
PRINTFORML [100] 終了

CALL INPUT_SELECT, 16, 90, 99, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	REDRAW 1
	RETURN 1
CASE 99
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	REDRAW 1
	RETURN 99
CASE 90
	SELECTCASE ARG
	CASE 10000 TO 1000000
		ARG *= 10
	CASE 10000000
		ARG = -1
	CASEELSE
		ARG = 10000
	ENDSELECT
CASEELSE
	IF PALAMNAME:CHOICE != "" && PALAMNAME:CHOICE != "反感"
		IF JUEL:CHOICE < 10000

		ELSEIF ARG > 0 && JUEL:CHOICE >= ARG
			JUEL:CHOICE -= ARG
			EXP:経験値 += 10 * ARG / 10000
		ELSE
			LOCAL = JUEL:CHOICE / 10000

			JUEL:CHOICE -= 10000 * LOCAL
			EXP:経験値 += 10 * LOCAL
		ENDIF
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART
