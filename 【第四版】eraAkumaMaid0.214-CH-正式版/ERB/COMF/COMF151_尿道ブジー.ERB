@COM151
;尿道探针
;何度も実行したくないので、消耗は激しいが効果は高い指令にしたい
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 151

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

RETURN 1

@FLAG_COM151, ARG
CALL SETFLAG, "尿道性感フラグ", TARGET
;尿的污垢発生
IF PENIS(TARGET)
	STAIN:Ｐ = SET_STAIN(STAIN:Ｐ, "尿")
ELSE
	STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
ENDIF
;胖次をずらす
SIF !CHECK_CLO("Ｖ露出") && INSERT("挿入") == 0
	CALL SETFLAG, "下身衣装を露出：テ接吻ト没有", TARGET


@SOURCE_COM151, ARG
;くぱぁ
CALL SOURCE_COM13, 50
CALL SOURCE_INSERTION, "尿道探针", ARG, 100


;尿道探针の太さを選択する
@SELECT_BOUGIE
#DIM LCOUNT
#DIM SELECT_BOUGIE
#DIM BOUGIE, 10
#DIM CHOICE

;どっちの尿道か
LOCALS = %CONDS("尿道拡張度")%

;今までに経験した太さが、疼痛のある太さかどうか。最小*1.05…{MIN_STR(LOCALS)*21/20}mmまでなら疼痛は少ない
;例えば、LOCALS = 10/10/10/9/9/9/9/8/8/8 ならMAX_STR(LOCALS) = 10, CALCF("尿道径") = 9, MIN_STR(LOCALS) = 8 となる
;ちなみにMAX_STR(LOCALS) = MIN_STR(LOCALS) + 1
;初回
IF COND("尿道履歴") == 0
	PRINTFORML %CALLNAME:TARGET%的尿道、完全沒有接受過任何異物的侵入。
	PRINTFORML 今回{MIN_STR(LOCALS)}mm、甚至更粗的{MIN_STR(LOCALS) + 1}mm尝试一下似乎也是可以的
	PRINTFORML ……要使用什麼粗細的尿道探針？
;ブジーよりも粗大的ものを入れられたことが有そう
ELSEIF MAX_STR(LOCALS) > COND("尿道履歴")
	PRINTFORML %CALLNAME:TARGET%曾經使用過的尿道探針中最粗的記錄應該是{COND("尿道履歴")}mm……但是、要想使用比那一次更粗的東西的話似乎也不是不能。
	PRINTFORML ……要使用什麼粗細的尿道探針？
ELSE
	SELECTCASE MAX_STR(LOCALS) - MIN_STR(LOCALS)*21/20
	CASE IS >= 1
		PRINTFORML %CALLNAME:TARGET%的尿道对于直径{MAX(MAX_STR(LOCALS), COND("尿道履歴"))}mm的已经経験済了、对于{MIN_STR(LOCALS)*21/20}mm的疼痛应该会较少吧
		PRINTFORML {MAX(MIN_STR(LOCALS)*21/20, MIN_STR(LOCALS) + 1)}mm以下的粗细还是比较好的……\@ PREVCOM == SELECTCOM ? 接下来 # 这次 \@、要使用什麼粗細的尿道探針？
	CASE 0
		PRINTFORML %CALLNAME:TARGET%的尿道对于直径{MAX(MAX_STR(LOCALS), COND("尿道履歴"))}mm的已经経験済了、按照目前来看似乎不会有太大疼痛的样子
		PRINTFORML {MAX(MAX_STR(LOCALS), COND("尿道履歴"))}mm以下的粗细还是比较好的……\@ PREVCOM == SELECTCOM ? 接下来 # 这次 \@、要使用什麼粗細的尿道探針？
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%的尿道对于直径{MAX(MAX_STR(LOCALS), COND("尿道履歴"))}mm的已经経験済了、这个粗细是没问题的
		PRINTFORML 看来挑戦{MAX_STR(LOCALS) + 1}mm也不会有什么問題……\@ PREVCOM == SELECTCOM ? 接下来 # 这次 \@、要使用什麼粗細的尿道探針？
	ENDSELECT
ENDIF

;連続実行だと２倍行う
IF PREVCOM == SELECTCOM
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORML ※因為是對同一指令的連續實行、將對%CALLNAME:TARGET%的尿道進行更加仔細地蹂躪※
	RESETCOLOR
;肉体的な限界がある
ELSEIF CALCF("尿道径") >= CALCF("尿道拡張の壁")
	SETCOLOR DEF_COLOR("黄色")
	IF CALCF("尿道径") >= CALCF("尿道拡張の限界")
		PRINTFORML ※%CALLNAME:TARGET%的尿道已经到极限了……在這之上的擴張已經是不可能的強人所難了※
	ELSE
		PRINTFORML ※%CALLNAME:TARGET%的尿道已經非常接近極限了……在這之上的擴張應該是非常困難的了※
	ENDIF

	SETCOLOR DEF_COLOR("爱心粉紅")
	;ちょっとお得な情報
	CALL COM_ABLE146
	IF RESULT && COND("身嗜み：尿道拡張") == 0 && ABL:尿道拡張 >= 0 && EXP:尿道経験 >= 5 && (EXP:尿道経験 >= 20 || EXP:尿道拡張経験 >= 5 || ABL:尿道拡張 >= 1)
		PRINTFORML 介於之前的身嗜管理中下達的指示、尿道的擴張已經能由%CALLNAME:TARGET%自己進行了。
		PRINTFORML 儘管很難與%CALLNAME:PLAYER%毫不留情的調教手段相比、如何呢？
	ENDIF
	RESETCOLOR
ENDIF
PRINTL 

;MIN_STR(LOCALS)mmの太さは拡張という点では弱いが、尿道経験は増えるので一応意味はある
SELECT_BOUGIE = MAX(MAX_STR(LOCALS) - MIN_STR(LOCALS), 3) + MIN_STR(LOCALS)/20
SIF ABL:PLAYER:施虐属性 >= 5 || ABL:受虐属性 >= 5
	SELECT_BOUGIE += 1
SIF ABL:PLAYER:施虐属性 >= 3
	SELECT_BOUGIE += 1
;調教知識によるBOUNS
SIF COND("性器拡張知識", PLAYER)
	SELECT_BOUGIE += 1
;太さの1.2倍までとする…最大値があまりに低い場合は３保障
SELECT_BOUGIE = MAX(LIMIT(SELECT_BOUGIE, 3, MIN_STR(LOCALS)/5 + 1), 3, MIN_STR(LOCALS)/10 + 1)

VARSET LOCAL
VARSET BOUGIE

FOR LCOUNT, 0, SELECT_BOUGIE
	;選択肢が多すぎる場合には削る
	SELECTCASE MIN_STR(LOCALS) + LCOUNT
	CASE IS >= 40
		SIF SELECT_BOUGIE >= 10 && (MIN_STR(LOCALS) + LCOUNT)%5
			CONTINUE
	ENDSELECT

	BOUGIE:LOCAL = MIN_STR(LOCALS) + LCOUNT
	PRINTFORML  [{LOCAL, 2}] {BOUGIE:LOCAL, 2}mm

	LOCAL += 1
NEXT

PRINTFORML  [99] 查看補充說明
PRINTFORML [100] 不、再考慮一下吧。

CALL INPUT_SELECT, LOCAL, 99, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	TFLAG:指令中止 = 1
	RETURN 0
CASE 99
	PRINTFORML □ 補足 □---------------------------------------------------------------------
	PRINTFORM 在對尿道進行擴張的時候、如果具有
	CALL PRINT_COLORTEXT, "[性器拡張知識]", DEF_COLOR("黄色")
	PRINTFORM 或
	CALL PRINT_COLORTEXT, "[拡張適性]", DEF_COLOR("黄色")
	PRINTFORML 的话会更容易出現進展。
	PRINTFORM 尽管并不是对拡張的容易度有影響的素質、
	CALL PRINT_COLORTEXT, "[倒錯的]", DEF_COLOR("黄色")
	PRINTFORML 有这些的话就可以抑制反感
	PRINTFORML 

	PRINTFORML 尿道產生的快感與尿道感覺是相互依存的……比如、在初期疼痛感很強的時候幾乎不可能獲得快感。
	PRINTFORML 随着経験的累积、痛苦会減少、习惯被玩弄尿道之后就没问题了
	PRINTFORM 积累了因为玩弄尿道而絶頂的経験之后、
	CALL PRINT_COLORTEXT, "[尿道性感]", DEF_COLOR("黄色")
	PRINTFORML 就可以觉醒出来了
	CALL PRINT_COLORTEXT, "[尿道性感]", DEF_COLOR("黄色")
	PRINTFORMW 没有的话就基本上不怎么能变舒服、所以请尽早取得
	PRINTFORML -------------------------------------------------------------------------------
CASEELSE
	IF BOUGIE:CHOICE
		TEQUIP:尿道探针 = BOUGIE:CHOICE
		;今までに使われた最大の尺寸を記録
		IF TEQUIP:尿道探针 > COND("尿道履歴")
			IF PENIS(TARGET)
				CFLAG:Ｐ尿道履歴 = TEQUIP:尿道探针
			ELSE
				CFLAG:Ｖ尿道履歴 = TEQUIP:尿道探针
			ENDIF
		ENDIF
		RETURN 1
	ENDIF
ENDSELECT

PRINTL 
RESTART


@TRAIN_MESSAGE_COM151
;ブジーを見た際の反応です
CALL KOJO_EVENT, "尿道探针追加口上"

;ブジーの太さ選択
CALL SELECT_BOUGIE

SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"

;地の文を飛ばすフラグを立てている(ターン開始時にOFFになる)
SIF TFLAG:地の文スキップ
	RETURN 0

;どっちの尿道か
LOCALS = %CONDS("尿道拡張度")%

PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的
IF GETBIT(TEQUIP:筋弛緩剤, 1) 
	PRINTFORM 因為藥物而鬆弛下來的尿道
ELSE
	PRINTFORM \@ PENIS(TARGET) ? 陰莖鈴口 # 尿道 \@用
ENDIF
SELECTCASE TEQUIP:尿道探针
CASE IS < 15
	PRINTFORMW 用塗上了果凍的{TEQUIP:尿道探针}mm探針挿入了。
;指並み
CASE 15 TO 25
	PRINTFORMW 用手指粗细的{TEQUIP:尿道探针}mm探針挿入了。
;短小阴茎並み
CASE 26 TO 32
	PRINTFORMW 用已经可以说是较細的阳具直径的{TEQUIP:尿道探针}mm探針挿入了。
;ふつうのちんこ並み
CASE 33 TO 39
	PRINTFORMW 用一般情况下根本不可能进入的{TEQUIP:尿道探针}mm探針挿入了。
;巨根並み～
CASEELSE
	PRINTFORMW 直径{TEQUIP:尿道探针}mm的光滑金属製阳具挿入了。
ENDSELECT

;EXP:尿道経験、TALENT:尿道性感、TALENT:倒錯的、TEQUIP:尿道探针 > CALCF("尿道径") あたりで分岐か
;拡張する段階ごとに中間報告（太さを飛ばしてると出ないが些細な事だろう）
IF TEQUIP:尿道探针 > MAX_STR(LOCALS) && CALCF("尿道拡張の限界") >= TEQUIP:尿道探针 + 2
	SELECTCASE MIN_STR(LOCALS)
	CASE 13
		IF CHOSECOM(SELECTCOM) == 0
			PRINTFORML ……%CALLNAME:TARGET%的尿道、被粗大的玩具擴張了。
			PRINTFORMW 在尿道回縮之前、手指粗細的東西也許也能接受了吧……
			RETURN 1
		ENDIF
	CASE 18
		IF CHOSECOM(SELECTCOM) == 0
			PRINTFORML ……%CALLNAME:TARGET%的尿道、似乎已經被擴張的相當鬆弛了。
			PRINTFORMW 或許、在努力一下就連普通粗細的男性器也能接受了吧……
			RETURN 1
		ENDIF
	CASE 25
		IF CHOSECOM(SELECTCOM) == 0
			PRINTFORML ……%CALLNAME:TARGET%的尿道、似乎已經被擴張的相當鬆弛了。
			PRINTFORMW 或許、在努力一下就連普通粗細的男性器也能接受了吧……
			RETURN 1
		ENDIF
	CASE 32
		IF CHOSECOM(SELECTCOM) == 0
			PRINTFORML ……%CALLNAME:TARGET%的尿道、已經完全化作另一個大張著的肉穴。
			PRINTFORMW 現在、恐怕就連性行為也能沒問題的直接進行吧……
			RETURN 1
		ENDIF
	ENDSELECT
ENDIF

;初めてのブジー受け入れ
IF FIRSTCOM(SELECTCOM) == 0 && EXP:尿道拡張経験 == 0
	;初回から拡張
	IF TEQUIP:尿道探针 > MIN_STR(LOCALS)
		PRINTFORMW 与洞口相比棒子\@ TEQUIP:尿道探针 - MIN_STR(LOCALS) >= 2 ? 過于 # 有点 \@粗了、光滑的金属棒不理会尿道括约肌的抵抗、挖掘着排尿孔强行让其适应这个粗度…
	ELSEIF PENIS(TARGET)
		PRINTFORMW 儘管對於第一次被插入錯誤的地方、但是%CALLNAME:TARGET%尿道還是將侵入的金屬小棒一直接受到抵達陰莖的根部為止…
	ELSE
		PRINTFORMW 儘管對於第一次被插入錯誤的地方、但是%CALLNAME:TARGET%尿道還是將侵入的金屬小棒一直接受到抵達膀胱為止…
	ENDIF
;各調教ごとの二回目以降のセリフ（簡潔）
ELSEIF CHOSECOM(SELECTCOM)
	IF CHECK("尿道PLAY")
		IF TALENT:尿道性感
			PRINTFORM %CALLNAME:TARGET%
			IF RAND:2
				PRINTFORM 將深深插入體內的金屬棒用\@ PENIS(TARGET) ? 陰莖的根部 # 膀胱 \@輕鬆地接受了、
			ELSE
				PRINTFORM 那原本只有液體通過的小孔\@ TALENT:尿道性感 ? 歡喜地 # \@將異物吞入體內、
			ENDIF
			IF TALENT:尿道性感 && RAND:3 == 0
				PRINTFORMW 在臉頰因快樂與羞恥而佈滿紅潮的同時、盡情享受著尿液隨著金屬棒的插拔飛濺而出的下流PLAY……
			ELSEIF RAND:2
				PRINTFORMW 在金屬棒抽插的同時配合的將括約肌繃緊又或放鬆……
			ELSE
				PRINTFORMW 在敏感粘膜被金屬小棒刮擦的時候、表現出令人興奮起來的淫亂反應……
			ENDIF
		ELSEIF PENIS(TARGET)
			PRINTFORMW 完全沒有受到任何阻礙的強硬闖入最深處的金屬小棒、在激烈地進出之時也將陰莖的海綿體不斷推開、讓%CALLNAME:TARGET%將尿道被異物侵入的感覺與由此而生的奇妙快感深深銘記下來……
		ELSE
			PRINTFORMW 就像滑动一様毫无阻碍地直达膀胱的金属小棒、在用進出時漏出的體液將地面的積水擴大的同時、讓%CALLNAME:TARGET%將尿道被異物侵入的感覺與由此而生的奇妙快感深深銘記下來……
		ENDIF
	ELSE
		PRINTFORMW 強烈地抵抗著的小小孔洞、在金屬材質的擴張探針的作用下、緩慢而切實地向另一個性器推進著……
	ENDIF
;拡張
ELSEIF TEQUIP:尿道探针 > MIN_STR(LOCALS)
	;疼痛や抵抗が少ない
	IF CHECK("尿道PLAY")
		IF TALENT:尿道性感 && RAND:2
			PRINTFORMW 已經完全作為性器覺醒了的%CALLNAME:TARGET%的尿道、不但將侵入的異物毫不在意地\@ PENIS(TARGET) ? 將20cm的長度完全吞下 # 用膀胱輕易接受了 \@、而且還在抽出時戀戀不捨的緊緊吮吸著……
		ELSE
			PRINTFORMW 完全沒有任何反抗之意的接受了金屬棒的小孔、順從著毫無空隙的擴張活動而被反復拉扯著柔軟的肉襞……
		ENDIF
	ELSEIF EXP:尿道経験 >= 20 || EXP:尿道拡張経験 >= 20
		PRINTFORMW 早已在反復地開發下將被異物侵入的感覺習慣下來的%CALLNAME:TARGET%的尿道、在過分粗大的金屬棒所帶來的痛苦之下還是產生了強烈的反抗……
	ELSE
		PRINTFORMW 開發經驗尚且不足的、仍然只是排泄器官的尿道、在插入內部的異物所帶來的痛苦之下無法抑制地繃緊了括約肌……
	ENDIF
;拡張はしないで慣らすのみ
ELSE
	IF CHECK("尿道PLAY")
		IF TALENT:尿道性感
			PRINTFORMW %CALLNAME:TARGET%在尿道被探針肆意進出的同時、因下體傳來的快感而顫抖著腰肢、將這異常的快樂與遊戲盡情地享受著……
		ELSE
			PRINTFORMW 儘管%CALLNAME:TARGET%並沒有感到特別的疼痛、但依然挺直脊背、因難以言喻的奇妙觸感而浮現出不安的神色……
		ENDIF
	ELSEIF PENIS(TARGET)
		PRINTFORMW 在粗大而光滑的金屬棒將%CALLNAME:TARGET%的抵抗毫不在意地闖入尿道的最深處、在激烈地插拔著將被敏感粘膜包裹著的狹窄通道擴張開來的同時、也將這異常的感觸深深地銘刻下來……
	ELSE
		PRINTFORMW 在粗大而光滑的金屬棒將%CALLNAME:TARGET%的抵抗毫不在意地闖入膀胱、激烈地插拔著、將被敏感粘膜包裹著的狹窄通道隨著尿液的飛濺而出擴張開來……
	ENDIF
ENDIF


@COM_ORDER_COM151, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:受虐属性
IF ABL:受虐属性
	LOCAL += MIN(ABL:受虐属性, 5) * 3
	LOCALS = %LOCALS% + 受虐属性LV{ABL:受虐属性}({MIN(ABL:受虐属性, 5) * 3})
ENDIF
;冷静
IF TALENT:冷静
	LOCAL -= 5
	LOCALS = %LOCALS% - 冷静(5)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 15
	LOCALS = %LOCALS% - 害羞(15)
ENDIF
;不知羞恥
IF TALENT:不知羞恥
	LOCAL += 2
	LOCALS = %LOCALS% + 不知羞恥(2)
ENDIF
;清楚
IF TALENT:清楚
	LOCAL -= 10
	LOCALS = %LOCALS% - 清楚(10)
ENDIF
;接受快感
IF TALENT:接受快感
	LOCAL += 5
	LOCALS = %LOCALS% + 接受快感(5)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 5
	LOCALS = %LOCALS% - 否定快感(5)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 10
	LOCALS = %LOCALS% + 倒錯的(10)
ENDIF
IF TEQUIP:拘束
	LOCAL += 30
	LOCALS = %LOCALS% + 拘束中(30)
ENDIF
;媚薬
IF TEQUIP:媚薬
	LOCAL += 2
	LOCALS = %LOCALS% + 媚薬(2)
ENDIF
;尿道性感
IF TALENT:尿道性感
	LOCAL += 10
	LOCALS = %LOCALS% + 尿道性感(10)
ELSE
	LOCAL -= 30
	LOCALS = %LOCALS% - 尿道性感没有(30)
ENDIF
;视频拍摄
IF TEQUIP:攝像機
	LOCAL -= 20
	LOCALS = %LOCALS% - 撮影中(20)
ENDIF
;野外or室内露出
IF TEQUIP:野外PLAY
	LOCAL -= 10
	LOCALS = %LOCALS% - 野外PLAY(10)
ELSEIF TEQUIP:室内露出PLAY
	LOCAL -= 10
	LOCALS = %LOCALS% - 室内露出(10)
ENDIF

;合計を表示(80以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 80

RETURN RESULT
