@COM33
;肛用振動棒
;0bit細い, 1bit普通, 2bit粗大的, 3bit極太, 4bit腕粗, 5bit馬型, 
;10bit双頭領帯プ, 11bit布ル領帯プ
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:肛用振動棒 == 0
	STR:前回指令 = 肛門拔出振動棒
ELSE
	IF COND("肛門拉珠")
		STR:前回指令 = 肛門拉珠
	ELSE
		STR:前回指令 = 肛用振動棒
	ENDIF
ENDIF

RETURN 1

;ARGに入れられる角色、ARGSは補足。
;現状だとARGS == "強制挿入"で[100] やめる を選択不可にできる
@SELECT_VIB_A, ARG, ARGS
#DIM LCOUNT
#DIM SIZE_DILDO

VARSET LOCAL, 1
;逆Ａ振動棒
IF ARG == PLAYER
	;大号的振動棒
	SIF ABL:ARG:Ａ拡張 == 0 || (ABL:ARG:Ａ拡張 <= 1 && ABL:ARG:受虐属性 < 3)
		LOCAL:2 = 0
	;極太振動棒
	SIF ABL:ARG:Ａ拡張 <= 1 || (ABL:ARG:Ａ拡張 <= 2 && ABL:ARG:受虐属性 < 3)
		LOCAL:3 = 0
	;腕粗振動棒
	SIF ABL:ARG:Ａ拡張 <= 2 || (ABL:ARG:Ａ拡張 <= 3 && ABL:ARG:受虐属性 < 3)
		LOCAL:4 = 0
	;馬型振動棒
	SIF ABL:ARG:Ａ拡張 <= 3 || (ABL:ARG:Ａ拡張 <= 4 && ABL:ARG:受虐属性 < 3)
		LOCAL:5 = 0
	LOCAL:11 = 0
ELSE
	;どの振動棒を使えるかを判定（双方の施虐狂受虐狂具合で変化）
	FOR LCOUNT, 1, 6
		;振動棒のサイズを代入
		SIZE_DILDO = LCOUNT

		SELECTCASE SIZE_DILDO - ABL:Ａ拡張
		CASE IS >= 3
			LOCAL:LCOUNT = 0
		CASE 2
			;尺寸が２上回っているなら、施虐属性５ＬＶor（施虐属性３ＬＶに加えて相手の受虐属性３ＬＶ）が必要
			SIF MAX(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 5 || MIN(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 3
				CONTINUE
		CASE 1
			;尺寸が１上回っているなら、施虐属性３ＬＶor（施虐属性１ＬＶに加えて相手の受虐属性１ＬＶ）が必要
			SIF MAX(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 3 || MIN(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 1
				CONTINUE
		CASEELSE
			CONTINUE
		ENDSELECT

		LOCAL:LCOUNT = 0
	NEXT
ENDIF

;道具所持による判定
SIF ITEM:肛用振動棒 == 0
	LOCAL:0 = 0
IF ITEM:振動棒套装 == 0
	LOCAL:1 = 0
	LOCAL:2 = 0
	LOCAL:10 = 0
ENDIF
SIF ITEM:極太振動棒 == 0
	LOCAL:3 = 0
SIF ITEM:腕型振動棒 == 0
	LOCAL:4 = 0
SIF ITEM:馬型振動棒 == 0
	LOCAL:5 = 0
SIF ITEM:肛門拉珠 == 0
	LOCAL:11 = 0

PRINTFORML 這一次要\@ ARG == PLAYER ? 讓她 # \@使用哪種大小的振動棒呢……？

VARSET LOCALS
LOCALS:0 = [ 0] 肛用振動棒
LOCALS:1 = [\@ LOCAL:1 ? {1, 2} # × \@] 普通振動棒　　\@ ABL:ARG:Ａ拡張 <= 0 ? ※初心者厳禁！ # \@
LOCALS:2 = [\@ LOCAL:2 ? {2, 2} # × \@] 大号振動棒　　\@ ABL:ARG:Ａ拡張 <= 1 ? ※習慣者用！ # \@
LOCALS:3 = [\@ LOCAL:3 ? {3, 2} # × \@] 极粗振動棒　　\@ ABL:ARG:Ａ拡張 <= 2 ? ※上級者専用！ # \@
LOCALS:4 = [\@ LOCAL:4 ? {4, 2} # × \@] 拳型振動棒　　\@ ABL:ARG:Ａ拡張 <= 3 ? ※超上級者専用！ # \@
LOCALS:5 = [\@ LOCAL:5 ? {5, 2} # × \@] 馬型振動棒　　\@ ABL:ARG:Ａ拡張 <= 4 ? ※面向不願回頭的人 # \@

LOCALS:11 = [\@ LOCAL:11 ? 11 # × \@] 肛門拉珠

FOR LCOUNT, 0, 20
	SIF STRLENS(LOCALS:LCOUNT) == 0
		CONTINUE

	SIF LOCAL:LCOUNT == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  %LOCALS:LCOUNT%
	RESETCOLOR
NEXT

SIF ARGS != "強制挿入"
	PRINTFORML [100] 不、再考慮一下吧。

$INPUT_LOOP
INPUT

IF RESULT == 100 && ARGS != "強制挿入"
	TEQUIP:ARG:肛用振動棒 = 0
	TFLAG:指令中止 = 1
	RETURN 0
ELSEIF RESULT == 0
	TEQUIP:ARG:肛用振動棒 = 1p0
ELSEIF RESULT == 1 && LOCAL:1
	TEQUIP:ARG:肛用振動棒 = 1p1
ELSEIF RESULT == 2 && LOCAL:2
	TEQUIP:ARG:肛用振動棒 = 1p2
ELSEIF RESULT == 3 && LOCAL:3
	TEQUIP:ARG:肛用振動棒 = 1p3
ELSEIF RESULT == 4 && LOCAL:4
	TEQUIP:ARG:肛用振動棒 = 1p4
ELSEIF RESULT == 5 && LOCAL:5
	TEQUIP:ARG:肛用振動棒 = 1p5
ELSEIF RESULT == 11 && LOCAL:11
	TEQUIP:ARG:肛用振動棒 = 1p0 + 1p11
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1


@TRAIN_MESSAGE_COM33
;肛用振動棒の着脱
IF TEQUIP:肛用振動棒
	SIF TEQUIP:電話中
		PRINTFORM 和%TEXTS("彼氏")%%TEXTR("電話着的/電話通話中的/继续打電話的")%、

	IF COND("肛門拉珠")
		PRINTFORMW %CALLNAME:TARGET%\@ TEQUIP:推倒 ? 将尻穴里的肛門拉珠 # 尻穴内的肛門拉珠被 \@氣勢十足地抜了出来…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%\@ TEQUIP:推倒 ? 将 # \@屁穴里的\@ COND("長振動棒") ? 、穿過Ｓ状結腸的%TEXTR("轻松超越50cm的/有手臂那么长的")% # \@%NAMES("Ａ振動棒", TARGET)%\@ TEQUIP:推倒 ? # 被 \@抜了出来…
	ENDIF
	;道端の菠萝包加筆分
	SIF COND("長振動棒")
		PRINTFORM 長度超出尋常的
	PRINTFORM 振動棒\@ TEQUIP:推倒 ? 拔出之后的 # 被抜出后的 \@尻穴
	SELECTCASE ABL:Ａ拡張
	CASE IS >= 3
		IF ABL:Ａ拡張 - SIZE("Ａ振動棒", TARGET) >= 2
			PRINTFORM 好像很遗憾地反复抽搐収縮着的同时
		ELSE
			PRINTFORM 像大洞一样保持着打開的同时
		ENDIF
		IF EXP_GAIN("肛内精液経験")
			PRINTFORMW 混杂了腸液的精液垂流下来…
		ELSEIF TALENT:腸液分泌体質
			PRINTFORMW 汚液垂流下来…
		ELSE
			SIF CHECK("Ａ完美契合")
				PRINTFORM 变成了%CALLNAME:MASTER%的阴茎形状的
			PRINTFORMW 腸内被显示出来了…
		ENDIF
	CASEELSE
		IF EX:Ａ中出し量 >= 30
			PRINTFORM 灑落着精液
		ELSEIF TALENT:腸液分泌体質
			PRINTFORM 垂落着腸液
		ENDIF
		PRINTFORMW 痙攣地想变回原来的形状…
	ENDSELECT

	SIF COND("肛門拉珠")
		CALL SETFLAG, "肛門拉珠拔出FLAG", TARGET

	TFLAG:地の文分岐 = TEQUIP:肛用振動棒
	TEQUIP:肛用振動棒 = 0
	CALL CALC, "調教ソースの計算"
	RETURN 0
ENDIF

$SELECT_VIB
;振動棒選択
CALL SELECT_VIB_A, TARGET

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:肛用振動棒 == 0
	TFLAG:地の文分岐 = 1
	GOTO SELECT_VIB
ENDIF

IF ABL:Ａ拡張 >= 4 && CHECK("Ａ振動棒") && COND("肛門拉珠") == 0
	IF EXP:結腸経験 == 0
		PRINTFORML ……對於經驗豐富的%CALLNAME:TARGET%來說、說不定已經可以嘗試用格外粗長的振動棒直接插入Ｓ狀結腸了。
		PRINTFORML 要怎么做呢？
		CALL PRINT_SELECT, "就這樣吧/再想想吧"
	ELSEIF EXP_GAIN("結腸経験")
		PRINTFORML 對於%CALLNAME:TARGET%的Ｓ狀結腸……
		CALL PRINT_SELECT, "調教吧！/暫時停下吧"
	ELSE
		PRINTFORML 要用特別粗長的振動棒將%CALLNAME:TARGET%的Ｓ狀結腸好好開發一番嗎……？
		CALL PRINT_SELECT, "調教吧/暫時停下吧"
	ENDIF
	SIF RESULT == 0
		SETBIT TEQUIP:肛用振動棒, 20
ENDIF

;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"

;地の文を飛ばすフラグを立てている(ターン開始時にOFFになる)
SIF TFLAG:地の文スキップ
	RETURN 0

IF TEQUIP:電話中
	PRINTFORM %CALLNAME:PLAYER%、将和%TEXTS("彼氏")%%TEXTR("電話着的/電話通話中的/继续打電話的")%、%CALLNAME:TARGET%的
ELSE
	PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的
ENDIF

;肛門が露出していないで、胖次をずらしていないなら自動的にずらす
IF !CHECK_CLO("Ａ露出")
	CALL SETFLAG, "下身衣装を露出：テ接吻ト有些", TARGET
	PRINT 、
ENDIF
IF (ABL:Ａ拡張 >= 5 || (PREVCOM >= 70 && PREVCOM <= 79) || PREVCOM == 142 || PREVCOM == 143) && ABL:Ａ拡張 >= 2 && RAND:3 == 0
	PRINT 不檢點地大張著洞口的
ELSEIF ABL:Ａ拡張 >= 2
	PRINT 在充分地開發下十分柔軟的
ELSEIF TALENT:恋慕 || RAND:3 == 0
	PRINT 可愛的
ELSEIF EXP:Ａ経験 < 50 && RAND:2 == 0
	PRINT 經驗不足的
ELSE
	PRINT 緊張地收縮著的
ENDIF
PRINT 肛門
IF TALENT:恋慕
	PRINTFORM 溫柔地揉搓著放鬆下來之後\@ COND("下身衣装を露出させた") ? # 、 \@用
ELSE
	PRINTFORM に
ENDIF
IF COND("肛門拉珠")
	PRINTFORMW 一粒粒分開的肛門拉珠填滿了。
ELSE
	SELECTCASE SIZE("Ａ振動棒")
	CASE 5
		PRINTFORM 幾乎不可能進入的馬型
	CASE 4
		PRINTFORM 成年男性手腕粗細的
	CASE 3
		PRINTFORM 常規而言最為粗壯的\@ COND("長振動棒") ? 长款 # \@
	CASE 2
		PRINTFORM \@ COND("長振動棒") ? 粗長的 # 粗大的 \@
	CASE 1
		PRINTFORM \@ COND("長振動棒") ? 擁有可怕長度的 # \@
	CASE 0
		PRINTFORM \@ COND("長振動棒") ? 粗略估計也至少有50cm長的 # \@肛門
	ENDSELECT
	PRINT 振動棒
	;拡張不足
	IF ABL:Ａ拡張 < SIZE("Ａ振動棒")
		PRINTFORMW %TEXTR("用力地/強硬的/毫不留情地/在不斷開闢膣道的同時/在將肉體強硬推開的感觸仔細品味著的同時/將所有的抵抗毫不在意地無視的同時")%塞滿了。
	ELSE
		IF CHECK("Ａ振動棒") == 0 && ABL:PLAYER:施虐属性 >= 3 && RAND:2 == 0
			PRINT 用力地
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:3 == 0
			PRINT 深深地
		ELSEIF TALENT:恋慕 && RAND:3 == 0
			PRINT 温柔地
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:2 == 0
			PRINT 一口気
		ELSE
			PRINT 溫柔緩慢地
		ENDIF
		IF RAND:4 == 0
			PRINTW 填滿了內部。
		ELSEIF CHECK("Ａ振動棒") == 0 && ABL:PLAYER:施虐属性 >= 3 && RAND:2 == 0
			PRINTW 衝入其中。
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:3 == 0
			PRINTW 沉入其中。
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:2 == 0
			PRINTW 刺入其中。
		ELSE
			PRINTW 挿入其中。
		ENDIF
	ENDIF
ENDIF

;拡張十分
IF CHECK("Ａ振動棒") && CHOSECOM(SELECTCOM) == 0
	PRINTFORM %CALLNAME:TARGET%的
	IF TALENT:娇小 && EXP:Ａ経験 < 100 && ABL:Ａ拡張 <= 1
		PRINT 小小的
	ELSEIF TALENT:娇小 && ABL:Ａ拡張 >= 2
		SIF ABL:Ａ拡張 > SIZE("Ａ振動棒")
			PRINT 完全
		PRINT 被充分擴張的
	ELSEIF EXP:Ａ経験 >= 100 && RAND:2 == 0
		PRINT 経験豊富的
	ELSEIF ABL:Ａ拡張 > SIZE("Ａ振動棒")
		PRINT 十分柔軟的
	ENDIF
	PRINT 尻穴
	IF SIZE("Ａ振動棒") >= 2 && ABL:Ａ拡張 > SIZE("Ａ振動棒") && RAND:2
		IF RAND:3 == 0
			PRINT 輕而易舉地
		ELSEIF RAND:2
			PRINT 毫無抵抗地
		ELSE
			PRINT 就像是在主動吮吸一樣地
		ENDIF
	ELSEIF ABL:Ａ拡張 >= SIZE("Ａ振動棒") + 1 && RAND:2 == 0
		SIF TALENT:内気 || TALENT:清楚
			PRINT 與平日裡清純老實的印象完全不同、
		PRINT 輕易地張開、
	ELSEIF ABL:Ａ拡張 >= SIZE("Ａ振動棒")
		PRINT 向橡膠一樣伸展開來、
	ENDIF
	IF COND("肛門拉珠")
		PRINT 將肛門拉珠
	ELSE
		SELECTCASE SIZE("Ａ振動棒")
		CASE IS >= 3
			IF TALENT:男性 || TALENT:処女
				PRINT 將根本沒法用嘴含住的
			ELSEIF ABL:Ｖ拡張 < SIZE("Ａ振動棒")
				PRINT 將就連前面的膣穴也無法接納的
			ENDIF
			PRINT 兇惡的
		CASE 2
			PRINT 粗大的
		CASE 0
			PRINT 肛門
		ENDSELECT
		PRINT 振動棒
	ENDIF
	IF COND("長振動棒")
		IF TALENT:結腸性感
			PRINTFORMW 直到將直腸的最深處乃至於Ｓ狀結腸穿過、被更加深入的下行結腸吞下了\@ SIZE("Ａ振動棒") >= 3 ? 。 # …… \@
			SIF SIZE("Ａ振動棒") >= 3
				PRINTFORMW 將過於碩大的異物用大腸的深處直接地吞下的%CALLNAME:TARGET%那光滑的小腹、與結腸的形狀配合著劇烈的扭曲起來……
		ELSEIF EXP:結腸経験 == 0
			PRINTFORMW 直接抵住直腸的最深處、在無數次的挑戰後、被完全未知的Ｓ狀結腸成功的吞下了……
		ELSE
			PRINTFORMW 直到穿過直腸、將Ｓ狀結腸也同樣填滿了……
		ENDIF
	ELSE
		PRINTW 一直填滿了直腸的最深處……
	ENDIF
ELSEIF CHECK("Ａ振動棒")
	IF TEQUIP:電話中
		IF ABL:Ａ拡張 - SIZE("Ａ振動棒") >= 2
			PRINTFORM %CALLNAME:TARGET%的这个振動棒可能太小了、
			PRINTFORMW 保持着%TEXTR("平静无邪的表情/安静的様子")%的和%TEXTS("彼氏")%%TEXTR("電話/通話")%还在继续着…
		ELSE
			PRINTFORM %CALLNAME:TARGET%将%TEXTR("通过電話的/通話时的/通話中的")%
			PRINTFORMW 不想让%TEXTS("彼氏")%得知情况的快要漏出来的声音%TEXTR("拼命忍受着/努力压抑着")%…
		ENDIF
	ELSEIF COND("肛門拉珠")
		PRINTFORMW 在肛門乃至於直腸都被異物填滿所帶來的奇妙感觸、讓%CALLNAME:TARGET%無法抑制的漏出了灼熱而濡濕的低吟聲……
	ELSEIF COND("長振動棒")
		PRINTFORMW 在身體的最深處、將直腸與結腸一併貫穿並劇烈顫動著的碩長振動棒所帶來的充實感與快感、讓%CALLNAME:TARGET%無法抑制的漏出了灼熱而濡濕的低吟聲……
	ELSE
		PRINTFORMW 在直腸中激烈震動的振動棒所傳來的異常快感、讓%CALLNAME:TARGET%無法抑制的漏出了灼熱而濡濕的低吟聲……
	ENDIF
	;道端の菠萝包加筆
	SIF ABL:Ａ拡張 - SIZE("Ａ振動棒") >= 2 && TEQUIP:電話中
		PRINTFORMW 但是这种振動棒对于%CALLNAME:TARGET%来说太小了、从那声音中能听出一丝遗憾的神色…
ELSEIF CHECK("Ａ振動棒") == 0 && ABL:Ａ拡張 < SIZE("Ａ振動棒")
	SIF TEQUIP:電話中
		PRINTFORM 和%TEXTS("彼氏")%%TEXTR("電話中的/通話的/通过電話连接着的")%

	PRINTFORM %CALLNAME:TARGET%的
	SIF TALENT:娇小 && EXP:Ａ経験 < 100 && ABL:Ａ拡張 == 0
		PRINT 小小的
	PRINT 肛門
	IF SIZE("Ａ振動棒") >= 1 && ABL:Ａ拡張 < SIZE("Ａ振動棒")
		PRINTW 無法承受振動棒的粗大、無法抑制的吐出了愈來愈高的哀鳴聲……
	ELSE
		PRINT 被強硬地擴張到幾乎要裂開的程度、但終究將
		IF COND("肛門拉珠")
			PRINT 相互連接著的球體
		ELSEIF SIZE("Ａ振動棒") == 0
			PRINT 肛門振動棒
		ELSE
			PRINT 振動棒
		ENDIF
		PRINTW 勉強地吞入體內……
	ENDIF
ELSEIF ABL:Ａ感覚 >= 2 && CUP:苦痛 < 500
	SIF TEQUIP:電話中
		PRINTFORM 和%TEXTS("彼氏")%%TEXTR("電話中/正在通話")%的
	PRINTFORMW %CALLNAME:TARGET%因腸道中傳來的奇妙感觸而迷惑了……
ELSE
	SIF TEQUIP:電話中
		PRINTFORM 加上和%TEXTS("彼氏")%%TEXTR("電話/通話")%的緊張感、

	PRINTFORM 對於
	IF COND("肛門拉珠")
		PRINT 肛門拉珠
	ELSE
		PRINT 振動棒
	ENDIF
	PRINTFORMW 所帶來的刺激還不習慣、%CALLNAME:TARGET%的肛門本能的緊繃著、表現著強烈的反抗心……
ENDIF

@FLAG_COM33, ARG
CALL FLAG_INSERTION, "Ａ振動棒", ARG, 100
;肛門が露出していないなら自動的にずらす
SIF CHECK_CLO("Ａ露出") == 0
	CALL SETFLAG, "下身衣装を露出：テ接吻ト没有", TARGET

@SOURCE_COM33, ARG
IF TEQUIP:肛用振動棒
	IF COND("肛門拉珠")
		CALL SOURCE_INSERTION, "Ａ振動棒", ARG, 30
	ELSE
		CALL SOURCE_INSERTION, "Ａ振動棒", ARG, 100
	ENDIF
ELSE
	IF COND("肛門拉珠拔出FLAG")
		CALL SOURCE_INSERTION, "Ａ振動棒", ARG, 100
	ELSE
		CALL SOURCE_INSERTION, "Ａ振動棒", ARG, 30
	ENDIF
ENDIF


@EQUIP_COM33
;Ａ振動棒挿入中
SIF TEQUIP:肛用振動棒 == 0
	RETURN 0
SIF SELECTCOM == 33 || SELECTCOM == 202
	RETURN 0

CALL SOURCE_COM33, 30
CALL FLAG_COM33, 30

RETURN 1

@EQUIP_COM33_2
#DIMS PLAY_ONANIE
;Ａ振動棒挿入中
SIF TEQUIP:肛用振動棒 == 0
	RETURN 0
SIF SELECTCOM == 33 || SELECTCOM == 202
	RETURN 0


IF COND("肛門拉珠")
	PRINTL ＜肛門拉珠挿入中＞
ELSE
	SELECTCASE SIZE("Ａ振動棒")
	CASE 6
		PRINTL ＜肛門規格外振動棒挿入中＞
	CASE 5
		PRINTL ＜肛門馬型振動棒挿入中＞
	CASE 4
		PRINTL ＜肛門腕並振動棒挿入中＞
	CASE 3
		PRINTL ＜肛門極太振動棒挿入中＞
	CASEELSE
		PRINTL ＜肛用振動棒挿入中＞
	ENDSELECT
ENDIF

PLAY_ONANIE = %CHECK_ONANIE(SELECTCOM)%

LOCAL = 1
;自慰実行時は経験＋１
IF STRCOUNT(PLAY_ONANIE, "Ａ") && SELECTCOM == 3
	SIF ABL:Ａ拡張 < SIZE("Ａ振動棒")
		LOCAL += 1
ENDIF

EXP:Ａ経験 += LOCAL

VARSET LOCAL
;Ａ拡張経験
IF SELECTCOM == 2 || (STRCOUNT(PLAY_ONANIE, "Ａ") && SELECTCOM == 3)
	SIF SIZE("Ａ振動棒") >= 2
		LOCAL = MAX(SIZE("Ａ振動棒") - ABL:Ａ拡張 + 1, SIZE("Ａ振動棒")/3)
ENDIF

IF LOCAL
	EXP:Ａ拡張経験 += LOCAL
	;拡張経験による追加の疲労
	DOWNBASE:体力 += LOCAL*50
ENDIF

RETURN 1
