@COM280
#DIM COMORDER_280
;挣扎
CALL PRINT_TRAIN_NAME(SELECTCOM)

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL SOURCE_COM280, 100

TFLAG:カウンター中 = 1

;FIRSTCOMフラグON
CALL SET_CHOSECOM, TARGET, TNUM("挣扎")

;成否判定
SIF TFLAG:もがき数 <= 0
	TFLAG:もがき数 = 0

STR:実行値 = 
COMORDER_280 = 0

IF ABL:施虐属性
	COMORDER_280 += MIN(ABL:施虐属性, 5)
	STR:実行値 = %STR:実行値% + 施虐属性LV{ABL:施虐属性}({MIN(ABL:施虐属性, 5)})
ENDIF
IF TALENT:強気
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 強気(1)
ENDIF
IF TALENT:高大
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 高大(1)
ENDIF
IF TALENT:大力士
	COMORDER_280 += TALENT:大力士*3
	STR:実行値 = %STR:実行値% + %TALENT_NAME(GETNUM(TALENT, "大力士"), TALENT:大力士)%({TALENT:大力士*3})
ENDIF
IF TALENT:好色
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 好色(1)
ENDIF
;体操服着用なら
IF CHECK_SHIRT("体操服")
	COMORDER_280 += 2
	STR:実行値 = %STR:実行値% + 体操服(2)
ENDIF
;二人推倒てるなら
IF TEQUIP:Ｗ推倒
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + Ｗ推倒中(5)
ENDIF
;騎乗位で挿入状態
IF INSERT("騎乗位")
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + 騎乗位(5)
ENDIF
;前ターンの絶頂具合
IF PREVEX("射精", PLAYER)
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + %CALLNAME:PLAYER%が射精直後(5)
ENDIF

;减少補正はこの後
IF TALENT:娇小
	COMORDER_280 -= 1
	STR:実行値 = %STR:実行値% - 娇小(1)
ENDIF
;前ターンの絶頂具合
IF PREVEX("絶頂数合計")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 絶頂直後(10)
ENDIF
IF COND("陶酔")
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 陶酔(5)
ELSEIF COND("弛緩")
	COMORDER_280 -= 2
	STR:実行値 = %STR:実行値% - 弛緩(2)
ENDIF
IF COND("疲労")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 疲労困憊(10)
ELSEIF BASE:体力 < MAXBASE:体力/2
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 体力半減(5)
ENDIF
IF CFLAG:マグロ
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 干劲无(10)
ENDIF
;拘束されてる場合
IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
	;拘束の種類による補正
	IF     BIND("縄：手足縛", TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    20
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](20)
	ELSEIF BIND("縄：亀甲縛り"    , TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    20
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](10)
	ELSEIF BIND("縄：逆海老"      , TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    30
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](30)
	ELSEIF BIND("縄：足吊"      , TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    40
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](40)
	ELSEIF BIND("手枷"            , TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    10
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](10)
	ELSEIF BIND("吊り"            , TEQUIP:PLAYER:拘束)
		COMORDER_280 += 99999
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](99999)
	ELSEIF BIND("Ｍ字開脚"        , TEQUIP:PLAYER:拘束)
		COMORDER_280 += 99999
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](99999)
	ELSEIF BIND("床"          , TEQUIP:PLAYER:拘束)
		COMORDER_280 += 99999
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](99999)
	ELSEIF BIND("四つん這い"      , TEQUIP:PLAYER:拘束)
		COMORDER_280 += 99999
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](99999)
	ELSEIF BIND("縄"              , TEQUIP:PLAYER:拘束)
		COMORDER_280 +=    20
		STR:実行値 = %STR:実行値% + 拘束強度[%BINDS(TEQUIP:PLAYER:拘束)%](20)
	ENDIF

	;拘束の種類が緊縛系なら、技巧による補正がある
	IF BIND("縄", TEQUIP:PLAYER:拘束)
		;拘束側の技巧（拘束側３倍段）
		IF ABL:技巧
			COMORDER_280 += MIN(ABL:技巧 * 3, 15)
			STR:実行値 = %STR:実行値% + 拘束強度[%CALLNAME:TARGET%の技巧LV{ABL:技巧}]({MIN(ABL:技巧 * 3, 15)})
		ENDIF

		;被拘束側の技巧
		IF ABL:PLAYER:技巧
			COMORDER_280 -= MIN(ABL:PLAYER:技巧, 5)
			STR:実行値 = %STR:実行値% - 縄抜け[%CALLNAME:PLAYER%の技巧LV{ABL:PLAYER:技巧}]({MIN(ABL:PLAYER:技巧, 5)})
		ENDIF
	ENDIF
ELSE
	IF ABL:PLAYER:技巧 - ABL:技巧
		COMORDER_280 -= LIMIT(ABL:PLAYER:技巧 - ABL:技巧, -5, 5)*2
		STR:実行値 = %STR:実行値% \@ ABL:PLAYER:技巧 - ABL:技巧 > 0 ? - # + \@ 技巧差({ABS(LIMIT(ABL:PLAYER:技巧 - ABL:技巧, -5, 5) )*2})
	ENDIF
ENDIF
IF TALENT:PLAYER:大力士
	COMORDER_280 -= TALENT:PLAYER:大力士*3
	STR:実行値 = %STR:実行値% - %CALLNAME:PLAYER%が%TALENT_NAME(GETNUM(TALENT, "大力士"), TALENT:PLAYER:大力士)%({TALENT:PLAYER:大力士*3})
ENDIF
IF TFLAG:もがき数
	COMORDER_280 -= TFLAG:もがき数*3
	STR:実行値 = %STR:実行値% - もがき数{TFLAG:もがき数}回({TFLAG:もがき数*3})
ENDIF
IF RANDF(7) - 3
	COMORDER_280 += RANDF(7) - 3
	STR:実行値 = %STR:実行値% \@ RANDF(7) - 3 > 0 ? - # + \@ 時の運({ABS(RANDF(7) - 3)})
ENDIF
PRINTL 
PRINTFORM %SUBSTRING(STR:実行値, 3)% = {COMORDER_280} 
SELECTCASE COMORDER_280
CASE IS < 0
	PRINT <
CASE 0
	PRINT =
CASEELSE
	PRINT >
ENDSELECT
PRINTFORMW  実行値{0}
PRINTL 
IF COMORDER_280 <= 0
	TFLAG:もがき数 = 0
	IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
		IF BIND("手枷", TEQUIP:PLAYER:拘束)
			PRINTFORMW %CALLNAME:PLAYER%は何とか主導権を取り返したが、手枷は外れなかった…！
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%は何とか拘束から逃れ、主導権を取り返した！
			TEQUIP:PLAYER:拘束 = 0
		ENDIF
		CALL REMOVE_STATE, PLAYER, "バインド"
		CVARSET TEQUIP, GETNUM(TEQUIP, "推倒")
	ELSE
		PRINTFORM %CALLNAME:PLAYER%将
		SIF PREVEX("絶頂数合計")
			PRINTFORM 從沉浸在絕頂的餘韻中的%CALLNAME:TARGET%的空隙中發起突襲、
		PRINTFORMW 將主導權奪回了！
		CVARSET TEQUIP, GETNUM(TEQUIP, "推倒")
		CALL KOJO_MESSAGE_COM
		;Ｗ推倒等での口上処理
		CALL KOJO_MESSAGE_COM_MULTI
		;助手との挿入はここで解消
		CALL SETFLAG, "Ｗ推倒解除"
		CALL SETFLAG, "今回指令"

		;TARGETにPLAYERが挿入中なら正常位に移行
		IF INSERT("Ｖ挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 60
		ELSEIF INSERT("Ａ挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 70
		ELSEIF INSERT("尿道挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 83
		ENDIF
	ENDIF

	TFLAG:カウンター中 = 0

	RETURN 0
ELSE
	IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
		PRINTFORM %CALLNAME:PLAYER%呢？何とか自由になろうともがいたが、
		IF     BIND("縄：手足縛", TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@摆成了Ｍ字開腿的姿势将手腕和脚腕绑在了一起
		ELSEIF BIND("縄：亀甲縛り"    , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@用亀甲縛绑住了身体
		ELSEIF BIND("縄：逆海老"      , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@像是反曲的虾一样绑住了身体
		ELSEIF BIND("縄：足吊"      , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@紧缚着吊起一只腿拘束住
		ELSEIF BIND("手枷"            , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@用手枷拘束住了双手
		ELSEIF BIND("吊り"            , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@用锁链捆住了双手吊起来
		ELSEIF BIND("Ｍ字開脚"        , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@拘束在了拘束台上
		ELSEIF BIND("床"          , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@摆成大字拘束在了床上
		ELSEIF BIND("四つん這い"      , TEQUIP:PLAYER:拘束)
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@摆成了四肢着地的姿势固定住
		ELSE
			PRINTFORM 因为被%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 们 # \@绑住
		ENDIF
		PRINTFORML 没有办法取回主导权…
	ELSE
		;前回の調教によって文章変化
		;面部騎乗系
		IF PREVCOM == 1 || PREVCOM == 9 || TEQUIP:面部騎乗 || TEQUIP:六九式
			PRINTFORM 從跨坐在臉上的%CALLNAME:TARGET%那裡、
		;逆強制口交
		ELSEIF PREVCOM == 4
			PRINTFORM 從帶著愉快的表情強硬的衝擊著%CALLNAME:PLAYER%口腔的%CALLNAME:TARGET%那裡、
		;奉仕系
		ELSEIF (PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 95 || PREVCOM == 97 || PREVCOM == 99) && PENIS(PLAYER)
			SIF PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 99
				PRINT 壓在身上
			PRINTFORM 蹂躪著陰莖的%CALLNAME:TARGET%那裡、
		;騎乗位系
		ELSEIF (PREVCOM >= 65 && PREVCOM <= 67) || PREVCOM == 75 || PREVCOM == 80 || PREVCOM == 88 || PREVCOM == 98 || PREVCOM == 120 || PREVCOM == 126
			PRINTFORM 從亢奮的侵犯著%CALLNAME:PLAYER%的%CALLNAME:TARGET%那裡、
		;肛門奉仕系
		ELSEIF PREVCOM == 96
			PRINTFORM 從愉快的蹂躪著肛門的%CALLNAME:TARGET%那裡、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%将
		ENDIF
		PRINTFORML 试图夺回主导权、但%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 她们 # \@完全没有破绽。
	ENDIF
	IF TEQUIP:接吻 && ABL:欲望 >= 5 && RAND:3 == 0
		PRINTFORMW 不止如此、%CALLNAME:TARGET%像是在享受%CALLNAME:PLAYER%的反抗一樣再次奪去了嘴唇、強行用舌頭交纏再一次、強硬的灌下香甜的唾液。
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ELSE
		PRINTFORMW 不但如此、像是在懲罰%CALLNAME:PLAYER%的抵抗一樣、更加猛烈地侵犯起來。
	ENDIF
	TFLAG:もがき数 += 1

	CALL KOJO_MESSAGE_COM
	PRINTL 

	CALL SET_COUNTER_ABL

	SIF TEQUIP:Ｗ推倒
		CALL SET_SELECTCOM_MULTI, "Ｗ推倒"

	TRYJUMPFORM COM{SELECTCOM}
ENDIF

RETURN 1

@FLAG_COM280, ARG

@SOURCE_COM280, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 280

SOURCE:0:性行動 = 160

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:性行動, 1.00
CASE IS < PALAMLV:2
	TIMES SOURCE:0:性行動, 1.20
CASE IS < PALAMLV:3
	TIMES SOURCE:0:性行動, 1.40
CASE IS < PALAMLV:4
	TIMES SOURCE:0:性行動, 1.60
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:性行動, 1.80
ENDSELECT

;ABL:施虐狂ッ気をみる
SELECTCASE ABL:施虐属性
CASE 0
CASE 1
	TIMES SOURCE:0:性行動, 1.20
CASE 2
	TIMES SOURCE:0:性行動, 1.40
CASE 3
	TIMES SOURCE:0:性行動, 1.60
CASE 4
	TIMES SOURCE:0:性行動, 1.80
CASEELSE
	TIMES SOURCE:0:性行動, 2.00
ENDSELECT

CALL CALC, "調教ソース", ARG

