@COM65
;騎乗位
;推倒の時に一定条件を満たすと、狂野摇摆になり、効果上昇。
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 65

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM65, ARG
IF INSERT("狂野摇摆")
	CALL FLAG_INSERTION, "Ｖ挿入", ARG, 120
ELSE
	CALL FLAG_INSERTION, "Ｖ挿入", ARG, 70
ENDIF


@SOURCE_COM65, ARG
IF INSERT("狂野摇摆")
	CALL SOURCE_INSERTION, "Ｖ挿入", ARG, 120
ELSE
	CALL SOURCE_INSERTION, "Ｖ挿入", ARG, 70
ENDIF

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 65

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:露出 = 900
SOURCE:0:情愛 = 500

;根元まで受け入れられる場合には阴蒂を擦られてしまう
IF ABL:Ｖ拡張 > SIZE("阴茎", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 30)
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰靈活", PLAYER)
ENDIF

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0 TO 5
	SOURCE:0:性行動 = 300 + ABL:奉仕精神*200
	SOURCE:0:達成感 = 100 + ABL:奉仕精神*300
	SOURCE:0:逸脱 = MAX(300 - ABL:奉仕精神*100, 0)
CASE 6 TO 10
	SOURCE:0:性行動 = 1300 + (ABL:奉仕精神 - 5)*100
	SOURCE:0:達成感 = 1600 + (ABL:奉仕精神 - 5)*200
CASEELSE
	SOURCE:0:性行動 = 1800 + (ABL:奉仕精神 - 10)*50
	SOURCE:0:達成感 = 2600 + (ABL:奉仕精神 - 10)*100
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM65
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

PRINTFORM %CALLNAME:TARGET%
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;穴開きでない胖次を是ていて、ずらされていないなら、必ずずらす
ELSEIF CHECK_CLO("Ｖ露出") == 0 && CHECK_CLO("Ｖ露出可")
	CALL SETFLAG, "下身衣装を露出：テ接吻ト没有", TARGET
ENDIF
IF TALENT:好色 && RAND:2 == 0
	PRINTFORM 讓%CALLNAME:PLAYER%的龜頭與自己的%TEXTR("陰唇/膣口")%相吻、
ELSE
	PRINTFORM 对着%CALLNAME:PLAYER%%TEXTR("直接跨了上去/確認了阴茎的位置后")%、
ENDIF

;裙などを是ているなら
IF EQUIP:裙 + TEQUIP:新妻PLAY && (TALENT:好色 + TALENT:淫乱 || ABL:露出癖 >= 3) && RAND:2
	IF TEQUIP:新妻PLAY
		PRINTFORM 將圍裙的下擺
	ELSE
		PRINTFORM 將%SKIRT(TARGET, "略")%
	ENDIF
	IF COND("口の使用") && RANDIF(2, COND("指の使用") )
		PRINT 用嘴唇噙住、
	ELSEIF COND("指の使用") && RAND:2
		PRINT 用手指拉起、
	ELSE
		PRINT 卷起、
	ENDIF
	IF RAND:2
		PRINTFORM 將%TEXTS("阴部：視覚")%
	ELSEIF RAND:2 && PALAM:Ｖ潤 >= 10000 && PALAM:欲情 >= 10000
		PRINTFORM 將不斷垂落期待的涎水的淫穴
	ELSE
		PRINTFORM 將%TEXTR("不知廉恥的張合著的雌穴/蒸騰著淫臭熱汽的股間")%
	ENDIF
	PRINTFORM %TEXTR("大膽地展示著/高興地炫耀著")%的同時、
ELSE
	SIF TEQUIP:電話中
		PRINTFORM 保持着和%TEXTS("彼氏")%的電話、

	IF TALENT:処女
		IF COND("視力")
			PRINTFORM %TEXTR("在用心醉的眼神注視著恍惚的面孔的同時/闭上了眼睛/屏住呼吸准备承受即将到来的疼痛的同时")%
		ELSE
			PRINTFORM %TEXTR("咬紧牙冠发出粗重的鼻息的同时/屏住呼吸准备承受即将到来的疼痛的同时")%
		ENDIF
	ELSEIF TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 && COND("視力") && RAND:3 == 0
		PRINT 在用心醉的眼神注視著恍惚的面孔的同時、
	ELSEIF TEQUIP:推倒 && ABL:施虐属性 >= 2 && RAND:2
		IF TALENT:高慢 + TALENT:強気 && RANDIF(2, TALENT:傲慢 + TALENT:小悪魔)
			PRINT 在浮現出大膽的笑容的同時、
		ELSEIF TALENT:傲慢 + TALENT:小悪魔 && RAND:2
			PRINT 在臉上露出戲謔的壞笑的同時、
		ELSE
			PRINT 在對痛苦的渴求與喜悅不禁滲透而出的同時、
		ENDIF
	ELSEIF ABL:Ｖ感覚 >= 3 && RAND:2
		PRINTFORM %TEXTR("在從唇角漏出甜美的呻吟/在露出恍惚而快樂的蕩漾表情")%的同時、
	ELSE
		PRINT 在小口的吐出灼熱的喘息的同時、
	ENDIF
	IF ABL:Ｖ拡張 >= SIZE("阴茎", PLAYER) && ABL:施虐属性 && RAND:2
		PRINT 一口気
	ELSE
		PRINT 溫柔緩慢地
	ENDIF
ENDIF
PRINTFORM 沉下了%TEXTR(@"腰/%TEXTS("お尻：視覚")%")%
IF PREVPLAY("清洁口交") && RAND:3 == 0
	PRINTFORMW 將%TEXTR("被漂亮的清潔乾淨的/清潔口交之後的/在口舌侍奉之後振作起來的")%陰莖\@ CHOSECOMS("Ｖ挿入") ? 再一次的 # 毫不猶豫的 \@用膣道深深含入體內…
ELSEIF ABL:Ｖ拡張 >= SIZE("阴茎", PLAYER) && PALAM:Ｖ潤 >= 3000 && RAND:3
	IF ABL:Ｖ拡張 > SIZE("阴茎", PLAYER) && RAND:2
		PRINTW 用正飢渴的蠕動著的淫穴毫無阻礙地將陰莖吞入體內、開始急促的扭動腰肢追逐著快樂……
	ELSE
		PRINTW 借助雌穴媚肉的蠕動與本身的體重將挺立的陰莖慢慢吞入體內……
	ENDIF
ELSE
	PRINTW 用膣道擼動著陰莖……
ENDIF


@COM_ORDER_COM65, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
IF COND("調教中")
	;PALAM:Ｖ潤（の不足）
	IF PALAM:Ｖ潤 < PALAMLV:3
		LOCAL -= 5
		LOCALS = %LOCALS% - Ｖ潤不足(5)
	ENDIF
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 10
	LOCALS = %LOCALS% - 害羞(10)
ENDIF
;Ｖ損傷
IF TALENT:Ｖ損傷
	LOCAL -= 30 * TALENT:Ｖ損傷
	LOCALS = %LOCALS%- Ｖ損傷({30 * TALENT:Ｖ損傷})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;Ｖ性向
IF TALENT:Ｖ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｖ性向(10)
ENDIF
;処女
IF TALENT:処女
	LOCAL -= 20
	LOCALS = %LOCALS% - 処女(20)
ELSEIF EXP:Ｖ経験 < EXPLV:2
	LOCAL -= 5
	LOCALS = %LOCALS% - Ｖ経験不足(5)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")})
ENDIF

;合計を表示(24以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 24

RETURN RESULT
