@COM99
;六九式
CALL PRINT_TRAIN_NAME(SELECTCOM)

IF TEQUIP:六九式
	PRINTL 
	PRINTFORMW \@ TEQUIP:推倒 ? %CALLNAME:TARGET% # %CALLNAME:PLAYER% \@は六九式の体勢をやめた…
	STR:前回指令 = %STR:今回指令%
	TEQUIP:六九式 = 0
	TEQUIP:PLAYER:六九式 = 0
	PREVCOM = 99
	RETURN 0
ENDIF
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 99

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
;相互お掃除PLAY
IF CHECK_COM("清洁口交", TNUM("口交")) + CHECK_COM("清洁舔阴", TNUM("奉仕舐陰")) && CHECK_COM("清洁口交", TNUM("舐陰莖")) + CHECK_COM("清洁舔阴", TNUM("舐陰"))
	IF CHECK_COM("清洁口交", TNUM("口交"))
		TEQUIP:六九式 = TNUM("口交")
	ELSE
		TEQUIP:六九式 = TNUM("奉仕舐陰")
	ENDIF
	IF CHECK_COM("清洁口交", TNUM("舐陰莖"))
		TEQUIP:PLAYER:六九式 = TNUM("舐陰莖")
	ELSE
		TEQUIP:PLAYER:六九式 = TNUM("舐陰")
	ENDIF
ELSE
	;阴茎あるならそっち優先
	IF PENIS(PLAYER)
		TEQUIP:六九式 = TNUM("口交")
	ELSE
		TEQUIP:六九式 = TNUM("奉仕舐陰")
	ENDIF
	IF PENIS(TARGET)
		TEQUIP:PLAYER:六九式 = TNUM("舐陰莖")
	ELSE
		TEQUIP:PLAYER:六九式 = TNUM("舐陰")
	ENDIF
ENDIF

CALL TRAIN_MESSAGE_B

IF REFUSE_CHECK()
	TEQUIP:六九式 = 0
	TEQUIP:PLAYER:六九式 = 0
	RETURN 0
ENDIF

STR:前回指令 = %STR:今回指令%

;面部騎乗ＯＮ
SIF TEQUIP:面部騎乗 + TEQUIP:推倒 && TEQUIP:PLAYER:六九式 == TNUM("舐陰")
	TEQUIP:面部騎乗 = 1
;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM99, ARG
TRYCALLFORM FLAG_COM{TEQUIP:PLAYER:六九式}, 70
TRYCALLFORM FLAG_COM{TEQUIP:六九式}, 70

@SOURCE_COM99, ARG
TRYCALLFORM SOURCE_COM{TEQUIP:PLAYER:六九式}, 70
TRYCALLFORM SOURCE_COM{TEQUIP:六九式}, 70

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "六九式"

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加") + 500
SOURCE:0:露出 = 1400
SOURCE:0:屈従 = 1300
SOURCE:0:逸脱 = 800

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM99
;相互お掃除PLAY
IF CHECK_COM("清洁口交", TNUM("口交")) + CHECK_COM("清洁舔阴", TNUM("奉仕舐陰")) && CHECK_COM("清洁口交", TNUM("舐陰莖")) + CHECK_COM("清洁舔阴", TNUM("舐陰"))
	PRINTFORMW %CALLNAME:PLAYER%與%CALLNAME:TARGET%將彼此性器上的淫靡污垢用唇舌打掃乾淨了……
;推倒
ELSEIF TEQUIP:推倒
	IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%將由自己主導的六九式口交盡情的享受著……
	ELSEIF PREVCOM == TNUM("口交") && COND("前Ｔ調教者") == PLAYER
		PRINTFORM 在%CALLNAME:TARGET%將挺立的陰莖含住吮吸著的同時、跨坐在%CALLNAME:PLAYER%的臉頰上、
		IF TEQUIP:PLAYER:六九式 == TNUM("舐陰莖")
			PRINTFORMW 焦急地催促對方盡心舔舐眼前的蜜泉……
		ELSE
			PRINTFORMW 迫不及待地將陰部直直地印在臉頰上……
		ENDIF
	ELSE
		IF TEQUIP:面部騎乗 && COND("前Ｔ調教者") == PLAYER
			PRINTFORM %CALLNAME:TARGET%將%CALLNAME:PLAYER%的臉頰用股間壓迫、騎乘著的同時、前傾身體
		ELSE
			PRINTFORM %CALLNAME:TARGET%將%CALLNAME:PLAYER%\@ COND("脚の使用") ? 越過、將 # 的 \@
			IF TEQUIP:PLAYER:六九式 == TNUM("舐陰莖")
				PRINTFORM 口腔用下體不斷侵犯著的同時
				SIF TEQUIP:六九式 == TNUM("口交")
					PRINTFORM \@ COND("脚の使用") ?  # 、 \@自己也
			ELSE
				PRINTFORM 臉頰用股間激烈摩擦著的同時
				SIF TEQUIP:六九式 == TNUM("奉仕舐陰")
					PRINTFORM \@ COND("脚の使用") ?  # 、 \@自己也
			ENDIF
		ENDIF
		IF TEQUIP:六九式 == TNUM("口交")
			PRINTFORMW 陰莖投入的吮吸著……
		ELSE
			PRINTFORMW 將陰部用舌頭反復舔舐著……
		ENDIF
	ENDIF
ELSE
	IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%與%CALLNAME:PLAYER%將彼此的%TEXTR("性器仔細地舔舐著/股間用臉頰填滿、將內部的秘處盡情地品味著")%……
	ELSE
		IF COND("脚の使用") == 0
			PRINTFORM %CALLNAME:PLAYER%與%CALLNAME:TARGET%轉動身體、形成六九式的姿勢、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%任由%CALLNAME:TARGET%跨坐在臉頰上、
		ENDIF
		IF PENIS(PLAYER) == PENIS(TARGET)
			PRINTFORMW 將彼此的\@ PENIS(PLAYER) ? 陰莖吮吸、侍奉著 # 陰部仔細地舔舐著 \@…
		ELSE
			IF TEQUIP:六九式 == TNUM("口交")
				PRINTFORM 在勃起的陰莖享受著溫熱而濕潤的口腔的殷切侍奉的同時、
			ELSE
				PRINTFORM 在微顫的陰部與不斷張合的雙唇接觸的邊角流出涎水與蜜液的混合液體的同時、
			ENDIF
			IF TEQUIP:PLAYER:六九式 == TNUM("舐陰莖")
				PRINTFORMW 也將眼前蒸騰著醉人熱汽的肉棒仔細品味著……
			ELSE
				PRINTFORMW 也將不斷散發出淫靡%TEXTS("气味")%的陰部用舌頭細心舔舐著……
			ENDIF
		ENDIF
	ENDIF
ENDIF

@SET_PLAY69_TURNEND
;道具指令等は、連続するのが自然な他の指令にする
SELECTCASE TEQUIP:PLAYER:六九式
CASE TNUM("振動棒")
	TEQUIP:PLAYER:六九式 = 8
CASE TNUM("肛用振動棒"), TNUM("肛塞")
	TEQUIP:PLAYER:六九式 = 2
CASE 141
	TEQUIP:PLAYER:六九式 = 8
CASE 142
	TEQUIP:PLAYER:六九式 = 2
CASE 143
	IF RAND:2
		TEQUIP:PLAYER:六九式 = 2
	ELSE
		TEQUIP:PLAYER:六九式 = 8
	ENDIF
ENDSELECT

@SET_PLAY69
;指令連続選択による派生
CALL COMABLE_69
;前回と今回の調教者が違うorＷ推倒中or六九式不可
IF RESULT == 0 || COND("前Ｔ調教者") != PLAYER || TEQUIP:Ｗ推倒
	TEQUIP:六九式 = 0
	TEQUIP:PLAYER:六九式 = 0
	RETURN 0
ELSE
	;褒めた時など、体勢が変わらない指令
	SELECTCASE SELECTCOM
	;推倒状態の媚薬は騎乗位に移行するので解除
	CASE 261
		IF TEQUIP:推倒
			TEQUIP:六九式 = 0
			TEQUIP:PLAYER:六九式 = 0
		ENDIF
	ENDSELECT
ENDIF

SELECTCASE CHECK_EQUIP_69(SELECTCOM)
CASE 1
	;前回の調教が口交、舐陰強制か六九式だと六九式に
	IF PREVCOM == TNUM("口交") || PREVCOM == TNUM("奉仕舐陰") || PREVCOM == TNUM("深喉") || TEQUIP:六九式
		SIF TEQUIP:六九式 == 0
			TEQUIP:六九式 = PREVCOM
		TEQUIP:PLAYER:六九式 = SELECTCOM
	ENDIF
CASE 2
	;前回の調教が舐陰、舐陰莖か六九式だと六九式に
	IF PREVCOM == TNUM("舐陰") || PREVCOM == TNUM("舐陰莖") || TEQUIP:面部騎乗 || TEQUIP:六九式
		;69に移行した時（TEQUIP:PLAYER:六九式は0も意味を持つので、TEQUIP:六九式で判定。）
		IF TEQUIP:六九式 == 0
			IF TEQUIP:面部騎乗
				TEQUIP:PLAYER:六九式 = TEQUIP:面部騎乗
			ELSE
				TEQUIP:PLAYER:六九式 = PREVCOM
			ENDIF
		ENDIF
		TEQUIP:六九式 = SELECTCOM
	ENDIF
CASE 3
	;六九式中だと継続
	IF TEQUIP:六九式
		TEQUIP:PLAYER:六九式 = SELECTCOM
		;partnerの行動に変化をつける
		IF RAND:3 == 0 && PENIS(PLAYER)
			IF CANTRAIN("乳交") && RAND:3 == 0
				TEQUIP:六九式 = TNUM("乳交")
			ELSEIF CANTRAIN("口交") && RAND:2
				TEQUIP:六九式 = TNUM("口交")
			ELSEIF CANTRAIN("手淫")
				TEQUIP:六九式 = TNUM("手淫")
			ENDIF
		ENDIF
	ENDIF
CASE 4
	;六九式中だと継続
	SIF TEQUIP:六九式
		TEQUIP:六九式 = SELECTCOM
;六九式も可能な指令(TEQUIP:六九式は変更させない)
CASE 6
	RETURN 0
CASEELSE
	IF SELECTCOM != 99
		TEQUIP:六九式 = 0
		TEQUIP:PLAYER:六九式 = 0
	ENDIF
ENDSELECT

@CHECK_EQUIP_69(ARG)
#FUNCTION

;時間を経過させない指令なら変化无
SIF NOTTIME_PLAY(ARG)
	RETURNF 6

SELECTCASE ARG
;PLAYERの行動　六九式に移行する股間弄り指令
CASE TNUM("舐陰"), TNUM("舐陰莖")
	RETURNF 1
;TARGETの行動　六九式に移行する奉仕指令
CASE TNUM("口交"), TNUM("奉仕舐陰"), TNUM("深喉")
	RETURNF 2
;PLAYERの行動　六九式体勢で可能な愛撫系の股間弄り指令
CASE 0 TO 2, 7 TO 10, 15
	RETURNF 3
;PLAYERの行動　道具指令や打屁股、拳交
CASE 30 TO 39, 130, 141 TO 143
	RETURNF 3
;TARGETの行動　六九式体勢で可能な奉仕指令
CASE TNUM("手淫") TO TNUM("乳交"), TNUM("内褲冲"), TNUM("奉仕舐陰"), TNUM("飞机杯冲")
	RETURNF 4
;六九式も可能な指令(TEQUIP:六九式は変更させない)
CASE 3, 13, 14, 24, 146, 176, 230 TO 300
	RETURNF 6
CASE 99
	RETURNF 7
ENDSELECT
RETURNF 0


@COMABLE_69
;六九式実行判定
SIF TEQUIP:六九式
	RETURN 1
;拘束（足or体勢変更不可）だとダメ
SIF GETBITS(TEQUIP:拘束, 21, 22)
	RETURN 0

;実行条件判定
CALL COM_ORDER_COM99, 1
RETURN RESULT


@EQUIP_COM69
SIF TEQUIP:六九式 == 0
	RETURN 0
;アクティブでない方の動きによるSOURCE等の変化を記述
SIF SELECTCOM == 99
	RETURN 0

SELECTCASE SELECTCOM
;６９自慰中
CASE 3
	CALLFORM SOURCE_COM{TEQUIP:六九式}, 50
	CALLFORM FLAG_COM{TEQUIP:六九式}, 50
;PLAYERの行動　舐陰or舐陰莖
CASE 0 TO 2, TNUM("舐陰莖"), 7 TO 10, 15, 30 TO 39, 130, 141 TO 143, TNUM("深喉")
	CALLFORM SOURCE_COM{TEQUIP:六九式}, 50
	CALLFORM FLAG_COM{TEQUIP:六九式}, 50
;TARGETの行動　口交or乳交or奉仕舐陰
CASE TNUM("手淫") TO TNUM("乳交"), TNUM("内褲冲"), TNUM("奉仕舐陰"), TNUM("飞机杯冲"), TNUM("深喉")
	CALLFORM SOURCE_COM{TEQUIP:PLAYER:六九式}, 50
	CALLFORM FLAG_COM{TEQUIP:PLAYER:六九式}, 50
ENDSELECT

@EQUIP_COM69_2
#DIM MEMO_SELECTCOM
SIF TEQUIP:六九式 == 0
	RETURN 0
;アクティブでない方の動きを描写する
SIF SELECTCOM == 99
	RETURN 0
PRINTL ＜六九式中＞

MEMO_SELECTCOM = SELECTCOM

SELECTCASE SELECTCOM
;６９自慰中
CASE 3
	SELECTCOM = TEQUIP:六九式
	CALL GET_EXP_COM

	;射精ゲージの増加
	CALL SAMEN_CHECK, LOCAL
;PLAYERの行動　舐陰or舐陰莖
CASE 0 TO 2, TNUM("舐陰莖"), 7 TO 10, 15, 30 TO 39, 130, 141 TO 143, TNUM("深喉")
	PRINTFORM %CALLNAME:TARGET%

	SELECTCASE SELECTCOM
	CASE 0
		LOCALS = 秘處
	CASE 2, 9, TNUM("肛用振動棒")
		LOCALS = 肛門
	CASE TNUM("舐陰莖"), 10
		LOCALS = 陰莖
	CASE 8
		LOCALS = 敏感地帶
	CASE 15
		LOCALS = 尿尿的穴
	CASE 30
		LOCALS = 陰蒂
	CASE TNUM("振動棒")
		LOCALS = 膣
	CASEELSE
		LOCALS = 陰部
	ENDSELECT

	LOCAL = 50
	IF TEQUIP:推倒
		IF SELECTCOM == TNUM("舐陰")
			IF CHECK_CLO("Ｖ露出")
				PRINT 將臉頰用陰部反復摩擦的同時、
			ELSE
				PRINT 將臉頰用重要的部分緊緊壓住的同時、
			ENDIF
		ELSE
			PRINT 將下體傳來的快樂愈加飢渴的索取著的同時、
		ENDIF
	ELSE
		IF SELECTCOM == 130
			PRINTFORM 將反復拍打在尻部的巴掌
		ELSEIF SELECTCOM >= 141 && SELECTCOM <= 143
			PRINTFORM 將突然擊打在腹部的拳頭
		ELSEIF SELECTCOM == TNUM("振動棒") || SELECTCOM == TNUM("肛用振動棒")
			PRINTFORM 將在%LOCALS%內部反復翻攪的振動棒所帶來的甘美感
		ELSE
			PRINTFORM 將從%LOCALS%襲來的快感
		ENDIF
		;快感の量で反撃力変動
		IF NOWEX:絶頂数合計 && RAND:(1 + NOWEX:絶頂数合計)
			PRINTL 全心地忍耐著、根本提不起反抗的氣力……
			LOCAL = 10
		ELSEIF SOURCE:疼痛 > MAX(PALAM_LV(ABL:受虐属性), 500) && RAND:3
			PRINT 所帶來的
			SIF SOURCE:疼痛 >= 3000
				PRINT 過於激烈的
			PRINTL 劇痛勉強的忍耐著、完全生不起反抗的想法……
			LOCAL = 10
		ELSEIF MAX(CUP:快Ｃ, CUP:快Ｖ, CUP:快Ａ, CUP:快Ｂ, CUP:快Ｍ) >= 4000 && RAND:3
			PRINT 竭力忍耐、不由得發出苦悶的低呼的同時、
			LOCAL = 30
		ELSEIF (MAX(CUP:快Ｃ, CUP:快Ｖ, CUP:快Ａ, CUP:快Ｂ, CUP:快Ｍ) >= 1000 && RAND:3) || SOURCE:疼痛 >= 500
			PRINT 拼盡全力的忍耐著的同時、
			LOCAL = 50
		ELSE
			PRINT 像是完全投身於眼前的美景一樣、顯得毫不在意、
			LOCAL = 70
		ENDIF
	ENDIF

	IF LOCAL == 10

	ELSE
		SELECTCASE TEQUIP:六九式
		CASE TNUM("手淫")
			PRINTFORM 將%CALLNAME:PLAYER%的陰莖
			IF ABL:技巧 >= 3 && RAND:3 == 0
				PRINTFORML 嫻熟的調整著節奏持續擼動著……
			ELSEIF ABL:施虐属性 && RAND:3 == 0
				PRINTFORML 像是為了給予痛苦而不是快樂一樣、過於激烈地擼動著……
			ELSEIF ABL:欲望 + ABL:奉仕精神 >= 3 && RAND:2 == 0
				PRINTFORML 如同耽溺在夢中一樣的持續擼動著……
			ELSE
				PRINTFORML 用雙手盡心地侍奉著……
			ENDIF
		CASE TNUM("口交"), TNUM("深喉")
			PRINTFORM 將%CALLNAME:PLAYER%的陰莖
			IF TEQUIP:深喉
				;技巧４かつ奉仕精神３
				IF ABL:技巧 >= 4 && ABL:奉仕精神 >= 3 && RAND:3 == 0
					PRINTFORML 咽喉含住、讓啾嚕啾嚕的淫亂水聲與壓抑著的呻吟隨著頭頸的動作不斷迴響著……
				;奉仕精神3以上か欲情4以上
				ELSEIF (ABL:奉仕精神 >= 3 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
					PRINTFORML 整個口腔包裹住、如同耽溺在夢中一樣的品味著……
				ELSEIF  RAND:2 == 0
					PRINTFORML 整個口腔包裹住、持續激烈地吮吸著……
				ELSE
					PRINTFORML 整個口腔包裹后再深深地吞入咽喉中、借助本能地痙攣起來的咽喉軟肉緊緊包裹住陰莖……
				ENDIF
			ELSEIF ABL:信頼 <= 1 || ABL:奉仕精神 <= 1
				;娇小で追加
				SIF TALENT:娇小
					PRINT 小小的
				PRINTFORML 舌尖旋轉著舔舐著整個莖身……
			ELSEIF (ABL:信頼 >= 4 || ABL:奉仕精神 >= 3 || ABL:受虐属性 >= 3) && ABL:技巧 >= 3 && EXP:口交経験 >= 100 && RAND:3 == 0
				;技巧４かつ奉仕精神３
				IF ABL:技巧 >= 4 && ABL:奉仕精神 >= 3 && RAND:3 == 0
					PRINTFORML 咽喉含住、讓啾嚕啾嚕的淫亂水聲與壓抑著的呻吟隨著頭頸的動作不斷迴響著……
				;奉仕精神3以上か欲情4以上
				ELSEIF (ABL:奉仕精神 >= 3 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
					PRINTFORML 整個口腔包裹住、如同耽溺在夢中一樣的品味著……
				ELSEIF  RAND:2 == 0
					PRINTFORML 整個口腔包裹住、持續激烈地吮吸著……
				ELSE
					PRINTFORML 整個口腔包裹后再深深地吞入咽喉中、借助本能地痙攣起來的咽喉軟肉緊緊包裹住陰莖……
				ENDIF
			ELSE
				;奉仕精神3以上か欲情4以上
				IF  (ABL:奉仕精神 >= 3  || PALAM:欲情 >= PALAMLV:4) && RAND:3 >= 1
					PRINTFORML 整個口腔包裹住、持續激烈地吮吸著……
				ELSEIF  RAND:2 == 0
					PRINTFORML 整個口腔包裹後再深深地吞入咽喉中、借助咽喉的肉襞不斷地擼動著陰莖的尖端……
				ELSE
					PRINTFORML 整個口腔包裹住、費力地操作著口中的舌頭將陰莖前段的每一處都仔細地舔舐著……
				ENDIF
			ENDIF
		CASE TNUM("乳交")
			PRINTFORM 將%CALLNAME:PLAYER%的陰莖
			IF ABL:技巧 >= 3 && RAND:3 == 0
				PRINTFORM 嫻熟的調整著節奏、用自己那
			ELSEIF ABL:施虐属性 && RAND:3 == 0
				PRINTFORM 熱情地緊緊夾住、用自己那
			ELSEIF ABL:欲望 + ABL:奉仕精神 >= 3 && RAND:2 == 0
				PRINTFORM 如同耽溺在夢中一樣、用自己那
			ENDIF
			IF TALENT:貧乳
				PRINT 貧乏單薄的胸部
			ELSEIF TALENT:巨乳
				PRINT 豊満的
			ENDIF
			PRINTL 不斷地上下摩擦著……
		CASE TNUM("内褲冲")
			PRINTFORM 將%CALLNAME:PLAYER%的陰莖
			IF RAND:3 == 0
				IF CHECK_CLO("湿的") && RAND:2 == 0
					PRINT 濡濕的
				ELSEIF CHECK_CLO("闷热的")
					PRINT 蒸騰著熱汽的
				ENDIF
			ENDIF
			PRINTFORM 將%CLO("内裤冲")%
			IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
				PRINTFORML 咯哧咯哧的擼動起來……
			ELSEIF RAND:2 == 0 && (TALENT:害羞 == 0 || TALENT:淫乱 || TALENT:恋慕)
				IF PENIS(TARGET)
					PRINTFORML 擼動起來…
				ELSE
					PRINTFORML %TEXTR("/用与小穴接触的部分包裹着/用中心的部分包裹着/用沾染了体液的部分")%擼動起來…
				ENDIF
			ELSE
				PRINTFORML 包裹著擼動起來……
			ENDIF
		CASE TNUM("奉仕舐陰")
			PRINTFORML 將%CALLNAME:PLAYER%不斷溢出甜蜜愛液的陰部投入地品味著……
		CASE TNUM("飞机杯冲")
			PRINTFORM 將%CALLNAME:PLAYER%的陰莖用飛機杯
			IF ABL:施虐属性 >= 3 && RAND:3 == 0
				PRINTFORM 套住後、用彷彿是想要捏斷一樣的大力
			ELSEIF TALENT:好奇心 && RAND:3 == 0
				PRINTFORM 套住後、在饒有興致的觀察著神態的同時、小心地
			ELSEIF MARK:屈服刻印 == 0 && RAND:3 == 0
				PRINTFORM 套住後、帶著相當複雜的表情
			ELSEIF RAND:3 == 0
				PRINTFORM 激烈地
			ELSEIF RAND:2 == 0
				PRINTFORM 噗嗤噗嗤地
			ELSE
				PRINTFORM 慢慢地、慢慢地
			ENDIF
			PRINTFORML 持續擼動著……
		ENDSELECT
	ENDIF

	SELECTCOM = TEQUIP:六九式
	CALL GET_EXP_COM

	;射精ゲージの増加
	CALL SAMEN_CHECK, LOCAL
;TARGETの行動　口交or乳交or奉仕舐陰
CASE 13, 14, TNUM("手淫") TO TNUM("乳交"), TNUM("内褲冲"), TNUM("奉仕舐陰"), TNUM("飞机杯冲"), TNUM("深喉")
	SELECTCOM = TEQUIP:PLAYER:六九式
	CALL GET_EXP_COM
ENDSELECT
SELECTCOM = MEMO_SELECTCOM


@COM_ORDER_COM99, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 10
	LOCALS = %LOCALS% - 害羞(10)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;接受快感
IF TALENT:接受快感
	LOCAL += 3
	LOCALS = %LOCALS% + 接受快感(3)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;相互お掃除PLAY
IF CHECK_COM("清洁口交", TNUM("口交")) + CHECK_COM("清洁舔阴", TNUM("奉仕舐陰")) && CHECK_COM("清洁口交", TNUM("舐陰莖")) + CHECK_COM("清洁舔阴", 1)
	IF CHECK_COM("清洁口交", TNUM("口交"))
		LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
	ELSE
		LOCAL:1 = COM_ORDER_STAIN("Ｖ", "Ｍ")
	ENDIF
ELSEIF PENIS(PLAYER)
	LOCAL:1 = COM_ORDER_STAIN("Ｐ", "Ｍ")
ELSE
	LOCAL:1 = COM_ORDER_STAIN("Ｖ", "Ｍ")
ENDIF

;汚れ有
IF LOCAL:1
	LOCAL -= LOCAL:1
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({LOCAL:1})
ENDIF

;合計を表示(43以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 43

RETURN RESULT
