@COM120
;正常位奉仕
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 120

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%
;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM120, ARG
CALL FLAG_INSERTION, "Ｖ逆挿入", ARG, 100

@SOURCE_COM120, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 120

SOURCE:0:情愛 = 1800
SOURCE:0:性行動 = 1400
SOURCE:0:達成感 = 800

SOURCE:0:情愛 += CALCF("実行値による欲情追加")

;男性か扶她
IF PENIS(TARGET)
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 3)
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰靈活", PLAYER)
	;阴茎への刺激にはBOUNS
	SOURCE:0:Ｃ快楽 += 500
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM120
CALL SETFLAG, "下半身を脱がす", PLAYER
CALL SETFLAG, "下身衣装を露出：テ接吻ト没有", TARGET
;同時推倒
IF TEQUIP:Ｗ推倒
	;二穴挿入
	IF TRAIN_GENRE("Ａ逆挿入", SELECTCOM:1)
		;二人とも前回と同じ行動
		IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
			PRINTFORM 兩人
			IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
				PRINT 神情恍惚、眼神濕潤的對視著的同時
			ELSEIF RAND:2 == 0
				PRINT 如同完全沉浸於快感一樣無意識的漏出涎水的同時
			ELSE
				PRINT 好像很開心
			ENDIF
			PRINTFORMW 持續侵犯著%CALLNAME:MASTER%前後的淫穴……！
		;助手が前回と同じ行動
		ELSEIF SELECTCOM:1 == PREVCOM:1
			PRINTFORM %CALLNAME:TARGET%在%CALLNAME:(TARGET:1)%粗暴地蹂躪著腸道的時候、將%CALLNAME:MASTER%微微痙攣的雙腿用腰肢隔開、將
			IF TALENT:MASTER:処女
				PRINT 不识男人滋味的
			ELSE
				PRINT 被雌性汁液充分濡濕的
			ENDIF
			IF TALENT:PLAYER:娇小
				PRINT 用幼小裂缝
			ELSE
				PRINT 阴道
			ENDIF
			;假阳具
			IF PENIS(TARGET) == 0
				IF ABL:施虐属性 >= 5
					PRINTFORM 兇惡的
				ELSEIF ABL:施虐属性 >= 3
					IF TALENT:MASTER:扶她 || TALENT:MASTER:男性
						PRINTFORM 比%CALLNAME:MASTER%更加粗壯的
					ELSE
						PRINTFORM 粗壯的
					ENDIF
				ENDIF
			ELSE
				IF RAND:3 == 0
					PRINT 堅挺的立起的
				ELSEIF RAND:2 == 0 && TEQUIP:眼罩 == 0
					PRINT 堅硬的勃起的
				ELSE
					PRINT 不斷脈動的
				ENDIF
			ENDIF
			PRINTFORM %NAMES("阴茎")%、
			IF ABL:施虐属性 >= 3 && RAND:3 == 0
				PRINT 好像很開心
			ELSEIF ABL:施虐属性 && RAND:2 == 0
				PRINT 像是在享受痛苦的反應一樣粗暴地
			ELSEIF ABL:欲望 >= 3 && RAND:2 == 0
				PRINT 深深地
			ENDIF
			PRINTFORMW 侵犯起來……！
		;役割交換
		ELSEIF TRAIN_GENRE("Ｖ逆挿入", PREVCOM:1)
			PRINTFORMW %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%一齊拔出陰莖、將%CALLNAME:MASTER%癱軟的身體翻過來後、又開始享受被另一個人開闢過的濕軟淫穴……！
		ELSE
			PRINTFORM %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
			IF RAND:2 == 0
				PRINTFORM 像是在競爭誰更能給予快樂一樣激烈地
			ELSE
				PRINTFORM 為了給予更多快樂而配合的
			ENDIF
			PRINTFORMW 侵犯著%CALLNAME:MASTER%前後的淫穴……！
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%
		;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
		SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
			PRINTFORM 、%TEXTS("Ｗ推倒中")%、

		IF SELECTCOM == PREVCOM
			PRINTFORMW 將%CALLNAME:MASTER%濕潤的膣道隨心所欲地激烈侵犯著……
		ELSE
			PRINTFORMW 將%CALLNAME:MASTER%濕潤的膣道粗暴地開闢著……
		ENDIF
	ENDIF
ELSEIF TEQUIP:推倒
	IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		PRINTFORM %CALLNAME:TARGET%向%CALLNAME:PLAYER%
		IF TEQUIP:接吻
			PRINTFORM %TEXTS("接吻継続助詞付き")%、呼吸絮亂的
			CALL SETFLAG, "追加接吻フラグ", TARGET
		ELSEIF ABL:技巧 >= 4 && STATE("超短", TARGET) && RAND:2 == 0
			PRINTFORM 的膣道的最深處開闢並摳挖著的同時、恍惚而投入地
		ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
			PRINTFORM 的Ｇ點當做重點目標反復摩擦著的同時、完全忽視對方感覺地
		ELSE
			PRINTFORM 的膣道所帶來的快樂完全沉浸其中的同時、恍惚地
		ENDIF
		PRINTFORMW 持續侵犯起來……
	ELSE
		PRINTFORM %CALLNAME:TARGET%
		SIF ABL:施虐属性 >= 3
			PRINTFORM 浮現出嗜虐的邪笑、
		;假阳具
		IF PENIS(TARGET) == 0
			IF TRAIN_GENRE("Ａ逆挿入", PREVCOM)
				PRINTFORM 用被肛門中的渾濁污液弄髒的
			ELSEIF ABL:施虐属性 >= 5
				PRINTFORM 兇惡的
			ELSEIF ABL:施虐属性 >= 3
				IF TALENT:MASTER:扶她 || TALENT:MASTER:男性
					PRINTFORM 比%CALLNAME:MASTER%更加粗壯的
				ELSE
					PRINTFORM 粗壯的
				ENDIF
			ENDIF
		ELSE
			IF TRAIN_GENRE("Ａ逆挿入", PREVCOM)
				PRINTFORM 用被肛門中的渾濁污液弄髒的
			ELSEIF RAND:3 == 0
				PRINT 堅挺的立起的
			ELSEIF RAND:2 == 0 && TEQUIP:眼罩 == 0
				PRINT 堅硬的勃起的
			ELSE
				PRINT 不斷脈動的
			ENDIF
		ENDIF
		PRINTFORM %NAMES("阴茎")%將%CALLNAME:PLAYER%的
		IF TALENT:PLAYER:処女
			PRINT 不识男人滋味的
		ELSE
			PRINT 被雌性汁液充分濡濕的
		ENDIF
		IF TALENT:PLAYER:娇小
			PRINT 用幼小裂缝
		ELSE
			PRINT 阴道
		ENDIF
		IF ABL:施虐属性 >= 3 && RAND:3 == 0
			PRINT 好像很開心
		ELSEIF ABL:施虐属性 && RAND:2 == 0
			PRINT 像是在享受痛苦的反應一樣粗暴地
		ELSEIF ABL:欲望 >= 3 && RAND:2 == 0
			PRINT 深深地
		ENDIF
		IF ABL:施虐属性 >= 5 && RAND:2 == 0
			PRINTFORMW 蹂躪、凌辱著……
		ELSE
			PRINTFORMW 侵犯起來……
		ENDIF
	ENDIF
;前回と今回の調教者が同じで、前回の調教が正常位、逆強姦で有、不拔出フラグがある場合
ELSEIF INSERT("Ｖ正常位", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%
	;恋慕で追加
	IF (TALENT:恋慕 && RAND:3 == 0) || TEQUIP:接吻
		PRINTFORM %TEXTS("接吻継続")%
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ENDIF
	;「TARGETが真性の施虐狂か」「施虐属性持ちか」で分岐
	IF ABL:技巧 >= 4 && EXP:腰靈活経験 + ABL:腰靈活*10 >= 50 && RAND:4 == 0
		PRINTFORM 子宮用陰莖強行插入、將子宮口當成套在龜頭上的圓環一樣地
	ELSEIF ABL:技巧 >= 3 && EXP:腰靈活経験 + ABL:腰靈活*10 >= 30 && RAND:3 == 0
		PRINTFORM Ｇ点用阴茎的顶端抵住之后就像是在捣药一様冲撞并反复研磨起来地
	ELSEIF RAND:2 == 0
		PRINTFORM 各處敏感點特意避開、像是在享受渴求快樂的焦躁神情一樣地
	ELSE
		PRINTFORM 膣道的最深處像是打樁機一樣毫不留情、從不間歇地
	ENDIF
	PRINTFORMW 持續擺動腰肢、從%CALLNAME:PLAYER%的身體中索取著快樂……
;同様に前回の調教が後背位か騎乗位の場合
ELSEIF INSERT("Ｖ挿入", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORMW %CALLNAME:TARGET%依然在不斷侵犯%CALLNAME:PLAYER%的膣穴、併命令%CALLNAME:PLAYER%仰面躺下、用壓在身體上的姿勢猛烈地撞擊著膣穴的最頂端……
ELSEIF INSERT("Ａ挿入", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORMW %CALLNAME:TARGET%將在%CALLNAME:PLAYER%的腸道中不斷衝刺的陰莖拔出、又立刻插進了愛液四溢的膣道中、再次以面對面相互擁抱的姿勢侵犯著膣道的深處……
ELSE
	PRINTFORM %CALLNAME:PLAYER%将
	;腰つきだし
	IF ABL:PLAYER:欲望 + ABL:PLAYER:Ｖ感覚 + TALENT:PLAYER:淫乱 >= 8 && RAND:3 == 0
		SIF TEQUIP:推倒
			PRINT 仰面躺下、
		IF TALENT:PLAYER:淫乱 && RAND:3 == 0
			PRINTFORM 用雙腳將%CALLNAME:TARGET%的頭勾住並慢慢拉近、
		ELSEIF RAND:2 == 0
			PRINTFORM 用%NAMES("両腕", PLAYER)%將大腿抓住並將股間拉得更開、
		ELSE
			PRINTFORM 為了能讓%CALLNAME:TARGET%清楚的看見而突出腰部、
		ENDIF
	;大開脚
	ELSEIF ABL:PLAYER:欲望 + ABL:PLAYER:Ｖ感覚 + TALENT:PLAYER:淫乱 >= 5 && RAND:2 == 0
		IF RAND:2 == 0
			PRINTFORM 朝著%CALLNAME:TARGET%的方向誇張地張開雙腿、
		ELSEIF TEQUIP:浴室PLAY
			PRINTFORM 倚靠在浴盆邊緣將雙腿誇張地張開、
		ELSEIF TEQUIP:室内露出PLAY || TEQUIP:野外PLAY
			PRINTFORM 蹲坐在地上將雙腿誇張地張開、
		ELSE
			PRINTFORM 坐在床上將雙腿誇張地張開、
		ENDIF
	;開脚
	ELSE
		SIF TEQUIP:推倒
			PRINT 保持著被逆推的姿勢、
		IF TALENT:PLAYER:不知羞恥 && RAND:2 == 0
			PRINTFORM 、毫不猶豫地張開雙腿、將
		ELSEIF (TALENT:PLAYER:内気 || TALENT:PLAYER:坦率 || TALENT:PLAYER:害羞) && RAND:2 == 0
			PRINTFORM 、怯生生地將雙腿左右分開、將
		ELSE
			PRINTFORM 乖巧順從地張開雙腿、
		ENDIF
	ENDIF
	IF TEQUIP:眼罩 == 0
		;「害羞」で「内気」でない
		IF TALENT:PLAYER:害羞 && TALENT:PLAYER:内気 == 0 && RAND:3 == 0
			PRINT 臉頰帶著羞澀的紅暈的同時、
		;「小悪魔」で随机1/2
		ELSEIF TALENT:PLAYER:小悪魔 && RAND:2 == 0
			PRINT 在臉上露出戲謔的壞笑的同時、
		ELSEIF RAND:2 == 0
			PRINT 微笑着一邊
		;叛逆かつ淫乱
		ELSEIF TALENT:PLAYER:叛逆 && TALENT:PLAYER:淫乱 && RAND:2 == 0
			PRINT 偏過頭、臉頰被羞恥染紅的同時、
		;淫乱or欲情3000以上
		ELSEIF (TALENT:PLAYER:淫乱 || PALAM:欲情 >= 3000) && RAND:4 > 0
			PRINT 一邊妖艶的微笑著一邊
		ELSEIF TALENT:恋慕
			PRINTFORM 將%CALLNAME:TARGET%佈滿快樂的臉頰用不住上翻的眼珠仰視著同時、
		ENDIF
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW 将%TEXTS("阴部：視覚", PLAYER)%撥開、死乞白賴的懇求著。
	ELSE
		PRINTFORMW 邀请著%CALLNAME:TARGET%的侵犯。
	ENDIF
	PRINTFORM %CALLNAME:TARGET%欣然響應邀請、
	IF TALENT:PLAYER:処女 == 1
		PRINTFORMW 將%CALLNAME:PLAYER%從沒有接受過其他男性的処女雌穴用%NAMES("阴茎")%毫不留情的貫穿了……
	ELSE
		PRINTFORM %CALLNAME:PLAYER%
		SIF TALENT:恋慕
			PRINT 在用心醉的眼神注視著恍惚的面孔的同時、
		PRINTFORMW 用期待已久的膣穴接下了%NAMES("阴茎")%、開始揮舞腰肢…
	ENDIF
ENDIF


@COM_ORDER_COM120, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

IF PENIS(TARGET)
	LOCAL += 25
	LOCALS = %LOCALS% + 阴茎有些(25)
ENDIF
;贞操重视
IF TALENT:贞操重视
	LOCAL -= 10
	LOCALS = %LOCALS% - 贞操重视(10)
ENDIF
;不在乎貞操
IF TALENT:不在乎貞操
	LOCAL += 5
	LOCALS = %LOCALS% + 不在乎貞操(5)
ENDIF
;接受快感
IF TALENT:接受快感
	LOCAL += 2
	LOCALS = %LOCALS% + 接受快感(2)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;調教者が処女
IF EXP:PLAYER:Ｖ経験 == 0 && PLAYER != MASTER
	LOCAL -= 15
	LOCALS = %LOCALS% - %CALLNAME:PLAYER%が処女(15)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｖ", "Ｐ")/2
	LOCAL -= COM_ORDER_STAIN("Ｖ", "Ｐ")/2
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｖ", "Ｐ")/2})
ENDIF

;合計を表示(45以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 45

RETURN RESULT
