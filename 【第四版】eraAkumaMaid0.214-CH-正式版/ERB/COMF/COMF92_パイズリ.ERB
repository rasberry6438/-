@COM92
;乳交
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 92

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
IF CHECK_COM("乳夾口交")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 91
	CALL SAMEN_CHECK, 50
	SELECTCOM = 92
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM92, ARG
SIF CHECK_COM("乳夾口交") == 92
	CALL FLAG_COM91, 50
;対象のＢ⇔調教者のＰ的污垢が移動
CALL EQ_STAIN, TARGET, "Ｂ", PLAYER, "Ｐ"
SIF ASSIPLAY == 0 && EXP:口交経験 >= EXPLV:3
	TFLAG:好感度BOUNS += 1

@SOURCE_COM92, ARG
;乳夾口交
SIF CHECK_COM("乳夾口交") == 92
	CALL SOURCE_COM91, 50

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "乳交"

CALL SOURCE_DOWNBASE_HP, 92

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｂ")*40
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 1800
SOURCE:0:逸脱 = 300

;巨乳
SIF TALENT:巨乳
	SOURCE:0:逸脱 -= 200
;貧乳
SIF TALENT:貧乳
	SOURCE:0:逸脱 += 400

;ABL:Ｂ感覚をみる
SOURCE:0:Ｂ快楽 = CALCF("Ｂ刺激", 2)

SIF TALENT:Ｂ性向
	CALL CALC, "好みのPLAY", SOURCE:0:Ｂ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM92
IF TALENT:巨乳 && RAND:3 == 0
	LOCALS = %TOFULL(CONDS("罩杯尺寸") )%罩杯的%TALENT_NAME(GETNUM(TALENT, "巨乳"), TALENT:巨乳)%
ELSEIF TALENT:男性 == 0 && RAND:2
	LOCALS = 乳房
ELSE
	LOCALS = 胸
ENDIF
IF CHECK_COM("Ｗ奉仕")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 兩人
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT 神情恍惚、眼神濕潤的對視著的同時
		ELSEIF RAND:2 == 0
			PRINT 如同完全沉浸於快感一樣無意識的漏出涎水的同時
		ENDIF
		PRINTFORMW 將%CALLNAME:MASTER%挺立的陰莖用柔軟的乳肉夾住、持續擼動起來……
	ELSE
		PRINTFORM %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 + TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 在用充滿慾望與依戀的雙瞳凝視著%CALLNAME:MASTER%赤裸身體的同時、
		IF RAND:3 == 0
			PRINTFORMW 像是在競爭一樣的將陰莖用自己的胸部摩擦著……
		ELSEIF RAND:2 == 0
			PRINTFORMW 用胸部的谷間將陰莖夾住、刺激起來……
		ELSE
			PRINTFORMW 相互協助的將陰莖用胸部擼動起來……
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;同時推倒
ELSEIF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM 、%TEXTS("Ｗ推倒中")%、
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW 將%CALLNAME:MASTER%的陰莖用柔軟的乳肉夾住、持續熱情地摩擦著……
	ELSE
		PRINTFORMW 將%CALLNAME:MASTER%的陰莖用胸部夾住、持續摩擦著……
	ENDIF
;乳夾口交
ELSEIF CHECK_COM("乳夾口交")
	PRINTFORM %CALLNAME:TARGET%
	IF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM 神志恍惚地將%CALLNAME:PLAYER%的
	ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORM 凝视着%CALLNAME:PLAYER%的脸
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORM 用嫻熟的手法調節節奏將%CALLNAME:PLAYER%
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の
	ENDIF
	IF RAND:4 == 0
		PRINTFORM 龜頭用舌頭反復地舔舐著的同時、用
	ELSEIF RAND:3 == 0
		PRINTFORM 尿道用舌尖小心地刺激著、
	ELSEIF RAND:2
		PRINTFORM 陰莖用舌頭爬上旋轉的舔舐著的同時、用
	ELSE
		PRINTFORM 陰莖用口腔含住、緊緊吮吸著的同時、用
	ENDIF
	;妊娠
	IF TALENT:妊娠 && TALENT:貧乳 == 0 && RAND:3 == 0
		PRINT 因充滿母乳而鼓脹起來的
	;巨乳
	ELSEIF TALENT:巨乳
		;爆乳
		IF TALENT:巨乳 >= 2 && RAND:3 == 0
			PRINT 擁有沉甸甸重量感的
		ELSEIF RAND:2
			PRINT 富含彈性的
		ELSE
			PRINT 丰腴的
		ENDIF
	;貧乳
	ELSEIF TALENT:貧乳
		PRINT 小小的
		LOCALS = 山包
	ELSEIF TALENT:男性 == 0 
		IF RAND:3 == 0
			PRINT 具有恰當的彈性的
		ELSEIF RAND:2
			PRINT 有张力
		ELSE
			PRINT 形状好
		ENDIF
	ENDIF

	PRINTFORM %LOCALS%將陰莖
	IF TALENT:巨乳 >= 2 && RAND:2
		PRINTFORM 包裹住
	ELSEIF TALENT:貧乳 == 0 && TALENT:男性 == 0 
		PRINTFORM 夾住
	ENDIF
	PRINTFORMW 持續摩擦起來……
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%、
	IF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM 神情恍惚的緊緊凝視著%CALLNAME:PLAYER%的同時、將挺立的陰莖
	ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORM 密切關注著%CALLNAME:PLAYER%神情的同時、將挺立的陰莖
	ELSEIF ABL:技巧 >= 3 && RAND:2
		PRINTFORM 用嫻熟的手法調節節奏的同時、將%CALLNAME:PLAYER%挺立的陰莖
	ELSEIF ABL:精液中毒 >= 3 && RAND:2
		PRINTFORM 用飢渴而狂熱地熱烈動作將%CALLNAME:PLAYER%挺立的陰莖
	ELSE
		PRINTFORM 將%CALLNAME:PLAYER%挺立的陰莖
	ENDIF
	IF TALENT:貧乳 || TALENT:男性
		PRINTFORMW 用%LOCALS%貼緊並持續摩擦著……
	ELSE
		PRINTFORMW 用%LOCALS%持續擼動著……
	ENDIF
;前回は雙乳間抽插
ELSEIF PREVCOM == 81 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM 在%CALLNAME:PLAYER%停止腰肢的挺動、發出用胸部侍奉的要求之後、%CALLNAME:TARGET%
	;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
	IF ( (TALENT:恋慕 && ABL:奉仕精神 == 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
		IF RAND:2 == 0
			PRINT 滿臉幸福快樂的
		ELSE
			PRINT 用不住上翻的眼珠仰視著神情的同時、
		ENDIF
	;恋慕or献身的or奉仕精神3以上
	ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
		PRINT 帶著難以掩藏的開心表情、
	;強気
	ELSEIF TALENT:強気 && RAND:3
		PRINTFORM 用與平日裡強硬眼神截然不同的濕潤瞳孔凝視著%CALLNAME:PLAYER%的同時、
	ENDIF
	IF TALENT:貧乳 || TALENT:男性
		PRINTFORM 用貧瘠單薄的胸膛拼命地將陰莖
	ELSEIF TALENT:巨乳
		PRINTFORM 用豐腴飽滿的果實將挺立的陰莖包裹住、
	ELSE
		PRINTFORM 用%LOCALS%將挺立的陰莖裹挾著、
	ENDIF
	PRINTW 持續摩擦起來……

;气味弱点分岐
ELSEIF PLAYER == MASTER && TALENT:气味弱点 && TALENT:恋慕 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
	PRINTFORM %CALLNAME:TARGET%
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") && RANDIF(2, CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液") )
		IF RAND:2
			PRINTFORM 將%CALLNAME:PLAYER%挺立的陰莖
			IF TALENT:貧乳 || TALENT:男性
				PRINTFORMW 上黏著的恥垢所散發著的淫靡%TEXTS("气味")%興奮地深深嗅聞著的同時、用自己的胸部摩擦著刺激起來……
			ELSE
				PRINTFORMW 用自己的胸部夾住、在將濃郁恥垢所散發的淫靡%TEXTS("气味")%投入地深深嗅聞著的同時、用柔軟的乳肉擼動起來……
			ENDIF
		ELSE
			PRINTFORM 在將%CALLNAME:PLAYER%蒸騰著熱汽的股間所散發的%TEXTS("气味")%深深嗅聞著的同時、
			IF TALENT:貧乳 || TALENT:男性
				PRINTFORMW 將挺立的陰莖與自己貧瘠的胸膛緊貼、拼命地摩擦著侍奉起來……
			ELSE
				PRINTFORMW 將挺立的陰莖用自己柔軟的乳肉夾住、投入地擼動著侍奉起來……
			ENDIF
		ENDIF
	ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液") && RAND:2
		IF RAND:2
			PRINTFORM 將被精液弄髒的%CALLNAME:PLAYER%的陰莖
			IF TALENT:貧乳 || TALENT:男性
				PRINTFORMW 與身體緊貼、在將%TEXTS("气味")%氣味細嗅著漸漸興奮起來的同時、用胸部不斷摩擦著侍奉起來……
			ELSE
				PRINTFORMW 用胸部夾住、在將%TEXTS("气味")%氣味細嗅著漸漸興奮起來的同時、用胸部不斷摩擦著侍奉起來……
			ENDIF
		ELSE
			PRINTFORM 將%CALLNAME:PLAYER%那散發著精液%TEXTS("气味")%的髒污陰莖
			IF TALENT:貧乳 || TALENT:男性
				PRINTFORMW 與身體緊貼、帶著快樂而幸福的表情持續用胸部摩擦著侍奉起來……
			ELSE
				PRINTFORMW 用胸部夾住、帶著快樂而幸福的表情將胸部上下搖動著擼動起來……
			ENDIF
		ENDIF
	ELSEIF TALENT:貧乳 || TALENT:男性
		PRINTFORMW 將散發著濃郁的雄性氣味的陰莖用胸部持續侍奉著的同時、將蒸騰著熱汽的%CALLNAME:PLAYER%的股間所散發著的淫靡%TEXTS("气味")%深深嗅聞著並因此而興奮著……
	ELSE
		IF RAND:2
			PRINTFORMW 將散發著濃郁的雄性氣味的陰莖用胸部夾住並持續摩擦著的同時、將蒸騰著熱汽的%CALLNAME:PLAYER%的股間所散發著的淫靡%TEXTS("气味")%深深嗅聞著並因此而興奮著……
		ELSE
			PRINTFORMW 將%CALLNAME:PLAYER%挺立的陰莖用柔軟的乳肉擼動著的同時、也將不時抵近鼻尖的紫紅龜頭所散發著淫靡%TEXTS("气味")%深深嗅聞著……
		ENDIF
	ENDIF
;被逆推ているとき
ELSEIF TEQUIP:推倒
	IF TEQUIP:面部騎乗
		PRINTFORMW %CALLNAME:TARGET%跨坐在%CALLNAME:PLAYER%的臉頰上、將胸口移向股間、
	ELSE
		PRINTFORMW %CALLNAME:TARGET%將%CALLNAME:PLAYER%的下半身用雙手壓住、將胸口移向股間、
	ENDIF
	;淫乱or施虐狂or施虐属性Lv.5以上で「害羞」を持たない
	SIF (TALENT:淫乱 || TALENT:小悪魔 || ABL:施虐属性 >= 5) && TALENT:害羞 == 0
		PRINT 將滴落的晶瑩涎水塗抹上去充當潤滑劑的同時、
	PRINTFORM %CALLNAME:TARGET%
	;胸元が露出しているなら、そこではさむ
	IF CHECK_CLO("谷間露出") && RAND:3
		IF TEQUIP:六九式
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:貧乳
		ELSEIF TALENT:巨乳 && RAND:2 == 0
			PRINT 任由深邃的谷間被一覽無遺、
		ELSE
			PRINT 將胸前的風景大膽地暴露著、
		ENDIF
	;半脱られるのに、胸元を半脱ていないなら、ずらす
	ELSEIF CHECK_CLO("半脱可")
		IF CHECK_SHIRT("半脱可")
			PRINTFORM 將%SHIRT(TARGET, "略")%將捲起、
		ELSE
			PRINT 將胸前的衣襟敞開、
		ENDIF
		CALL SETFLAG, "胸を半脱る", TARGET
	ENDIF
	IF TALENT:貧乳 || TALENT:男性
		PRINT 用貧瘠單薄的胸膛將陰莖
	ELSEIF GETBITS(CFLAG:妊娠で巨乳化, 0, 1) && RAND:2 == 0
		PRINT 用與過去相比膨脹許多的胸部夾住陰莖、
	ELSEIF TALENT:巨乳 && RAND:2
		PRINT 用丰腴柔軟的乳肉將陰莖包裹住、
	ELSE
		PRINTFORM 用%LOCALS%將挺立的陰莖裹挾著、
	ENDIF
	PRINTW 擦好了……
;お浴室(胸は当然剥き出しと想定)
ELSEIF TEQUIP:浴室PLAY
	PRINTFORM %CALLNAME:TARGET%
	IF TALENT:貧乳 || TALENT:男性
		PRINTW 用塗滿了泡沫的單薄胸膛、拼命地將陰莖擦洗著……
	ELSEIF TALENT:巨乳
		PRINTW 用谷間充滿了泡沫的豐腴胸部、將陰莖直達根部的包住、揉搓著擦洗起來……
	ELSE
		PRINTW 將塗滿泡沫的綿軟胸部當做海綿、將陰莖夾住擦洗著……
	ENDIF
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%
	;胸元が露出しているなら、そこではさむ
	IF CHECK_CLO("谷間露出")
		IF TEQUIP:六九式
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:貧乳
		ELSEIF TALENT:巨乳 && RAND:2 == 0
			PRINT 任由深邃的谷間被一覽無遺、
		ELSE
			PRINT 將胸前的風景大膽地暴露著、
		ENDIF
	;半脱られるのに、胸元を半脱ていないなら、ずらす
	ELSEIF CHECK_CLO("半脱可")
		IF CHECK_SHIRT("半脱可")
			PRINTFORM 將%SHIRT(TARGET, "略")%將捲起、
		ELSE
			PRINT 將胸前的衣襟敞開、
		ENDIF
		CALL SETFLAG, "胸を半脱る", TARGET
	ENDIF
	IF TALENT:貧乳 || TALENT:男性
		PRINTFORM 用貧瘠單薄的胸膛拼命地將陰莖
	ELSEIF TALENT:巨乳
		PRINTFORM 用豐腴飽滿的果實將挺立的陰莖包裹住、
	ELSE
		PRINTFORM 用%LOCALS%將挺立的陰莖裹挾著、
	ENDIF
	PRINTW 擦好了……
ENDIF
;施虐属性LV3以上で尿道刺激、口球使用中は不可
SIF ABL:施虐属性 >= 3 && TEQUIP:口枷 == 0 && RAND:2 == 0
	PRINTFORMW 同時、將微微張合著的尿道口用舌尖小心而溫柔地刺激著……


@COM_ORDER_COM92, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 3
	LOCALS = %LOCALS% - 害羞(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;Ｂ性向
IF TALENT:Ｂ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｂ性向(10)
ENDIF
;男性
IF TALENT:男性 && TALENT:兩面通吃 == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - 男性(15)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｂ")})
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
