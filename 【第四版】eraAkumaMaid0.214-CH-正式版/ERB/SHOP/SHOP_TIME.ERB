;-------------------------------------------------
;SHOP.ERBのTINPUT関連の処理を関数化
;-------------------------------------------------
;-------------------------------------------------
;颜绘の动画のフレーム周りの処理
;-------------------------------------------------
@FLIRT_AND_ANIMATION
#DIM LCOUNT
#DIM PRANK
#DIM PRANK_COUNT

PRANK = 0
PRANK_COUNT = 1

;动画ーションONの場合の自动调戏初期設定
IF CONFIG("自动调戏動作中")
	SELECTCASE CONFIG("自动调戏")
	CASE 0
		PRANK = 201
	CASEELSE
		IF FLAG:なんかして欲しい
			SELECTCASE FLAG:なんかして欲しい
			CASE 3
				PRANK = CONFIG("自动调戏")*2 + 1
			CASE 2
				PRANK = CONFIG("自动调戏")*3 + 1
			CASEELSE
				PRANK = CONFIG("自动调戏")*5 + 1
			ENDSELECT
		ELSEIF STATE("酔酒", MASTER) || STATE("酔酒", TARGET)
			PRANK = CONFIG("自动调戏")*(RAND:6 + 5) + 1
		ELSE
			PRANK = CONFIG("自动调戏")*10 + 1
		ENDIF
	ENDSELECT
ENDIF

;フレーム数カウント初期値
FOR LCOUNT, 0, NUM_ANIME
	ANIME_FCOUNT:LCOUNT = RAND:20
NEXT

CALL FLIRT_AND_ANIMATION_MAIN, PRANK, PRANK_COUNT

@FLIRT_AND_ANIMATION_MAIN, PRANK, PRANK_COUNT
#DIM LCOUNT
#DIM PRANK
#DIM PRANK_COUNT
#DIM CHOICE

SELECTCASE NUM_ANIME
CASE IS >= 1
	TINPUT 100, 9999, 0, ""
;ここには来ないが念のため
CASEELSE
	INPUT
ENDSELECT

CHOICE = RESULT

SELECTCASE CHOICE
CASE 9999
	CLEARLINE 1
	SELECTCASE PRANK_COUNT
	CASE PRANK
		CALL EVENT_FLIRT
	CASEELSE
		FOR LCOUNT, 0, NUM_ANIME
			IF CFLAG:(ANIME_CHARA:LCOUNT):睡眠
				SELECTCASE ANIME_FCOUNT:LCOUNT
				CASE 40, 71
					CALL OVERLAY_GCREATE, LATESTFACEID:LCOUNT, @"%EXTSTR(@"%MAIDFACE_ANIME(ANIME_CHARA:LCOUNT, LCOUNT)%", "", ",")%"
				ENDSELECT
			ELSEIF CFLAG:(ANIME_CHARA:LCOUNT):刚睡醒 || STATE("困倦", ANIME_CHARA:LCOUNT)
				SELECTCASE ANIME_FCOUNT:LCOUNT
				CASE 76, 78, 81, 110, 111
					CALL OVERLAY_GCREATE, LATESTFACEID:LCOUNT, @"%EXTSTR(@"%MAIDFACE_ANIME(ANIME_CHARA:LCOUNT, LCOUNT)%", "", ",")%"
				ENDSELECT
			ELSE
				SELECTCASE ANIME_FCOUNT:LCOUNT
				;まばたき
				CASE 39, 40, 41, 42
					CALL OVERLAY_GCREATE, LATESTFACEID:LCOUNT, @"%EXTSTR(@"%MAIDFACE_ANIME(ANIME_CHARA:LCOUNT, LCOUNT)%", "", ",")%"
				;よそ見は保留
				;CASE 55, 69, 70, 71, 72, 80

				ENDSELECT
			ENDIF
		NEXT

		;フレームカウント処理
		FOR LCOUNT, 0, NUM_ANIME
			IF CFLAG:(ANIME_CHARA:LCOUNT):睡眠
				SELECTCASE ANIME_FCOUNT:LCOUNT
				CASE IS >= 71
					ANIME_FCOUNT:LCOUNT = RAND:20
				CASEELSE
					ANIME_FCOUNT:LCOUNT += 1
				ENDSELECT
			ELSEIF CFLAG:(ANIME_CHARA:LCOUNT):刚睡醒 || STATE("困倦", ANIME_CHARA:LCOUNT)
				SELECTCASE ANIME_FCOUNT:LCOUNT
				CASE IS >= 111
					ANIME_FCOUNT:LCOUNT = RAND:20
				CASEELSE
					ANIME_FCOUNT:LCOUNT += 1
				ENDSELECT
			ELSE
				SELECTCASE ANIME_FCOUNT:LCOUNT
				CASE IS >= 80
					ANIME_FCOUNT:LCOUNT = RAND:20
				CASEELSE
					ANIME_FCOUNT:LCOUNT += 1
				ENDSELECT
			ENDIF
		NEXT

		;自动调戏しない場合に放置しているとカウントが無限に増加するので１万毎に重置する
		;（PRANKの最大値は6001）
		PRANK_COUNT = PRANK_COUNT%10000 + 1
		RESTART
	ENDSELECT
CASE IS >= 0
	FLAG:なんかして欲しい = 0
	CALL USERSHOP_PRE, CHOICE
ENDSELECT


;-------------------------------------------------
;自动调戏のTINPUT関連の処理を関数化
;-------------------------------------------------
@FLIRT
#DIM CHOICE
IF CONFIG("自动调戏動作中")
	SELECTCASE CONFIG("自动调戏")
	CASE 0
		TINPUT 20000, 9999, 0, ""
	CASEELSE
		IF FLAG:なんかして欲しい
			SELECTCASE FLAG:なんかして欲しい
			CASE 3
				TINPUT CONFIG("自动调戏")*200, 9999, 0, ""
			CASE 2
				TINPUT CONFIG("自动调戏")*350, 9999, 0, ""
			CASEELSE
				TINPUT CONFIG("自动调戏")*500, 9999, 0, ""
			ENDSELECT
		ELSEIF STATE("酔酒", MASTER) || STATE("酔酒", TARGET)
			TINPUT CONFIG("自动调戏")*(RAND:501 + 500), 9999, 0, ""
		ELSE
			TINPUT CONFIG("自动调戏")*1000, 9999, 0, ""
		ENDIF
	ENDSELECT
ELSE
	INPUT
ENDIF
CHOICE = RESULT

SELECTCASE CHOICE
CASE 9999
	CLEARLINE 1
	SIF CONFIG("自动调戏動作中")
		CALL EVENT_FLIRT
CASE IS >= 0
	FLAG:なんかして欲しい = 0
	CALL USERSHOP_PRE, CHOICE
ENDSELECT


;-------------------------------------------------
;自动调戏関連の処理を関数化
;-------------------------------------------------
@EVENT_FLIRT
#DIM MEMO_TARGET
#DIM MEMO_LINECOUNT

MEMO_TARGET = TARGET

MEMO_LINECOUNT = LINECOUNT
;特定行動後の派生イベントなど(partner以外のイベントが起きる事もある)
IF RAND:2
	SETBIT FLAG:状況判定, 38
	CALL EVENT_DAILY_EX, DAILYNUM("卖个破绽"), "卖个破绽"
	CLEARBIT FLAG:状況判定, 38
ENDIF

IF LINECOUNT == MEMO_LINECOUNT
	PRINTL 
	TARGET = 0
	;発情期や躁动难忍している人優先
	SIF RAND:3 == 0
		TARGET = FIND_COND("発情期", "現在存在")
	SIF TARGET == 0 && RAND:3 == 0
		TARGET = FIND_COND("躁动难忍", "現在存在")
	SIF TARGET == 0
		TARGET = FIND_COND("現在存在")
	SETBIT FLAG:状況判定, 39

	;卖个破绽が動的化したので
	CALL USERSHOP_PRE, DAILY_時間経過イベント
	CLEARBIT FLAG:状況判定, 39
ENDIF

IF CONFIG("partnerの自動変更") && TARGET != MEMO_TARGET
	CALL SETFLAG, "対象指定", TARGET
ELSEIF FLAG:SELECT_USERSHOP == 0
	;partnerがイベントで居なくなった場合を除いて、partnerを復帰させる
	SIF COND("現在存在", MEMO_TARGET)
		TARGET = MEMO_TARGET
	SIF CONFIG("partnerの自動変更") && FIND_COND("現在存在")
		CALL SETFLAG, "対象指定", FIND_COND("現在存在")
ENDIF

FLAG:なんかして欲しい += 1
SIF FLAG:なんかして欲しい >= 4
	FLAG:なんかして欲しい = RAND:4
CVARSET CFLAG, GETNUM(CFLAG, "KOJO_DAILY済")
