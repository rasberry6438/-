;横幅全角60文字 半角120文字
;HtmlLineData：改行が必要かどうか判別するHTML文
;Xsize：表示するウィンドウの横幅サイズ　何文字で開業するかの識別符号
@D20XX_OS_Window_AutoNextLine(HtmlLineData,Xsize)
	#DIMS DYNAMIC HtmlLineData
	#DIM DYNAMIC Xsize

	;カウンター
	#DIM DYNAMIC Lcount
	#DIM DYNAMIC Lcount2

	;
	#DIM DYNAMIC TextCount
	#DIM DYNAMIC HtmlTagCount
	#DIMS DYNAMIC MEMO_RESULTS,5000

	#DIM DYNAMIC WindowTextWidth
	#DIMS DYNAMIC ActiveTags
	#DIMS DYNAMIC ClosedTags

	#DIMS DYNAMIC LocalResults

	IF PRINT_TUIMaidFace != ""
		Xsize -= 1000
	ENDIF

	TextCount = 0
	WindowTextWidth = Xsize/50 -2

	HTML_TAGSPLIT HtmlLineData
	HtmlTagCount = RESULT

	FOR Lcount,0,HtmlTagCount
		MEMO_RESULTS:LCOUNT = %RESULTS:LCOUNT%
	NEXT
	
	FOR Lcount,0,HtmlTagCount
		IF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 4) == "<img"
			;亜文字しか使わないという前提にしておく
			;颜绘も表示したい事有るだろうし、それは後で考えよう
			TextCount += 2
			LocalResults = %LocalResults%%MEMO_RESULTS:Lcount%
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 4) == "<!--"
			;コメントなので無視
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 4) == "<br>"
			;そもそも使用禁止なので存在しない事になっている
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 6) == "<shape"
			;図形描画
			LocalResults = %LocalResults%%MEMO_RESULTS:Lcount%
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 2) == "</"
			;タグが閉じられた時

			;ActiveTagの末尾にあるタグを削除する
			HTML_TAGSPLIT ActiveTags
			IF RESULT > 0
				ActiveTags = 
				FOR Lcount2,0,RESULT-1
					ActiveTags += RESULTS:Lcount2
				NEXT
			ENDIF

			;ClosedTagの先頭にあるタグを削除する
			HTML_TAGSPLIT ClosedTags
			IF RESULT > 0
				ClosedTags = 
				FOR Lcount2,1,RESULT
					ClosedTags += RESULTS:Lcount2
				NEXT
			ENDIF
			LocalResults = %LocalResults%%MEMO_RESULTS:Lcount%
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 1) == "<"
			;ActiveTagにタグを追加する
			LocalResults = %LocalResults%%MEMO_RESULTS:Lcount%
			ActiveTags = %ActiveTags%%MEMO_RESULTS:Lcount%

			;ClosedTagにタグを追加する
			SPLIT MEMO_RESULTS:Lcount," ",LOCALS
			LOCALS:0 = %REPLACE(LOCALS:0,"<","</")%
			IF STRFIND(LOCALS:0, ">") == -1	;タグに含まれる属性を削除する過程で>が失われるので、末尾に>を付けるかどうかの判別処理
				ClosedTags = %LOCALS:0%>%ClosedTags%
			ELSE 
				ClosedTags = %LOCALS:0%%ClosedTags%
			ENDIF
		ELSE
			STRLENFORM %MEMO_RESULTS:Lcount%
			TextCount += RESULT
			IF WindowTextWidth < TextCount
				TextCount -= RESULT
				;改行挿入位置から左側を取得
				LocalResults = %LocalResults%%SUBSTRING(MEMO_RESULTS:Lcount,0,(WindowTextWidth - TextCount))%%ClosedTags%
				VARSET RESULTS:0, ""
				RESULTS:0 = %LocalResults%

				;改行先の生成
				RESULTS:1 = %ActiveTags%%SUBSTRING(MEMO_RESULTS:Lcount,(WindowTextWidth - TextCount),-1)%
				FOR Lcount2,Lcount+1,HtmlTagCount+1
					RESULTS:1 += MEMO_RESULTS:Lcount2
				NEXT
				RETURN 1
			ELSE
				LocalResults = %LocalResults%%MEMO_RESULTS:Lcount%
			ENDIF
		ENDIF
	NEXT
	RETURN 0
;
@D20XX_OS_WindowFORM(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor = "#FFFFF" ,WindowColor = "#000070")
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID
	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor

	#DIM DYNAMIC Lcount
	#DIM DYNAMIC Lcount2
	
	#DIMS DYNAMIC Temp,100

	FOR Lcount,0,44
		CALL D20XX_OS_Window_AutoNextLine(D20XX_OS_WindowInput:Lcount,Xsize)
		IF RESULT
			;まずTempに避難させる
			FOR Lcount2,0,44
				Temp:Lcount2 = %D20XX_OS_WindowInput:Lcount2%
				;PRINTFORML Temp:{Lcount2} = %Temp:Lcount2%
			NEXT
			IF Lcount < 43
				D20XX_OS_WindowInput:Lcount = %RESULTS:0%
				D20XX_OS_WindowInput:(Lcount+1) = %RESULTS:1%
				;PRINTFORML RESULTS:0 = %RESULTS:0%
				;PRINTFORMW RESULTS:1 = %RESULTS:1%
			ENDIF
			FOR Lcount2,Lcount+2,44
				IF Lcount2 < 43
					D20XX_OS_WindowInput:Lcount2 = %Temp:(Lcount2-1)%
				ENDIF
			NEXT
		ENDIF
	NEXT
	RESULTS:0 = 
	RESULTS:1 = 

	IF PRINT_TUIMaidFace != ""
		FOR Lcount,0,8
			D20XX_OS_WindowInput:Lcount = <shape type='space' param='1000'>%D20XX_OS_WindowInput:Lcount%
		NEXT
		D20XX_OS_WindowInput:8 = %PRINT_TUIMaidFace%%D20XX_OS_WindowInput:8%
	ENDIF
	
	CALL D20XX_OS_Window(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor,WindowColor)
;WAIT付き

@D20XX_OS_WindowSTRFORMW(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor = "#FFFFF" ,WindowColor = "#000070")
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID
	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor

	CALL D20XX_OS_WindowSTRFORM(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor,WindowColor)
	WAIT 
@D20XX_OS_WindowSTRFORM(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor = "#FFFFF" ,WindowColor = "#000070")
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID
	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor

	
	#DIM DYNAMIC Lcount
	#DIM DYNAMIC Lcount2
	#DIMS DYNAMIC TempHtml

	VARSET LOCALS

	;文字列を分解する
	FOR Lcount, 0, 44
		SPLIT D20XX_OS_WindowInput:Lcount, "_", LOCALS 
		TempHtml = 
		;%HUNI("H")%%HUNI("HH")%%HUNI("BH")%%("BHH")%%HUNI("WH")%%HUNI("WHH")%%HUNI("BBH")%%HUNI("BBHH")%%HUNI("BWH")%%HUNI("BWHH")%%HUNI("UH")%%HUNI("／／／")%%HUNI("U扶她")%
		FOR Lcount2, 0, 100
			SELECTCASE LOCALS:Lcount2
				CASE "H", "HH", "BH", "BHH", "WH", "WHH", "BBH", "BBHH", "BWH", "BWHH", "UH", "／／／", "／", "///", "/", "U便当", "Uドラゴン", "U創口貼", "U男性", "U女性", "U扶她", "U伪娘"
					TempHtml += @"%HUNI(LOCALS:Lcount2)%"
				CASEELSE 
					TempHtml += @"%LOCALS:Lcount2%"
			ENDSELECT
		NEXT
		D20XX_OS_WindowInput:Lcount = %TempHtml%
	NEXT
	CALL D20XX_OS_WindowFORM(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor,WindowColor)

;
@D20XX_OS_Window(Xpos,Ypos,Xsize,Ysize,WindowID,LineColor = "#FFFFF" ,WindowColor = "#000070")
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID
	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor

	#DIMS DYNAMIC BaseViews,44
	#DIMS DYNAMIC MainHtml,44


	#DIM DYNAMIC Lcount
	
	VARSET RESULTS:0, ""
	FOR Lcount,0,44
		D20XX_OS_WindowsHTML:WindowID:Lcount =
	NEXT


	;PRINTL
	FOR Lcount,0,44
		BaseViews:Lcount = %HTML_GETPRINTEDSTR(44-Lcount)%
		CALL D20XX_NO_NOBR2(BaseViews:Lcount)
		BaseViews:Lcount = %RESULTS%
	NEXT
	CLEARLINE 45
	;CLEARLINE LINECOUNT

	;背景の青い部分
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<nonbutton pos ='{Xpos}'><shape type='rect' param='50,50,{Xsize},106' color='%WindowColor%' ></nonbutton>"
	FOR Lcount,Ypos+1,44
		SIF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'><shape type='rect' param='50,0,{Xsize},106' color='%WindowColor%' ></nonbutton>"
	NEXT
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<nonbutton pos ='{Xpos}'><shape type='rect' param='50,0,{Xsize},50' color='%WindowColor%' ></nonbutton>"
	
	;左の角
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<nonbutton pos='{Xpos}'>┏"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<nonbutton pos='{Xpos}'>┗"

	;上下の横線
	FOR Lcount,0,Xsize/100-1
		D20XX_OS_WindowsHTML:WindowID:Ypos += @"━"
		D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"━"
	NEXT

	CALL D20XX_OS_HTMLReInput(Xpos)
	
	FOR Lcount,Ypos+1,44
		IF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
			
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount-(Ypos+1))%"
			;D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount)%"
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos+Xsize}'>┃</nonbutton>"
			;PRINTFORML D20XX_OS_WindowInput:(Lcount-(Ypos+1)) = %D20XX_OS_WindowInput:(Lcount-(Ypos+1))%
		ENDIF
	NEXT
	;DRAWLINE
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"┓</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"┛</nonbutton>"

	FOR Lcount,0,44
		;PRINTFORML <nobr>%BaseViews:Lcount%%D20XX_OS_WindowsHTML:WindowID:Lcount%</nobr>
		HTML_PRINT @"<nobr>%BaseViews:Lcount%%D20XX_OS_WindowsHTML:WindowID:Lcount%</nobr>"
		;HTML_PRINT @"<nobr>%D20XX_OS_WindowsHTML:WindowID:Lcount%</nobr>"
	NEXT
	CALL D20XX_OS_WindowInput_Constructor

;

;;;上がコントロールバーのようになっている領帯プのウィンドウ
;;;Window
@D20XX_OS_WindowBar(Xpos,Ypos,Xsize,Ysize,WindowID,WindowControlBarName,WindowName,LineColor = "#FFFFF" ,WindowColor = "#000070",InputData)
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID

	#DIMS DYNAMIC WindowName
	#DIMS DYNAMIC WindowControlBarName
	#DIMS DYNAMIC BaseViews,44
	#DIMS DYNAMIC MainHtml,44

	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor
	#DIMS DYNAMIC InputData

	#DIM DYNAMIC Lcount
	
	VARSET RESULTS:0, ""
	FOR Lcount,0,46
		D20XX_OS_WindowsHTML:WindowID:Lcount = 
	NEXT

	;FOR Lcount,0,44
	;	BaseViews:Lcount = %HTML_GETPRINTEDSTR(44-Lcount)%
	;	CALL D20XX_NO_NOBR2(BaseViews:Lcount)
	;	BaseViews:Lcount = %RESULTS%
	;NEXT
	;CLEARLINE 44
	;CLEARLINE LINECOUNT

	;背景の青い部分
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,50,{Xsize},106' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	FOR Lcount,Ypos+1,44
		SIF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,0,{Xsize},106' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	NEXT
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,0,{Xsize},50' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	
	;左の角
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<nonbutton pos='{Xpos}'>┏"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"<nonbutton pos='{Xpos}'>┣"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<nonbutton pos='{Xpos}'>┗"

	;上下の横線
	FOR Lcount,0,Xsize/100-1
		D20XX_OS_WindowsHTML:WindowID:Ypos += @"━"
		D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"━"
		D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"━"
	NEXT
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"┓</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"┫</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"┛</nonbutton>"
	

	
	CALL D20XX_OS_HTMLReInput(Xpos)
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{Xpos+100}'>%WindowControlBarName%</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<button pos = '{Xpos+Xsize-200}' value= '{WindowID*100000+999}'>[<b>X</b>]</button>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{(Xpos+Xsize)}'>┃</nonbutton>"
	
	FOR Lcount,Ypos+3,44
		IF (Ypos+Ysize) > Lcount && Lcount == Ypos+3
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
			
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount-(Ypos+3))%"
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos+Xsize}'>┃</nonbutton>"
		ELSEIF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
			
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount-(Ypos+3))%"
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos+Xsize}'>┃</nonbutton>"
		ENDIF
	NEXT
	
	D20XX_OS_WindowsHTML:WindowID:44 = %WindowName%
	D20XX_OS_WindowsHTML:WindowID:45 = %InputData%

	;FOR Lcount,0,44
	;	HTML_PRINT @"<nobr>%BaseViews:Lcount%%D20XX_OS_WindowsHTML:WindowID:Lcount%</nobr>"
	;NEXT
	CALL D20XX_OS_WindowInput_Constructor
;;;上がコントロールバーのようになっている領帯プのモーダルウィンドウ
@D20XX_OS_ModalWindowBar(Xpos,Ypos,Xsize,Ysize,WindowID,WindowControlBarName,WindowName,LineColor = "#FFFFF" ,WindowColor = "#000070",InputData)
	#DIM DYNAMIC Xpos
	#DIM DYNAMIC Ypos
	#DIM DYNAMIC Xsize
	#DIM DYNAMIC Ysize
	#DIM DYNAMIC WindowID

	#DIMS DYNAMIC WindowName
	#DIMS DYNAMIC WindowControlBarName
	#DIMS DYNAMIC BaseViews,44
	#DIMS DYNAMIC MainHtml,44

	#DIMS DYNAMIC LineColor
	#DIMS DYNAMIC WindowColor
	#DIMS DYNAMIC InputData

	#DIM DYNAMIC Lcount
	
	;初期化
	VARSET RESULTS:0, ""
	FOR Lcount,0,46
		D20XX_OS_WindowsHTML:WindowID:Lcount = 
	NEXT

	;画面ロック用に背景を全て押下不能状態にする
	FOR Lcount,0,45
		D20XX_OS_WindowsHTML:WindowID:Lcount += @"<button pos ='0' value = '-1'><img src = 'ModalLock' height = '{110}' width = '{850*50}'></button>"
	NEXT

	;背景の青い部分
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,50,{Xsize},106' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	FOR Lcount,Ypos+1,44
		SIF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,0,{Xsize},106' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	NEXT
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<button pos ='{Xpos}' value = '-1'><shape type='rect' param='50,0,{Xsize},50' color='%WindowColor%' bcolor='%WindowColor%'></button>"
	
	;左の角
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"<nonbutton pos='{Xpos}'>┏"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"<nonbutton pos='{Xpos}'>┣"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"<nonbutton pos='{Xpos}'>┗"

	;上下の横線
	FOR Lcount,0,Xsize/100-1
		D20XX_OS_WindowsHTML:WindowID:Ypos += @"━"
		D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"━"
		D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"━"
	NEXT
	D20XX_OS_WindowsHTML:WindowID:Ypos += @"┓</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+2) += @"┫</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+Ysize) += @"┛</nonbutton>"
	

	
	CALL D20XX_OS_HTMLReInput(Xpos)
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{Xpos+100}'>%WindowControlBarName%</nonbutton>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<button pos = '{Xpos+Xsize-200}' value= '{WindowID*100000+999}'>[<b>X</b>]</button>"
	D20XX_OS_WindowsHTML:WindowID:(Ypos+1) += @"<nonbutton pos ='{(Xpos+Xsize)}'>┃</nonbutton>"
	
	FOR Lcount,Ypos+3,44
		IF (Ypos+Ysize) > Lcount && Lcount == Ypos+3
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
			
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount-(Ypos+3))%"
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos+Xsize}'>┃</nonbutton>"
		ELSEIF (Ypos+Ysize) > Lcount
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos}'>┃</nonbutton>"
			
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"%D20XX_OS_WindowInput:(Lcount-(Ypos+3))%"
			D20XX_OS_WindowsHTML:WindowID:Lcount += @"<nonbutton pos ='{Xpos+Xsize}'>┃</nonbutton>"
		ENDIF
	NEXT
	
	D20XX_OS_WindowsHTML:WindowID:44 = %WindowName%
	D20XX_OS_WindowsHTML:WindowID:45 = %InputData%

	;FOR Lcount,0,44
	;	HTML_PRINT @"<nobr>%BaseViews:Lcount%%D20XX_OS_WindowsHTML:WindowID:Lcount%</nobr>"
	;NEXT
	CALL D20XX_OS_WindowInput_Constructor
;
@D20XX_OS_HTMLReInput(Xpos)
	#DIM DYNAMIC Xpos

	#DIM DYNAMIC Ycount
	#DIM DYNAMIC LCount
	#DIM DYNAMIC HtmlTagCount
	#DIM DYNAMIC HtmlElementCount

	#DIMS DYNAMIC HtmlTag,500
	#DIM DYNAMIC HtmlTagNum
	#DIMS DYNAMIC HtmlElement,500
	#DIM DYNAMIC HtmlElementNum
	#DIMS DYNAMIC Temp

	;#region
		;HTML_PRINT RESULTS
		;D20XX_OS_WindowInput:0 += @"<nonbutton pos='1000'>テスト</nonbutton>"
		;D20XX_OS_WindowInput:1 += @"<nonbutton pos='1000'>テスト2</nonbutton>"
		;D20XX_OS_WindowInput:2 += @"<nonbutton pos='1000'>テスト3</nonbutton>"
		;D20XX_OS_WindowInput:3 += @"<nonbutton pos='1000'>テスト4</nonbutton>"
		;D20XX_OS_WindowInput:4 += @"<nonbutton pos='1000'>テスト5</nonbutton>"
		;D20XX_OS_WindowInput:5 += @"<nonbutton pos='1000'>テスト6</nonbutton>"
		;D20XX_OS_WindowInput:6 += @"<nonbutton pos='1000'>テスト7</nonbutton>"
		;D20XX_OS_WindowInput:7 += @"<nonbutton pos='1000'>テスト8</nonbutton>"
		;D20XX_OS_WindowInput:8 += @"%RESULTS%<nonbutton pos='1000'>テスト9</nonbutton>"
	;#endregion

	FOR Ycount, 0, 44
		HTML_TAGSPLIT D20XX_OS_WindowInput:Ycount
		HtmlTagNum = RESULT
		;PRINTFORML {Ycount} HtmlTagNum = {HtmlTagNum}
		
		;RESULTSにタグを格納しっぱ无だと今後困るのでHtmlTag配列に引っ越しさせる
		FOR LCount, 0, HtmlTagNum
			HtmlTag:LCount = %RESULTS:LCount%
		NEXT
		FOR HtmlTagCount, 0, HtmlTagNum
			;タグの要素に使用している符号が"の場合
			SPLIT HtmlTag:HtmlTagCount, "\"", HtmlElement
			HtmlElementNum = RESULT

			IF HtmlElementNum <= 1
				;タグの要素に使用している符号が"じゃなかったら'で切り分ける
				;混入してる場合？ 知らん
				SPLIT HtmlTag:HtmlTagCount, "\'", HtmlElement
				HtmlElementNum = RESULT
			ENDIF

			FOR HtmlElementCount, 0, HtmlElementNum
				STRLENFORM %HtmlElement:HtmlElementCount%

				;...は64ビット符号付き整数の範囲を超えています というエラー対策で一定の桁数以下の場合のみに制限
				IF RESULT < 19
					IF ISNUMERIC(HtmlElement:HtmlElementCount)
						IF HtmlElementCount > 0
							IF STRCOUNT(HtmlElement:(HtmlElementCount-1), "ypos") > 0
								PRINT 
							ELSEIF  STRCOUNT(HtmlElement:(HtmlElementCount-1), "pos") > 0
								Temp = %HtmlElement:HtmlElementCount%
								;HtmlElement:HtmlElementCount = %TOSTR(TOINT(Temp)+(Xpos+2)+100)%
								HtmlElement:HtmlElementCount = %TOSTR(TOINT(Temp)+(Xpos+2))%
							ENDIF
						ENDIF
					ENDIF
				ENDIF
			NEXT
			HtmlTag:HtmlTagCount = 
			
			;数値の箇所の頭とお尻に ' を入れる
			FOR Lcount, 0, HtmlElementNum
				SIF Lcount%2
					HtmlTag:HtmlTagCount += @"\'"

				HtmlTag:HtmlTagCount += @"%HtmlElement:Lcount%"
				
				SIF Lcount%2
					HtmlTag:HtmlTagCount += @"\'"
			NEXT
		NEXT

		D20XX_OS_WindowInput:Ycount = 
		
		;D20XX_OS_WindowInputに再格納する
		FOR Lcount, 0, HtmlTagNum
			D20XX_OS_WindowInput:Ycount += @"%HtmlTag:Lcount%"
		NEXT
	NEXT
;

;;;ウィンドウ表示にあたって、ypos=-845のスプライトが欲しいので変換する奴
@D20XX_OS_SpriteYposReverse(SpriteText)
	#DIMS DYNAMIC SpriteText
	
	IF STRCOUNT(SpriteText, "ypos") == 0
		REPLACE SpriteText, ">" , " ypos = '0'>"
		SpriteText = %RESULTS%
	ENDIF
	;なぜ二種類必用なんだ？？？？？
	IF STRFIND(SpriteText,"ypos = '200'") >= 1
		REPLACE SpriteText, "ypos = '200'", "ypos = '-845'"
	ELSEIF STRFIND(SpriteText,"ypos = '0'") >= 1
		REPLACE SpriteText, "ypos = '0'", "ypos = '-845'"
	ENDIF
;
@D20XX_NO_NOBR2(ARGS)
	#DIM LCOUNT
	#DIMS ResultHtml
	#DIM TagCount
	#DIMS DYNAMIC MEMO_RESULTS,5000
	#DIM MEMO_REDRAW

	MEMO_REDRAW = CURRENTREDRAW()

	REDRAW 0
	ResultHtml = 
	;HTML_PRINT CONVERTSTR(ARGS)
	;HTML_PRINT ARGS
	;LOCALS = %HTML_GETPRINTEDSTR(0)%
	;CLEARLINE 1
	HTML_TAGSPLIT ARGS
	TagCount = RESULT
	
	;PRINTFORMW HTML_TAGSPLIT ARGSやった結果のRESULT = {RESULT}
	FOR LCOUNT, 0, TagCount
		MEMO_RESULTS:LCOUNT = %RESULTS:LCOUNT%
	NEXT
	FOR LCOUNT, 0, TagCount
		IF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 5) == "<nobr"
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 6) == "</nobr"
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 2) == "<p"
		ELSEIF SUBSTRING(MEMO_RESULTS:LCOUNT, 0, 3) == "</p"
		ELSE
			ResultHtml += @"%MEMO_RESULTS:LCOUNT%"
		ENDIF
	NEXT
	RESULTS = %ResultHtml%
	;PRINTFORML %ResultHtml%
	REDRAW MEMO_REDRAW
;;;空白を詰める
@D20XX_OS_WindowsHTML_Filter(UnitListTab)
	#DIM DYNAMIC Lcount
	#DIM DYNAMIC LCount2
	#DIM DYNAMIC IsNull
	#DIM DYNAMIC writeIndex 
	#DIM DYNAMIC readIndex
	#DIMS DYNAMIC HtmlOutputText,44
	#DIMS DYNAMIC UnitListTab

	;Null詰め
	writeIndex = 0
	FOR readIndex,0,100
		IsNull = 0
		FOR Lcount,0,46
			IF D20XX_OS_WindowsHTML:readIndex:Lcount != ""
				IsNull = 1
			ENDIF
		NEXT
		IF IsNull == 1
			FOR Lcount,0,46
				D20XX_OS_WindowsHTML:writeIndex:Lcount = %D20XX_OS_WindowsHTML:readIndex:Lcount%
				;D20XX_OS_WindowsHTML:readIndex:Lcount = 
			NEXT
			IF writeIndex < readIndex
				FOR Lcount,0,46
					D20XX_OS_WindowsHTML:readIndex:Lcount = 
				NEXT
			ENDIF
			writeIndex++
		ENDIF
	NEXT

	;番号が変わってしまうので一旦再生成する
	FOR Lcount,0,writeIndex
		CALL RESET_RANDOMIZE
		CALL D20XX_OS_WindowInput_Constructor
		VARSET LOCALS
		SPLIT D20XX_OS_WindowsHTML:Lcount:45, "D20XXSPLIT", LOCALS
		;TRYCALLFORM %LOCALS:0%, TOSTR(LOCALS:0),TOSTR(LOCALS:0),TOSTR(LOCALS:0),TOSTR(LOCALS:0)
		
		
		;SELECTCASE D20XX_OS_WindowsHTML:Lcount:44
		;	CASE "ショップベース画面"
		;		CALL D20XX_Shop_BaseWindow(1,1,Lcount,"#000070")
		;	CASE "ユ针织新規購入画面"
		;		CALL D20XX_Shop_UnitListWindow(UnitListTab,80,2,Lcount,"#700000")
		;	CASE "リーダー泳池画面"
		;		CALL D20XX_LeaderInventory(200,2,Lcount,"#007000")
		;	CASE "リーダー新規雇用画面"
		;		CALL D20XX_LeaderEmployment(200,3,Lcount,"#007070")
		;	CASE "リーダー割当確認画面"
		;		
		;	CASE "リーダー解雇確認画面"
		;ENDSELECT
		SELECTCASE D20XX_OS_WindowsHTML:Lcount:44
			CASE "ショップベース画面"
				CALL D20XX_Shop_BaseWindow(TOINT(LOCALS:0),TOINT(LOCALS:1),Lcount,LOCALS:3)
			CASE "ユ针织新規購入画面"
				;CALL D20XX_Shop_UnitListWindow(UnitListTab,80,2,Lcount,"#700000")
				;PRINTFORMW %LOCALS:0%,{TOINT(LOCALS:1)},{TOINT(LOCALS:2)},{Lcount},%LOCALS:4%
				;%ShopSelectingUnitID% %SelectLeftTab% %SelectingUnit% {Xpos} {Ypos} {WindowID} %WindowColor%
				CALL D20XX_Shop_UnitListWindow(TOINT(LOCALS:0),LOCALS:1,LOCALS:2,TOINT(LOCALS:3),TOINT(LOCALS:4),Lcount,LOCALS:6)
			CASE "リーダー泳池画面"
				CALL D20XX_LeaderInventory(TOINT(LOCALS:0),TOINT(LOCALS:1),TOINT(LOCALS:2),Lcount,LOCALS:4)
			CASE "リーダー新規雇用画面"
				CALL D20XX_LeaderEmployment(TOINT(LOCALS:0),TOINT(LOCALS:1),Lcount,LOCALS:3)
				
			CASE "リーダー割当確認画面"
				CALL D20XX_Shop_LeaderAssignWin(TOINT(LOCALS:0),TOINT(LOCALS:1),TOINT(LOCALS:2),TOINT(LOCALS:3),Lcount,LOCALS:5)
			CASE "リーダー解雇確認画面"
				CALL D20XX_Shop_LeaderDeleteConfirm(TOINT(LOCALS:0),TOINT(LOCALS:1),TOINT(LOCALS:2),Lcount,LOCALS:4)
		ENDSELECT
	NEXT

	CALL D20XX_OS_WindowsHTML_Output

@D20XX_OS_WindowsHTML_GetLastWindowID
	#FUNCTION
	#DIM DYNAMIC LCount
	FOR Lcount,0,100
		SIF D20XX_OS_WindowsHTML:Lcount:44 == ""
			RETURNF Lcount
	NEXT

@D20XX_OS_WindowsHTML_Output
	#DIM DYNAMIC LCount
	#DIM DYNAMIC LCount2
	#DIMS DYNAMIC HtmlOutputText,44

	VARSET HtmlOutputText:0,""
	FOR LCount,0,100
		FOR LCount2,0,44
			HtmlOutputText:LCount2 += @"%D20XX_OS_WindowsHTML:LCount:LCount2% "
		NEXT
	NEXT
	
	FOR LCount,0,44
		;HTML_PRINT @"<nobr>%D20XX_OS_Base:LCount%%D20XX_OS_WindowsHTML:WindowID:LCount% </nobr>"
		HTML_PRINT @"<nobr>%HtmlOutputText:LCount%</nobr>"
	NEXT
@D20XX_OS_WindowsHTML_SearchWin(SerachWinName)
	#FUNCTION
	#DIM DYNAMIC LCount
	#DIMS DYNAMIC SerachWinName
	FOR Lcount,0,100
		SIF D20XX_OS_WindowsHTML:LCount:44 == SerachWinName
			RETURNF Lcount
	NEXT
	RETURNF -1

