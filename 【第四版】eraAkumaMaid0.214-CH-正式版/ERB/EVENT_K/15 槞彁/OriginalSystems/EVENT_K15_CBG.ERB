;-------------------------------------------------
;POPUP_SPRIT, str spriteName, int x, int y, int width, int height
;ウィンドウの指定した位置に画像を表示する
;ARGS:0にはスプライトの名前、重ねて表示したい場合は"+"で区切る
;幅と高さの指定がない場合、最初に認識したスプライトの尺寸を参考にする
;-------------------------------------------------
@K15_POPUP_SPRIT, ARGS, GRAPHICS_XPOS, GRAPHICS_YPOS, GRAPHICS_WIDTH, GRAPHICS_HEIGHT
	#DIM DYNAMIC LCOUNT
	#DIM DYNAMIC SPLIT_COUNT
	#DIM DYNAMIC GRAPHICS_ID
	#DIM DYNAMIC GRAPHICS_XPOS
	#DIM DYNAMIC GRAPHICS_YPOS
	#DIM DYNAMIC GRAPHICS_WIDTH
	#DIM DYNAMIC GRAPHICS_HEIGHT

	GRAPHICS_ID = POPUP_SETG_COUNT + 30
	SIF GRAPHICS_ID >= 40
		RETURN 0

	;画像の作成
	CALL OVERLAY_GCREATE, GRAPHICS_ID, ARGS, GRAPHICS_WIDTH, GRAPHICS_HEIGHT
	SIF RESULT == 0		;作成失敗の場合は中断
		RETURN 0

	;PRINTFORML Debug 301 SPRITECREATED("BMP_WAINDOW") = {SPRITECREATED("BMP_WAINDOW")}
	;ボタンMapが作成されていないなら作成
	SIF SPRITECREATED("BMP_WAINDOW") == 0
		CALL BMP_WINDOW_CREATE, 0x00000000

	;CBGSETG GRAPHICS_ID, GRAPHICS_XPOS, GRAPHICS_YPOS, Z_DEPTH
	CBGSETG GRAPHICS_ID, GRAPHICS_XPOS, GRAPHICS_YPOS, 2

	Z_DEPTH -= 1
	POPUP_SETG_COUNT += 1

	RETURN 1


;-------------------------------------------------
;POPUP_BUTTON, str spriteName or str cARGB, int x, int y, int width, int height
;ARGS:0に入力したものがマウスカーソルを合わせると表示される
;スプライトの名前、もしくは"0xAARRGGBB"の形で色を入力する、空にするとなにも不顯示
;INPUTMOUSEKEYで返す値はボタンの作成した順番(0、1、2)、ボタンの配置が被ったりするとあとから作成した物が優先される
;@POPUP_SPRITの位置とサイズを合わせるようにするとよい
;-------------------------------------------------
@K15_POPUP_BUTTON, ARGS, BUTTON_XPOS, BUTTON_YPOS, BUTTON_WIDTH, BUTTON_HEIGHT
	#DIM DYNAMIC LCOUNT
	#DIM DYNAMIC SPLIT_COUNT
	#DIM DYNAMIC BUTTON_ID
	#DIM DYNAMIC BUTTON_VALUE
	#DIM DYNAMIC BUTTON_XPOS
	#DIM DYNAMIC BUTTON_YPOS
	#DIM DYNAMIC BUTTON_WIDTH
	#DIM DYNAMIC BUTTON_HEIGHT
	#DIMS DYNAMIC BUTTON_NAME

	BUTTON_ID = POPUP_SETB_COUNT + 40
	SIF BUTTON_ID >= 50
		RETURN 0
	BUTTON_VALUE = POPUP_SETB_COUNT
	BUTTON_NAME = G_BUTTON%TOSTR(POPUP_SETB_COUNT, "D2")%


	;マウスオーバー時の画像の作成
	CALL OVERLAY_GCREATE, BUTTON_ID, ARGS, BUTTON_WIDTH, BUTTON_HEIGHT
	SIF RESULT == 0		;作成失敗の場合は中断
		RETURN 0

	;スプライトを作成
	SPRITECREATE BUTTON_NAME, BUTTON_ID

	;ボタンMapが作成されていないなら作成
	SIF SPRITECREATED("BMP_WAINDOW") == 0
		CALL BMP_WINDOW_CREATE, 0x00000000

	;ボタンMapを解除
	CBGREMOVEBMAP


	;BMAPにボタンの位置を書き込む
	GSETBRUSH 11, 0xFF000000 + BUTTON_VALUE
	GFILLRECTANGLE 11, BUTTON_XPOS, (Y_VALIGN(BUTTON_HEIGHT, "上") * -1) + BUTTON_YPOS, BUTTON_WIDTH, BUTTON_HEIGHT		;GFILLRECTANGLEは左上基準なのでYの位置を補正する

	;ボタンMap再表示
	CBGSETBMAPG 11

	;ボタン用の画像を表示
	CBGSETBUTTONSPRITE BUTTON_VALUE, "", BUTTON_NAME, BUTTON_XPOS, BUTTON_YPOS, Z_DEPTH

	Z_DEPTH -= 1
	POPUP_SETB_COUNT += 1

	RETURN 1