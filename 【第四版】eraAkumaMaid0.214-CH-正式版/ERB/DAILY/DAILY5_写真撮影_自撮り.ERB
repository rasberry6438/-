;-------------------------------------------------
;写真撮影の自撮りイベントだけ分離
;製作者:hexagon
;-------------------------------------------------
;自撮り写真生成
;ARGで自撮りを行う人物を一人指定、ARGSで自撮りシチュを指定できる
;-------------------------------------------------
@PHOTO_SELFY, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_TARGET

SAVESTR:自撮り写真 =

SIF COND("夜") || COND("体育") || COND("水泳") || CHARA_NUM("自撮り可能") == 0
	RETURN 0

MEMO_TARGET = TARGET
TARGET = 0

FOR LCOUNT, 0, 10
	SELECTCASE ARGS
	CASE "阴部"
		TARGET = FIND_COND("自撮り可能", "阴部自撮り可能")
	CASE "胖次"
		TARGET = FIND_COND("自撮り可能", "胖次自撮り可能")
	CASEELSE
		TARGET = FIND_COND("自撮り可能")
	ENDSELECT

	SIF LCOUNT == 0 && ARG
		TARGET = ARG
	SIF TARGET == 0
		BREAK
	CALL PHOTO_SELFY_MAIN, ARGS
NEXT

TARGET = MEMO_TARGET

;-------------------------------------------------
;ARGSで自撮りの種類を指定できる
;-------------------------------------------------
@PHOTO_SELFY_MAIN, ARGS
#DIMS CLOTHES_SELFY
#DIMS CLOTHES_TABLE
#DIMS ACT_TABLE
#DIM LCOUNT
;換装前の衣装の記録
#DIMS MEMO_CLO, 50
#DIM MEMO_EQUIP, 50
#DIM MEMO_STAIN, 20

;着ていた服と汚れを記憶する
FOR LCOUNT, 40, 50
	MEMO_CLO:LCOUNT = %CSTR:LCOUNT%
NEXT
FOR LCOUNT, 40, 50
	MEMO_EQUIP:LCOUNT = EQUIP:LCOUNT
NEXT
FOR LCOUNT, 10, 20
	MEMO_STAIN:LCOUNT = STAIN:LCOUNT
NEXT

;自撮り中フラグON
SETBIT FLAG:状況判定, 41

;どの服装で撮るかを選ぶ
;部屋に一人でいる場合は居家服で撮る
CLOTHES_TABLE = 

SIF COND("今年の泳装選択済") && COND("夜")
	CLOTHES_TABLE += "今年の泳装/"
;裸体　泳装から便装に着替えた場合も撮るかも
SIF COND("夜") + CFLAG:水泳
	CLOTHES_TABLE += "裸体/"
;内衣　体操服から便装に着替えた場合も撮るかも
SIF COND("夜") + CFLAG:体育 && EQUIP:胖次
	CLOTHES_TABLE += "内衣/"

;体操服
IF COND("体育") && CHECK_CLO("体操服姿")
	CLOTHES_TABLE += "体操服/"
;水泳
ELSEIF COND("水泳") && CHECK_CLO("泳装姿")
	CLOTHES_TABLE += "泳装/"
;CSVに設定されてる服
ELSE
	IF CHECK_CLO("裸体")
		CLOTHES_TABLE += "裸体/"
	ELSEIF CHECK_CLO("内衣姿")
		CLOTHES_TABLE += "内衣/"
	ELSEIF CHECK_CLO("学生服姿")
		CLOTHES_TABLE += "学生服/"
	ELSEIF CFLAG:Cosplay中
		CLOTHES_TABLE += "Cosplay/"
	ELSE
		CLOTHES_TABLE += "便装/"
	ENDIF
ENDIF

;格好ごとに何しながら撮るかを決める
CLOTHES_SELFY = %TEXTR(CLOTHES_TABLE)%
ACT_TABLE = 

SELECTCASE CLOTHES_SELFY
CASE "泳装"
	ACT_TABLE += "全身自撮り/"
	IF CHECK_CLO("比基尼姿")
		ACT_TABLE += "セパレート写真/"
	ELSE
		ACT_TABLE += "泳装写真：連衣裙/"
	ENDIF
	SIF CFLAG:水泳
		ACT_TABLE += "授業直後：泳装姿/"
CASE "体操服"
	ACT_TABLE += "チラ見せ写真：上/チラ見せ写真：下/全身自撮り/"
	SIF CFLAG:体育
		ACT_TABLE += "授業直後：体操服姿/"
CASE "学生服"
	ACT_TABLE += "チラ見せ写真：上/"
	SIF CHECK_CLO("胖次視認可能")
		ACT_TABLE += "チラ見せ写真：下/" 
CASE "今年の泳装"
	ACT_TABLE += "泳装写真：今年の泳装/全身自撮り/"
CASE "内衣"
	;变成内衣姿态
	CALL SETFLAG, "变成内衣姿态", TARGET
	ACT_TABLE += "セパレート写真/全身自撮り/"
	SIF CFLAG:体育
		ACT_TABLE += "授業直後：内衣姿/"
CASE "裸体"
	;脱成裸体
	CALL SETFLAG, "扒成裸体", TARGET
	ACT_TABLE += "ヌード写真/全身自撮り/"
	SIF CFLAG:水泳
		ACT_TABLE += "授業直後：裸体/"
CASE "Cosplay"
	ACT_TABLE += "全身自撮り/チラ見せ写真：下/"
	SIF EQUIP:服
		ACT_TABLE += "チラ見せ写真：上/"
CASE "便装"
	ACT_TABLE += "チラ見せ写真：下/"
	SIF EQUIP:服
		ACT_TABLE += "チラ見せ写真：上/"
ENDSELECT


STR:写真 = 
STR:補足 = 

SELECTCASE TEXTR(ACT_TABLE)
CASE "授業直後：泳装姿"
	STR:写真 += @"游泳课後頭发湿透的、%PHOTO_COSTUME(TARGET)%"
CASE "授業直後：裸体"
	STR:写真 += @"游泳课後頭发湿透的、一丝不挂的样子の"
CASE "授業直後：体操服姿"
	STR:写真 += @"体育课後\@ COND("夏") ? 全身是 # 出了少量 \@汗的%PHOTO_COSTUME(TARGET)%"
CASE "授業直後：内衣姿"
	STR:写真 += @"体育课後\@ COND("夏") ? 全身是 # 出了少量 \@汗的内衣姿の"
CASE "泳装写真：今年の泳装"
	IF RAND:3 == 0
		STR:写真 += @"穿着%SWIM(CSTR:今年の泳装)%、仰身挺着大胸の"
	ELSEIF RAND:2
		STR:写真 += @"屁股对着镜子、穿着%SWIM(CSTR:今年の泳装)%回頭の"
	ELSE
		STR:写真 += @"将%SWIM(CSTR:今年の泳装)%移開\@ TALENT:男性 ? 阴茎 # 阴部 \@露出来の"
	ENDIF
CASE "泳装写真：連衣裙"
	IF RAND:3 == 0
		STR:写真 += @"穿着%BODYS(TARGET)%、挺胸身體後仰の"
	ELSEIF RAND:2
		STR:写真 += @"屁股对着镜子、摆好%BODYS(TARGET)%姿用智能手机拍照の"
	ELSE
		STR:写真 += @"将%BODYS(TARGET)%移開\@ TALENT:男性 ? 阴茎 # 阴部 \@露出来の"
	ENDIF
CASE "セパレート写真"
	IF RAND:2 || EQUIP:胸罩 == 0
		IF CHECK_PANTIES("可以移開")
			STR:写真 += @"%PANTIES(TARGET)%%TEXTR("脱到小腿附近/脱到能看到大腿的状态")%の"
		ELSE
			STR:写真 += @"%PANTIES(TARGET)%和大腿在同一画面の"
		ENDIF
		STR:補足 += "_胖次"
		STR:補足 = _%SAVE_GRAPHICSDATE( CUTIN("胖次正面", TARGET, 0) )%%STR:補足%
	ELSE
		STR:写真 += @"%BRA(TARGET)%向上卷起、%TEXTR("右/左/＿")%\@ TALENT:男性 ? 乳頭 # 乳房 \@露出の"
		;補足一個追加させてください…
		STR:補足 += "_胸部"
	ENDIF
CASE "チラ見せ写真：上"
	IF CHECK_SHIRT("提起裙角")
		STR:写真 += @"%TEXTR(@"%SHIRT(TARGET)%向上卷起/用嘴叼着%SHIRT(TARGET)%下摆")%、"
	ELSEIF CHECK_SHIRT("女式衬衫")
		STR:写真 += @"%SHIRT(TARGET)%的纽扣全部解開、"
	ELSE
		STR:写真 += @"%SHIRT(TARGET)%半脱、"
	ENDIF
	IF EQUIP:胸罩 == 0
		STR:写真 += @"\@ TALENT:男性 ? 胸 # 胸部 \@露出"
		STR:補足 += "_胸部"
	ELSEIF CHECK_BRA("乳頭露出")
		STR:写真 = @"%SHIRT(TARGET)%向上卷起"
		IF CHECK_BRA("１／４杯胸罩")
			STR:写真 += @"可以透过%BRA(TARGET)%看到胸部の"
		ELSE
			STR:写真 += @"穿着%BRA(TARGET)%乳頭露出の"
		ENDIF
		STR:補足 += "_胸罩"
	ELSE
		STR:写真 += @"%BRA(TARGET)%露出の"
		STR:補足 += "_胸罩"
	ENDIF
CASE "チラ見せ写真：下"
	;部屋に鏡あるのかな？
	SIF COND("夜")
		STR:写真 += "在镜子前"
	IF EQUIP:裙 && (COND("昼") || RAND:3 == 0)
		STR:写真 += @"%CLO("掀起裙子")%掀起"
	ELSEIF COND("脚が無い")
		STR:写真 += "坐下"
	ELSEIF RAND:2
		STR:写真 += "Ｍ字開腿"
	ELSE
		STR:写真 += "体育座"
	ENDIF
	SIF CHECK_CLO("连褲襪露出")
		STR:写真 += @"透过%BOTTOM(TARGET)%能看到"
	IF EQUIP:胖次 == 0
		IF COND("身嗜み：Ｖ拡張")
			STR:写真 += "振動棒挿入的"
		ELSEIF CHECK_CLO("Ｖ露出")
			STR:写真 += "赤裸的"
		ENDIF
		IF PENIS(TARGET)
			STR:写真 += "阴茎"
			STR:補足 += "_阴茎"
		ELSE
			STR:写真 += "阴部"
			STR:補足 += "_阴部"
		ENDIF
	ELSE
		STR:写真 += @"%PANTIES(TARGET)%"
		STR:補足 += "_胖次"
		STR:補足 = _%SAVE_GRAPHICSDATE( CUTIN("胖次正面/下身衣装OFF", TARGET, 0) )%%STR:補足%
	ENDIF
	STR:写真 += @"露出の%PHOTO_COSTUME(TARGET)%"
CASE "ヌード写真"
	STR:写真 += @"一丝不挂地在\@ COND("昼") ? 更衣室的 # \@镜子前"
	IF RAND:2 && COND("夜")
		STR:写真 += @"单膝跪坐、\@ TALENT:男性 ? 阴茎 # 阴部 \@"
		IF RAND:2
			STR:写真 += "用一只手捂着"
		ELSE
			STR:写真 += "暴露在外"
		ENDIF
	ELSE
		STR:写真 += "站着、"
		IF RAND:2 && TALENT:男性 == 0
			STR:写真 += "一只手遮着胸の"
		ELSE
			STR:写真 += @"手指像是强调一样指着乳頭の"
		ENDIF
	ENDIF
;この辺のは衣装がえろくないとえろくないかも…
CASE "全身自撮り"
	SELECTCASE CLOTHES_SELFY
	CASE "裸体"
		STR:写真 += "一丝不挂、"
	CASE "今年の泳装"
		STR:写真 += @"身穿%SWIM(CSTR:今年の泳装)%"
	CASE "内衣"
		IF EQUIP:胸罩 && EQUIP:胖次
			STR:写真 += @"%CLO_ABOUT(TARGET, "姿")%"
			STR:補足 += "_胸罩"
		ELSEIF EQUIP:胸罩
			STR:写真 += @"穿着%BRA(TARGET)%下半身裸的状態"
			STR:補足 += "_阴部"
		ELSE
			STR:写真 += @"%PANTIES(TARGET)%穿在身上"
			STR:補足 += "_胖次"
			STR:補足 += "_胸部"
		ENDIF
	ENDSELECT
	IF RAND:2 
		STR:写真 += @"在\@ COND("昼") ? 更衣室的 # \@镜子前用智能手机拍照の"
	ELSE
		STR:写真 += @"%TEXTR("一只手遮住眼睛/在脸旁边摆手势/抬頭看着镜頭")%の"
	ENDIF
	SELECTCASE CLOTHES_SELFY
	CASE "体操服", "泳装", "Cosplay"
		STR:写真 += @"%PHOTO_COSTUME(TARGET)%"
	ENDSELECT
ENDSELECT
STR:写真 += @"%CALLNAME:TARGET%"


;場合によっては胖次の自撮りとか
IF COND("阴部自撮り可能") && ARGS == "阴部"
	CALL PHOTO_PUSSY
	STR:写真 += "の自拍"
ELSEIF COND("胖次自撮り可能") && (RAND:2 || ARGS == "胖次")
	CALL PHOTO_PANTIES
	STR:写真 += "の自拍"
ELSE
	SIF CLOTHES_SELFY == "今年の泳装"
		STR:補足 += "_今年の泳装"
	SIF CLOTHES_SELFY == "泳装"
		STR:補足 += "_泳装"
	SIF CLOTHES_SELFY == "体操服"
		STR:補足 += "_体操服"
	SIF CLOTHES_SELFY == "学生服"
		STR:補足 += "_学生服"
ENDIF


;自撮りタグ付ける
STR:補足 += "_自撮り"

;人物の補足
STR:補足 += @"_%CALLNAME:TARGET%"
;日期の記録
STR:補足 += @"_日期%TOSTR(DAY*10 + TIME)%"

SAVESTR:自撮り写真 += @"%STR:写真%%STR:補足%/"


;自撮り中フラグOFF
CLEARBIT FLAG:状況判定, 41

;着ていた服と汚れを戻す
FOR LCOUNT, 40, 50
	CSTR:LCOUNT = %MEMO_CLO:LCOUNT%
NEXT
FOR LCOUNT, 40, 50
	EQUIP:LCOUNT = MEMO_EQUIP:LCOUNT
NEXT
FOR LCOUNT, 10, 20
	STAIN:LCOUNT = MEMO_STAIN:LCOUNT
NEXT


;-------------------------------------------------
;自撮り写真を送りつけます
;-------------------------------------------------
@SEND_SELFY
#DIM MEMO_TARGET

MEMO_TARGET = TARGET
TARGET = 0

TARGET = FIND_COND("自撮り送信可能")

SIF TARGET && NUM("写真") < 300
	CALL SEND_SELFY_MAIN, TARGET

TARGET = MEMO_TARGET

;角色指定して送れるように分割
@SEND_SELFY_MAIN, ARG
#DIM LCOUNT
#DIM LCOUNT2
#DIMS MEMO_SELFY

VARSET MEMO_SELFY

SPLIT SAVESTR:自撮り写真, "/", LOCALS

FOR LCOUNT, 0, 10
	IF STRCOUNTS(LOCALS:LCOUNT, CALLNAME:ARG)
		MEMO_SELFY = %LOCALS:LCOUNT%
		SPLIT LOCALS:LCOUNT, "_", LOCALS
		DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%的手机收信音响起了。
		PRINTFORML ……確認すると『%LOCALS:0%』が届いていた。

		;画像タグが埋め込んであったら画像を表示する
		SIF STRCOUNT(MEMO_SELFY, "GFX：")
			CALL LOAD_GRAPHICSDATE, EXTSTR(MEMO_SELFY , "GFX：", "_")
		WAIT

		;自撮りメッセージ表示
		CALL SELFY_MAIL, MEMO_SELFY
		CALL SAVE_PHOTO, MEMO_SELFY

		;写真が時間帯を確定するものならフラグを弄る
		SIF STRCOUNT(LOCALS, "授業直後")
			SETBIT FLAG:状況判定, 18

		;送った写真を送信済みにする　2回以上送らないため
		SAVESTR:送信済み自撮り写真 += @"%MEMO_SELFY%\/"
		;送信済みの自撮り胖次を記録
		FOR LCOUNT2, 2, CHARANUM
			;誰の自撮り？
			SIF STRCOUNT(LOCALS, CALLNAME:LCOUNT2) == 0
				CONTINUE
			SIF STRCOUNT(LOCALS, PANTIES(LCOUNT2) )== 0
				CONTINUE
			SAVESTR:送信済み自撮り胖次 += @"%PANTIES(LCOUNT2)%_%CALLNAME:LCOUNT2%\/"
		NEXT
		BREAK
	ENDIF
NEXT

;-------------------------------------------------
;@EVENT_DAILY_EX内で相手して無かった角色が自撮りを送ってくるイベント
;-------------------------------------------------
@SEND_SELFY_DAILY_EX
#DIM MEMO_TARGET
#DIMS EVENT_TABLE
#DIM IS_PANTU_SELFY

;発生確率２０％くらいにする？
SIF RAND:5 || NUM("写真") >= 300
	RETURN 0

IS_PANTU_SELFY = 0

MEMO_TARGET = TARGET
TARGET = FIND_COND("胖次自撮り送信可能")
IF TARGET <= 0
	;TARGETを元に戻す(これをしないと本来自撮りを送ってこないはずのTARGETが送ってくる)
	TARGET = MEMO_TARGET
	TARGET = FIND_COND("自撮り送信可能")
ENDIF

;候補がいない場合は終了 発生確率２０％くらい？
IF TARGET <= 0
	TARGET = MEMO_TARGET
	RETURN 0
ENDIF

EVENT_TABLE = 

;現在的状況次第で写真を変える。同じ部屋にいる場合は胖次や阴部の自撮りになる
IF COND("現在存在")
	IF CHECK_SELFIE(TARGET, "胖次", "阴部", "阴茎", "胸部")
	ELSEIF COND("阴部自撮り可能") && COND("胖次形状確認")
		CALL PHOTO_SELFY, TARGET, "阴部"
	ELSEIF COND("胖次形状確認") == 0 && COND("胖次自撮り可能")
		CALL PHOTO_SELFY, TARGET, "胖次"
	ELSE
		RETURN 0
	ENDIF
ELSE
	CALL PHOTO_SELFY, TARGET
ENDIF

SIF COND("胖次自撮り送信可能")
	IS_PANTU_SELFY = 1

CALL SEND_SELFY_MAIN, TARGET

IF COND("現在存在")
	PRINTFORM %TEXTR("突然向/侧目向/反射性地向")%送信人的方向看过去、%CALLNAME:TARGET%
	IF TALENT:好色 || TALENT:小悪魔 || TALENT:開朗 || TALENT:好奇心
		IF IS_PANTU_SELFY && CHECK_SKIRT("裙") && COND("自撮りと同じ胖次")
			PRINTFORM 將%SKIRT(TARGET, "略")%摘み上げて写真と同じ%PANTIES(TARGET, "胖次")%を
			IF CHECK_PANTIES("前貼") || CHECK_PANTIES("Ｃ胖次")
				PRINTFORM 一邊偷偷抬起大腿將貼著布料的股間偷偷展示給了%CALLNAME:MASTER%看
			ELSE
				PRINTFORM 一邊偷偷抬起大腿將穿著布料的股間偷偷展示給了%CALLNAME:MASTER%看
			ENDIF
			CALL SETFLAG_DAILY, "胖次形状確認"
		ELSE
			PRINTFORM 对%CALLNAME:MASTER%的反応
		ENDIF
		PRINTFORMW %TEXTR("咯咯地笑着/開心地笑着/呼呼地笑着")%…
	ELSEIF TALENT:害羞 || TALENT:内気 || TALENT:謙虚 
		PRINTFORMW 红着脸别过頭去…
	ELSE
		IF RAND:2
			PRINTFORMW 像是在窥察%CALLNAME:MASTER%的情况一样看着这边…
		ELSE
			PRINTFORMW 敏捷地躲開了%CALLNAME:MASTER%的视线…
		ENDIF
	ENDIF
	STR:日常イベント名 = 自撮り送信
ELSE
	PRINTFORMW %CALLNAME:MASTER%的写真保存在了手机里。
ENDIF

TARGET = MEMO_TARGET

;-------------------------------------------------
;送られた自撮りの内容を判定し、メッセージの内容を決める
;-------------------------------------------------
@SELFY_MAIL, ARGS
#DIMS SELFY_DETAIL

VARSET SELFY_DETAIL

IF STRCOUNTS(ARGS, "一糸まとわぬ")
	SELFY_DETAIL = ヌード自撮り
ELSEIF STRCOUNTS(ARGS, "阴部")
	SELFY_DETAIL = 阴部自撮り
ELSEIF STRCOUNTS(ARGS, "阴茎")
	SELFY_DETAIL = 阴茎自撮り
ELSEIF STRCOUNTS(ARGS, "胸部")
	SELFY_DETAIL = 胸部自撮り
ELSEIF STRCOUNTS(ARGS, "体操服")
	SELFY_DETAIL = 体操服自撮り
ELSEIF STRCOUNTS(ARGS, "今年の泳装")
	SELFY_DETAIL = 今年の泳装自撮り
ELSEIF STRCOUNTS(ARGS, "泳装")
	SELFY_DETAIL = 学校指定の泳装自撮り
ELSEIF STRCOUNTS(ARGS, "胸罩")
	SELFY_DETAIL = 胸罩自撮り
ELSEIF STRCOUNTS(ARGS, "胖次")
	SELFY_DETAIL = 胖次自撮り
ELSE
	SELFY_DETAIL =
ENDIF

TRYCALLFORM SELFY_MAIL_K{NO:TARGET}, SELFY_DETAIL

