;-------------------------------------------------
;ARGを样本にして衣装換装をシミュレートする
;ARGSに文字列が入っている場合には特殊な操作を行う
;-------------------------------------------------
@SIMULATE_CLOTHES, ARG, ARGS
#DIMS MEMO_PLACE
#DIM MEMO_CFLAG
#DIM MEMO_CFLAG2
#DIM MEMO_CFLAG3
#DIM MEMO_FLAG
#DIMS MEMO_CSTR
#DIM CHOICE

SIF ARG == 0 && TARGET
	ARG = TARGET

MEMO_PLACE = %SAVESTR:現在位置%
SAVESTR:現在位置 = 試着室
MEMO_CFLAG = CFLAG:ARG:掀起裙子
CFLAG:ARG:掀起裙子 = 0
MEMO_CFLAG2 = CFLAG:ARG:電源BOX
CFLAG:ARG:電源BOX = 0
MEMO_CSTR = %CSTR:ARG:晒痕%
MEMO_CFLAG3 = CFLAG:ARG:晒痕強度
MEMO_FLAG = FLAG:状況判定

;颜绘シミュ中フラグON
SETBIT FLAG:状況判定, 48

PRINTL 
SELECTCASE ARGS
CASE "整理内衣"
	PRINTFORML %CALLNAME:MASTER%のタンスの引き出しに入れる整理内衣をします。
	CALL PRINT_STRL, "なお、所持できる内衣は胸罩＆胖次の两件套で_黄色_18種類まで_です。_黄色_(无胸罩＆没穿内褲もＯＫ）_"
	CALL PRINT_STRW, "所持している内衣セットは、_黄色_制服、体操服、便装、外出装を着る際に随机選択で着用_するようになります"

	CALL SETFLAG, "衣装の記録", MASTER
	STR:最初の衣装 = %STR:衣装記録%
	CALL SETFLAG, "内衣以外全脱ぎ", MASTER
	;これでSIMULATE_CLOTHES_MAIN中のモードをコントロールする
	TFLAG:換装过滤器 = 3
CASEELSE
	CALL PRINT_STRL, "对制服和体操服进行设定的情况下、推荐先_黄色_选择套装搭配再具体进行服装的配置_"
	PRINTFORML 因此当对应画像中没有衣服时、可能会难以判断选择的服装是否适合。请悉知
	;これでSIMULATE_CLOTHES_MAIN中のモードをコントロールする
	TFLAG:換装过滤器 = 0
ENDSELECT

STR:LASTCHOICE = 

CALL SIMULATE_CLOTHES_MAIN, ARG, ARGS

SELECTCASE ARGS
CASE "整理内衣"
	STR:衣装記録 = %STR:最初の衣装%
	CALL SETFLAG, "衣装記録からの復元", MASTER
ENDSELECT

STR:衣装記録 = 
STR:LASTCHOICE = 

SAVESTR:現在位置 = %MEMO_PLACE%
CFLAG:ARG:掀起裙子 = MEMO_CFLAG
CFLAG:ARG:電源BOX = MEMO_CFLAG2
;CSTR:ARG:晒痕 = %MEMO_CSTR%
;CFLAG:ARG:晒痕強度 = MEMO_CFLAG3
FLAG:状況判定 = MEMO_FLAG

;-------------------------------------------------
;TFLAG:換装过滤器で現在設定しているものの種類を管理する
;0ベース装扮選択, 1個別選択
;現在選択中の装扮はARGSに記録する
;-------------------------------------------------
@SIMULATE_CLOTHES_MAIN, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIMS CHOICES
#DIMS TYPE_LASTCHOICE
#DIMS NAME_LASTCHOICE
#DIMS CHANGE_SUBCOLOR
#DIMS MENU_SELECT
#DIMS TEXT_OFFSELECT, 2
#DIMS TEXT_ENDSELECT
#DIMS STR_FACE
#DIMS MEMO_MASTERCLO

MEMO_LINECOUNT = LINECOUNT

SELECTCASE TFLAG:过滤器
CASE 10
	MEMO_MASTERCLO = 制服
CASE 11
	MEMO_MASTERCLO = 体操服
CASE 12
	MEMO_MASTERCLO = 便装
CASE 13
	MEMO_MASTERCLO = 外出装
CASE 14
	MEMO_MASTERCLO = 泳装
CASEELSE
	MEMO_MASTERCLO = 
ENDSELECT

MENU_SELECT = 
SELECTCASE ARGS
CASE "整理内衣"
	MENU_SELECT += @"現在%CALLNAME:ARG%の引き出しに入っている内衣は_黄色_{COND("所持内衣数", ARG)}セット_です"
	TFLAG:換装过滤器 = 3
CASEELSE
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 0 ? 緑_ # \@[0]装扮 _"
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 1 ? 緑_ # \@[1]外衣＆裙 _"
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 2 ? 緑_ # \@[2]下身衣装＆贴身制服 _"
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 3 ? 緑_ # \@[3]胖次＆胸罩 _"
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 4 ? 緑_ # \@[4]手套＆襪子＆靴 _"
	MENU_SELECT += @"\@ TFLAG:換装过滤器 == 5 ? 緑_ # \@[5]飾品 _"
ENDSELECT

PRINTL 
CALL PRINT_STRL, "黄色_※想要脱掉衣服的时候_水色_可以连续选中现在穿着的衣服_or_水色_按下画像下的 脱衣: 右侧的按钮_黄色_※_"
CALL PRINT_STRL, MENU_SELECT
VARSET LOCALS
SPLIT STR:LASTCHOICE, "_", LOCALS
TYPE_LASTCHOICE = %LOCALS:0%
NAME_LASTCHOICE = %LOCALS:1%
CHANGE_SUBCOLOR = 
STR_FACE = /%CSTR:ARG:颜绘変化%

IF CSTR:ARG:颜绘変化 != ""
	CALL EMOTION_PLUS, CSTR:ARG:颜绘変化, ARG

;表情変化は不要な気がしてきたのでいったんオミット
;ELSEIF ARG == MASTER && COND("池面", ARG)
;	CALL EMOTION_PLUS, @"重置%STR_FACE%", ARG
;ELSEIF CHECK_CLO("裸体", ARG)
;	IF TALENT:ARG:不知羞恥 || TALENT:ARG:小悪魔
;		CALL EMOTION_PLUS, @"小悪魔%STR_FACE%", ARG
;	ELSEIF TALENT:ARG:害羞
;		CALL EMOTION_PLUS, "害羞/对话框汗２/唇边手", ARG
;	ELSE
;		CALL EMOTION_PLUS, @"红潮恍惚/对话框・・・%STR_FACE%", ARG
;	ENDIF
;ELSEIF TALENT:ARG:恋慕 || TALENT:ARG:淫乱
;	CALL EMOTION_PLUS, @"心情好%STR_FACE%", ARG
;ELSEIF TALENT:ARG:不知羞恥 || TALENT:ARG:小悪魔
;	CALL EMOTION_PLUS, @"小悪魔%STR_FACE%", ARG
;ELSEIF CHECK_CLO("恥ずかしい恰好", ARG)
;	IF TALENT:ARG:害羞
;		CALL EMOTION_PLUS, @"害羞%STR_FACE%", ARG
;	ELSE
;		CALL EMOTION_PLUS, @"重置/紅潮%STR_FACE%", ARG
;	ENDIF

ELSE
	CALL EMOTION_PLUS, @"重置%STR_FACE%", ARG
ENDIF
CALL START_PRINT_LINEUP

SELECTCASE TFLAG:換装过滤器
CASE 0
	CALL PRINT_STRL, @"%BL(21)%_緑_装扮_:\@ ARGS != "" ? _黄色_%ARGS%_ #  \@"
	CALL SELECTSL, "没有", "装扮_", BL(21)
	CALL SELECTSL, "西装外衣/水手服/女式衬衫/美式校服/背心/V领毛衣/女教師/校服", "装扮_", BL(21)
	CALL SELECTSL, "体操服布鲁马/体操服五分褲/体操服紧身短褲/体操服运动衫/工装裤", "装扮_", BL(21)
	CALL SELECTSL, "Micro比基尼/淚珠泳衣比基尼/比基尼/新死库水/死库水/競泳泳装/衝浪胖次", "装扮_", BL(21)
	CALL SELECTSL, "兔女郎制服/逆兔女郎/女仆装/執事服/修女服/旗袍/聖誕服/啦啦队制服", "装扮_", BL(21)
	CALL SELECTSL, "巫女/和服/浴衣/振袖/紋付袴/婚纱礼服/拘束衣", "装扮_", BL(21)
CASE 1
	CALL PRINT_STRL, @"%BL(21)%_緑_外衣_:\@ EQUIP:ARG:服 ? _黄色_%SHIRT(ARG)%_ #  \@"
	CALL SELECTSL, "西装外衣/水手服/背心/V领毛衣/女式衬衫/无袖女式衬衫/Ｔ恤/圆领衬衫", "外衣_", BL(21)
	CALL SELECTSL, "体操服/运动衫/吊帯背心/連帽風衣/前開的衬衫/校服/紧身長袍/制服", "外衣_", BL(21)
	CALL SELECTSL, "针织毛衣/无袖高領毛衣/高領毛衣/露肩外衣/哥特式披风", "外衣_", BL(21)
	;CALL SELECTSL, "連衣裙/褶边連衣裙/露肩連衣裙/哥特連衣裙/洛丽塔連衣裙", "外衣_", BL(21)
	CALL SELECTSL, "格子連衣裙/連衣裙/吊帯衫/皮革夹克", "外衣_", BL(21)
	CALL SELECTSL, "晨衣/睡衣/情趣内衣/哥特蘿莉礼服/夏日礼服/哥特礼服", "外衣_", BL(21)

	PRINTL 
	IF CHECK_SHIRT("連衣裙", ARG) == 0
		CALL PRINT_STRL, @"%BL(21)%_緑_裙_:\@ EQUIP:ARG:裙 ? _黄色_%SKIRT(ARG)%_ #  \@"
		CALL SELECTSL, "百褶裙/格子裙/丝绸裙/紧身裙/荷葉邊", "裙_", BL(21)
	ENDIF
CASE 2
	CALL PRINT_STRL, @"%BL(21)%_緑_下身衣装_:\@ EQUIP:ARG:下身衣装 ? _黄色_%BOTTOM(ARG)%_ #  \@"
	CALL SELECTSL, "布鲁马/紧身短褲/热褲/休闲褲/五分褲/运动衫休闲褲/牛仔褲/工装裤", "下身衣装_", BL(21)
	CALL SELECTSL, "開裆皮裤/连褲襪/裤襪/网纹裤襪", "下身衣装_", BL(21)
	PRINTL 
	CALL PRINT_STRL, @"%BL(21)%_緑_贴身制服_:\@ EQUIP:ARG:贴身制服 ? _黄色_%BODYS(ARG)%_ #  \@"
	CALL SELECTSL, "新死库水/紧身短褲型死库水/死库水/競泳泳装/彈弓泳衣/兔女郎制服", "贴身制服_", BL(21)
CASE 3
	CALL PRINT_STRL, @"%BL(21)%_緑_胖次_:\@ EQUIP:ARG:胖次 ? _黄色_%PANTIES(ARG)%_ #  \@"
	CALL SELECTSL, "內褲/三角胖次/透明/褶边の胖次/條紋胖次/蕾丝的胖次/低腰胖次", "胖次_", BL(21)
	CALL SELECTSL, "高開衩胖次/丁字褲/Micro內褲/四角内褲/開放型開洞胖次", "胖次_", BL(21)
	CALL SELECTSL, "超低腰胖次/高開衩布鲁马/兜襠布/前貼/Ｃ胖次/儿童胖次/襯褲/叶子胖次", "胖次_", BL(21)
	CALL SELECTSL, "衝浪胖次/比基尼/丁字褲比基尼/Micro比基尼/淚珠泳衣比基尼", "胖次_", BL(21)
	PRINTL 
	CALL PRINT_STRL, @"%BL(21)%_緑_胸罩_:\@ EQUIP:ARG:胸罩 ? _黄色_%BRA(ARG)%_ #  \@"
	CALL SELECTSL, "满杯胸罩/３／４杯胸罩/半杯胸罩/Micro胸罩/開放型胸罩/拘束衣胸罩", "胸罩_", BL(21)
	CALL SELECTSL, "比基尼胸罩/猫型胸罩/蕾丝胸罩/吊帯背心/抹胸/褶边吊罩/乳贴", "胸罩_", BL(21)
CASE 4
	CALL PRINT_STRL, @"%BL(21)%_緑_手套_:\@ EQUIP:ARG:手套 ? _黄色_%GLOVE(ARG)%_ #  \@"
	CALL SELECTSL, "手套/指抜きグローブ/礼服手套/礼服袖套", "手套_", BL(21)
	PRINTL 
	CALL PRINT_STRL, @"%BL(21)%_緑_短襪_:\@ EQUIP:ARG:襪子 ? _黄色_%SOCKS(ARG)%_ #  \@"
	CALL SELECTSL, "襪子/膝下襪/及膝襪/高筒襪/褶边短襪/短布襪", "襪子_", BL(21)
	CALL SELECTSL, "吊帯長筒襪/逆兔女郎裤襪", "襪子_", BL(21)
	PRINTL 
	CALL PRINT_STRL, @"%BL(21)%_緑_靴_:\@ EQUIP:ARG:靴 ? _黄色_%SHOES(ARG)%_ #  \@"
	CALL SELECTSL, "小皮靴/運動鞋/平底鞋/高跟鞋/凉鞋/靴/草履/木屐/革靴", "靴_", BL(21)
CASE 5
	CALL PRINT_STRL, @"%BL(21)%_緑_飾品_:\@ EQUIP:ARG:飾品 ? _黄色_%REPLACE(REPLACE(CSTR:ARG:飾品, "\/", "＆"), "＆\$", "")%_ #  \@"
	CALL SELECTSL, "蝶形領帯/围巾/围巾Ｂ/围巾Ｃ/铃铛项圈/项圈/牛铃/口罩/顎口罩", "饰_", BL(21)
	CALL SELECTSL, "眼鏡/頭上太阳鏡/太阳鏡/眼罩/顔面包帯/顔面包帯左眼罩/顔面包帯右眼罩/顔面包帯両眼罩", "饰_", BL(21)
	CALL SELECTSL, "頭戴式耳机/机器耳", "饰_", BL(21)
	CALL SELECTSL, "魔女の帽子/贝雷帽/礼服頭花/女仆帽/草帽/鸭舌帽/水兵帽", "饰_", BL(21)
	CALL SELECTSL, "修女帽/頭纱/护士帽/聖誕帽/胖次帽", "饰_", BL(21)
	CALL SELECTSL, "狐面/カボチャ面/兔耳/猫耳/牛耳/髮箍/頭冠/簪子/花飾り", "饰_", BL(21)
	CALL SELECTSL, "外套/前開运动衫/前開V领毛衣/前開連帽風衣", "饰_", BL(21)
	CALL SELECTSL, "露肩夹克/裤吊帯/圍裙/褞袍", "饰_", BL(21)
ENDSELECT

CALL PRINT_LINEUP

IF STRCOUNT(CSTR:ARG:颜绘変化, "胖次花绳")
	CALL PRINT_CUTIN, "胖次花绳", ARG
ELSE
	CALL PRINT_CUTIN, "縮小", ARG
ENDIF
CALL START_PRINT_LINEUP, "CIP"
PRINTL 

IF CLO_PAT(NAME_LASTCHOICE, TYPE_LASTCHOICE) != ""
	CALL SELECTSL, CLO_PAT(NAME_LASTCHOICE, TYPE_LASTCHOICE), "類型選択_", @"%BL(21)%_緑_類型変更_:"
ELSE
;	PRINTL 
ENDIF
IF CUSEABLE(NAME_LASTCHOICE, ARG) != ""
	CALL SELECTSL, CUSEABLE(NAME_LASTCHOICE, ARG), "", @"%BL(21)%_緑_花纹変更(%NAME_LASTCHOICE%)_:"
ELSE
	PRINTL 
ENDIF

IF NAME_LASTCHOICE == "怪图案Ｔ恤"
	CALL SELECTSL, CUSEABLE("変な柄", ARG), "", @"%BL(21)%_緑_変Ｔ文字_:"
ELSEIF CUSEABLE(@"正面蝴蝶結%NAME_LASTCHOICE%", ARG) != ""
	CALL SELECTSL, CUSEABLE(@"正面蝴蝶結%NAME_LASTCHOICE%", ARG), "", @"%BL(21)%_緑_花纹変更(正面蝴蝶結)_:"
ELSEIF CUSEABLE(SHIRT(ARG, "種類"), ARG) != "" && (TYPE_LASTCHOICE == "裙" || TYPE_LASTCHOICE == "下身衣装") && SHIRT(ARG, "種類") != NAME_LASTCHOICE
	CALL SELECTSL, CUSEABLE(SHIRT(ARG, "種類"), ARG), "外衣色変化_", @"%BL(21)%_緑_花纹変更(%SHIRT(ARG, "種類")%)_:"
ELSEIF CUSEABLE(SKIRT(ARG, "種類"), ARG) != "" && TYPE_LASTCHOICE != "裙" && SKIRT(ARG, "種類") != NAME_LASTCHOICE
	CALL SELECTSL, CUSEABLE(SKIRT(ARG, "種類"), ARG), "裙色変化_", @"%BL(21)%_緑_花纹変更(%SKIRT(ARG, "種類")%)_:"
ELSEIF CUSEABLE(BOTTOM(ARG, "種類"), ARG) != "" && TYPE_LASTCHOICE != "下身衣装" && BOTTOM(ARG, "種類") != NAME_LASTCHOICE
	CALL SELECTSL, CUSEABLE(BOTTOM(ARG, "種類"), ARG), "下身衣装色変化_", @"%BL(21)%_緑_花纹変更(%BOTTOM(ARG, "種類")%)_:"
ELSEIF CUSEABLE(SOCKS(ARG, "種類"), ARG) != "" && TYPE_LASTCHOICE != "襪子" && SOCKS(ARG, "種類") != NAME_LASTCHOICE
	CALL SELECTSL, CUSEABLE(SOCKS(ARG, "種類"), ARG), "襪子色変化_", @"%BL(21)%_緑_花纹変更(%SOCKS(ARG, "種類")%)_:"
ELSEIF EQUIP:ARG:下身衣装 + EQUIP:ARG:裙 == 0 && TYPE_LASTCHOICE != "胖次" && PANTIES(ARG, "種類") != NAME_LASTCHOICE && CUSEABLE(PANTIES(ARG, "種類"), ARG) != "" && CHECK_CLO("泳装姿", ARG) == 0
	CALL SELECTSL, CUSEABLE(PANTIES(ARG, "種類"), ARG), "胖次色変化_", @"%BL(21)%_緑_花纹変更(%PANTIES(ARG, "種類")%)_:"
ELSE
	PRINTL 
ENDIF
IF TYPE_OTHER(NAME_LASTCHOICE) == "外套"
	PRINTL 
ELSEIF CHECK_OTHER("前開运动衫", ARG)
	CALL SELECTSL, CUSEABLE("前開运动衫", ARG), "外套色変化_", @"%BL(21)%_緑_花纹変更(前開运动衫)_:"
ELSEIF CHECK_OTHER("露肩夹克", ARG)
	CALL SELECTSL, CUSEABLE("露肩夹克", ARG), "外套色変化_", @"%BL(21)%_緑_花纹変更(露肩夹克)_:"
ELSEIF CHECK_OTHER("V领毛衣", ARG)
	CALL SELECTSL, CUSEABLE("前開V领毛衣", ARG), "外套色変化_", @"%BL(21)%_緑_花纹変更(前開V领毛衣)_:"
ELSEIF CHECK_OTHER("工作围裙", ARG)
	CALL SELECTSL, CUSEABLE("工作围裙", ARG), "外套色変化_", @"%BL(21)%_緑_花纹変更(工作围裙)_:"
ELSE
	SIF LINECOUNT - MEMO_LINECOUNT < 17
		PRINTL 
ENDIF
CALL PRINT_LINEUP, "CIP"
CALL PRINT_CUTIN, "縮小背面", ARG
CALL MULTILINE, MAX(23 - (LINECOUNT - MEMO_LINECOUNT), 0)

VARSET TEXT_OFFSELECT, "水色_脱衣_:_BUTTON_[全部]"
IF EQUIP:ARG:服
	TEXT_OFFSELECT += "_BUTTON_[外衣]"
ELSE
	TEXT_OFFSELECT += "_灰色_[外衣]"
ENDIF
IF EQUIP:ARG:裙
	TEXT_OFFSELECT += "_BUTTON_[裙]"
ELSE
	TEXT_OFFSELECT += "_灰色_[裙]"
ENDIF
IF EQUIP:ARG:下身衣装
	IF CHECK_BOTTOM("网纹裤襪", ARG)
		TEXT_OFFSELECT += "_BUTTON_[网纹裤襪]"
	ELSEIF CHECK_BOTTOM("裤襪", ARG)
		TEXT_OFFSELECT += "_BUTTON_[裤襪等]"
	ELSEIF CHECK_BOTTOM("连褲襪", ARG)
		TEXT_OFFSELECT += "_BUTTON_[连褲襪]"
	ELSE
		TEXT_OFFSELECT += "_BUTTON_[下身衣装]"
	ENDIF
ELSE
	TEXT_OFFSELECT += "_灰色_[下身衣装]"
ENDIF
IF EQUIP:ARG:贴身制服
	TEXT_OFFSELECT += "_BUTTON_[贴身制服]"
ELSE
	TEXT_OFFSELECT += "_灰色_[贴身制服]"
ENDIF
IF EQUIP:ARG:胖次
	TEXT_OFFSELECT += "_BUTTON_[胖次]"
	TEXT_OFFSELECT:1 += "_BUTTON_[胖次]"
ELSE
	TEXT_OFFSELECT += "_灰色_[胖次]"
	TEXT_OFFSELECT:1 += "_灰色_[胖次]"
ENDIF
IF EQUIP:ARG:胸罩
	TEXT_OFFSELECT += "_BUTTON_[胸罩]"
	TEXT_OFFSELECT:1 += "_BUTTON_[胸罩]"
ELSE
	TEXT_OFFSELECT += "_灰色_[胸罩]"
	TEXT_OFFSELECT:1 += "_灰色_[胸罩]"
ENDIF
IF EQUIP:ARG:手套
	TEXT_OFFSELECT += "_BUTTON_[手套]"
ELSE
	TEXT_OFFSELECT += "_灰色_[手套]"
ENDIF
IF EQUIP:ARG:襪子
	TEXT_OFFSELECT += "_BUTTON_[襪子]"
ELSE
	TEXT_OFFSELECT += "_灰色_[襪子]"
ENDIF
IF EQUIP:ARG:靴
	TEXT_OFFSELECT += "_BUTTON_[靴]"
ELSE
	TEXT_OFFSELECT += "_灰色_[靴]"
ENDIF
IF EQUIP:ARG:飾品
	TEXT_OFFSELECT += "_BUTTON_[饰]"
ELSE
	TEXT_OFFSELECT += "_灰色_[饰]"
ENDIF
SIF OTHER(ARG, "首") != ""
	TEXT_OFFSELECT += "_BUTTON_[首]"
SIF OTHER(ARG, "眼鏡") != ""
	TEXT_OFFSELECT += "_BUTTON_[眼鏡]"
SIF OTHER(ARG, "帽子") != ""
	TEXT_OFFSELECT += "_BUTTON_[帽子]"
SIF OTHER(ARG, "饰耳") != ""
	TEXT_OFFSELECT += "_BUTTON_[饰耳]"
SIF OTHER(ARG, "髪飾") != ""
	TEXT_OFFSELECT += "_BUTTON_[髪飾]"
SIF OTHER(ARG, "外套") != ""
	TEXT_OFFSELECT += "_BUTTON_[外套等]"
SIF OTHER(ARG, "耳罩") != ""
	TEXT_OFFSELECT += "_BUTTON_[耳罩]"
SIF OTHER(ARG, "顔") != ""
	TEXT_OFFSELECT += "_BUTTON_[顔]"
IF EQUIP:ARG:胖次
	IF CHECK_CLO("胖次下ろし可", ARG)
		TEXT_OFFSELECT += "_BUTTON_[拉下胖次]"
	ELSEIF CHECK_PANTIES("下拉半遮", ARG)
		TEXT_OFFSELECT += "_BUTTON_[整理胖次]"
	ENDIF
ENDIF
IF EQUIP:ARG:裙
	IF CFLAG:ARG:掀起裙子 == 0
		TEXT_OFFSELECT += "_BUTTON_[卷起裙子]"
	ELSE
		TEXT_OFFSELECT += "_BUTTON_[放好裙子]"
	ENDIF
ENDIF

SELECTCASE ARGS
CASE "整理内衣"
	CALL PRINT_STRL, TEXT_OFFSELECT:1
CASEELSE
	CALL PRINT_STRL, TEXT_OFFSELECT
ENDSELECT

PRINTL 
;CALL PRINT_LINEUP, "CIP"

PRINTFORM 服装詳細:
CALL PRINT_DRESSL, ARG, "衣装表示"
PRINTL 
CALL PRINT_STRL, @"只今の格好は　_黄色_%CLO_ABOUT(ARG)%_%SHOW_APP_CHANGE(ARG, " with")%です"
PRINTL 

TEXT_ENDSELECT = 这様就好？ 　　　　
SELECTCASE ARGS
CASE "整理内衣"
	TEXT_ENDSELECT += "_BUTTON_[整理終了]"
	TEXT_ENDSELECT += "_BUTTON_[现在的内衣放入抽屉中]"
	IF COND("所持内衣数", ARG)
		TEXT_ENDSELECT += "_BUTTON_[检查抽屉里面(要処分掉的话也在这里)]"
	ELSE
		TEXT_ENDSELECT += "_灰色_[检查抽屉里面(现在里面什么都没有)]"
	ENDIF

CASEELSE
	IF MEMO_MASTERCLO != ""
		TEXT_ENDSELECT += @"_BUTTON_[%MEMO_MASTERCLO%に設定する]_BUTTON_[取消设定]"
	ELSE
		TEXT_ENDSELECT += "_BUTTON_[換装終了]"
	ENDIF
	TEXT_ENDSELECT += "_BUTTON_[関数表示]_BUTTON_[衣装保存]_BUTTON_[衣装加载]"
	SIF STR:最初の衣装 != ""
		TEXT_ENDSELECT += "_BUTTON_[返回最初的衣装]"
	TEXT_ENDSELECT += "_BUTTON_[晒痕]_BUTTON_[跳蛋]"
ENDSELECT

CALL PRINT_STRL, TEXT_ENDSELECT

;	[IF_DEBUG]
;		PRINTFORML STR:LASTCHOICE = %STR:LASTCHOICE%
;		VARSET LOCALS
;		SPLIT STR:CIP再現文字列, "/", LOCALS
;		FOR LCOUNT, 0, 5
;			SELECTCASE LOCALS:LCOUNT
;			CASE ""
;				SIF LCOUNT >= 1
;					BREAK
;				PRINTL 
;			CASEELSE
;				PRINTFORML %LOCALS:LCOUNT%
;			ENDSELECT
;		NEXT
;	[ENDIF]

INPUTS
CHOICES = %EXTSTR(RESULTS, "[", "]")%
CLEARLINE 1

;特殊な選択
IF STRCOUNT(CHOICES, "色変化")
	VARSET LOCALS
	SPLIT CHOICES, "_", LOCALS
	CHOICES = %LOCALS:1%
	CHANGE_SUBCOLOR = %LOCALS%
ENDIF

SELECTCASE CHOICES
;純粋に数字を見る場合
CASE "0", "1", "2", "3", "4", "5"
	TFLAG:換装过滤器 = TOINT(CHOICES)
CASE "衣装保存"
	CALL SETFLAG, "衣装の記録", ARG
	CALL SAVE_CLO, STR:衣装記録, ARG
CASE "衣装加载"
	CALL SETFLAG, "衣装の記録", ARG
	CALL LOAD_CLO, STR:衣装記録, ARG
CASE "返回最初的衣装"
	STR:衣装記録 = %STR:最初の衣装%
	CALL SETFLAG, "衣装記録からの復元", ARG
CASE "取消设定"
	TFLAG:过滤器 = 0
;	CSTR:ARG:颜绘変化 = 
	TFLAG:換装过滤器 = 0
	RETURN 1
CASE "換装終了", @"%MEMO_MASTERCLO%に設定する"
	IF ARG == MASTER && MEMO_MASTERCLO != ""
		CALL SETFLAG, "衣装の記録", MASTER
		IF SAVESTR:MEMO_MASTERCLO != STR:衣装記録
			SAVESTR:MEMO_MASTERCLO = %STR:衣装記録%
			CALL SETFLAG, "衣装記録からの復元", MASTER
			PRINTL 
			CALL PRINT_DRESSL, ARG, "衣装表示"
			PRINTFORMW を%CALLNAME:MASTER%の%MEMO_MASTERCLO%に設定しました。
		ENDIF
		TFLAG:过滤器 = 0
	ENDIF

;	CSTR:ARG:颜绘変化 = 
	TFLAG:換装过滤器 = 0
	RETURN 1

CASE "整理終了"
	TFLAG:过滤器 = 0
	TFLAG:換装过滤器 = 0
	RETURN 1
CASE "现在的内衣放入抽屉中"
	IF COND("所持内衣数", ARG) < 18
		CALL SETFLAG, "所持内衣に加える", ARG
		PRINTL 
		CALL PRINT_UNDERWEARS, ARG
		PRINTFORMW 放到了抽屉里面。
	ELSE
		PRINTFORMW 抽屉里面塞满了。要处分掉哪个呢。
	ENDIF
CASE "检查抽屉里面(要処分掉的话也在这里)"
	CALL CHANGE_DRAWER_UNDERWEAR, ARG

CASE "整理胖次", "拉下胖次"
	CALL SETFLAG, CHOICES, ARG

CASE "卷起裙子", "放好裙子"
	INVERTBIT CFLAG:ARG:掀起裙子, 0
CASE "跳蛋"
	INVERTBIT CFLAG:ARG:電源BOX, 0
CASE "晒痕"
	IF COND("晒痕", ARG)
		CALL SETFLAG, "晒痕重置", ARG
		TALENT:ARG:常時晒痕 = 0
	ELSE
		CALL SETFLAG, "衣装の記録", ARG
		CALL SETFLAG, "晒痕", ARG
		TALENT:ARG:常時晒痕 = 1
	ENDIF
CASE "ポーズ重置"
	CSTR:ARG:颜绘変化 = 
CASE "剪刀手"
	IF CSTR:ARG:颜绘変化 == "剪刀/"
		CSTR:ARG:颜绘変化 = 
	ELSE
		CSTR:ARG:颜绘変化 = 剪刀/
	ENDIF
CASE "萌袖"
	IF CSTR:ARG:颜绘変化 == "萌袖/"
		CSTR:ARG:颜绘変化 = 
	ELSE
		CSTR:ARG:颜绘変化 = 萌袖/
	ENDIF
CASE "関数表示"
	IF CHECK_CLO("全裸", ARG) && EQUIP:ARG:飾品 == 0
		PRINTFORMW 现在身上什么都没有穿
	ELSE
		PRINTL 
		PRINTFORML 现在的服装函数为
		PRINTL 
		SIF EQUIP:ARG:服
			PRINTFORML CALL SET_SHIRT, "%SHIRT(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:服)%"
		SIF EQUIP:ARG:裙 && CHECK_SHIRT("連衣裙", ARG) == 0
			PRINTFORML CALL SET_SKIRT, "%SKIRT(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:裙)%"
		SIF EQUIP:ARG:贴身制服
			PRINTFORML CALL SET_BODYS, "%BODYS(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:贴身制服)%"
		SIF EQUIP:ARG:下身衣装
			PRINTFORML CALL SET_BOTTOM, "%BOTTOM(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:下身衣装)%"
		SIF EQUIP:ARG:胖次
			PRINTFORML CALL SET_PANTIES, "%PANTIES(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:胖次)%"
		SIF EQUIP:ARG:胸罩
			PRINTFORML CALL SET_BRA, "%BRA(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:胸罩)%"
		SIF EQUIP:ARG:手套
			PRINTFORML CALL SET_GLOVE, "%GLOVE(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:手套)%"
		SIF EQUIP:ARG:襪子
			PRINTFORML CALL SET_SOCKS, "%SOCKS(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:襪子)%"
		SIF EQUIP:ARG:靴
			PRINTFORML CALL SET_SHOES, "%SHOES(ARG, "種類")%%CHECK_ADDCLO(CSTR:ARG:靴)%"
		SIF EQUIP:ARG:飾品
			PRINTFORML CALL SET_OTHER, "%SUBSTRING(CSTR:ARG:飾品, 0, STRLENS(CSTR:ARG:飾品) - 1)%"
		PRINTL 
		PRINTFORMW 。往EVENT_KX1_CLOTHES.ERB中进行复制的话就可以立即使用
	ENDIF
CASE "全部"
	SELECTCASE ARGS
	CASE "整理内衣"
		CALL SET_PANTIES, "", ARG
		CALL SET_BRA, "", ARG
	CASEELSE
		CALL SETFLAG, "衣装全脱ぎ", ARG
	ENDSELECT
CASE "外衣"
	CALL SET_SHIRT, "", ARG
CASE "裙"
	CALL SET_SKIRT, "", ARG
	CFLAG:ARG:掀起裙子 = 0
CASE "连褲襪", "下身衣装", "网纹裤襪", "裤襪等"
	CALL SET_BOTTOM, "", ARG
CASE "贴身制服"
	CALL SET_BODYS, "", ARG
CASE "胖次"
	CALL SET_PANTIES, "", ARG
CASE "胸罩"
	CALL SET_BRA, "", ARG
CASE "手套"
	CALL SET_GLOVE, "", ARG
CASE "襪子"
	CALL SET_SOCKS, "", ARG
CASE "靴"
	CALL SET_SHOES, "", ARG
CASE "首", "眼鏡", "帽子", "饰耳",  "髪飾", "外套", "耳罩", "顔"
	CALL RESET_OTHER, CHOICES, ARG
CASE "外套等"
	CALL RESET_OTHER, "外套", ARG
CASE "饰"
	CALL SET_OTHER, "", ARG

;柄＆色
CASE "漢字１", "英字１", "英字２", "ＡＡ１"
	CALL SET_SHIRT, @"怪图案Ｔ恤/%CLOCOLOR_MAIDFACE("Ｔ恤", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%/%CHOICES%", ARG
CASE "三角黄", "三角黒", "三角青", "三角赤", "三角白", "紐黄", "紐黒", "紐青", "紐赤", "紐白"
	CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, @"正面蝴蝶結%CHOICES%")%
CASE "草莓紋", "小熊紋", "鱼紋", "猫紋", "小鸡紋", "蝴蝶结紋", "水玉模様"
	CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, CHOICES)%

CASE "茶色", "黄色", "橙色", "黒", "紫", "青", "紺色", "薄青色", "真紅", "赤", "薄桃色", "桃色", "白", "純白", "緑色", "薄緑", "灰色", "水色", "豹纹", "奶牛", "虎紋", "柄", "米色", "濃紫", "藤の花", "焦茶色", "紅白", "緑白", "青白", "黄白", "黒白", "水玉粉紅", "水玉黒", "水玉黄色", "水玉水色", "水玉白黒", "水玉白"
	SELECTCASE CHANGE_SUBCOLOR
	CASE "外衣色変化"
		CSTR:ARG:服 = %MERGE_STR(CSTR:ARG:服, CHOICES)%
		IF CHECK_SHIRT("連衣裙", ARG)
			CSTR:ARG:裙 = %MERGE_STR(CSTR:ARG:裙, CHOICES)%
		ELSEIF CHECK_SHIRT("和裤子颜色搭配", ARG) && CHECK_CUSEABLE(BOTTOM(ARG, "種類"), CHOICES, ARG)
			CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
		ENDIF
	CASE "裙色変化"
		CSTR:ARG:裙 = %MERGE_STR(CSTR:ARG:裙, CHOICES)%
	CASE "下身衣装色変化"
		CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
	CASE "襪子色変化"
		CSTR:ARG:襪子 = %MERGE_STR(CSTR:ARG:襪子, CHOICES)%
	CASE "胖次色変化"
		CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, CHOICES)%
		SIF EQUIP:ARG:胸罩 && CHECK_CUSEABLE(BRA(ARG, "種類"), CHOICES, ARG)
			CSTR:ARG:胸罩 = %MERGE_STR(CSTR:ARG:胸罩, CHOICES)%
	CASE "外套色変化"
		SELECTCASE CHOICES
		CASE "赤", "青", "黒", "白"
			CHOICES += "い"
		CASEELSE
			CHOICES += "の"
		ENDSELECT

		IF CHECK_OTHER("前開运动衫", ARG)
			CALL SET_OTHER, @"%CHOICES%前開运动衫", ARG
		ELSEIF CHECK_OTHER("露肩夹克", ARG)
			CALL SET_OTHER, @"%CHOICES%露肩夹克", ARG
		ELSEIF CHECK_OTHER("V领毛衣", ARG)
			CALL SET_OTHER, @"%CHOICES%前開V领毛衣", ARG
		ELSEIF CHECK_OTHER("工作围裙", ARG)
			CALL SET_OTHER, @"%CHOICES%工作围裙", ARG
		ENDIF
	CASEELSE
		SELECTCASE TYPE_LASTCHOICE
		CASE "装扮"
			CALL SET_COSTUME, @"%ARGS%/%CHOICES%", ARG
			IF NAME_LASTCHOICE == SHIRT(ARG, "種類")
				IF CHECK_SHIRT("連衣裙", ARG)
					CSTR:ARG:裙 = %MERGE_STR(CSTR:ARG:裙, CHOICES)%
				ELSEIF CHECK_SHIRT("和裤子颜色搭配", ARG) && CHECK_CUSEABLE(BOTTOM(ARG, "種類"), CHOICES, ARG)
					CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
				ENDIF
			ENDIF
		CASE "外衣"
			CSTR:ARG:服 = %MERGE_STR(CSTR:ARG:服, CHOICES)%
			IF CHECK_SHIRT("連衣裙", ARG)
				CSTR:ARG:裙 = %MERGE_STR(CSTR:ARG:裙, CHOICES)%
			ELSEIF CHECK_SHIRT("和裤子颜色搭配", ARG) && CHECK_CUSEABLE(BOTTOM(ARG, "種類"), CHOICES, ARG)
				CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
			ENDIF
		CASE "裙"
			CSTR:ARG:裙 = %MERGE_STR(CSTR:ARG:裙, CHOICES)%
		CASE "下身衣装"
			CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
		CASE "连褲襪"
			CSTR:ARG:下身衣装 = %MERGE_STR(CSTR:ARG:下身衣装, CHOICES)%
		CASE "吊帯内衣"
			CSTR:ARG:襪子 = %MERGE_STR(CSTR:ARG:襪子, CHOICES)%
		CASE "贴身制服"
			CSTR:ARG:贴身制服 = %MERGE_STR(CSTR:ARG:贴身制服, CHOICES)%
		CASE "胖次"
			CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, CHOICES)%
			SIF EQUIP:ARG:胸罩 && CHECK_CUSEABLE(BRA(ARG, "種類"), CHOICES, ARG)
				CSTR:ARG:胸罩 = %MERGE_STR(CSTR:ARG:胸罩, CHOICES)%
		CASE "胸罩"
			CSTR:ARG:胸罩 = %MERGE_STR(CSTR:ARG:胸罩, CHOICES)%
			SIF EQUIP:ARG:胖次 && CHECK_CUSEABLE(PANTIES(ARG, "種類"), CHOICES, ARG)
				CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, CHOICES)%
		CASE "手套"
			CSTR:ARG:手套 = %MERGE_STR(CSTR:ARG:手套, CHOICES)%
		CASE "襪子"
			CSTR:ARG:襪子 = %MERGE_STR(CSTR:ARG:襪子, CHOICES)%
		CASE "靴"
			CSTR:ARG:靴 = %MERGE_STR(CSTR:ARG:靴, CHOICES)%
		CASE "饰"
			SELECTCASE CHOICES
			CASE "赤", "青", "黒", "白"
				CHOICES += "い"
			CASEELSE
				CHOICES += "の"
			ENDSELECT
			CALL SET_OTHER, @"%CHOICES%%NAME_LASTCHOICE%", ARG
		ENDSELECT
	ENDSELECT
;それ以外は種類選択。区分_名称 となっている
CASEELSE
	STR:LASTCHOICE = %CHOICES%
	VARSET LOCALS
	SPLIT CHOICES, "_", LOCALS
	CHOICES = %LOCALS:1%

	SELECTCASE LOCALS
	CASE "類型選択"
		STR:LASTCHOICE = %TYPE_LASTCHOICE%_%CHOICES%
		SELECTCASE TYPE_LASTCHOICE
		CASE "装扮"
			ARGS = %CHOICES%
			CALL SETFLAG, "内衣以外全脱ぎ", ARG
			CALL SET_OTHER, "", ARG
			CALL SET_COSTUME, CHOICES, ARG
		CASE "外衣"
			CALL SET_SHIRT, CHOICES, ARG
		CASE "裙"
			CALL SET_SKIRT, CHOICES, ARG
		CASE "下身衣装"
			CALL SET_BOTTOM, CHOICES, ARG
		CASE "贴身制服"
			CALL SET_BODYS, CHOICES, ARG
		CASE "胖次"
			CALL SET_PANTIES, CHOICES, ARG
		CASE "胸罩"
			CALL SET_BRA, CHOICES, ARG
		CASE "手套"
			CALL SET_GLOVE, CHOICES, ARG
		CASE "襪子"
			CALL SET_SOCKS, CHOICES, ARG
		CASE "靴"
			CALL SET_SHOES, CHOICES, ARG
		CASE "饰"
			CALL SET_OTHER, CHOICES, ARG
		ENDSELECT
	CASE "装扮"
		CALL SETFLAG, "内衣以外全脱ぎ", ARG
		CALL SET_OTHER, "", ARG
		IF CHOICES == "没有"
			ARGS = 
		ELSE
			ARGS = %CHOICES%
			CALL SET_COSTUME, CHOICES, ARG
		ENDIF
	CASE "外衣"
		IF ALLSAMES(SHIRT(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_SHIRT, "", ARG
		ELSE
			CALL SET_SHIRT, CHOICES, ARG
		ENDIF
	CASE "裙"
		IF ALLSAMES(SKIRT(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_SKIRT, "", ARG
		ELSE
			CALL SET_SKIRT, CHOICES, ARG
		ENDIF
	CASE "下身衣装"
		IF ALLSAMES(BOTTOM(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_BOTTOM, "", ARG
		ELSE
			CALL SET_BOTTOM, CHOICES, ARG
		ENDIF
	CASE "贴身制服"
		IF ALLSAMES(BODYS(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_BODYS, "", ARG
		ELSE
			CALL SET_BODYS, CHOICES, ARG
		ENDIF
	CASE "胖次"
		IF ALLSAMES(PANTIES(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_PANTIES, "", ARG
		ELSE
			CALL SET_PANTIES, CHOICES, ARG
		ENDIF
	CASE "胸罩"
		IF ALLSAMES(BRA(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_BRA, "", ARG
		ELSE
			CALL SET_BRA, CHOICES, ARG
		ENDIF
	CASE "手套"
		IF ALLSAMES(GLOVE(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_GLOVE, "", ARG
		ELSE
			CALL SET_GLOVE, CHOICES, ARG
		ENDIF
	CASE "襪子"
		IF ALLSAMES(SOCKS(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_SOCKS, "", ARG
		ELSE
			CALL SET_SOCKS, CHOICES, ARG
		ENDIF
	CASE "靴"
		IF ALLSAMES(SHOES(ARG, "種類"), CHOICES, NAME_LASTCHOICE)
			CALL SET_SHOES, "", ARG
		ELSE
			CALL SET_SHOES, CHOICES, ARG
		ENDIF
	CASE "饰"
		CALL INVERTSET_OTHER, CHOICES, ARG
	ENDSELECT

	SIF CHOICES == "没有"
		STR:LASTCHOICE = 
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;-------------------------------------------------
;SAVE, LOAD共にARGSに記録する衣装の文字列が入っている
;GLOBALS:0～23を使う
;-------------------------------------------------
@SAVE_CLO, ARGS, ARG
#DIM CHOICE
#DIM MEMO_LINECOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

MEMO_LINECOUNT = LINECOUNT
LOADGLOBAL

PRINTL 
PRINTFORML 要记录到几号位置呢？点击[DEL]可以进行删除（请注意存档是全游戏存档公用的）
CALL SHOW_CLOSAVE, ARG
PRINTFORML [100] 返回　[200] 整理编号　[300] \@ GLOBAL:衣装シミュ颜绘表示 ? 文字列 # 颜绘 \@表示に変更　[400] \@ TFLAG:页数 ? 前 # 次 \@一页

CALL INPUT_SELECT, 48, 100, 200, 300, 400
CHOICE = RESULT

SELECTCASE CHOICE
CASE 400
	INVERTBIT TFLAG:页数, 0
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 300
	INVERTBIT GLOBAL:衣装シミュ颜绘表示, 0
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 200
	CALL SORT_CLOSAVE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 100
CASE 24 TO 48
	GLOBALS:(CHOICE-24) = 
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASEELSE
	IF GLOBALS:CHOICE != ""
		PRINTFORML 覆盖[{CHOICE, 2}]号存档可以吗？
		CALL PRINT_SELECT, "是/否"
		IF RESULT
			CLEARLINE LINECOUNT - MEMO_LINECOUNT
			RESTART
		ENDIF
	ENDIF
	STR:衣装記録 = %ARGS%
	CALL SETFLAG, "衣装記録からの復元", ARG
	;あってはならない装備を取下
	SIF CHECK_OTHER("黒色颈环", ARG)
		CALL RESET_OTHER, "黒色颈环", ARG
	CALL SETFLAG, "衣装の記録", ARG
	GLOBALS:CHOICE = %STR:衣装記録%
ENDSELECT

STR:衣装記録 = %ARGS%
CALL SETFLAG, "衣装記録からの復元", ARG
SAVEGLOBAL
RETURN 1


@LOAD_CLO, ARGS, ARG
#DIM CHOICE
#DIM MEMO_LINECOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

MEMO_LINECOUNT = LINECOUNT
LOADGLOBAL

PRINTL 
PRINTFORML 使用几号存档呢？点击[DEL]可以进行删除
CALL SHOW_CLOSAVE, ARG
PRINTFORML [100] 返回　[200] 整理编号　[300] \@ GLOBAL:衣装シミュ颜绘表示 ? 文字列 # 颜绘 \@表示に変更　[400] \@ TFLAG:页数 ? 前 # 次 \@一页

CALL INPUT_SELECT, 48, 100, 200, 300, 400
CHOICE = RESULT

SELECTCASE CHOICE
CASE 400
	INVERTBIT TFLAG:页数, 0
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 300
	INVERTBIT GLOBAL:衣装シミュ颜绘表示, 0
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 200
	CALL SORT_CLOSAVE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 100
	STR:衣装記録 = %ARGS%
CASE 24 TO 48
	GLOBALS:(CHOICE-24) = 
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASEELSE
	IF GLOBALS:CHOICE == ""
		PRINTFORMW [{CHOICE, 2}]号存档里面什么都没有
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ELSE
		STR:衣装記録 = %GLOBALS:CHOICE%
	ENDIF
ENDSELECT

CALL SETFLAG, "衣装記録からの復元", ARG
SAVEGLOBAL
RETURN 1



@SHOW_CLOSAVE, ARG
#DIM LCOUNT
#DIM LCOUNT2

;阴蒂ック指令表示中ON
SETBIT FLAG:メイン画面, 20
;颜绘保存加载中ON
SETBIT FLAG:状況判定, 51

FOR LCOUNT, TFLAG:页数*12, TFLAG:页数*12 + 12
	ADDCOPYCHARA ARG
	CFLAG:(CHARANUM - 1):角色用 = LCOUNT
	SELECTCASE GLOBALS:LCOUNT
	CASE ""
		CALL SET_CEVENT, "颜绘没有", CHARANUM - 1
		CALL SETFLAG, "衣装全脱ぎ", CHARANUM - 1
	CASEELSE
		CALL DEL_CEVENT, "颜绘没有", CHARANUM - 1
		STR:衣装記録 = %GLOBALS:LCOUNT%
		CALL SETFLAG, "衣装記録からの復元", CHARANUM - 1
	ENDSELECT
	IF GLOBAL:衣装シミュ颜绘表示
		SELECTCASE LCOUNT
		CASE 5, 11, 17, 23
			CALL PRINT_MAIDFACE, CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1
			CALL PRINT_CUTIN, "縮小", CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1

			FOR LCOUNT2, 0, 6
				IF CEVENT("颜绘没有", CHARANUM - 6 + LCOUNT2)
					PRINTFORM %TEXT_LJ(@" [{LCOUNT-5+LCOUNT2, 2}]", NUMC_MAIDFACE() )%
				ELSE
					PRINTFORM %TEXT_LJ(@" [{LCOUNT-5+LCOUNT2, 2}]", NUMC_MAIDFACE()-7)%
					PRINTBUTTON "[DEL]", 24+LCOUNT-5+LCOUNT2
					PRINTPLAIN 　
				ENDIF
			NEXT
			PRINTL 
			DELCHARA CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1
		ENDSELECT
	ELSE
		SELECTCASE GLOBALS:LCOUNT
		CASE ""
			PRINTFORML  [{LCOUNT, 2}] ----
		CASEELSE
			PRINTFORM  [{LCOUNT, 2}] 
			CALL PRINT_DRESS, CHARANUM - 1, "衣装表示"
			PRINTBUTTON "　[DEL]", 24+LCOUNT
			PRINTL 
		ENDSELECT
		DELCHARA CHARANUM - 1
		SIF LCOUNT == TFLAG:页数*12 + 12 - 1 || GLOBALS:LCOUNT + GLOBALS:(LCOUNT + 1) != ""
			CALL BLANKLINE
	ENDIF
NEXT

CLEARBIT FLAG:メイン画面, 20
CLEARBIT FLAG:状況判定, 51



@SHOW_CLOSAVE_OLD, ARG
#DIM LCOUNT
FOR LCOUNT, 0, 20
	SELECTCASE GLOBALS:LCOUNT
	CASE ""
		PRINTFORML  [{LCOUNT, 2}] ----
	CASEELSE
		PRINTFORM  [{LCOUNT, 2}] 
		STR:衣装記録 = %GLOBALS:LCOUNT%
		CALL SETFLAG, "衣装記録からの復元", ARG
		CALL PRINT_DRESS, ARG, "衣装表示"
		PRINTBUTTON "　[DEL]", 20+LCOUNT
		PRINTL 
	ENDSELECT
	SIF LCOUNT == 19 || GLOBALS:LCOUNT + GLOBALS:(LCOUNT + 1) != ""
		CALL BLANKLINE
NEXT

@SORT_CLOSAVE
#DIM LCOUNT
#DIM LCOUNT2

;24番目は見る必要ないので最初から19番までとする
FOR LCOUNT, 0, 23
	SELECTCASE GLOBALS:LCOUNT
	CASE ""
		FOR LCOUNT2, LCOUNT+1, 24
			SELECTCASE GLOBALS:LCOUNT2
			CASE ""
			CASEELSE
				SWAP GLOBALS:LCOUNT, GLOBALS:LCOUNT2
				BREAK
			ENDSELECT
		NEXT
	ENDSELECT
NEXT
SAVEGLOBAL


;-------------------------------------------------
;引き出しの内衣管理
;-------------------------------------------------
@CHANGE_DRAWER_UNDERWEAR, ARG
#DIM CHOICE
#DIM MEMO_LINECOUNT
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

MEMO_LINECOUNT = LINECOUNT
;[300]のために呼んでおく
LOADGLOBAL

;タンスの中身を出す
VARSET LOCALS
SPLIT CSTR:ARG:所持内衣, "＠", LOCALS

PRINTL 
PRINTFORML 現在%CALLNAME:MASTER%的秘密抽屉里面放入的内衣有
PRINTFORML 选择内衣可以穿上。点击[DEL]的话会删除＆点击空的编号会将现在的内衣放入(编号自动整理)
CALL SHOW_DRAWER_UNDERWEAR, ARG
PRINTFORML [100] 返回　[300] \@ GLOBAL:衣装シミュ颜绘表示 ? 文字列 # 颜绘 \@表示

CALL INPUT_SELECT, 36, 100, 300
CHOICE = RESULT

SELECTCASE CHOICE
CASE 300
	INVERTBIT GLOBAL:衣装シミュ颜绘表示, 0
	SAVEGLOBAL
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
;[DEL]した場合
CASE 18 TO 35
	LOCALS:(CHOICE-18) = 
	CSTR:ARG:所持内衣 = 
	FOR LCOUNT, 0, 18
		SELECTCASE LOCALS:LCOUNT
		CASE ""

		CASEELSE
			CSTR:ARG:所持内衣 +=  @"%LOCALS:LCOUNT%＠"
		ENDSELECT
	NEXT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 100

CASEELSE
	;更衣場合
	IF LOCALS:CHOICE != ""
		STR:換装衣装 = %LOCALS:CHOICE%
		CALL SETFLAG, "内衣の換装", ARG
	;しまう場合
	ELSE
		CALL SETFLAG, "所持内衣に加える", ARG
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
ENDSELECT
RETURN 1




@SHOW_DRAWER_UNDERWEAR, ARG
#DIM LCOUNT
#DIM LCOUNT2

;阴蒂ック指令表示中ON
SETBIT FLAG:メイン画面, 20
;颜绘保存加载中ON
SETBIT FLAG:状況判定, 51

;タンスの中身を出す
VARSET LOCALS
SPLIT CSTR:ARG:所持内衣, "＠", LOCALS

FOR LCOUNT, TFLAG:页数*18, TFLAG:页数*18 + 18
	ADDCOPYCHARA ARG
	CFLAG:(CHARANUM - 1):角色用 = LCOUNT
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		CALL SET_CEVENT, "颜绘没有", CHARANUM - 1
		CALL SETFLAG, "衣装全脱ぎ", CHARANUM - 1
	CASEELSE
		CALL DEL_CEVENT, "颜绘没有", CHARANUM - 1
		STR:衣装記録 = %LOCALS:LCOUNT%
		CALL SETFLAG, "衣装記録からの復元", CHARANUM - 1
	ENDSELECT
	IF GLOBAL:衣装シミュ颜绘表示
		SELECTCASE LCOUNT
		CASE 5, 11, 17, 23
			CALL PRINT_MAIDFACE, CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1
			CALL PRINT_CUTIN, "縮小", CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1

			FOR LCOUNT2, 0, 6
				IF CEVENT("颜绘没有", CHARANUM - 6 + LCOUNT2)
					PRINTFORM %TEXT_LJ(@" [{LCOUNT-5+LCOUNT2, 2}]", NUMC_MAIDFACE() )%
				ELSE
					PRINTFORM %TEXT_LJ(@" [{LCOUNT-5+LCOUNT2, 2}]", NUMC_MAIDFACE()-7)%
					PRINTBUTTON "[DEL]", 18+LCOUNT-5+LCOUNT2
					PRINTPLAIN 　
				ENDIF
			NEXT
			PRINTL 
			DELCHARA CHARANUM - 6, CHARANUM - 5, CHARANUM - 4, CHARANUM - 3, CHARANUM - 2, CHARANUM - 1
		ENDSELECT
	ELSE
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			PRINTFORML  [{LCOUNT, 2}] ----
			PRINTL 
		CASEELSE
			PRINTFORM  [{LCOUNT, 2}] 
			CALL PRINT_DRESS, CHARANUM - 1, "衣装表示"
			PRINTBUTTON "　[DEL]", 18+LCOUNT
			PRINTL 
		ENDSELECT
		DELCHARA CHARANUM - 1
		SIF LCOUNT == TFLAG:页数*18 + 18 - 1 || LOCALS:LCOUNT + LOCALS:(LCOUNT + 1) != ""
			CALL BLANKLINE
	ENDIF
NEXT

CLEARBIT FLAG:メイン画面, 20
CLEARBIT FLAG:状況判定, 51

