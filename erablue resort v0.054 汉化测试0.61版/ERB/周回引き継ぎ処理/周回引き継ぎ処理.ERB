@周回引き継ぎ処理(戻れないフラグ = 0)
#DIM 引き継ぎ枠
#DIM 同行枠
#DIM 戻れないフラグ
#DIMS HTML文
#DIM DYNAMIC 起動直後

IF 起動直後 == 0
	VARSET 引き継ぎキャラ番号
	VARSET キャラ妊娠引き継ぎ
	起動直後 = 1
ENDIF

DRAWLINE
PRINTL 正在继承部分数据并开始新游戏
PRINTL
PRINTL ・继承数据
PRINTL 　　玩家特殊能力・随机角色存在状态（开启时）
PRINTL 　　各角色身体熟知度及弱点披露状态・持有卢比・戒指・耳饰・素材道具
PRINTL 　　探索道具・职业证・员工制服・选定角色的全部能力
PRINTL 　　其他特殊标记
DRAWLINE
引き継ぎ枠 = 引き継ぎ枠判定()
PRINTFORML ■预定继承角色（最多{引き継ぎ枠}名）
FOR LOCAL, 0, 引き継ぎ枠
	IF 引き継ぎキャラ番号:LOCAL
		PRINTBUTTON @"{LOCAL, 2}：%NAME表示((引き継ぎキャラ番号:LOCAL))%", LOCAL
		IF TALENT:(引き継ぎキャラ番号:LOCAL):妊娠
			PRINT 　　
			IF キャラ妊娠引き継ぎ:LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAIN [继承怀孕状态]
				RESETCOLOR
				PRINT  / 
				PRINTBUTTON "[不继承怀孕状态]", LOCAL + 200
			ELSE
				PRINTBUTTON "[继承怀孕状态]", LOCAL + 200
				PRINT  / 
				SETCOLOR カラーパレット("黄")
				PRINTPLAIN [不继承怀孕状态]
				RESETCOLOR
			ENDIF
		ENDIF
		PRINTL
	ELSE
		PRINTBUTTON @"{LOCAL, 2}：--------------------", LOCAL
		PRINTL
	ENDIF
NEXT
PRINTL
IF ランダムキャラ引き継ぎフラグ
	HTML_PRINT @"<button value='100'>[100] 随机角色存在状态 继承 / <font color='#%カラーパレット_HTML("グレーアウト")%'>不继承</font></button>", 1
ELSE
	HTML_PRINT @"<button value='100'>[100] 随机角色存在状态 <font color='#%カラーパレット_HTML("グレーアウト")%'>继承</font> / 不继承</button>", 1
ENDIF
PRINT 　　
PRINTBUTTON "[说明]", 101
PRINTL
SELECTCASE あなた引き継ぎフラグ
	CASE 0
		HTML文 = %CALLNAME:MASTER%の能力・履歴を
		HTML文 += @"<button value='202'><font color='#%カラーパレット_HTML("グレーアウト")%'>全部继承</font></button>"
		HTML文 += " / "
		HTML文 += @"<button value='201'><font color='#%カラーパレット_HTML("グレーアウト")%'>仅继承能力</font></button>"
		HTML文 += " / "
		HTML文 += "不继承"
		HTML_PRINT HTML文, 1
	CASE 1
		HTML文 = %CALLNAME:MASTER%の能力・履歴を
		HTML文 += @"<button value='202'><font color='#%カラーパレット_HTML("グレーアウト")%'>全部继承</font></button>"
		HTML文 += " / "
		HTML文 += "仅继承能力"
		HTML文 += " / "
		HTML文 += @"<button value='200'><font color='#%カラーパレット_HTML("グレーアウト")%'>不继承</font></button>"
		HTML_PRINT HTML文, 1
	CASE 2
		HTML文 = %CALLNAME:MASTER%の能力・履歴を
		HTML文 += @"全部继承"
		HTML文 += " / "
		HTML文 += @"<button value='201'><font color='#%カラーパレット_HTML("グレーアウト")%'>仅继承能力</font></button>"
		HTML文 += " / "
		HTML文 += @"<button value='200'><font color='#%カラーパレット_HTML("グレーアウト")%'>不继承</font></button>"
		HTML_PRINT HTML文, 1
ENDSELECT
PRINT 　　
PRINTBUTTON "[说明]", 103
PRINTL
IF キャラ弱点引き継ぎフラグ
	HTML_PRINT @"<button value='104'>[104] 角色弱点披露状态及随机弱点 继承 / <font color='#%カラーパレット_HTML("グレーアウト")%'>不继承</font></button>", 1
ELSE
	HTML_PRINT @"<button value='104'>[104] 角色弱点披露状态及随机弱点 <font color='#%カラーパレット_HTML("グレーアウト")%'>继承</font> / 不继承</button>", 1
ENDIF
PRINT 　　
PRINTBUTTON "[说明]", 105
PRINTL
DRAWLINE

PRINT 多周目起始点：
SIF 周回スタート != 0
	SETCOLOR カラーパレット("グレーアウト")
PRINTBUTTON "[全新开始]", 1000
RESETCOLOR
PRINT  / 
SIF 周回スタート != 1
	SETCOLOR カラーパレット("グレーアウト")
PRINTBUTTON "[从度假区等级Ｄ开始]", 1001
RESETCOLOR

PRINTL
PRINTBUTTON "[起始点说明]", 106
PRINTL
DRAWLINE
PRINTL
PRINTBUTTON "[998] 确认开始新游戏", 998
PRINTL
IF 戻れないフラグ == 0
	PRINTBUTTON "[999] 返回", 999
	PRINTL
ENDIF
PRINTL
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1000 TO 1010
		周回スタート = LOCAL - 1000
		RESTART
	CASE 100
		INVERTBIT ランダムキャラ引き継ぎフラグ, 0
		RESTART
	CASE 101
		DRAWLINE
		PRINTL 将本周目存在的随机角色延续至下周目
		PRINTL 随机角色将不再作为员工，转为正式角色待遇
		PRINTL 即使关闭此选项，单独选中的继承角色仍将保留
		WAIT
		RESTART
	CASE 200 TO 202
		あなた引き継ぎフラグ = LOCAL - 200
		RESTART
	CASE 103
		DRAWLINE
		PRINTFORML 继承%CALLNAME:MASTER%的状态、已获特殊能力及经历
		PRINTFORML 继承后，%CALLNAME:MASTER%与未继承角色的经历将产生矛盾
		PRINTL 与继承角色的经历关联性将保持完整
		WAIT
		RESTART
	CASE 104
		INVERTBIT キャラ弱点引き継ぎフラグ, 0
		RESTART
	CASE 105
		DRAWLINE
		PRINTFORML 继承角色弱点披露状态及随机分配的弱点槽位
		PRINTFORML 继承后已披露弱点将直接解锁，角色弱点配置与本周目相同
		PRINTL 不继承则重置所有弱点状态，除剧情设定弱点外均随机重置
		WAIT
		RESTART
	CASE 106
		DRAWLINE
		PRINTL 可选择多周目游戏起始进度
		PRINTL 将禁用快速启动模式的自选起始点功能
		PRINTL
		PRINTL ・全新开始
		PRINTL 　重置除继承要素外的所有数据
		PRINTL 　剧情进度与地城通关状态均重置
		PRINTL
		PRINTL ・从度假区等级Ｄ开始
		PRINTL 　模拟达成度假区等级Ｄ时的初始状态
		PRINTL 　　　度假区等级Ｄ、人气度301、持有卢比300,000、持有ZP3,000
		PRINTL 　　　露营地/餐厅/游戏厅/温泉/泳池均完成Lv1改造
		PRINTL 　　　图书室/训练场/自宅已建造，商业区&锻造设施已创建
		PRINTL 　　　解锁2个大型区域，居住区&海域已建造
		PRINTL 　　　已通关6种地城（沿岸森林/食人鸟之岛/无名遗迹/
		PRINTL 　　　　　　　　　　　　　巴鲁慈地下废弃工厂/深海古战场遗迹/风之溪谷参道）
		PRINTL 　　　相关功能与事件均已解锁
		PRINTL 
		FORCEWAIT
		RESTART
	CASE 999
		IF 戻れないフラグ == 0
			VARSET 引き継ぎキャラ番号
			RETURN 1
		ELSE
			RESTART
		ENDIF
	CASE 998
		CALL 周回実処理(引き継ぎ枠)
		RETURN -1
	CASE 200 TO 299
		INVERTBIT キャラ妊娠引き継ぎ:(LOCAL - 200), 0
		RESTART
	CASEELSE
		CALL 陥落キャラ選択()
		引き継ぎキャラ番号:LOCAL = RESULT
		RESTART
ENDSELECT



@引き継ぎ枠判定
#FUNCTION
#DIM 引き継ぎ枠
引き継ぎ枠 = 0

;ハーレム能力に応じて増加
SELECTCASE あなた特殊能力:ハーレム
	CASE 1
		引き継ぎ枠 += 1
	CASE 2
		引き継ぎ枠 += 1
	CASE 3
		引き継ぎ枠 += 2
	CASE 4
		引き継ぎ枠 += 3
	CASE 5
		引き継ぎ枠 += 10
ENDSELECT

;セフレ数に応じて増加
引き継ぎ枠 += SQRT(MIN(あなたセフレ人数(), 100))

;最低１人
引き継ぎ枠 = MAX(引き継ぎ枠, 1)
RETURNF 引き継ぎ枠

@引き継ぎ不能キャラ(キャラ番号)
#FUNCTION
#DIM キャラ番号

IF TALENT:キャラ番号:妊娠
	RESULTS '= "无法继承怀孕中的角色"
	RETURNF 1
ENDIF

RESULTS '= ""
RETURNF 0

@陥落キャラ選択
#DIM 現在ページ
#DIM 表示候補, 500
#DIM ソート基準値, 500
#DIM 候補人数
現在ページ = 0

$最初から
;表示候補選定
LOCAL:1 = 0
VARSET 表示候補
VARSET LOCALS
;陥落者のみ
FOR LOCAL, 1, CHARANUM
	SIF MATCH(引き継ぎキャラ番号, LOCAL)
		CONTINUE
	IF TALENT:LOCAL:恋慕 || TALENT:LOCAL:セフレ
		表示候補:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	ENDIF
NEXT
候補人数 = LOCAL:1

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, 候補人数
	IF 引き継ぎ不能キャラ(表示候補:LOCAL)
		LOCALS += @"<font color='#666666'><button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME表示(表示候補:LOCAL)), "]"),41,LEFT%</button>"

		IF TALENT:(表示候補:LOCAL):恋慕
			LOCALS += "　　　爱慕沦陷　"
		ELSE
			LOCALS += "　　　炮友沦陷　"
		ENDIF
		LOCALS += "</font>"
	ELSE
		LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME表示(表示候補:LOCAL)), "]"),41,LEFT%</button>"

		IF TALENT:(表示候補:LOCAL):恋慕
			LOCALS += "　　　<font color='#FF3399'>爱慕沦陷</font>　"
		ELSE
			LOCALS += "　　　炮友沦陷　"
		ENDIF
	ENDIF

	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 29
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [900]上一页
ELSE
	PRINTLC  
ENDIF
IF 現在ページ < 候補人数 / 30
	PRINTLC  [901]下一页
ENDIF
PRINTL
DRAWLINE
PRINTL
PRINTBUTTONLC "[1000]返回", 1000
PRINTL

BINPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 900
	現在ページ = MAX(現在ページ - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 901
	現在ページ = MIN(現在ページ + 1, 候補人数 / 30)
	GOTO 表示処理_LOOP
ELSE
	IF 引き継ぎ不能キャラ(RESULT)
		PRINTFORMW %RESULTS%
		GOTO 表示処理_LOOP
	ENDIF
	RETURN RESULT
ENDIF


@周回実処理(引き継ぎ枠)
#DIM 弱点配列
#DIM 同行枠
#DIM 候補数
#DIM 相手キャラ
#DIM 引き継ぎ枠
; ・继承数据
; あなた特殊能力・ランダムキャラの存在（従業員ではなくなります）
; 各角色身体熟知度及弱点披露状态・持有卢比・戒指・耳饰・素材道具
; 探索道具・职业证・员工制服・选定角色的全部能力
; 其他特殊标记

引き継ぎ中フラグ = 1

;人物データベースを最後に更新しておく
FOR LOCAL, 0, CHARANUM
	CALL UPDATE_PERSON_FROM_CHARA(LOCAL)
NEXT

;指輪と耳飾りの装備を外しておく
FOR LOCAL, 0, DT_ROW_LENGTH("所持指輪データベース")
	IF DT_CELL_GET("所持指輪データベース", LOCAL, "装備キャラ番号")
		装備ステータス補正保存:(DT_CELL_GET("所持指輪データベース", LOCAL, "装備キャラ番号")):装備指輪番号 = 0
		DT_CELL_SET "所持指輪データベース", LOCAL, "装備キャラ番号", 0
	ENDIF
NEXT
FOR LOCAL, 0, DT_ROW_LENGTH("所持耳飾りデータベース")
	IF DT_CELL_GET("所持耳飾りデータベース", LOCAL, "装備キャラ番号")
		装備ステータス補正保存:(DT_CELL_GET("所持耳飾りデータベース", LOCAL, "装備キャラ番号")):装備指輪番号 = 0
		DT_CELL_SET "所持耳飾りデータベース", LOCAL, "装備キャラ番号", 0
	ENDIF
NEXT

;データベースを退避
DT_TOXML "人物データベース"
SAVETEXT RESULTS:0, "dat/人物DT_XML.txt"
SAVETEXT RESULTS:1, "dat/人物DT_schema.txt"
DT_TOXML "所持素材データベース"
SAVETEXT RESULTS:0, "dat/素材DT_XML.txt"
SAVETEXT RESULTS:1, "dat/素材DT_schema.txt"
DT_TOXML "所持ジョブ証データベース"
SAVETEXT RESULTS:0, "dat/証DT_XML.txt"
SAVETEXT RESULTS:1, "dat/証DT_schema.txt"
DT_TOXML "所持指輪データベース"
SAVETEXT RESULTS:0, "dat/指輪DT_XML.txt"
SAVETEXT RESULTS:1, "dat/指輪DT_schema.txt"
DT_TOXML "所持耳飾りデータベース"
SAVETEXT RESULTS:0, "dat/耳飾りDT_XML.txt"
SAVETEXT RESULTS:1, "dat/耳飾りDT_schema.txt"
DT_TOXML "所持アイテムデータベース"
SAVETEXT RESULTS:0, "dat/アイテムDT_XML.txt"
SAVETEXT RESULTS:1, "dat/アイテムDT_schema.txt"
DT_TOXML "履歴データベース"
SAVETEXT RESULTS:0, "dat/履歴DT_XML.txt"
SAVETEXT RESULTS:1, "dat/履歴DT_schema.txt"
DT_TOXML "エンディング用変数配列"
SAVETEXT RESULTS:0, "dat/エンディングDT_XML.txt"
SAVETEXT RESULTS:1, "dat/エンディングDT_schema.txt"
DT_TOXML "弱点の欠片データベース"
SAVETEXT RESULTS:0, "dat/弱点の欠片DT_XML.txt"
SAVETEXT RESULTS:1, "dat/弱点の欠片DT_schema.txt"

;変数を退避
制服引き継ぎ = %所持制服文字列%
エロ衣装引き継ぎ = %所持エロ衣装文字列%
所持ルピ引き継ぎ = MONEY
周回後スタート地点 = 周回スタート

LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	キャラ変数引き継ぎ:(LOCAL):キャラナンバー = NO:LOCAL
	IF NO:LOCAL == 999
		キャラ変数引き継ぎ:(LOCAL):キャラナンバー = ランダムキャラ記録位置判別(LOCAL) * -1
	ENDIF
	キャラ変数引き継ぎ:(LOCAL):肉体熟知 = EXP:LOCAL:肉体熟知
	IF キャラ弱点引き継ぎフラグ
		FOR 弱点配列, 0, 弱点コマンド数上限
			キャラ変数引き継ぎ:(LOCAL):(弱点配列 + 2) = 弱点看破:LOCAL:弱点配列
			キャラ文字列引き継ぎ:(LOCAL):弱点配列 = %弱点コマンド枠:LOCAL:弱点配列%
		NEXT
		FOR 弱点配列, 0, 追加弱点コマンド数上限
			キャラ文字列引き継ぎ:(LOCAL):(弱点配列 + 50) = %追加弱点コマンド枠:LOCAL:弱点配列%
		NEXT
	ENDIF
NEXT

ARRAYCOPY "オート射精先オプション", "オート射精設定引き継ぎ"
ARRAYCOPY "OPTION変数", "オプション引き継ぎ"
ARRAYCOPY "素質切り替えOPTION記憶", "素質切り替え引き継ぎ"
ARRAYCOPY "画像サイズ切り替えOPTION記憶", "画像サイズ切り替え引き継ぎ"
ARRAYCOPY "一日終了時オナニーOPTION", "一日終了時オナニー引き継ぎ"
ARRAYCOPY "ITEM", "アイテム引き継ぎ"



VARSET あなた特殊能力引き継ぎ
VARSET あなた技巧引き継ぎ
;あなたの退避
IF あなた引き継ぎフラグ == 2
	SAVECHARA "あなた", "あなた", 0
	あなた引き継ぎ = 2
	FOR LOCAL, 0, 100
		あなた特殊能力引き継ぎ:LOCAL = あなた特殊能力:LOCAL
	NEXT
	FOR LOCAL, 0, 20
		あなた技巧引き継ぎ:LOCAL = ABL:MASTER:(LOCAL + 20)
	NEXT
ELSEIF あなた引き継ぎフラグ == 1
	あなた引き継ぎ = 1
	FOR LOCAL, 0, 100
		あなた特殊能力引き継ぎ:LOCAL = あなた特殊能力:LOCAL
	NEXT
	FOR LOCAL, 0, 20
		あなた技巧引き継ぎ:LOCAL = ABL:MASTER:(LOCAL + 20)
	NEXT
ENDIF

;選択したキャラの退避
キャラ同士関係性データ = 
引き継ぎ人数 = 引き継ぎ枠
FOR LOCAL, 0, 引き継ぎ枠
	IF 引き継ぎキャラ番号:LOCAL > 0
		;関係性は一旦リセットする
		;ただし、引き継ぐキャラ同士は維持する
		
		CSTR:(引き継ぎキャラ番号:LOCAL):引き継ぎ用同行キャラ確率保存 = 
		FOR 同行枠, 0, 100
			SIF 同行候補キャラ番号:(引き継ぎキャラ番号:LOCAL):同行枠 == 0
				CONTINUE
			IF FINDELEMENT(引き継ぎキャラ番号, 同行候補キャラ番号:(引き継ぎキャラ番号:LOCAL):同行枠) > -1
				CSTR:(引き継ぎキャラ番号:LOCAL):引き継ぎ用同行キャラ確率保存 += @"%NAME表示((同行候補キャラ番号:(引き継ぎキャラ番号:LOCAL):同行枠))%_{同行候補キャラ確率:(引き継ぎキャラ番号:LOCAL):同行枠}_"
			ENDIF
		NEXT

		VARSET RELATION_NO:(引き継ぎキャラ番号:LOCAL):0
		VARSET RELATION_VAL:(引き継ぎキャラ番号:LOCAL):0
		VARSET 同行候補キャラ番号:(引き継ぎキャラ番号:LOCAL):0
		VARSET 同行候補キャラ確率:(引き継ぎキャラ番号:LOCAL):0

		TALENT:(引き継ぎキャラ番号:LOCAL):従業員 = 0
		CFLAG:(引き継ぎキャラ番号:LOCAL):長期雇用 = 0
		TALENT:(引き継ぎキャラ番号:LOCAL):定住者 = 0
		TALENT:(引き継ぎキャラ番号:LOCAL):店持ち = 0
		IF キャラ妊娠引き継ぎ:LOCAL == 0
			TALENT:(引き継ぎキャラ番号:LOCAL):妊娠 = 0
			CFLAG:(引き継ぎキャラ番号:LOCAL):妊娠経過日 = 0
			CFLAG:(引き継ぎキャラ番号:LOCAL):子の父親 = -1
			CFLAG:(引き継ぎキャラ番号:LOCAL):妊娠発覚 = 0
			CFLAG:(引き継ぎキャラ番号:LOCAL):今回受胎人数 = 0
		ENDIF
		;SAVECHARAメモにキャラのNOを入れておく
		SAVECHARA TOSTR(LOCAL), TOSTR(NO:(引き継ぎキャラ番号:LOCAL)), 引き継ぎキャラ番号:LOCAL

		;キャラ同士恋慕セフレの記録
		VARSET RESULT
		候補数 = DT_SELECT("関係性データベース", @"対象キャラ１ = {CFLAG:(引き継ぎキャラ番号:LOCAL):人物番号} Or 対象キャラ２ = {CFLAG:(引き継ぎキャラ番号:LOCAL):人物番号}")
		IF 候補数
			FOR LOCAL:1, 1, 候補数 + 1
				相手キャラ = FIND_CHARA_FROM_PERSON_ID(関係性_相手人物番号取得(引き継ぎキャラ番号:LOCAL, RESULT:(LOCAL:1)))
				SIF 相手キャラ <= 0
					CONTINUE
				SIF MATCH(引き継ぎキャラ番号, 相手キャラ) < 1
					CONTINUE
				;既にあるならスルー
				SIF STRFIND(キャラ同士関係性データ, @"%NAME:(引き継ぎキャラ番号:LOCAL)%_%NAME:相手キャラ%_%DT_CELL_GETS("関係性データベース", RESULT:(LOCAL:1), "関係性種別", 1)%") >= 0
					CONTINUE
				SIF STRFIND(キャラ同士関係性データ, @"%NAME:相手キャラ%_%NAME:(引き継ぎキャラ番号:LOCAL)%_%DT_CELL_GETS("関係性データベース", RESULT:(LOCAL:1), "関係性種別", 1)%") >= 0
					CONTINUE
				;引き継ぎ用に登録
				キャラ同士関係性データ += @"%NAME:(引き継ぎキャラ番号:LOCAL)%_%NAME:相手キャラ%_%DT_CELL_GETS("関係性データベース", RESULT:(LOCAL:1), "関係性種別", 1)%+++"
			NEXT
		ENDIF
	ENDIF
NEXT

周回回数累計:0 += 1

;最大レベルの引き継ぎ
CALL 最大レベル算出保存()
最大レベル引き継ぎ = 最大レベル保存
;リゾート名退避
リゾート名引き継ぎ '= リゾート名称

ARRAYCOPY "周回回数累計", "周回回数累計_退避"
IF ランダムキャラ引き継ぎフラグ
	ARRAYCOPY "従業員初期値記録", "従業員初期値記録_退避"
	RESETDATA
	ARRAYCOPY "従業員初期値記録_退避", "従業員初期値記録"
ELSE
	RESETDATA
ENDIF
ARRAYCOPY "周回回数累計_退避", "周回回数累計"


@ランダムキャラ記録位置判別(キャラ番号)
#FUNCTION
#DIM キャラ番号
#DIM 記録位置
#DIMS 分割文字列, 2

FOR 記録位置, 0, 従業員初期値最大記録数
	SPLIT 従業員初期値記録:記録位置, "_", 分割文字列
	IF TOINT(分割文字列:1) == キャラ番号
		RETURNF 記録位置
	ENDIF
NEXT
