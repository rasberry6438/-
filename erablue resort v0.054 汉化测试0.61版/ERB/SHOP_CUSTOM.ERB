@CUSTOM_CHARAMAKE(ARG)
#DIM スタイル記録
;NOは適当に設定
NO:ARG = 0

;種族(1=ヒューマン 2=エルーン 3=ドラフ 4=ハーヴィン 5=星晶獣 6=その他)
TALENT:ARG:種族 = 1
;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:性別 = 1
SIF ARG == 0
	TALENT:ARG:性別 = 2
;年齢層
TALENT:ARG:年齢層 = 年齢層_大人
;体力
MAXBASE:ARG:体力 = 2000
BASE:ARG:体力 = MAXBASE:ARG:体力

DRAWLINE
PRINTL 请选择风格。
DRAWLINE
PRINTBUTTON "[0] 标准风格", 0
PRINTL
PRINTL 　　…可自由决定您设定的风格。
PRINTL
PRINTBUTTON "[1] 标准风格_快速启动模式", 1
PRINTL
PRINTL 　　…可自由决定您设定的风格。包含初始特典
PRINTL
PRINTBUTTON "[10] 古兰风格", 10
PRINTL
PRINTL 　　…将您设定为古兰（Gran）的风格。由于开发中，可能存在预期外的行为。
PRINTL
PRINTBUTTON "[11] 姬塔风格", 11
PRINTL
PRINTL 　　…将您设定为姬塔（Djeeta）的风格。由于开发中，可能存在预期外的行为。
PRINTL
PRINTBUTTON "[20] 自选角色风格", 20
PRINTL
PRINTL 　　…将您设定为其他角色的风格。由于开发中，可能存在预期外的行为。
PRINTL
BINPUT
スタイル記録 = RESULT
SELECTCASE スタイル記録
	CASE 0
		PRINTL ■常规风格
		PRINTL 　可自由设定您个人数据的风格。
		PRINTL 　本游戏版本默认基于此风格进行开发。
	CASE 1
		PRINTL ■常规风格
		PRINTL 　可自由设定您个人数据的风格。
		PRINTL 　本游戏版本默认基于此风格进行开发。
		PRINTL
		PRINTL ■快速启动模式
		PRINTL 　提供游戏初期的快速启动支援。
		PRINTL 　可从随机角色或自选角色（含EX角色）中挑选4人，提升初始好感度并直接雇佣为员工。
		PRINTL 　（部分角色不可选）
		PRINTL 　首月员工薪资全免。
		PRINTL 　此选择不会影响事件及剧情发展。
	CASE 10
		PRINTL ■古兰风格
		PRINTL 　将您设定为古兰（Gran）的特殊风格。
		PRINTL 　您的属性将与古兰同步，度假区内的古兰将不再出现。
		PRINTL 　初始雇佣「露莉亚」「碧」为员工，持有「生命连结」特质的角色将设定为您与露莉亚。
		PRINTL 　部分角色初始好感度提升，并获得「常客」特性。
		PRINTL 　与常规模式相同，您将无法参与战斗。
		PRINTL
		PRINTL ※开发中版本可能发生预期外的程序错误。
		PRINTL ※因事件文本基于常规风格编写，可能出现剧情逻辑异常。
	CASE 11
		PRINTL ■姬塔风格
		PRINTL 　将您设定为姬塔（Djeeta）的特殊风格。
		PRINTL 　您的属性将与姬塔同步，度假区内的姬塔将不再出现。
		PRINTL 　初始雇佣「露莉亚」「碧」为员工，持有「生命连结」特质的角色将设定为您与露莉亚。
		PRINTL 　部分角色初始好感度提升，并获得「常客」特性。
		PRINTL 　与常规模式相同，您将无法参与战斗。
		PRINTL
		PRINTL ※开发中版本可能发生预期外的程序错误。
		PRINTL ※因事件文本基于常规风格编写，可能出现剧情逻辑异常。
	CASE 20
		PRINTL ■自选角色风格
		PRINTL 　将您设定为自选角色的特殊风格。
		PRINTL 　您的属性将与所选角色同步，度假区内的该角色将不再出现。
		PRINTL 　与常规模式相同，您将无法参与战斗。
		PRINTL 　不提供其他额外加成。
		PRINTL
		PRINTL ※开发中版本可能发生预期外的程序错误。
		PRINTL ※因事件文本基于常规风格编写，可能出现剧情逻辑异常。
ENDSELECT
PRINTL
PRINTL 确定要以此风格开始吗？
PRINTBUTTONLC "[0] 确定", 0
PRINTBUTTONLC "[1] 取消", 1
PRINTL
BINPUT
IF RESULT == 0
	SELECTCASE スタイル記録
		CASE 0
			SAVESTR:あなたスタイル = 
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 1
			PRINTW 初始员工将在完成初次设定后进行选择。
			SAVESTR:あなたスタイル = スタートダッシュモード
			CALL NAME_CUSTOM(ARG)
			;簡易的に種族と性別だけ選ぶ形のを仮置きする
			CALL 仮置きCUSTOM_TERMINAL(ARG)
		CASE 10
			SAVESTR:あなたスタイル = グランくんスタイル
		CASE 11
			SAVESTR:あなたスタイル = ジータちゃんスタイル
		CASE 20
			SAVESTR:あなたスタイル = 任意キャラスタイル
			PRINTFORMW 自选角色将在完成初次设定后进行选择。
	ENDSELECT
ELSE
	RESTART
ENDIF


@仮置きCUSTOM_TERMINAL(ARG)
LOCAL = 3
SIF BASE:ARG:身長
	CLEARBIT LOCAL, 0
SIF BASE:ARG:バスト
	CLEARBIT LOCAL, 1
SIF LOCAL
	CALL SET_BODYSIZE(ARG, LOCAL)

$個別素質変更_LOOP
PRINTL
DRAWLINE
PRINTL 请设定各项属性。
DRAWLINE
PRINTL ■种族
FOR LOCAL, 1, 7
	IF TALENT:ARG:種族 == LOCAL
		PRINTFORM [%GET_TALENTNAME(200, LOCAL)%]
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", 100 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■性别
SELECTCASE TALENT:ARG:性別
	CASE 2
		PRINT [男性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[扶她]", 203
		RESETCOLOR
	CASE 1
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		PRINT [女性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[扶她]", 203
		RESETCOLOR
	CASE 3
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		PRINT [扶她]
ENDSELECT
PRINTL
DRAWLINE
PRINTL ■容貌
FOR LOCAL, -1, 3
	IF TALENT:ARG:容姿 == LOCAL
		PRINTFORM [%GET_TALENTNAME(160, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(160, LOCAL)%]", 300 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTL ■印象
FOR LOCAL, 0, 5
	IF TALENT:ARG:印象 == LOCAL
		IF LOCAL == 0
			PRINTFORM [淡泊]
		ELSE
			PRINTFORM [%GET_TALENTNAME(95, LOCAL)%]
		ENDIF
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[淡泊]", 330
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(95, LOCAL)%]", 330 + LOCAL
		ENDIF
		RESETCOLOR
	ENDIF
	SIF LOCAL != 4
		PRINT  / 
NEXT
IF TALENT:ARG:印象 == 0
	HTML_PRINT "<br>■淡泊<br>性格和态度干脆利落，无执念或纠缠的表现。<br>对好感度要素无修正加成。<br> "
ELSE
	HTML_PRINT GET_TALENTNAME(95, TALENT:ARG:印象, 1)
ENDIF
PRINTL
PRINTL ■年龄
FOR LOCAL, 年齢層_幼い, 年齢層_人外 + 1
	IF TALENT:ARG:年齢層 == LOCAL
		PRINTFORM [%GET_TALENTNAME(164, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(164, LOCAL)%]", 340 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINTL
PRINTFORML ■体格 \@ BASE:ARG:身長 ? （当前%DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cm）# \@
FOR LOCAL, -3, 3
	IF TALENT:ARG:体格 == LOCAL
		PRINTFORM [%GET_TALENTNAME(161, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(161, LOCAL)%]", 400 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 2
		PRINT  / 
NEXT
PRINT  / 
PRINTBUTTON @"[手动输入]", 410
PRINTL
PRINTL ■体型
FOR LOCAL, -3, 4
	IF TALENT:ARG:体型 == LOCAL
		PRINTFORM [%GET_TALENTNAME(163, LOCAL)%]
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(163, LOCAL)%]", 450 + LOCAL
		RESETCOLOR
	ENDIF
	SIF LOCAL != 3
		PRINT  / 
NEXT
PRINTL
IF TALENT:ARG:性別 & 2
	PRINTL ■男性生殖器尺寸
	FOR LOCAL, -2, 3
		IF TALENT:ARG:男性器サイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(162, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", 500 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 2
			PRINT  / 
	NEXT
	PRINTL
ENDIF
IF TALENT:ARG:性別 & 1
	PRINTFORML ■胸围尺寸 \@ BASE:ARG:バスト ? （当前%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%厘米 %GET_CUPSIZE(ARG), LOCAL, LEFT%）# \@
	FOR LOCAL, -2, 4
		IF TALENT:ARG:バストサイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(205, LOCAL)%]
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", 600 + LOCAL
			RESETCOLOR
		ENDIF
		SIF LOCAL != 3
			PRINT  / 
	NEXT
	PRINT  / 
	PRINTBUTTON @"[手动输入]", 610
	PRINTL
ENDIF

PRINTL
PRINTBUTTON "[999] 确认设置完成", 999
PRINTL

BINPUT

SELECTCASE RESULT
	CASE 101 TO 106
		TALENT:ARG:種族 = RESULT - 100
		CALL RESET_種族(ARG)
		GOTO 個別素質変更_LOOP
	CASE 201 TO 203
		TALENT:ARG:性別 = RESULT - 200
		CALL RESET_性別(ARG)
		GOTO 個別素質変更_LOOP
	CASE 299 TO 302
		TALENT:ARG:容姿 = RESULT - 300
		GOTO 個別素質変更_LOOP
	CASE 330 TO 334
		TALENT:ARG:印象 = RESULT - 330
		GOTO 個別素質変更_LOOP
	CASE 350 TO 357
		TALENT:ARG:年齢層 = RESULT - 340
		GOTO 個別素質変更_LOOP
	CASE 397 TO 402
		TALENT:ARG:体格 = RESULT - 400
		CALL SET_BODYSIZE(ARG, 0)
		GOTO 個別素質変更_LOOP
	CASE 410
		DRAWLINE
		PRINTL 请输入身高（单位毫米）
		INPUT
		BASE:ARG:身長 = RESULT
		PRINTFORML 身高已设定为%DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%厘米
		TALENT:ARG:体格 = 0
		CALL INIT_BODYSIZE_HEIGHT(ARG)
		PRINTFORMW 体格特质已设定为%GET_TALENTNAME(161, TALENT:ARG:体格)%
		GOTO 個別素質変更_LOOP
	CASE 447 TO 453
		TALENT:ARG:体型 = RESULT - 450
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 498 TO 502
		TALENT:ARG:男性器サイズ = RESULT - 500
		GOTO 個別素質変更_LOOP
	CASE 598 TO 603
		TALENT:ARG:バストサイズ = RESULT - 600
		CALL SET_BODYSIZE(ARG, 2)
		GOTO 個別素質変更_LOOP
	CASE 610
		DRAWLINE
		PRINTL 请输入胸围尺寸（单位毫米）
		INPUT
		BASE:ARG:バスト = RESULT
		PRINTFORML 胸围尺寸已设定为%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%厘米
		TALENT:ARG:バストサイズ = 0
		CALL INIT_BODYSIZE_BUST(ARG)
		PRINTFORMW 胸围尺寸特质已设定为%GET_TALENTNAME(205, TALENT:ARG:バストサイズ)%
		GOTO 個別素質変更_LOOP
	CASE 999
		RETURN 0
ENDSELECT


;性別に関連する経験等の初期化
@RESET_性別(ARG)
IF !(TALENT:ARG:性別 & 1)
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF

IF !(TALENT:ARG:性別 & 2)
	EXP:ARG:射精経験 = 0
ENDIF

IF TALENT:ARG:性別 == 1
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSEIF TALENT:ARG:性別 == 2
	EQUIP:ARG:上半身下着あり = 0
	EQUIP:ARG:スカートめくり可 = 0
	EQUIP:ARG:下半身下着抜き取り可 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ELSE
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:非童貞 = 0
	TALENT:ARG:性転換 = 0
ENDIF

CALL SET_BODYSIZE(ARG, 0)


;種族に関連する素質等の初期化
@RESET_種族(ARG)
SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
		TALENT:ARG:体格 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		IF TALENT:ARG:性別 == 2
			TALENT:ARG:体格 = 2
		ELSE
			TALENT:ARG:体格 = -1
		ENDIF
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
		TALENT:ARG:体格 = -2
	CASEELSE
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
ENDSELECT
CALL SET_BODYSIZE(ARG, 0)


;名前の設定
@NAME_CUSTOM(ARG)
PRINTL 请输入角色名称
IF STRLENS(NAME:ARG)
	PRINTFORM 当前名称：%NAME表示(ARG)%      
	PRINTBUTTON "[保持当前]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF ARG == 0 && RESULTS == ""
	NAME:ARG = %NAME表示(ARG)%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 请输入角色昵称
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 当前昵称：%CALLNAME:ARG%      
	PRINTBUTTON "[保持当前]", ""
	PRINTL
ENDIF
CALL 入力処理_HTML記法警告付き()
IF ARG == 0 && RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF


;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:体力, MAXBASE:ARG:体力, 16
PRINTFORM ({BASE:ARG:体力, 4}/{MAXBASE:ARG:体力, 4})
IF BASE:ARG:体力 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:体力 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL


;素質の設定
@TALENT_CUSTOM(ARG, ARG:1)
VARSET LOCAL
FOR LOCAL, ARG:1 * 40, 40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155) || (LOCAL >= 211 && LOCAL <= 215)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL, 1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS = [{LOCAL, 3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100, 100, 100
		LOCALS =  [{LOCAL, 3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS, 35, LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 上一页", 998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 上一页", 35, LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 5
	PRINTBUTTON "[999] - 下一页", 999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 下一页", 35, LEFT%
	RESETCOLOR
ENDIF
PRINTL


@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL, 0, 50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL, 12%:{ABL:ARG:LOCAL, 5, LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL


@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL, 0, 200
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:性別 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:性別 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL, 2}] - %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5, LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL


@任意キャラスタイル_キャラ選定()
#DIM 候補配列, キャラクタ数上限
#DIM 候補数
#DIM キャラ番号
#DIM ページ数

;キャラを選んだ後、キャラのNOを返す
VARSET 候補配列
候補数 = 0
FOR キャラ番号, 1, CHARANUM
	;ランダムキャラはダメ
	SIF NO:キャラ番号 == 999
		CONTINUE
	;ジータとグランはダメ
	SIF NO:キャラ番号 == 27
		CONTINUE
	SIF NO:キャラ番号 == 146
		CONTINUE
	候補配列:候補数 = キャラ番号
	候補数 ++
NEXT
CALL キャラ配列フィルタ処理(候補配列)
候補数 = RESULT


$表示位置

;キャラ一覧を表示
DRAWLINE
PRINTL 请选择作为老板的初始角色
PRINTL ※可选择全部角色作为领主。
PRINTL ※部分角色可能导致事件与剧本的逻辑矛盾。
DRAWLINE

LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	SIF 候補配列:LOCAL < 1
		BREAK
	IF ページ数 == LOCAL:1 / 30
		LOCALS += @"<button value='{LOCAL}'>[{LOCAL,3}] %GET_NAME(候補配列:LOCAL, 1)%</button>　%ソート情報表示(候補配列:LOCAL, 1)%　"
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[9000] 上一页", 9000
ELSE
	PRINTC   
ENDIF
IF ページ数 < (LOCAL:1 + 1) / 30
	PRINTBUTTONLC "[9001] 下一页", 9001
ELSE
	PRINTC   
ENDIF
PRINTL
DRAWLINE
CALL フィルタソートボタンセット表示(9020, 9030, 9031, 9040)
DRAWLINE
PRINTBUTTON "[9999] 返回", 9999

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9020, 9030, 9031, 9040
		CALL フィルタソートボタンセット実行部(LOCAL, 9020, 9030, 9031, 9040)
		ページ数 = 0
		RESTART
	CASE 9999
		ページ数 = 0
		RETURN 0
	CASE 9000
		ページ数 --
		GOTO 表示位置
	CASE 9001
		ページ数 ++
		GOTO 表示位置
	CASEELSE
		WHILE 1
			CALL 任意キャラスタイル_セット確認(候補配列:LOCAL)
			SELECTCASE RESULT
				CASE 0
					;- キャンセル
					; リスト表示へ戻る
					GOTO 表示位置
				CASE 1
					;- キャラ選択
					COPYCHARA 候補配列:LOCAL, MASTER
					NO:MASTER = 0
					CFLAG:(候補配列:LOCAL):招待不可フラグ = -1
					FLAG:あなたキャラ化記録 = NO:(候補配列:LOCAL)
					RETURN NO:(候補配列:LOCAL)
				CASE 10
					;- 前のキャラ
					; 候補番号を更新、ループして前のキャラを表示
					LOCAL -=  1
					SIF LOCAL < 0
						LOCAL = 候補数 - 1
				CASE 11
					;- 次のキャラ
					; 候補番号を更新、ループして次のキャラを表示
					LOCAL +=  1
					SIF LOCAL >= 候補数
						LOCAL = 0
			ENDSELECT
		WEND
ENDSELECT

RESTART

@任意キャラスタイル_セット確認(対象キャラ)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 返り値
#DIM 描画開始行
#DIM 対象画像数
#DIM リロールボタン有効
#DIM 実行結果
#DIM セットフラグ
#DIM リスタートフラグ
#DIM TFLAG保存
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	描画開始行 = LINECOUNT
ENDIF

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
PRINTFORML 确定要选择%NAME表示(対象キャラ)%作为老板开始吗？
; ボタン表示
PRINTBUTTONLC "[0]确定", 0
PRINTBUTTONLC "[1]取消", 1
PRINTBUTTONLC "[10]显示详细能力", 10
PRINTL
PRINTBUTTONLC "[2]上一个角色", 2
PRINTBUTTONLC "[3]下一个角色", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]重新随机显示画像", 4
PRINTL

; 入力ループ
WHILE 1
	BINPUT
	SELECTCASE RESULT
		CASE 0
			返り値 = 1
			BREAK
		CASE 1
			返り値 = 0
			BREAK
		CASE 4
			RESTART
		CASE 2
			;- [2]前のキャラ
			返り値 = 10
			BREAK
		CASE 3
			;- [3]次のキャラ
			返り値 = 11
			BREAK
		CASE 10
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					返り値 = 10
					BREAK
				CASE 101
					;次のキャラへ
					返り値 = 11
					BREAK
				CASE 1
					;はい
					返り値 = 1
					BREAK
				CASE 0
					;いいえ
					返り値 = 0
					BREAK
			ENDSELECT
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)

RETURN 返り値
