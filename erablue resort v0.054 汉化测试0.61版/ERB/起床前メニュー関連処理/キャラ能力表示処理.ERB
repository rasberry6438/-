
;ステータス表示
@SHOW_CHARADATA
#DIM ページ数, 2
#DIM 表示候補, キャラクタ数上限
#DIM 候補人数
#DIM 得意武器番号
#DIM スキップフラグ
#DIM 文字色フラグ
#DIM 表示人数 = 50
#DIM L_TIME,2
#DIMS ソート情報
#DIMS ボタンテキスト
VARSET ページ数

$最初から

;表示候補選定＆ソート処理
VARSET 表示候補
候補人数 = 0
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	CALL 装備再計算処理(LOCAL)
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	SIF 滞在者のみフィルタ == 1 && CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:候補人数 = LOCAL
	候補人数 ++
NEXT
CALL キャラ配列フィルタ処理(表示候補)
候補人数 = RESULT


LOCAL:1 = 1

$表示処理_LOOP
ページ数:1 = MAX(候補人数 / 表示人数 - (候補人数%表示人数 ? 0# 1), 0)
DRAWLINE
PRINTFORML 　 [0] %NAME表示(MASTER)%
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, ページ数 * 表示人数, 候補人数
	SIF 表示候補:LOCAL < 1
		BREAK
	IF 文字色グループ取得(表示候補:LOCAL) >= 0
		LOCALS += @"<font color='#%十六進数文字列化(同室時_グループ文字色:(文字色グループ取得(表示候補:LOCAL)))%'>"
		文字色フラグ = 1
	ELSEIF CFLAG:(表示候補:LOCAL):同室文字色
		LOCALS += @"<font color='#%十六進数文字列化(CFLAG:(表示候補:LOCAL):同室文字色)%'>"
		文字色フラグ = 1
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル") && CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
		LOCALS += "<font color='#FF3399'>"
		文字色フラグ = 2
	ELSE
		文字色フラグ = 0
	ENDIF
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,4}] %GET_NAME(表示候補:LOCAL, 1, 50)%</button>　"
	SIF 文字色フラグ == 1
		LOCALS += "</font>"
	SIF 文字色フラグ == 2
		LOCALS += "</font>"
	ソート情報 '= ソート情報表示(表示候補:LOCAL, 1)
	IF ソート情報 != ""
		LOCALS += ソート情報 + "　"
	ELSE
		IF CFLAG:(表示候補:LOCAL):長期雇用
			LOCALS += "　　长期雇佣中　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 1
			LOCALS += @"　　　<font color='#%カラーパレット_HTML("黄")%'>今天回家</font>　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 999
			LOCALS += "　　　常驻人员　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 1
			LOCALS += @"　剩余天数：{CFLAG:(表示候補:LOCAL):滞在期間 - 1, 2}天　"
		ELSE
			LOCALS += "　　　　　　　　"
		ENDIF
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 表示人数 - 1
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
LOCALS:2 = 
IF ページ数 > 0
	LOCALS:2 += @"<button value='9000'>[9000] 上一页 </button>"
ELSE
	LOCALS:2 += " " * STRLENS("[9000] 前のページ ")
ENDIF
LOCALS:2 += " " * 24
LOCALS:2 += @"<button value='9006'>{1+ページ数, 4, RIGHT}/{1+ページ数:1, 4, RIGHT}</button>"
LOCALS:2 += " " * 23
IF ページ数 < ページ数:1
	LOCALS:2 += @"<button value='9001'>[9001] 下一页 </button>"
ENDIF
LOCALS:2 += "<br>"
IF ページ数 > 0
	LOCALS:2 += "<button value='9002'>[|≪]</button>"
ELSE
	LOCALS:2 += "     "
ENDIF
IF ページ数 > 4
	LOCALS:2 += "<button value='9003'> [－５]</button>"
ELSE
	LOCALS:2 += " 　　　"
ENDIF
LOCALS:2 += " " * 26
LOCALS:2 += @"<button value='9007'>显示人数：{表示人数, 3}人/页</button>"
LOCALS:2 += " " * (56-20-20)
IF ページ数 <= ページ数:1 - 5
	LOCALS:2 += "<button value='9004'>[＋５] </button>"
ELSE
	LOCALS:2 += " 　　　"
ENDIF
LOCALS:2 += " " * 5
IF ページ数 < ページ数:1
	LOCALS:2 += "<button value='9005'>[≫|]</button>"
ENDIF
HTML_PRINT LOCALS:2
DRAWLINE
IF 滞在者のみフィルタ
	ボタンテキスト '= "显示入住及非入住人员"
ELSE
	ボタンテキスト '= "仅显示入住人员"
ENDIF
CALL フィルタソートボタンセット表示(2000, 3000, 3100, 4000, 2001, ボタンテキスト)
DRAWLINE
PRINTBUTTONLC "[9998]系统说明", 9998
PRINTL
PRINTBUTTONLC "[9999]返回", 9999
PRINTL

$INPUT_LOOP
IF チュートリアルフラグ:能力表示 == 0
	RESULT = 9998
ELSE
	BINPUT
ENDIF
IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 9998
	CALL 能力表示チュートリアル()
	GOTO 表示処理_LOOP
ELSEIF GROUPMATCH(RESULT, 2000, 3000, 3100, 4000)
	CALL フィルタソートボタンセット実行部(RESULT, 2000, 3000, 3100, 4000)
	ページ数 = 0
	RESTART
ELSEIF RESULT == 2001
	INVERTBIT 滞在者のみフィルタ, 0
	ページ数 = 0
	RESTART
ELSEIF RESULT == 9000
	ページ数 = MAX(ページ数 - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9001
	ページ数 = MIN(ページ数 + 1, 候補人数 / 表示人数)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9002
	ページ数 = 0
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9003
	ページ数 = MAX(ページ数 - 5, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9004
	ページ数 = MIN(ページ数 + 5, 候補人数 / 表示人数)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9005
	ページ数 = ページ数:1
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9006
	PRINTFORML 请输入要跳转的页码（1～{1+ページ数:1}）
	INPUT
	SIF INRANGE(RESULT, 1, 1+ページ数:1)
		ページ数 = RESULT - 1
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9007
	PRINTFORML 输入每页显示的人数（1～100）
	INPUT
	IF INRANGE(RESULT, 1, 100)
		表示人数 = RESULT
		ページ数 = 0
	ENDIF
	GOTO 表示処理_LOOP
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	REUSELASTLINE 
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
$再表示
IF 候補人数 == 1
	CALL PRINT_STATE(LOCAL, 0)
ELSEIF 表示候補:0 == LOCAL
	CALL PRINT_STATE(LOCAL, 2)
ELSEIF 表示候補:(候補人数 - 1) == LOCAL
	CALL PRINT_STATE(LOCAL, 1)
ELSE
	CALL PRINT_STATE(LOCAL, 3)
ENDIF
IF RESULT == 100
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 - 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ELSEIF RESULT == 101
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 + 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ENDIF

GOTO 表示処理_LOOP
