@大規模エリア建築メニュー処理

DRAWLINE
PRINTL 开发度假岛上的大规模土地。
PRINTL ※功能开发中，可能存在异常情况。
DRAWLINE

CALL リゾート遠景表示処理("")

PRINT [999] 返回

IF チュートリアルフラグ:大規模エリア建築 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 998
		CALL 大規模エリア建築チュートリアル()
		RESTART
	CASE 999
		RETURN 0
	CASEELSE
		CALL 大規模エリア建築実処理(LOCAL)
ENDSELECT

RESTART



@リゾート遠景表示処理(処理モード)
#DIMS エリア名
#DIMS 処理モード
#DIM ボタン数値
#DIM 表示位置X, 10
#DIM 表示位置Y, 10

LOCALS = <div rect='81,31,5900,3812' border='31' bcolor='#C0C0C0'><img src='リゾート遠景' height='3800' width='5900'></div>

IF 処理モード == "移動処理"
	IF CFLAG:MASTER:現在マップ種別 != 0
		ボタン数値 = 0
	ELSE
		ボタン数値 = 999
	ENDIF
	エリア名 = 　度假村　
	LOCALS += @"<div rect='3200,2400,700,500'><button value='{ボタン数値}'><img src='リゾートハウス' height='500'></button></div>"
	LOCALS += @"<div rect='3100,2830,{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'><button value='{ボタン数値}'>%エリア名%</button></div>"
ELSE
	エリア名 = 　度假村　
	LOCALS += @"<div rect='3200,2400,700,500'><img src='リゾートハウス' height='500'></div>"
	LOCALS += @"<div rect='3100,2830,{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'>%エリア名%</div>"
ENDIF

IF ダンジョンクリアフラグ_沿岸部の森
	IF 処理モード == "移動処理"
		IF CFLAG:MASTER:現在マップ種別 != 1
			ボタン数値 = 1
		ELSE
			ボタン数値 = 999
		ENDIF
		エリア名 = 　空艇港区　
		LOCALS += @"<div rect='2650,2900,700,500'><button value='{ボタン数値}'><img src='空艇港' height='500'></button></div>"
		LOCALS += @"<div rect='2600,3330,{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'><button value='{ボタン数値}'>%エリア名%</button></div>"
	ELSE
		エリア名 = 　空艇港区　
		LOCALS += @"<div rect='2650,2900,700,500'><img src='空艇港' height='500'></div>"
		LOCALS += @"<div rect='2600,3330,{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'>%エリア名%</div>"
	ENDIF
ENDIF

表示位置X:0 = 4200
表示位置Y:0 = 2730
表示位置X:1 = 3500
表示位置Y:1 = 2100

FOR LOCAL, 0, 2
	IF 開拓エリア解放フラグ:LOCAL
		IF 処理モード == "移動処理"
			IF CFLAG:MASTER:現在マップ種別 != 開拓エリア開発番号:LOCAL
				ボタン数値 = 開拓エリア開発番号:LOCAL
			ELSE
				ボタン数値 = 999
			ENDIF
			IF 開拓エリア開発番号:LOCAL == 0
				エリア名 = ＊空置区域＊
				LOCALS += @"<div rect='{表示位置X:LOCAL},{表示位置Y:LOCAL},{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'>%エリア名%</div>"
			ELSE
				CALLFORM エリア情報登録_{開拓エリア開発番号:LOCAL}("エリア名")
				エリア名 = %TSTR:場所名受渡%
				LOCALS += @"<div rect='{表示位置X:LOCAL + 50},{表示位置Y:LOCAL - 430},700,500'><button value='{ボタン数値}'><img src='%エリア名%' height='500'></button></div>"
				LOCALS += @"<div rect='{表示位置X:LOCAL},{表示位置Y:LOCAL},{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'><button value='{ボタン数値}'>%エリア名%</button></div>"
			ENDIF
		ELSE
			ボタン数値 = LOCAL
			IF 開拓エリア開発番号:LOCAL == 0
				エリア名 = ＊空置区域＊
				LOCALS += @"<div rect='{表示位置X:LOCAL},{表示位置Y:LOCAL},{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'><button value='{ボタン数値}'>%エリア名%</button></div>"
			ELSE
				CALLFORM エリア情報登録_{開拓エリア開発番号:LOCAL}("エリア名")
				エリア名 = %TSTR:場所名受渡%
				LOCALS += @"<div rect='{表示位置X:LOCAL + 50},{表示位置Y:LOCAL - 430},700,500'><img src='%エリア名%' height='500'></div>"
				LOCALS += @"<div rect='{表示位置X:LOCAL},{表示位置Y:LOCAL},{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'>%エリア名%</div>"
			ENDIF
		ENDIF
	ENDIF
NEXT

HTML_PRINT LOCALS
FOR LOCAL, 0, 34
	PRINTL
NEXT
DRAWLINE


@大規模エリア建築実処理(建築位置)
#DIMS エリア名
#DIM 建築位置
#DIM 候補数
#DIM 必要ルピ

;候補を表示
DRAWLINE
PRINTL 请选择要在此区域建设的大规模区域
DRAWLINE
LOCALS = <div rect='81,0,5900,2012' padding='75' border='31' bcolor='#C0C0C0'>
LOCALS += "</div>"

VARSET RESULTS
LOCAL:1 = 0
候補数 = ENUMFUNCBEGINSWITH("大規模エリア建設条件_")
FOR LOCAL, 0, 候補数
	CALLFORM %RESULTS:LOCAL%(建築位置)
	SIF RESULT == 0
		CONTINUE
	エリア名 = %REPLACE(RESULTS:LOCAL, "大規模エリア建設条件_", "")%
	LOCALS += @"<div rect='{LOCAL:1 * 1000 + 356},{LOCAL:1 / 5 * 800 + 155},600,500'><button value='{LOCAL}'><img src='%エリア名%' height='500'></button></div>"
	LOCALS += @"<div rect='{LOCAL:1 * 1000 + 106 + (20 - STRLENS(エリア名)) * 25},{LOCAL:1 / 5 * 800 + 585},{STRLENS(エリア名) * 50 + 120},212' padding='35,25,35' border='31' bcolor='#C0C0C0' color='#333333'><button value='{LOCAL}'>%エリア名%</button></div>"
	LOCAL:1 ++
NEXT

HTML_PRINT LOCALS
FOR LOCAL, 0, 18
	PRINTL
NEXT
DRAWLINE
PRINTBUTTON "[999] 返回", 999
PRINTL

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASEELSE
		エリア名 = %REPLACE(RESULTS:LOCAL, "大規模エリア建設条件_", "")%
		CALLFORM 大規模エリア解説文章_%エリア名%
		必要ルピ = RESULT
		DRAWLINE
		PRINTFORML ──%エリア名%──
		PRINTL
		HTML_PRINT 詳細文文字列受け渡し変数
		PRINTL
		DRAWLINE
		IF MONEY < 必要ルピ
			PRINTFORML 是否要建设「%エリア名%」？
			PRINTFORML 所需卢比：%NUM_FORMAT(必要ルピ)%卢比
			PRINTL
			PRINTW 卢比不足。
		ELSE
			PRINTFORML 是否要建设「%エリア名%」？
			PRINTFORML 所需卢比：%NUM_FORMAT(必要ルピ)%卢比
			PRINTBUTTON "[0] 确定", 0
			PRINTL
			PRINTBUTTON "[1] 取消", 1
			PRINTL
			BINPUT
			IF RESULT == 0
				MONEY -= 必要ルピ
				PRINTFORMW 已在空置区域建设「%エリア名%」。
				開拓エリア開発番号:建築位置 = GETVAR(@"大規模番号_%エリア名%")
				RETURN 0
			ENDIF
		ENDIF
ENDSELECT

RESTART
