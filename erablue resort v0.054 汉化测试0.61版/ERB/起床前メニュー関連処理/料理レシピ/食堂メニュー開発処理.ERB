@食堂メニュー開発処理
#DIM 画面表示用変数
#DIM サブ食材ランク補正
#DIM ルピ収入
#DIM ZP収入

DRAWLINE
PRINTL 开始研发将在度假村提供的料理菜单。
DRAWLINE

開発料理ランク保存 = 0

LOCALS = <div rect='81,31,5925,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='281,1670,3000,200' border='20' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,156,5850,2812'>"
;どの食べ物を使うか登録
LOCALS += "<br>■主要使用食材<br>　　"
IF 使用食材:0
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:0, "素材属性", 1))%
	LOCALS += @"<font color='#%LOCALS:1%'><button value='0'>[等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", 使用食材:0, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", 使用食材:0, "素材アイテム名", 1)%]</button></font><br><br>"
ELSE
	LOCALS += "<button value='0'>[--------------------]</button><br><br>"
ENDIF
LOCALS += "■附加食材<br>　　"
IF 使用食材:1
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:1, "素材属性", 1))%
	LOCALS += @"<font color='#%LOCALS:1%'><button value='1'>[等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", 使用食材:1, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", 使用食材:1, "素材アイテム名", 1)%]</button></font><br>"
ELSE
	LOCALS += "<button value='1'>[--------------------]</button><br>"
ENDIF
IF 使用食材:2
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:2, "素材属性", 1))%
	LOCALS += @"　　<font color='#%LOCALS:1%'><button value='2'>[等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", 使用食材:2, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", 使用食材:2, "素材アイテム名", 1)%]</button></font><br>"
ELSE
	LOCALS += "　　<button value='2'>[--------------------]</button><br>"
ENDIF
IF 使用食材:3
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:3, "素材属性", 1))%
	LOCALS += @"　　<font color='#%LOCALS:1%'><button value='3'>[等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", 使用食材:3, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", 使用食材:3, "素材アイテム名", 1)%]</button></font><br><br>"
ELSE
	LOCALS += "　　<button value='3'>[--------------------]</button><br><br>"
ENDIF
LOCALS += "■主要烹饪器具<br>　　"
IF 使用食材:9
	LOCALS += @"<button value='9'>[%調理器具名:(使用食材:9)%]</button><br><br>"
ELSE
	LOCALS += "<button value='9'>[--------------------]</button><br><br>"
ENDIF
LOCALS += "■预计完成料理<br><br>　　"
IF 使用食材:9 == 0 || 使用食材:0 == 0
	LOCALS += "--------------------<br><br><br>"
ELSE
	開発料理ランク保存 = DT_CELL_GET("所持素材データベース", 使用食材:0, "素材ランク", 1)
	LOCAL:1 = DT_CELL_GET("所持素材データベース", 使用食材:1, "素材ランク", 1)
	LOCAL:2 = DT_CELL_GET("所持素材データベース", 使用食材:2, "素材ランク", 1)
	LOCAL:3 = DT_CELL_GET("所持素材データベース", 使用食材:3, "素材ランク", 1)
	IF MAX(LOCAL:1, LOCAL:2, LOCAL:3) == 0
		;なんも食材登録されてないorランク０は寄与しない
	ELSEIF MIN(LOCAL:1, LOCAL:2, LOCAL:3) == 開発料理ランク保存
		;全部違うメインと同ランク食材ならランク＋２
		IF NOSAMES(使用食材:1, 使用食材:2, 使用食材:3)
			開発料理ランク保存 += 2
		ELSE
			開発料理ランク保存 += 1
		ENDIF
	ELSEIF MAX(LOCAL:1, LOCAL:2, LOCAL:3) >= 開発料理ランク保存
		開発料理ランク保存 += 1
	ELSEIF MIN(LOCAL:1, LOCAL:2, LOCAL:3) == 開発料理ランク保存 - 1
		;全部違うメインと同ランク食材ならランク＋１
		IF NOSAMES(使用食材:1, 使用食材:2, 使用食材:3)
			開発料理ランク保存 += 1
		ENDIF
	ENDIF
	;基本方針
	IF 条例発布:0 == 3
		開発料理ランク保存 += 1
	ENDIF
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:0, "素材属性", 1))%
	CALL 生成予定メニュー名算出()
	IF 特殊料理フラグ
		LOCALS:2 = 
		SIF STRCOUNT(開発済み料理名フラグ文字列, @"_%料理名受け渡し%_") == 0
			料理名受け渡し = ？？？？？？？？？？
	ELSE
		LOCALS:2 = 通用料理
	ENDIF
	LOCALS += @"%LOCALS:2%等级%TOFULL(TOSTR(開発料理ランク保存))%：<font color='#%LOCALS:1%'>%料理名受け渡し%</font><br><br><br>"
ENDIF
IF 使用食材:9 == 0 || 使用食材:0 == 0
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[100] 确认研发此菜单</font>"
ELSE
	LOCALS += "<button value='100'>[100] 确认研发此菜单</button>"
ENDIF
LOCALS += "</div>"

LOCALS += "<div rect='156,2600,5850,150'>"


LOCALS += "<button value='997'>[997] 查阅料理图鉴</button>"
LOCALS += "　　　　　　　<button value='998'>[998] 查看已有菜单</button>"
LOCALS += "　　　　　　　<button value='999'>[999] 返回</button>"
LOCALS += "　　　<button value='996'>[996] 系统说明</button>"
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 26
	PRINTL
NEXT
DRAWLINE

IF チュートリアルフラグ:食事開発 == 0
	RESULT = 996
ELSE
	BINPUT
ENDIF
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 996
		CALL 食事メニュー開発チュートリアル()
		RESTART
	CASE 997
		CALL レシピブック閲覧()
		RESTART
	CASE 998
		CALL 既存メニュー閲覧(0)
		RESTART
	CASE 999
		FOR LOCAL, 0, 4
			IF 使用食材:LOCAL
				LOCAL:1 = DT_CELL_GET("所持素材データベース", 使用食材:LOCAL, "所持素材数", 1) + 1
				DT_CELL_SET "所持素材データベース", 使用食材:LOCAL, "所持素材数", LOCAL:1, 1
			ENDIF
		NEXT
		VARSET 使用食材
		RETURN -1
	CASE 0 TO 3
		CALL 素材アイテム一覧表示("食材分類 <> '' and 所持素材数 > 0", 1, 1)
		LOCAL:10 = RESULT
		IF LOCAL:10 == -2
			IF 使用食材:LOCAL
				LOCAL:1 = DT_CELL_GET("所持素材データベース", 使用食材:LOCAL, "所持素材数", 1) + 1
				DT_CELL_SET "所持素材データベース", 使用食材:LOCAL, "所持素材数", LOCAL:1, 1
			ENDIF
			使用食材:LOCAL = 0
		ELSEIF LOCAL:10 != -1
			IF 使用食材:LOCAL
				LOCAL:1 = DT_CELL_GET("所持素材データベース", 使用食材:LOCAL, "所持素材数", 1) + 1
				DT_CELL_SET "所持素材データベース", 使用食材:LOCAL, "所持素材数", LOCAL:1, 1
			ENDIF
			使用食材:LOCAL = LOCAL:10
			LOCAL:1 = DT_CELL_GET("所持素材データベース", 使用食材:LOCAL, "所持素材数", 1) - 1
			DT_CELL_SET "所持素材データベース", 使用食材:LOCAL, "所持素材数", LOCAL:1, 1
		ENDIF
		RESTART
	CASE 9
		CALL 調理道具選択処理
		SIF RESULT != -1
			使用食材:9 = RESULT
		RESTART
	CASE 100
		;ルピ収入補正とZP収入補正をセットする
		VARSET RESULT
		CALL 生成予定メニュー名算出()
		IF 特殊料理フラグ
			IF 料理名受け渡し == "失败料理"
				CALL 特殊料理_1種レシピ_失败料理("料理基礎ステータス")
			ELSE
				CALLFORM 特殊料理_{9 - MATCH(使用食材, 0)}種レシピ_%料理名受け渡し%("料理基礎ステータス")
			ENDIF
			;RESULT:1にルピ、REUSLTにZP収入が入る
			ルピ収入 = RESULT:1
			ZP収入 = RESULT
		ELSE
			ルピ収入 = MAX(開発料理ランク保存 * 開発料理ランク保存 * 2, 1)
			;追加食材あるほど＋補正
			SIF 使用食材:1
				ルピ収入 += MAX(DT_CELL_GET("所持素材データベース", 使用食材:1, "素材ランク", 1) / 3, 1)
			SIF 使用食材:2
				ルピ収入 += MAX(DT_CELL_GET("所持素材データベース", 使用食材:2, "素材ランク", 1) / 3, 1)
			SIF 使用食材:3
				ルピ収入 += MAX(DT_CELL_GET("所持素材データベース", 使用食材:3, "素材ランク", 1) / 3, 1)
			;ルピはここから仕入れ額で減額される
			;仕入額は食材ランク*相場が基本
			ZP収入 = MAX(開発料理ランク保存 * 開発料理ランク保存 / 10, 1)
			;全部追加埋めたら＋補正
			SIF GROUPMATCH(0, 使用食材:1, 使用食材:2, 使用食材:3) == 0
				ZP収入 += 1
		ENDIF
		;データベースに料理を登録する
		DT_ROW_ADD "料理メニューデータベース", "メニュー名", 料理名受け渡し, "料理ランク", 開発料理ランク保存, "料理属性", DT_CELL_GET("所持素材データベース", 使用食材:0, "素材属性", 1), "ルピ収入補正", ルピ収入, "ZP収入補正", ZP収入, "メイン食材ID", 使用食材:0, "追加食材１ID", 使用食材:1, "追加食材２ID", 使用食材:2, "追加食材３ID", 使用食材:3, "調理器具ID",  使用食材:9, "特殊料理フラグ", 特殊料理フラグ
		LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("所持素材データベース", 使用食材:0, "素材属性", 1))%
		IF 特殊料理フラグ
			LOCALS:2 = 
		ELSE
			LOCALS:2 = 通用料理
		ENDIF
		LOCALS = %LOCALS:2%等级%TOFULL(TOSTR(開発料理ランク保存))%：<font color='#%LOCALS:1%'>开发了%料理名受け渡し%</font>。
		SIF STRCOUNT(開発済み料理名フラグ文字列, @"_%料理名受け渡し%_") == 0
			開発済み料理名フラグ文字列 += @"%料理名受け渡し%_"
		HTML_PRINT LOCALS
		PRINTL
		WAIT
		VARSET 使用食材
		RETURN 1
ENDSELECT

@既存メニュー閲覧(ボタン化フラグ = 0, 選択解除フラグ = 0, 被り禁止フラグ = 0)
#DIMS 表示条件
#DIM ページ番号
#DIM ページ登録
#DIM ボタン化フラグ
#DIM 被り禁止フラグ
#DIM ソート後料理順, 1000
#DIM ソート後ページ登録, 1000
#DIM 現在表示ランク
#DIM 選択解除フラグ
#DIM 行数カウント
VARSET ソート後料理順, -1
VARSET ソート後ページ登録, -1

;データベースをランク順にソート
DT_SELECT "料理メニューデータベース", "", "料理ランク ASC, 料理属性 ASC", ソート後料理順

;ソート素材順にページ番号を振る
行数カウント = 0
現在表示ランク = -1
ページ登録 = 0
VARSET LOCAL
VARSET LOCALS
FOR COUNT, 0, 1000
	SIF ソート後料理順:COUNT < 0
		BREAK
	IF 現在表示ランク < DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "料理ランク", 1)
		SIF LOCAL
			行数カウント ++
		IF LOCAL % 2 == 1
			行数カウント ++
			LOCAL += 1
		ENDIF
		現在表示ランク = DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "料理ランク", 1)
		行数カウント ++
	ENDIF
	ソート後ページ登録:COUNT = ページ登録
	LOCAL += 1
	IF LOCAL % 2 == 0
		行数カウント ++
	ENDIF
	IF 行数カウント >= 25 && LOCAL % 2 == 0
		ページ登録 ++
		行数カウント = 0
	ENDIF
NEXT

ページ登録 = 0

DO
VARSET LOCAL
VARSET LOCALS
LOCALS = <div rect='200,0,5537,3625' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='262,75,5537,3625'><nobr>"

現在表示ランク = -1
FOR COUNT, 0, 1000
	IF ソート後料理順:COUNT < 0
		BREAK
	ENDIF
	IF ソート後ページ登録:COUNT !=ページ番号
		CONTINUE
	ENDIF
	IF 被り禁止フラグ == 1
		SIF MATCH(現在設定メニュー, ソート後料理順:COUNT)
			CONTINUE
	ENDIF
	IF 現在表示ランク < DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "料理ランク", 1)
		SIF LOCAL
			LOCALS += "<br>"
		IF LOCAL % 2 == 1
			LOCALS += "<br>"
			LOCAL += 1
		ENDIF
		現在表示ランク = DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "料理ランク", 1)
		LOCALS += @"---- 等级%TOFULL(TOSTR(現在表示ランク))% ------------------------------------------------------------<br>"
	ENDIF
	LOCALS:1 = %属性数値文字色変換_HTML(DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "料理属性", 1))%
	IF DT_CELL_GET("料理メニューデータベース", ソート後料理順:COUNT, "特殊料理フラグ", 1)
		LOCALS:2 = 
	ELSE
		LOCALS:2 = 通用料理：
	ENDIF
	IF ボタン化フラグ
		LOCALS += @"　<button value='{COUNT}'>%LOCALS:2%<font color='#%LOCALS:1%'>%DT_CELL_GETS("料理メニューデータベース", ソート後料理順:COUNT, "メニュー名", 1), 26 - STRLENS(LOCALS:2), LEFT%</font></button><button value='{COUNT + 10000}'>[详情]</button>　　　　　　　　　"
	ELSE
		LOCALS += @"　%LOCALS:2%<font color='#%LOCALS:1%'>%DT_CELL_GETS("料理メニューデータベース", ソート後料理順:COUNT, "メニュー名", 1), 26 - STRLENS(LOCALS:2), LEFT%</font><button value='{COUNT + 10000}'>[详情]</button>　　　　　　　　　"
	ENDIF
	LOCAL += 1
	IF LOCAL % 2 == 0
		LOCALS += "<br>"
	ENDIF
NEXT

LOCALS += "</div>"

LOCALS += "<div rect='262,3437,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]上一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF MAXARRAY(ソート後ページ登録) > ページ番号
	LOCALS += "<button value='1001'>[1001]下一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 選択解除フラグ
	LOCALS += "　　　　　　　　　　　　　　　　　　　<button value='998'>[998] 取消选择</button>"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='999'>[999] 返回</button></div>"

DRAWLINE
HTML_PRINT LOCALS, 1
FOR COUNT, 0, 33
	PRINTL
NEXT

BINPUT
LOCAL = RESULT
SELECTCASE RESULT
	CASE 999
		ページ番号 = 0
		選択用変数 = 0
		RETURN -1
	CASE 998
		ページ番号 = 0
		選択用変数 = 0
		RETURN -2
	CASE 1000
		ページ番号 -= 1
	CASE 1001
		ページ番号 += 1
	CASE IS >= 10000
		DRAWLINE
		IF DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "特殊料理フラグ", 1)
			LOCALS:2 = 
		ELSE
			LOCALS:2 = 通用料理：
		ENDIF
		PRINTFORML ■%LOCALS:2%%DT_CELL_GETS("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "メニュー名", 1)%
		PRINTFORM 　料理等级：%TOFULL(TOSTR(DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "料理ランク", 1)))%　　料理属性：
		SETCOLOR 属性数値文字色変換(DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "料理属性", 1))
		PRINTFORML %属性数値文字列変換(DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "料理属性", 1))%属性
		RESETCOLOR
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "ZP収入補正", 1)
		PRINTFORML 　卢比收益修正：{DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "ルピ収入補正", 1), 6}　　ＺＰ收益修正：{LOCAL:1 / 1000}.{(LOCAL:1 % 1000) / 100}{(LOCAL:1 % 100) / 10}{(LOCAL:1 % 10)}
		PRINT 　メイン食材：
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "メイン食材ID", 1)
		SETCOLOR 属性数値文字色変換(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材属性", 1))
		PRINTFORML 等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", LOCAL:1, "素材アイテム名", 1)%
		RESETCOLOR
		PRINT 　附加食材１：
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "追加食材１ID", 1)
		IF LOCAL:1
			SETCOLOR 属性数値文字色変換(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材属性", 1))
			PRINTFORML 等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", LOCAL:1, "素材アイテム名", 1)%
			RESETCOLOR
		ELSE
			PRINTL
		ENDIF
		PRINT 　附加食材２：
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "追加食材２ID", 1)
		IF LOCAL:1
			SETCOLOR 属性数値文字色変換(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材属性", 1))
			PRINTFORML 等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", LOCAL:1, "素材アイテム名", 1)%
			RESETCOLOR
		ELSE
			PRINTL
		ENDIF
		PRINT 　附加食材３：
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "追加食材３ID", 1)
		IF LOCAL:1
			SETCOLOR 属性数値文字色変換(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材属性", 1))
			PRINTFORML 等级%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", LOCAL:1, "素材ランク", 1)))%：%DT_CELL_GETS("所持素材データベース", LOCAL:1, "素材アイテム名", 1)%
			RESETCOLOR
		ELSE
			PRINTL
		ENDIF
		PRINT 　烹饪器具　：
		LOCAL:1 = DT_CELL_GET("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "調理器具ID", 1)
		PRINTFORML %調理器具名:(LOCAL:1)%
		PRINTL
		PRINTBUTTON "[0] 删除此料理", 0
		PRINTL
		PRINTBUTTON "[999] 返回", 999
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTL
			PRINTFORML 确认要删除%LOCALS:2%%DT_CELL_GETS("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "メニュー名", 1)%的食谱吗？
			PRINTL
			PRINTBUTTON "[0] 确定", 0
			PRINTL
			PRINTBUTTON "[1] 取消", 1
			PRINTL
			BINPUT
			IF RESULT == 0
				PRINTFORML 已成功删除%LOCALS:2%%DT_CELL_GETS("料理メニューデータベース", ソート後料理順:(LOCAL - 10000), "メニュー名", 1)%的食谱。
				DT_ROW_REMOVE "料理メニューデータベース", ソート後料理順:(LOCAL - 10000)
				RESTART
			ENDIF
		ENDIF
	CASEELSE
		ページ番号 = 0
		選択用変数 = 0
		RETURN ソート後料理順:RESULT
ENDSELECT
LOOP 1



@調理道具選択処理
DRAWLINE
PRINTL 请选择主要烹饪器具
DRAWLINE
FOR LOCAL, 1, VARSIZE("調理器具名")
	PRINTBUTTON @"[{LOCAL}] %調理器具名:LOCAL%", LOCAL
	PRINTL 
NEXT
PRINTL 
PRINTBUTTON "[999] 返回", 999
PRINTL 

BINPUT
SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASEELSE
		RETURN RESULT
ENDSELECT

@生成予定メニュー名算出()
#DIM 関数総数
VARSET RESULTS

;特殊料理条件との照らし合わせ
;使用している食材の数に応じて呼ぶ関数を絞る
関数総数 = ENUMFUNCBEGINSWITH(@"特殊料理_{9 - MATCH(使用食材, 0)}種レシピ_")
料理名受け渡し = 
FOR LOCAL, 0, 関数総数
	CALLFORM %RESULTS:LOCAL%("レシピ参照")
	IF RESULT > 0
		;フラグに使用食材数を入れる
		特殊料理フラグ = 9 - MATCH(使用食材, 0)
		RETURN 1
	ENDIF
NEXT

;ないなら通用料理を見る
特殊料理フラグ = 0
SELECTCASE 使用食材:9
	CASE 1
		;包丁
		SELECTCASE DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1)
			CASE "肉"
				料理名受け渡し = 烤牛肉
			CASE "魚"
				料理名受け渡し = 生鱼片
			CASE "野菜"
				料理名受け渡し = 沙拉
			CASE "穀物"
				料理名受け渡し = 三明治
		ENDSELECT
	CASE 2
		;オーブン
		SELECTCASE DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1)
			CASE "肉"
				料理名受け渡し = 肉馅派
			CASE "魚"
				料理名受け渡し = 烤鱼
			CASE "野菜"
				料理名受け渡し = 焗烤蔬菜
			CASE "穀物"
				料理名受け渡し = 焗烤
			CASE "卵"
				料理名受け渡し = 蛋挞
		ENDSELECT
	CASE 3
		;鍋
		SELECTCASE DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1)
			CASE "肉"
				料理名受け渡し = 炖汉堡肉
			CASE "魚"
				料理名受け渡し = 炖鱼
			CASE "野菜"
				料理名受け渡し = 蔬菜汤
			CASE "穀物"
				料理名受け渡し = 拉面
			CASE "卵"
				料理名受け渡し = 水波蛋
		ENDSELECT
	CASE 4
		;フライパン
		SELECTCASE DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1)
			CASE "肉"
				料理名受け渡し = 牛排
			CASE "魚"
				料理名受け渡し = 意式水煮鱼
			CASE "野菜"
				料理名受け渡し = 炒蔬菜
			CASE "穀物"
				料理名受け渡し = 炒面
			CASE "卵"
				料理名受け渡し = 煎蛋
		ENDSELECT
	CASE 5
		;蒸し器
		SELECTCASE DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1)
			CASE "肉"
				料理名受け渡し = 口水鸡
			CASE "魚"
				料理名受け渡し = 清蒸鱼
			CASE "野菜"
				料理名受け渡し = 蒸蔬菜
			CASE "穀物"
				料理名受け渡し = 糯米饭
			CASE "卵"
				料理名受け渡し = 鸡蛋羹
		ENDSELECT
ENDSELECT

IF 料理名受け渡し == ""
	特殊料理フラグ = 1
	料理名受け渡し = 失败料理
ENDIF

@レシピブック閲覧
#DIM レシピ数
#DIMS レシピ配列, 200
#DIM 選択レシピ
;作ったことのある特殊料理を並べて表示

選択レシピ = -1

;書籍によるレシピ習得をここでセット
SIF STRCOUNT(開発済み料理名フラグ文字列, "カツサンド") < 1 && ITEM:书籍『肉类烹饪入门』
	開発済み料理名フラグ文字列 += "炸猪排三明治_"
SIF STRCOUNT(開発済み料理名フラグ文字列, "１ポンド牛排") < 1 && ITEM:书籍『肉类烹饪入门』
	開発済み料理名フラグ文字列 += "一磅牛排_"
SIF STRCOUNT(開発済み料理名フラグ文字列, "焼肉セット") < 1 && ITEM:书籍『肉类烹饪入门』
	開発済み料理名フラグ文字列 += "烤肉套餐_"
SIF STRCOUNT(開発済み料理名フラグ文字列, "海鮮炒面") < 1 && ITEM:书籍『美味海鲜推荐』
	開発済み料理名フラグ文字列 += "海鲜炒面_"
SIF STRCOUNT(開発済み料理名フラグ文字列, "生鱼片の昆布締め") < 1 && ITEM:书籍『美味海鲜推荐』
	開発済み料理名フラグ文字列 += "昆布腌刺身_"
SIF STRCOUNT(開発済み料理名フラグ文字列, "寿司盛り合わせ") < 1 && ITEM:书籍『美味海鲜推荐』
	開発済み料理名フラグ文字列 += "寿司拼盘_"

$表示RESTART

DRAWLINE
PRINTL 可查看特殊料理配方。
DRAWLINE
;左ウィンドウ描画
LOCALS = <div rect='81,31,1937,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>
;存在する特殊料理を全て表示
VARSET レシピ配列
レシピ数 = ENUMFUNCBEGINSWITH("特殊料理_")
ARRAYCOPY "RESULTS", "レシピ配列"
FOR COUNT, 0, レシピ数
	SPLIT レシピ配列:COUNT, "_", RESULTS
	IF STRCOUNT(開発済み料理名フラグ文字列, @"_%RESULTS:2%_") == 0
		LOCALS += @"？？？？？？？？？？<br>"
	ELSE
		LOCALS += @"<button value='{COUNT}'>%RESULTS:2%</button><br>"
	ENDIF
NEXT
LOCALS += "</div>"

;右ウィンドウ描画
LOCALS += "<div rect='2050,31,3875,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
IF 選択レシピ > -1
	SPLIT レシピ配列:選択レシピ, "_", RESULTS
	LOCALS += @"■%RESULTS:2%<br><br>"
	CALLFORM %レシピ配列:選択レシピ%("レシピ参照")
	LOCALS += @"・食谱<br>%RESULTS%<br>"
	CALLFORM %レシピ配列:選択レシピ%("料理基礎ステータス")
	LOCALS += @"・收益效果<br>%RESULTS%<br>"
	CALLFORM %レシピ配列:選択レシピ%("料理特殊ステータス")
	LOCALS += @"・特殊效果<br>%詳細文文字列受け渡し変数%<br>"
ENDIF
LOCALS += "</div>"

;戻るボタン
LOCALS += "<div rect='1400,2650,700,200'><button value='999'>[999] 返回</button></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 26
	PRINTL
NEXT

BINPUT
SELECTCASE RESULT
	CASE 999
		選択レシピ = -1
		RETURN 0
	CASEELSE
		選択レシピ = RESULT
		GOTO 表示RESTART
ENDSELECT


@レシピランダム取得処理
#DIM レシピ数
#DIMS レシピ配列, 200
#DIM 候補数
#DIMS 候補配列, 200
;所持してないレシピをランダムで取る

;まずは持ってないレシピをリスト化
VARSET レシピ配列
VARSET 候補配列
候補数 = 0
レシピ数 = ENUMFUNCBEGINSWITH("特殊料理_")
ARRAYCOPY "RESULTS", "レシピ配列"
FOR COUNT, 0, レシピ数
	SPLIT レシピ配列:COUNT, "_", RESULTS
	IF STRCOUNT(開発済み料理名フラグ文字列, @"_%RESULTS:2%_") == 0
		候補配列:候補数 = %RESULTS:2%
		候補数 += 1
	ENDIF
NEXT

IF 候補数 < 1
	;全部レシピがあるなら戻る
	RETURN -1
ENDIF

;候補からランダムでレシピGET
VARSET RESULTS
RESULTS = %候補配列:(RAND:候補数)%
開発済み料理名フラグ文字列 += @"%RESULTS%_"

