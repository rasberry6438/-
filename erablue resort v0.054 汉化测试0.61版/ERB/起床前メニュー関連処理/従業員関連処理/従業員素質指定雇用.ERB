@従業員素質指定雇用
#DIM DYNAMIC ランダムキャラ素質, 70
#DIM DYNAMIC TALENT一時保存, 300
#DIM DYNAMIC TALENT数値保存, 300
#DIM DYNAMIC 肉体特徴数値保存, 10
#DIM DYNAMIC 年齢保存
#DIM DYNAMIC 身長保存
#DIM DYNAMIC 体型保存
#DIM DYNAMIC バスト保存
#DIM DYNAMIC ウエスト保存
#DIM DYNAMIC ヒップ保存
#DIM DYNAMIC 下半身保存
#DIM DYNAMIC Ａ処女保存
#DIM DYNAMIC 年齢数値設定フラグ
#DIM DYNAMIC 身長数値設定フラグ
#DIM DYNAMIC バスト数値設定フラグ, 2
#DIM DYNAMIC ウエスト数値設定フラグ
#DIM DYNAMIC ヒップ数値設定フラグ
#DIM DYNAMIC フレーバー素質数値保存, 10
#DIM DYNAMIC 知識素質数値保存, 100
#DIM DYNAMIC 性癖素質数値保存, 20
#DIM DYNAMIC 服装数値保存, 31
#DIMS CONST 泳装一覧 = "ビキニ型","ワンピース型","ボーイレッグ・ビキニ型","プランジングネック型"
#DIMS CONST 男泳装一覧 = "通常","全身タイプ"
#DIM DYNAMIC ABL一時保存, 50
#DIM DYNAMIC ABL数値保存, 50
#DIM DYNAMIC EXP一時保存, 100
#DIM DYNAMIC EXP数値保存, 100
#DIMS CONST 好物一覧 = "飲食系","肉","魚","野菜","お菓子","珍味","酒","茶類","芸術品","宝石","絵画","彫刻","詩","美容系","アクセサリ","化粧品","実用品","文房具","健康器具","日用品","調理器具","工具","娯楽","花","本","おもちゃ","機械"
#DIMS CONST 好物一覧_飲食系 = "肉","魚","野菜","お菓子","珍味","酒","茶類"
#DIMS CONST 好物一覧_芸術品 = "宝石","絵画","彫刻","詩"
#DIMS CONST 好物一覧_美容系 = "アクセサリ","化粧品"
#DIMS CONST 好物一覧_実用品 = "文房具","健康器具","日用品","調理器具","工具"
#DIMS CONST 好物一覧_その他 = "娯楽","花","本","おもちゃ","機械"
#DIMS DYNAMIC 好物一時保存
#DIM DYNAMIC 同行者一時保存, キャラクタ数上限
#DIM DYNAMIC 同行確率一時保存, キャラクタ数上限
#DIMS CONST 同行確率一覧 = "-25","-5","+5","+25"
#DIM DYNAMIC ランダムキャラ経験, 80
#DIM DYNAMIC ランダムキャラ戦闘タイプ, 10
#DIMS DYNAMIC 固有素質一時保存, 10
#DIMS DYNAMIC ランダムキャラネーム
#DIMS DYNAMIC ランダムキャラCALLネーム
#DIMS DYNAMIC ランダムキャラ二つ名
#DIMS DYNAMIC ランダムキャラ人称, 2
#DIMS DYNAMIC ランダムキャラ出演作品
#DIM DYNAMIC ランダムキャラ招待不可フラグ
#DIMS DYNAMIC ランダムキャラ服装名
#DIMS DYNAMIC ランダムキャラ特殊種族
#DIM DYNAMIC 重複チェック用, 10
#DIM DYNAMIC 表示モード, 2
#DIM DYNAMIC 表示数カウント
#DIM DYNAMIC ページ数
#DIM DYNAMIC 必要ルピ
#DIM DYNAMIC 登録番号
#DIMS DYNAMIC 汎用画像キー
#DIMS CONST 表示切替 = "情報・素質","能力・経験","服装","同行者"
#DIM DYNAMIC 部屋使用数
#DIM DYNAMIC CSV番号
#DIMS DYNAMIC CSTR切り分け文字列格納, 2
#DIMS DYNAMIC ランダムキャラ奥義名, 6
#DIM DYNAMIC ループ用
#DIMS DYNAMIC 見出し文字色
見出し文字色 = 959595

;VARSET 表示モード
;ページ数 = 0
;表示数カウント = 0
;ランダムキャラネーム = 
;ランダムキャラCALLネーム = 
ランダムキャラ二つ名 = （未定）
ランダムキャラ服装名 = （未定）
;ランダムキャラ出演作品 = 
;ランダムキャラ特殊種族 = 
;汎用画像キー = 
SIF CSVエディタフラグ
	OPTION変数:汎用立ち絵制御 = 0
;体型保存 = 0
;下半身保存 = 0
;VARSET ランダムキャラ素質:0
;VARSET ランダムキャラ経験:0
;VARSET TALENT一時保存:0
;VARSET フレーバー素質数値保存
;VARSET 知識素質数値保存
;VARSET 肉体特徴数値保存
;VARSET 服装数値保存
VARSET TALENT数値保存:0, 5
VARSET ABL一時保存:0, -1
;VARSET ABL数値保存:0
VARSET EXP一時保存:0, -1
;VARSET EXP数値保存:0
;VARSET ランダムキャラ戦闘タイプ:0
;VARSET ランダムキャラ奥義名
VARSET LOCAL
ランダムキャラ素質:0 = 1
ランダムキャラ素質:1 = 1
好物一時保存 = %好物一覧:(RAND(VARSIZE("好物一覧")))%_
服装数値保存:30 = !RAND:3 + !RAND:3 + !RAND:3
;ビキニ　8/27 ワンピース 12/27 ボーイレッグ 6/27 プランジングネック 1/27
ランダムキャラ招待不可フラグ = 3


CALL 従業員人数チェック()

; FOR LOCAL, 10, 71
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 77, 132
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 140, 150
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 220, 300
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT

FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "優先表示"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "性格"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "肉体"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "その他"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 21
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT
FOR LOCAL, 30, 50
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 100
	SIF GROUPMATCH(LOCAL:0, 89, 33)
		CONTINUE
	IF EXPNAME:LOCAL != ""
		EXP一時保存:(LOCAL:12) = LOCAL
		LOCAL:12 += 1
	ENDIF
NEXT

身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
ウエスト保存 = EDIT用ウエスト算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存)
ヒップ保存 = EDIT用ヒップ算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存, 下半身保存)

$従業員_LOOP
DRAWLINE
IF CSVエディタフラグ
	PRINTL 可指定资质等参数生成CSV角色文件。
	PRINTL 生成的文件将保存在「CSV_EDITFILE」文件夹，使用时请手动转移至「CSV」目录。
	DRAWLINE
	LOCALS = 
	IF !登録番号
		;空き番号取得
		FOR LOCAL, 3000, キャラクタ数上限
			IF EXISTCSV(LOCAL) == 0
				登録番号 = LOCAL
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	PRINTL 完全自定义员工资质进行雇佣。
	PRINTPLAIN 注意：每增加一项资质指定，所需卢比会相应增加。
	PRINTL
	DRAWLINE
	LOCALS = <div rect='81,0,1937,344' border='31' bcolor='#C0C0C0'></div>
	LOCALS += @"<div rect='156,62,1912,281'>持有卢比：%NUM_FORMAT(MONEY)%<br>持有ZP　：%ZP所持量全文字列()%</div>"


	LOCALS += "<div rect='2050,0,3875,344' border='31' bcolor='#C0C0C0'></div>"
	LOCALS += "<div rect='2125,62,3812,281'>"
	必要ルピ = 1000
	;名前・戦闘タイプではルピ増加無し
	;肉体情報
	;バストサイズ・容姿は普通から外れるほどルピが嵩む
	IF ランダムキャラ素質:0 == 3
		必要ルピ += ABS(ランダムキャラ素質:3 - 3) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 1) * 500
	ELSEIF ランダムキャラ素質:0 == 4
		必要ルピ += ABS(ランダムキャラ素質:3 + 2) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 2) * 500
	ELSE
		必要ルピ += ABS(ランダムキャラ素質:3) * 200
		必要ルピ += ABS(ランダムキャラ素質:31) * 200
	ENDIF
	必要ルピ += ABS(ランダムキャラ素質:6) * 300
	必要ルピ += ABS(ランダムキャラ素質:30) * 500
	必要ルピ += ABS(ランダムキャラ素質:32) * 200
	SIF INRANGE(ランダムキャラ素質:33, 年齢層_幼い, 年齢層_老年)
		必要ルピ += ABS(ランダムキャラ素質:33 - 年齢層_成人) * 200
	SIF ランダムキャラ素質:34
		必要ルピ += 1000
	LOCAL:1 = 0
	FOR LOCAL, 0, 300
		SIF TALENT数値保存:LOCAL == 0
			CONTINUE
		SIF TALENT数値保存:LOCAL == 5
			CONTINUE
		LOCAL:1 += 1
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 5 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 50
		SIF ABL数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += ABL数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 10 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 100
		SIF EXP数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += EXP数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 *  5 * (LOCAL:1 / 100 + 1)

	必要ルピ += ランダムキャラ素質:30 * ランダムキャラ素質:30 * 100


	LOCALS += @"当前所需卢比：%NUM_FORMAT(必要ルピ), 12%卢比<br>"
	部屋使用数 = 0
	FOR LOCAL, 0, 従業員部屋数
		IF 従業員部屋割り配列:LOCAL != 0
			部屋使用数 ++
		ENDIF
	NEXT
	LOCALS += @"当前已使用宿舍/总容量：{部屋使用数}间 / {従業員部屋数}间"
	LOCALS += "</div>"
ENDIF

LOCALS += "<div rect='81,375,1937,3312' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,3312' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,500,1937,3312'>"
LOCAL = 99 + !CSVエディタフラグ
LOCALS += @"<button value='{LOCAL}'>[{LOCAL, 3, RIGHT}] \@ CSVエディタフラグ? パーソナリティ# 名前\@を設定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ && GETBIT(表示モード:1, 0)
	LOCALS += "<button value='107'>├[107] 设置CSV编号</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='100'>├[100] 设置姓名</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='101'>├[101] 设置称号</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='102'>├[102] 设置称呼方式</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='108'>├[108] 设置第一人称</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='109'>├[109] 设置第二人称</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='103'>├[103] 设置出自作品</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='104'>├[104] 设置礼物偏好</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='105'>├[105] 设置同行候补</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='106'>├[106] 设置可邀请・不可邀请</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='111'>└[111] 设置初始关系性</button>"
	LOCALS += "<br>"
ELSEIF CSVエディタフラグ
	LOCALS += "<button value='99'><font color='#808080'>└ 展开更多</font></button>"
	LOCALS += "<br>"
ELSE
	LOCALS += "<button value='102'>├[102] 设置称呼方式</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='110'>[110] 设置战斗类型</button>"
LOCALS += "<br>"
LOCALS += "<button value='120'>[120] 编辑身体数据</button>"
LOCALS += "<br>"
LOCALS += "<button value='130'>[130] 设置特质</button>"
LOCALS += "<br>"
IF フレーバー素質設定 != 1 && CSVエディタフラグ == 0
	LOCALS += "<button value='131'>[131] 设置装饰性特质</button>"
	LOCALS += "<br>"
ELSEIF CSVエディタフラグ
	LOCALS += "<button value='131'>[131] 设置知识等特质</button>"
	LOCALS += "<br>"
ENDIF
IF CSVエディタフラグ && GETBIT(表示モード:1, 1)
	LOCALS += "<button value='132'>├[132] 设置知识特质</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='133'>├[133] 设置性癖特质</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='134'>└[134] 设置专属特质</button>"
	LOCALS += "<br>"
ELSEIF CSVエディタフラグ
	LOCALS += "<button value='131'><font color='#808080'>└ 展开更多</font></button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='140'>[140] 设置能力</button>"
LOCALS += "<br>"
LOCALS += "<button value='150'>[150] 设置经验</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='160'>[160] 设置服装・泳装</button>"
	LOCALS += "<br>"
ELSE
	IF OPTION変数:汎用立ち絵制御
		LOCALS += "<button value='170'>[170] 选择通用立绘</button>"
		LOCALS += "<button value='171'>[171] 关闭通用立绘</button>"
		LOCALS += "<br>"
	ELSE
		LOCALS += "<button value='171'>[171] 启用通用立绘</button>"
		LOCALS += "<br>"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='200'>[200] 确认并生成</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='997'>[997] 启动评论/背景编辑器</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='998'>[998] 复制已有角色模板</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='999'>[999] 返回</button>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,500,3675,3312'>"

IF GROUPMATCH(表示モード, 0, 1, 2)
	;現状の設定表示
	IF CSVエディタフラグ
		LOCALS += @"<font color='#%見出し文字色%'>■</font><button value='107'>No.{登録番号, 4, LEFT}</button>　"
	ENDIF
	IF ランダムキャラネーム != ""
		IF CSVエディタフラグ
			LOCALS += @"<button value='101'>[%ランダムキャラ二つ名%]</button>"
			LOCALS += @"<button value='100'>%ランダムキャラネーム, 18, LEFT%"
		ELSE
			LOCALS += @"[员工]<button value='100'>%ランダムキャラネーム, 18, LEFT%"
		ENDIF
	ELSE
		IF CSVエディタフラグ
			LOCALS += @"<button value='101'>[%ランダムキャラ二つ名%]</button>"
			LOCALS += @"<button value='100'>（随机决定）　　　　　"
		ELSE
			LOCALS += @"[员工]<button value='100'>（随机决定）　　　"
		ENDIF
	ENDIF
	LOCALS += @"</button>"
;　呼び名くらいは従業員雇用でも決めれていい気がする。オリヴァーに対して「ナル」とか呼びたいやん？
;	IF CSVエディタフラグ
		LOCALS += @"<button value='102'>"
		IF ランダムキャラCALLネーム != ""
			LOCALS += @"<br>　<font color='#%見出し文字色%'>昵称：</font>%ランダムキャラCALLネーム, 24, LEFT%"
		ELSE
			LOCALS += @"<br>　<font color='#%見出し文字色%'>昵称：</font>%"（与名字共通）", 24, LEFT%"
		ENDIF
		LOCALS += @"</button>"
;	ENDIF
	IF CSVエディタフラグ
		LOCALS += @"<br>　<button value='108'><font color='#%見出し文字色%'>第一人称：</font>%ランダムキャラ人称:0, 24, LEFT%</button>"
		LOCALS += @"　<button value='109'><font color='#%見出し文字色%'>第二人称：</font>%ランダムキャラ人称:1, 24, LEFT%</button>"
	ENDIF
	LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
	LOCALS += @"<br>　<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
	LOCALS += @"　<font color='#%見出し文字色%'>类型：</font>%成長タイプ:(ランダムキャラ戦闘タイプ:2), 10, LEFT%　"
	LOCALS += @"　<font color='#%見出し文字色%'>擅长武器：</font>%得意武器表示文字列(ランダムキャラ戦闘タイプ:1)%"
	IF ランダムキャラ出演作品 != ""
		LOCALS += @"<br>　<button value='103'><font color='#%見出し文字色%'>出自作品：</font>%ランダムキャラ出演作品%</button>"
	ENDIF
	IF CSVエディタフラグ
		SELECTCASE ランダムキャラ招待不可フラグ
			CASE 0
				LOCALS:1 = 初始即为可邀请对象
			CASE 1
				LOCALS:1 = 初始状态下，会搭载于ZP商店的「添加特殊角色至候选名单」中
			CASE 2
				LOCALS:1 = 不搭载于ZP商店且不可邀请。需通过事件解锁
			CASE 3
				LOCALS:1 = 通关事件［连接「平行世界」的大门］后解锁，<br>并搭载于ZP商店的「添加特殊角色至候选名单」中
		ENDSELECT
		LOCALS += @"<br>　<button value='106' title='%LOCALS:1%'><font color='#%見出し文字色%'>不可邀请标志：</font>{ランダムキャラ招待不可フラグ}</button>"
	ENDIF
	LOCALS += "<br><br>"
ENDIF

SELECTCASE 表示モード
	CASE 0
		;現状の設定表示
		LOCALS += @"<button value='1010'>□ \@ GETBIT(表示モード:1, 10)? |▶ # ▽\@ 信息　 □ --------------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 10)
			LOCALS:1 '= @"　<font color='#%見出し文字色%'>种族：</font>"
			LOCALS:2 = 
			SELECTCASE ランダムキャラ素質:0
				CASE 1
					LOCALS:1 += "人类"
				CASE 2
					LOCALS:1 += "艾伦族"
				CASE 3
					LOCALS:1 += "多拉夫族"
				CASE 4
					LOCALS:1 += "哈文族"
				CASE 5
					LOCALS:1 += "星晶兽"
				CASE 6
					LOCALS:1 += "その他"
					SIF ランダムキャラ特殊種族 != ""
						LOCALS:2 += @" [%ランダムキャラ特殊種族%]"
			ENDSELECT
			LOCAL:1 = STRLENS(HTML_TOPLAINTEXT(LOCALS:1))
			LOCALS:1 += LOCALS:2
			LOCALS:2 '= ""
			FOR LOCAL, 0, VARSIZE("肉体特徴数値保存")
				IF 肉体特徴数値保存:LOCAL
					SELECTCASE LOCAL
						CASE 0
							LOCALS:2 '= "（有角）"
						CASE 1
							LOCALS:2 '= "（尾巴）"
						CASE 2
							LOCALS:2 '= "（艾伦耳）"
						CASE 3
							LOCALS:2 '= "（哈文耳）"
					ENDSELECT
				ELSE
					LOCALS:2 '= ""
				ENDIF
				IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3675/50) > 0
					LOCALS += LOCALS:1 + "<br>"
					LOCALS:1 '= " " * LOCAL:1
				ENDIF
				LOCALS:1 += LOCALS:2
				LOCALS:1 '= REPLACE(LOCALS:1, "）（", "／")
			NEXT
			LOCALS += LOCALS:1 + "<br>"
			;年齢
			;PRINT_STATEと同じ表記に変更
;			IF CSTR:ARG:特殊年齢 != ""
				;これを設定する場合はcsvの手打ちを推奨。
;				LOCALS:1 '= CSTR:ARG:特殊年齢
			IF ランダムキャラ素質:34
				IF GETBIT(OPTION変数:身体項目表示切り替え, 5) && 年齢保存 > 0
					LOCALS:1 '= GET_年齢文字列(ランダムキャラ素質:1, ランダムキャラ素質:33, ランダムキャラ素質:34, 年齢保存)
				ELSE
					LOCALS:1 '= GET_年齢文字列(ランダムキャラ素質:1, ランダムキャラ素質:33, ランダムキャラ素質:34)
				ENDIF
			ELSEIF 年齢保存 || ランダムキャラ素質:33
				IF GETBIT(OPTION変数:身体項目表示切り替え, 5) && 年齢保存 > 0
					LOCALS:1 = {年齢保存}岁
				ELSE
					LOCALS:1 '= GET_年齢文字列(ランダムキャラ素質:1, ランダムキャラ素質:33, ランダムキャラ素質:34)
				ENDIF
			ELSE
				;本来は↓だけど、表記されないということを表記しないとエディタとしては片手落ちでしょう
;				LOCALS:1 '= ""
				LOCALS:1 '= "此设定中不会显示年龄"
			ENDIF
			LOCALS:1 '= @"<button value='12140'><font color='#%見出し文字色%'>　\@ ランダムキャラ素質:34 ? 外表# 年龄\@：</font>%LOCALS:1%</button>"
			LOCALS += LOCALS:1 + " " * (40 - MIN(40-1, STRLENS(HTML_TOPLAINTEXT(LOCALS:1))))
			LOCALS += @"　<font color='#%見出し文字色%'>性知识等级：</font>"
			SELECTCASE ランダムキャラ素質:6
				CASE -2
					LOCALS += "无知"
				CASE -1
					LOCALS += "动物认知"
				CASE 0
					LOCALS += "懵懂无知"
				CASE 1
					LOCALS += "基本认知"
				CASE 2
					LOCALS += "知识专家"
				CASE 3
					LOCALS += "身经百战"
			ENDSELECT
			LOCALS += @"<br>　<font color='#%見出し文字色%'>性別：</font>"
			SELECTCASE ランダムキャラ素質:1
				CASE 1
					LOCALS:1 = 女性
				CASE 2
					LOCALS:1 = 男性
				CASE 3
					LOCALS:1 = 扶她
			ENDSELECT
			LOCALS:1 += \@ GETBIT(ランダムキャラ素質:1, 0) && !GETBIT(ランダムキャラ素質:2, 0) ? （処女） # \@
			LOCALS:1 += \@ GETBIT(ランダムキャラ素質:1, 1) && !GETBIT(ランダムキャラ素質:2, 1) ? （童貞） # \@
			LOCALS:1 += \@ Ａ処女保存 ? # （Ａ処女）\@
			LOCALS:1 = %REPLACE(LOCALS:1, "）（", "／"), 30, LEFT%
			LOCALS += LOCALS:1
			IF GETBIT(ランダムキャラ素質:1, 1)
				LOCALS += @"　　<font color='#%見出し文字色%'>男性生殖器尺寸：</font>"
				LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:32)%"
			ENDIF
			LOCALS += "<br>"
			IF !身長数値設定フラグ
				LOCALS += @"　<font color='#%見出し文字色%'>体格：</font>"
				LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:31), 12, LEFT%"
			ELSE
				LOCALS += @"　<font color='#%見出し文字色%'>身高：</font>%DECIMAL_STRING(身長保存, 1) + "cm", 12, LEFT%"
			ENDIF
			IF !GETBIT(ランダムキャラ素質:1, 0)
				LOCALS += @"　<font color='#%見出し文字色%'>体型：</font>"
				LOCALS += @"%GET_TALENTNAME(163,体型保存), 12, LEFT%"
			ELSE
				LOCALS += "　" * 10
			ENDIF
			IF !GETBIT(ランダムキャラ素質:1, 0) || (!ウエスト数値設定フラグ && ヒップ数値設定フラグ)
				LOCALS += @"　　　　<font color='#%見出し文字色%'>下半身：</font>"
				LOCALS += @"%GET_TALENTNAME(210,下半身保存)%"
			ENDIF
			IF GETBIT(ランダムキャラ素質:1, 0)
				LOCALS += "<br>"
				LOCALS += @"　<font color='#%見出し文字色%'>胸围：</font>"
				IF バスト数値設定フラグ
					LOCALS:1 '= @"%DECIMAL_STRING(バスト保存, 1)%cm"
					LOCALS:1 += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"
					LOCALS += @"%LOCALS:1, 16, LEFT%"
				ELSE
					LOCALS += @"%GET_TALENTNAME(205,ランダムキャラ素質:3), 16, LEFT%"
				ENDIF
				IF ウエスト数値設定フラグ
					LOCALS:1 '= @"　<font color='#%見出し文字色%'>腰围：</font>" + DECIMAL_STRING(ウエスト保存, 1) + "cm"
				ELSE
					LOCALS:1 '= @"　<font color='#%見出し文字色%'>体型：</font>" + GET_TALENTNAME(163,体型保存)
				ENDIF
				LOCALS += LOCALS:1 + " " * (20 - MIN(20-1, STRLENS(HTML_TOPLAINTEXT(LOCALS:1))))
				IF ヒップ数値設定フラグ
					LOCALS += @"　<font color='#%見出し文字色%'>臀围：</font>"
					LOCALS += @"%DECIMAL_STRING(ヒップ保存, 1)%cm"
				ELSE
					LOCALS += @"　<font color='#%見出し文字色%'>下半身：</font>"
					LOCALS += @"%GET_TALENTNAME(210,下半身保存)%"
				ENDIF
			ENDIF
			IF CSVエディタフラグ == 0
				LOCALS += @"<br>　　　<font color='#%見出し文字色%'>外貌：</font>"
				IF ランダムキャラ素質:30 == 0
					LOCALS += "大众脸　　　"
				ELSE
					LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:30), 12, LEFT%"
				ENDIF
				LOCALS += "<br>"
			ENDIF
			LOCALS += "<br>"
			REPLACE 好物一時保存, "_", "、"
			LOCALS += @"　<font color='#%見出し文字色%'>礼物偏好</font>：%SUBSTRINGU(RESULTS, 0, STRLENSU(RESULTS) - 1)%"
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"<button value='1011'>□ \@ GETBIT(表示モード:1, 11)? |▶ # ▽\@ 特质　 □ --------------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 11)
			LOCALS:1 '= "　"
			LOCALS:2 = 
			FOR LOCAL, 0, 300
				SIF TALENT数値保存:LOCAL == 0
					CONTINUE
				SIF TALENT数値保存:LOCAL == 5
					CONTINUE
				LOCALS:2 '= @"[%GET_TALENTNAME(TALENT一時保存:LOCAL, TALENT数値保存:LOCAL - 5)%]"
				IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3675/50) > 0
					LOCALS += LOCALS:1 + "<br>"
					LOCALS:1 '= "　"
				ENDIF
				LOCALS:1 += LOCALS:2
			NEXT
			LOCALS += LOCALS:1
			LOCALS += "<br>"
			;知識素質
			LOCALS:1 = 
			LOCALS:2 = 
			FOR LOCAL, 1, 100
				SIF 知識素質数値保存:LOCAL == 0
					CONTINUE
				LOCALS:2 '= @"[%ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}]"
				SIF LOCALS:1 == ""
					LOCALS:1 '= "　<font color='#888888'>知识：</font>"
				IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3675/50) > 0
					LOCALS += LOCALS:1 + "<br>"
					LOCALS:1 '= "　　　　"
				ENDIF
				LOCALS:1 += LOCALS:2
			NEXT
			LOCALS += LOCALS:1
			LOCALS += "<br>"
			;固有素質
			LOCAL:1 = 0
			LOCALS:1 = 
			LOCALS:2 = 
			FOR LOCAL, 0, 10
				SIF 固有素質一時保存:LOCAL == ""
					CONTINUE
				LOCALS:2 '= @"[%固有素質一時保存:LOCAL%]"
				SIF LOCALS:1 == ""
					LOCALS:1 '= "　<font color='#888888'>固有：</font>"
				IF STRLENS(HTML_TOPLAINTEXT(LOCALS:1+LOCALS:2)) / (3675/50) > LOCAL:1
					LOCALS += LOCALS:1 + "<br>"
					LOCALS:1 '= "　　　　"
				ENDIF
				LOCALS:1 += LOCALS:2
			NEXT
			LOCALS += LOCALS:1
			LOCALS += "<br>"
		ENDIF
		IF CSVエディタフラグ == 0
			LOCALS += @"<button value='1012'>□ \@ GETBIT(表示モード:1, 12)? |▶ # ▽\@ 风味特质　 □ ----------------------------------------------</button><br>"
			IF !GETBIT(表示モード:1, 12)
				LOCALS:1 = 
				LOCAL = 0
				FOR COUNT, 0, 10
					IF COUNT == 6
						PRINTL
						LOCAL = 0
					ENDIF
					SIF フレーバー素質数値保存:COUNT == 0
						CONTINUE
					LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 10, LEFT%：%GET_フレーバー素質NAME(COUNT, フレーバー素質数値保存:COUNT), 12, LEFT%"
					LOCAL += 1
					IF LOCAL == 3
						LOCAL = 0
						PRINTL
					ENDIF
				NEXT
			ENDIF
		ENDIF
	CASE 1
		;現状の設定表示（能力素質）
		LOCALS += @"<button value='1013'>□ \@ GETBIT(表示モード:1, 13)? |▶ # ▽\@ 奥义　 □ --------------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 13)
			LOCAL = 0
			DO
				IF LOCAL % 3 == 0
					SIF LOCAL == 3
						LOCALS += "<br>"
					LOCALS += @"　\@ LOCAL == 0? 通常# 沦陷\@状态奥义名称："
					IF ランダムキャラ奥義名:((LOCAL/3)*3) == "" && ランダムキャラ奥義名:0 == ""
						LOCALS += @"<font color='#888888'>（通用\@ LOCAL == 0? # 沦陷\@状态奥义）</font>"
						LOCAL += 3
						CONTINUE
					ENDIF
				ELSE
					LOCALS += " / "
				ENDIF
				IF ランダムキャラ奥義名:LOCAL != ""
					LOCALS += @"%ランダムキャラ奥義名:LOCAL%"
				ELSE
					LOCALS += @"<font color='#888888'>"
					IF LOCAL < 3 || ランダムキャラ奥義名:3 != ""
						LOCALS += @"%ランダムキャラ奥義名:((LOCAL/3)*3) + "＋" * (LOCAL%3)%"
					ELSEIF ランダムキャラ奥義名:(LOCAL-3) != ""
						LOCALS += @"%ランダムキャラ奥義名:(LOCAL-3) + UNICODE(0x2661)%"
					ELSEIF LOCAL > 3
						LOCALS += @"%ランダムキャラ奥義名:0 + UNICODE(0x2665) * (LOCAL%3)%"
					ELSE
						;ここにはこないはず
						LOCALS += @"分岐式おかしいよ"
					ENDIF
					LOCALS += @"</font>"
				ENDIF
				LOCAL ++
			LOOP LOCAL < 6
			LOCALS += "<br><br>"
		ENDIF
		LOCALS += @"<button value='1014'>□ \@ GETBIT(表示モード:1, 14)? |▶ # ▽\@ 能力　 □ --------------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 14)
			LOCAL:2 = 0
			FOR LOCAL, 0, 50
				SIF ABL数値保存:LOCAL < 1
					CONTINUE
				IF ABL一時保存:LOCAL > -1
					LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 16%:{ABL数値保存:LOCAL, 2}   "
					LOCAL:2 += 1
					IF LOCAL:2 == 3
						LOCALS += "<br>"
						LOCAL:2 = 0
					ENDIF
				ENDIF
			NEXT
			SIF LOCAL:2 != 0
				LOCALS += "<br>"
			LOCALS += "<br>"
		ENDIF
		LOCALS += @"<button value='1015'>□ \@ GETBIT(表示モード:1, 15)? |▶ # ▽\@ 经验　 □ -----------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 15)
			LOCAL:2 = 0
			FOR LOCAL, 0, 100
				SIF EXP数値保存:LOCAL < 1
					CONTINUE
				IF EXP一時保存:LOCAL > -1
					LOCALS += @"%EXPNAME:(EXP一時保存:LOCAL), 16%:{EXP数値保存:LOCAL, 3}   "
					LOCAL:2 += 1
					IF LOCAL:2 == 3
						LOCALS += "<br>"
						LOCAL:2 = 0
					ENDIF
				ENDIF
			NEXT
			LOCALS += "<br><br>"
		ENDIF
		LOCALS += @"<button value='1016'>□ \@ GETBIT(表示モード:1, 16)? |▶ # ▽\@ 性癖　 □ --------------------------------------------------------</button><br>"
		IF !GETBIT(表示モード:1, 16)
			LOCALS:1 = 
			FOR LOCAL, 1, 20
				SIF 性癖素質数値保存:LOCAL == 0
					CONTINUE
				LOCALS += @"[%ERDNAME(性癖素質, LOCAL)%"
				SELECTCASE 性癖素質数値保存:LOCAL
					CASE 1
						LOCALS += "(兴趣)]"
					CASE 2
						LOCALS += "(爱好)]"
					CASE 3
						LOCALS += "(成瘾)]"
				ENDSELECT
			NEXT
		ENDIF
	CASE 2
		;現状の設定表示（服装）
		LOCALS += "□ 服装 □---------------------------------------------------------------<br>"
		LOCALS += @"　名称：%ランダムキャラ服装名%<br>"
		LOCAL = 0
		;鎧
		IF 服装数値保存:1
			LOCALS += "　　[上半身护甲]"
			LOCAL = 1
		ENDIF
		;鎧
		IF 服装数値保存:11 && 服装数値保存:12
			LOCALS += "[下半身护甲]"
			LOCAL = 1
		ELSEIF 服装数値保存:11
			LOCALS += "[下半身后方护甲]"
			LOCAL = 1
		ELSEIF 服装数値保存:12
			LOCALS += "[下半身前方护甲]"
			LOCAL = 1
		ENDIF
		SIF LOCAL
			LOCALS += "<br>"

		LOCAL = 0
		;上半身服
		IF 服装数値保存:2
			LOCALS += "　　[上半身服装"
			IF 服装数値保存:3
				LOCALS += "（露出腋下）"
			ENDIF
			IF 服装数値保存:4
				LOCALS += "（露出腹部）"
			ENDIF
			IF 服装数値保存:5
				LOCALS += "（可以解开）"
			ENDIF
			IF 服装数値保存:6
				LOCALS += "（手可插入）"
			ENDIF
			IF 服装数値保存:7
				LOCALS += "（可穿衣乳交）"
			ENDIF
			IF 服装数値保存:9
				LOCALS += "（可脱掉胸罩）"
			ENDIF
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		;下半身服
		IF 服装数値保存:13
			LOCALS += "　　[下半身服装"
			SIF 服装数値保存:14
				LOCALS += "（可掀裙摆）"
			SIF 服装数値保存:15
				LOCALS += "（可移位）"
			SIF 服装数値保存:18
				LOCALS += "（可脱掉内裤）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		IF 服装数値保存:20
			LOCALS += "　　[连体紧身衣"
			SIF 服装数値保存:21 || 服装数値保存:22
				LOCALS += "（破损）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:8
			LOCALS += "　　[内搭衣物"
			SIF 服装数値保存:21
				LOCALS += "（破损）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:16
			LOCALS += "　　[紧身衣"
			SIF 服装数値保存:22
				LOCALS += "（破损）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		LOCALS += "　　"
		SIF 服装数値保存:9 == 0
			LOCALS += "[无胸罩]"
		SIF 服装数値保存:9 == 1
			LOCALS += "[穿着胸罩]"
		SIF 服装数値保存:9 == 2
			LOCALS += "[乳贴]"
		SIF 服装数値保存:17 == 0
			LOCALS += "[无内裤]"
		SIF 服装数値保存:17 == 1
			LOCALS += "[穿着内裤]"
		SIF 服装数値保存:17 == 2
			LOCALS += "[阴贴]"
		SIF 服装数値保存:17 == 3
			LOCALS += "[兜裆布]"

		LOCALS += "<br>"

		LOCALS += "<br>"
		LOCALS += @"　泳装：%泳装一覧:(服装数値保存:30)%"
		LOCALS += "<br>"
	CASE 3
		LOCALS += "■同行候选角色上限为100人"
		IF DT_EXIST("DT_CHARA")
			VARSET 同行者一時保存
			VARSET 同行確率一時保存
			LOCAL:1 = 0
			FOR LOCAL, 0, DT_ROW_LENGTH("DT_CHARA")
				SIF !DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
					CONTINUE
				同行者一時保存:(LOCAL:1) = LOCAL
				同行確率一時保存:(LOCAL:1) = DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
				LOCAL:1 ++
				SIF LOCAL:1 >= VARSIZE("同行者一時保存")
					BREAK
			NEXT
			LOCALS += @"(当前{LOCAL:1, 3, RIGHT}人)■<br>"
			FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
				SIF LOCAL >= LOCAL:1
					BREAK
				LOCAL:2 = 同行者一時保存:LOCAL
				LOCALS += @"<nonbutton>[{DT_CELL_GET("DT_CHARA", LOCAL:2, "NO"), LOG10(キャラクタ数上限)+1, RIGHT}] %ADD_STR_SPACE(NAME表示省略(DT_CELL_GETS("DT_CHARA", LOCAL:2, "NAME")), "]"), 41, LEFT%：{DT_CELL_GET("DT_CHARA", LOCAL:2, "VALUE"), 3}％</nonbutton>"
				LOCALS += "<br>"
			NEXT
		ELSE
			LOCALS += "(当前  0人)■<br>"
		ENDIF
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3406,3675,2962'>"
		IF ページ数 > 0
			LOCALS += "<button value='29994'>[|≪]</button>"
		ELSE
			LOCALS += "     "
		ENDIF
		IF ページ数 > 5
			LOCALS += "<button value='29996'> [－５]</button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		LOCALS += @"　　　　　　　{ページ数, 4, RIGHT}/{LOCAL:1 / 20, 4, RIGHT}　　　　　 　　"
		IF ページ数 < LOCAL:1 / 20 - 5
			LOCALS += "<button value='29997'>[＋５] </button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29995'>[≫|]</button>"
		ENDIF
		LOCALS += "<br>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF

	CASE 110
		LOCALS += "属性："
		FOR LOCAL, 0, 6
			IF ランダムキャラ戦闘タイプ:0 == LOCAL
				LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11000 + LOCAL}'>[%属性数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "擅长武器："
		FOR LOCAL, 0, 10
			IF GETBIT(ランダムキャラ戦闘タイプ:1, LOCAL)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11010 + LOCAL}'>[%得意武器数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "类型："
		FOR LOCAL, 0, 8
			IF ランダムキャラ戦闘タイプ:2 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11020 + LOCAL}'>[%成長タイプ:LOCAL%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCAL = 0
		DO
			IF LOCAL % 3 == 0
				LOCALS += @"<br>\@ LOCAL == 0? 通常# 沦陷\@状态奥义名称："
				IF ランダムキャラ奥義名:((LOCAL/3)*3) == "" && ランダムキャラ奥義名:0 == ""
					LOCALS += @"<font color='#888888'><button value='{11040 + LOCAL}'>（通用\@ LOCAL == 0? # 沦陷\@状态奥义）</button></font>"
					LOCAL += 3
					CONTINUE
				ENDIF
			ELSE
				LOCALS += " / "
			ENDIF
			LOCALS += @"<button value='{11040 + LOCAL}'>"
			IF ランダムキャラ奥義名:LOCAL != ""
				LOCALS += @"%ランダムキャラ奥義名:LOCAL%"
			ELSE
				LOCALS += @"<font color='#888888'>"
				IF LOCAL < 3 || ランダムキャラ奥義名:3 != ""
					LOCALS += @"%ランダムキャラ奥義名:((LOCAL/3)*3) + "＋" * (LOCAL%3)%"
				ELSEIF ランダムキャラ奥義名:(LOCAL-3) != ""
					LOCALS += @"%ランダムキャラ奥義名:(LOCAL-3) + UNICODE(0x2661)%"
				ELSEIF LOCAL > 3
					LOCALS += @"%ランダムキャラ奥義名:0 + UNICODE(0x2665) * (LOCAL%3)%"
				ELSE
					;ここにはこないはず
					LOCALS += @"分岐式おかしいよ"
				ENDIF
				LOCALS += @"</font>"
			ENDIF
			LOCALS += @"</button>"
			LOCAL ++
		LOOP LOCAL < 6
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[返回状态界面]</button>"
	CASE 120
		;- 肉体情報
		; 種族
		LOCALS:3 = C0C0C0
		LOCALS:2 = 333333
		LOCALS += @"<font color='#%見出し文字色%'>种族：</font>"
		FOR LOCAL, 1, 7
			LOCALS += @"<font color='#%LOCALS:(2+(ランダムキャラ素質:0 == LOCAL))%'><button value='{12000 + LOCAL}'>[%GET_TALENTNAME(200,LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		; 特殊種族名
		IF ランダムキャラ素質:0 == 6
			IF ランダムキャラ特殊種族 != ""
				LOCALS:1 = %ランダムキャラ特殊種族%
			ELSE
				LOCALS:1 = 未设定
			ENDIF
			LOCALS += @"<font color='#%見出し文字色%'>种族名:</font><button value='{12096}'>[%LOCALS:1%]</button>"
			LOCALS += "<br>"
		ENDIF
		; 身体特徴
		LOCALS += @"<font color='#%見出し文字色%'>角：</font>"
		IF 肉体特徴数値保存:0
			LOCALS += @"<font color='#%LOCALS:3%'><button value='{12070}'>[有]</button></font>"
		ELSE
			LOCALS += @"<font color='#%LOCALS:2%'><button value='{12070}'>[无]</button></font>"
		ENDIF
		LOCALS += @"　<font color='#%見出し文字色%'>尾巴：</font>"
		IF 肉体特徴数値保存:1
			LOCALS += @"<font color='#%LOCALS:3%'><button value='{12071}'>[有]</button></font>"
		ELSE
			LOCALS += @"<font color='#%LOCALS:2%'><button value='{12071}'>[无]</button></font>"
		ENDIF
		LOCALS += @"　<font color='#%見出し文字色%'>艾伦耳：</font>"
		IF 肉体特徴数値保存:2
			LOCALS += @"<font color='#%LOCALS:3%'><button value='{12072}'>[有]</button></font>"
		ELSE
			LOCALS += @"<font color='#%LOCALS:2%'><button value='{12072}'>[无]</button></font>"
		ENDIF
		LOCALS += @"　<font color='#%見出し文字色%'>哈文耳：</font>"
		IF 肉体特徴数値保存:3
			LOCALS += @"<font color='#%LOCALS:3%'><button value='{12073}'>[有]</button></font>"
		ELSE
			LOCALS += @"<font color='#%LOCALS:2%'><button value='{12073}'>[无]</button></font>"
		ENDIF
		LOCALS += "<br>"

		LOCALS += @"<font color='#%見出し文字色%'>性別：</font>"
		LOCALS += @"<font color='#\@ ランダムキャラ素質:1 == 1? %LOCALS:3%# %LOCALS:2%\@'><button value='{12007}'>[女性]</button></font>"
		LOCALS += @"<font color='#\@ ランダムキャラ素質:1 == 2? %LOCALS:3%# %LOCALS:2%\@'><button value='{12008}'>[男性]</button></font>"
		LOCALS += @"<font color='#\@ ランダムキャラ素質:1 == 3? %LOCALS:3%# %LOCALS:2%\@'><button value='{12009}'>[扶她]</button></font>"
		LOCALS += "<br>"
		LOCALS += @"<font color='#%見出し文字色%'>纯洁：</font>"
		IF GETBIT(ランダムキャラ素質:1, 0)
			LOCAL = GETBIT(ランダムキャラ素質:2, 0)
			LOCALS += @"<font color='#%LOCALS:(3-LOCAL)%'><button value='{12010}'>[处女]</button></font>"
			LOCALS += @"<font color='#%LOCALS:(2+LOCAL)%'><button value='{12011}'>[非处女]</button></font>"
		ENDIF
		IF ランダムキャラ素質:1 != 1
			LOCAL = GETBIT(ランダムキャラ素質:2, 1)
			LOCALS += @"<font color='#%LOCALS:(3-LOCAL)%'><button value='{12012}'>[童贞]</button></font>"
			LOCALS += @"<font color='#%LOCALS:(2+LOCAL)%'><button value='{12013}'>[非童贞]</button></font>"
		ENDIF
		LOCAL = !(Ａ処女保存 == 0)
		LOCALS += @"<font color='#%LOCALS:(3-LOCAL)%'><button value='{12014}'>[Ａ处女]</button></font>"
		LOCALS += @"<font color='#%LOCALS:(2+LOCAL)%'><button value='{12015}'>[非Ａ处女]</button></font>"
		LOCALS += "<br>"
		LOCALS += @"<font color='#%見出し文字色%'>性知识等级：</font>"
		FOR LOCAL, -2, 4
			IF ランダムキャラ素質:6 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12030 + LOCAL}'>[%性知識段階表示(LOCAL, -1)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		IF 年齢数値設定フラグ
			LOCALS += @"<font color='#%見出し文字色%'>实际年龄：</font>"
			LOCALS += @"<button value='{12097}'>[{年齢保存}岁]</button>"
			LOCALS += @"　　　<button value='{12098}'>[粗略设置]</button>"
			LOCALS += @"　　　<button value='{12139}'>[重新随机]</button>"
		ELSE
			LOCALS += @"<font color='#%見出し文字色%'>年龄层：</font>"
			LOCALS += @"%GET_TALENTNAME(164, ランダムキャラ素質:33)%"
			LOCALS += @"　　　<button value='{12098}'>[按实际年龄设置]</button>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"<font color='#%見出し文字色%'>年龄层：</font>"
		FOR LOCAL, 年齢層_最小, 年齢層_最大 + 1
			SIF LOCAL != 年齢層_不詳 && !INRANGE(LOCAL, 年齢層_幼い, 年齢層_人外)
				CONTINUE
			IF ランダムキャラ素質:33 == LOCAL
				IF 年齢数値設定フラグ
					LOCALS:1 = FFFF00
				ELSE
					LOCALS:1 = C0C0C0
				ENDIF
			ELSE
				LOCALS:1 = 333333
			ENDIF
			SELECTCASE LOCAL
				CASE 年齢層_幼い TO 年齢層_老年
					LOCALS:2 '= @"不论种族，按实际年龄设定为{年齢ボーダー:(LOCAL-1)}～{年齢ボーダー:LOCAL-1}岁左右"
				CASE 年齢層_人外
					LOCALS:2 '= @"不论种族，按实际年龄设定为{年齢ボーダー:(LOCAL-1)}岁以上"
				CASEELSE
					LOCALS:2 = 
			ENDSELECT
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12100 + LOCAL}' title='%LOCALS:2%'>[\@ LOCAL == 年齢層_不詳? 年龄# \@%GET_TALENTNAME(164, LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		;外見年齢が年齢層区分±1の場合、外見年齢を0（＝年相応）にする
		SIF INRANGE(ランダムキャラ素質:34, ランダムキャラ素質:33-1, ランダムキャラ素質:33+1)
			ランダムキャラ素質:34 = 0
		LOCALS:2 = 非单纯童颜或显老程度，仅限设定萝莉老太婆或出生3年就基本成年这类<br>外表年龄与实际年龄存在显著差异的特殊情况。
		LOCALS += @"<font color='#%見出し文字色%'><nonbutton title='%LOCALS:2%'>外表年龄：</nonbutton></font>"
		LOCALS += @"<font color='#\@ ランダムキャラ素質:34? 333333# C0C0C0\@'><button value='{12120}'>[符合年龄层]</button></font>"
		FOR LOCAL, 年齢層_最小, 年齢層_最大 + 1
			SIF !INRANGE(LOCAL, 年齢層_幼い, 年齢層_人外)
				CONTINUE
			IF ランダムキャラ素質:34 == LOCAL && !INRANGE(LOCAL, ランダムキャラ素質:33-1, ランダムキャラ素質:33+1)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS:2 = 
			IF INRANGE(LOCAL, ランダムキャラ素質:33-1, ランダムキャラ素質:33+1)
				LOCALS += "<clearbutton>"
				LOCALS:2 = 无法设定接近实际年龄层的外表年龄
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12120 + LOCAL}' title='%LOCALS:2%'>[%GET_TALENTNAME(164, LOCAL)%]</button></font>"
			SIF INRANGE(LOCAL, ランダムキャラ素質:33-1, ランダムキャラ素質:33+1)
				LOCALS += "</clearbutton>"
		NEXT
		LOCALS += "<br>"
		IF GETBIT(ランダムキャラ素質:1, 0)
			LOCALS += "<br>"
			LOCALS += @"<font color='#%見出し文字色%'>胸围尺寸：</font>"
			IF バスト数値設定フラグ
				LOCALS:1 = %DECIMAL_STRING(バスト保存, 1)%cm
				LOCALS:1 += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"

				LOCALS += @"<button value='{12026}'>[%LOCALS:1%]</button>"
				LOCALS += " " * (14 - MIN(14-1, STRLENS(LOCALS:1)))
				LOCALS += @"　<button value='{12999}'>[按罩杯数指定]</button>"
				LOCALS += @"　<button value='{12997}'>[重新随机]</button>"
			ELSE
				LOCALS += @"%GET_TALENTNAME(205, ランダムキャラ素質:3), 16, LEFT%"
				LOCALS += @"　<button value='{12999}'>[设置数值]</button>"
			ENDIF
			LOCALS += "<br>"
			LOCALS:2 '= " " * 12 + @"<font color='#%見出し文字色%'>├</font>"
			FOR LOCAL, -2, 4
				IF ランダムキャラ素質:3 == LOCAL
					IF バスト数値設定フラグ
						LOCALS:1 = FFFF00
					ELSE
						LOCALS:1 = C0C0C0
					ENDIF
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS:3 '= @"<font color='#%LOCALS:1%'><button value='{12020 + LOCAL}'>[%GET_TALENTNAME(205,LOCAL)%]</button></font>"
				IF STRLENS(HTML_TOPLAINTEXT(LOCALS:2+LOCALS:3)) / (3675/50) > 0
					LOCALS += LOCALS:2 + "<br>"
					LOCALS:2 '= " " * 12 + @"<font color='#%見出し文字色%'>└</font>"
				ENDIF
				LOCALS:2 += LOCALS:3
			NEXT
			LOCALS += LOCALS:2
			LOCALS += "<br>"
			LOCALS += @"<font color='#%見出し文字色%'>腰围尺寸：</font>"
			IF ウエスト数値設定フラグ
				LOCALS:1 = %DECIMAL_STRING(ウエスト保存, 1)%cm

				LOCALS += @"<button value='{12990}'>[%LOCALS:1%]</button>"
				LOCALS += " " * (14 - MIN(14-1, STRLENS(LOCALS:1)))
				LOCALS += @"　<button value='{12994}'>[重新随机]</button>"
				LOCALS += @"　<button value='{12992}'>[自动设定]</button>"
			ELSE
				LOCALS += @"（根据体格・体型自动计算）"
				LOCALS += @"　<button value='{12992}'>[手动设定]</button>"
			ENDIF
			LOCALS += "<br>"
			LOCALS += @"<font color='#%見出し文字色%'>臀围尺寸：</font>"
			IF ヒップ数値設定フラグ
				LOCALS:1 = %DECIMAL_STRING(ヒップ保存, 1)%cm

				LOCALS += @"<button value='{12991}'>　[%LOCALS:1%]</button>"
				LOCALS += " " * (14 - MIN(14-1, STRLENS(LOCALS:1)))
				LOCALS += @"　<button value='{12995}'>[重新随机]</button>"
				LOCALS += @"　<button value='{12993}'>[自动设定]</button>"
			ELSE
				LOCALS += @"　（根据体格・体型自动计算）"
				LOCALS += @"　<button value='{12993}'>[手动设定]</button>"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		IF CSVエディタフラグ == 0
			LOCALS += @"<font color='#%見出し文字色%'>外貌：</font>"
			FOR LOCAL, -1, 3
				IF ランダムキャラ素質:30 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[大众脸]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[%GET_TALENTNAME(160, LOCAL)%]</button></font>"
				ENDIF
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"<font color='#%見出し文字色%'>身高：</font>"
		IF 身長数値設定フラグ
			LOCALS:1 = %DECIMAL_STRING(身長保存, 1)%cm

			LOCALS += @"<button value='{12025}'>[%LOCALS:1%]</button>"
			LOCALS += " " * (12 - MIN(12-1, STRLENS(LOCALS:1)))
			LOCALS += @"　<button value='{12998}'>[粗略设置]</button>"
			LOCALS += @"　<button value='{12996}'>[重新随机]</button>"
		ELSE
			LOCALS += @"%GET_TALENTNAME(161, ランダムキャラ素質:31), 14, LEFT%"
			LOCALS += @"　<button value='{12998}'>[设置数值]</button>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"<font color='#%見出し文字色%'>体格：</font>"
		FOR LOCAL, -3, 3
			IF ランダムキャラ素質:31 == LOCAL
				IF 身長数値設定フラグ
					LOCALS:1 = FFFF00
				ELSE
					LOCALS:1 = C0C0C0
				ENDIF
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[%GET_TALENTNAME(161, LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		IF !ウエスト数値設定フラグ
			LOCALS += @"<font color='#%見出し文字色%'>体型：</font>"
			FOR LOCAL, -3, 4
				IF 体型保存 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12080 + LOCAL}'>[%GET_TALENTNAME(163, LOCAL)%]</button></font>"
			NEXT
			LOCALS += "<br>"
		ENDIF
		IF !ウエスト数値設定フラグ || !ヒップ数値設定フラグ
			LOCALS += @"<font color='#%見出し文字色%'>下半身：</font>"
			FOR LOCAL, -1, 2
				IF 下半身保存 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12090 + LOCAL}' title='%GET_TALENTNAME(210, LOCAL, 1)%'>[%GET_TALENTNAME(210, LOCAL)%]</button></font>"
			NEXT
			LOCALS += "<br>"
		ENDIF
		IF GETBIT(ランダムキャラ素質:1, 1)
			LOCALS += @"<font color='#%見出し文字色%'>男性生殖器尺寸：</font>"
			FOR LOCAL, -2, 3
				IF ランダムキャラ素質:32 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[%GET_TALENTNAME(162, LOCAL)%]</button></font>"
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[返回状态界面]</button>"
		LOCALS += "　<button value='9'>[更改项目名称颜色]</button>"
	CASE 130
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF TALENT一時保存:LOCAL == 0
				BREAK
			LOCALS += @"%TALENTNAME:(TALENT一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, -2, 4
				;母乳体質だけ例外
				IF TALENT一時保存:LOCAL == 149 && LOCAL:1 > 1
					CONTINUE
				ENDIF
				LOCALS:2 = %GET_TALENTNAME(TALENT一時保存:LOCAL,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = 无
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF TALENT数値保存:LOCAL - 5 == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{20000 + LOCAL * 10 + LOCAL:1 + 5}' title='%GET_TALENTNAME(TALENT一時保存:LOCAL, LOCAL:1, 1)%'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:10 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF
	CASE 131
		FOR COUNT, 1, 6
			SIF ERDNAME(フレーバー素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 6
				LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = 隐藏
				ENDIF
				IF COUNT == 5 && LOCAL:1 == 4
					CONTINUE
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF フレーバー素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += @"%ERDNAME(フレーバー素質, 6), 12, LEFT%：　　　　"
		FOR LOCAL:1, 0, 4
			LOCALS:2 = %GET_フレーバー素質NAME(6, POWER(2, LOCAL:1))%
			IF LOCALS:2 == ""
				CONTINUE
			ELSEIF LOCAL:1 == 3 && GETBIT(ランダムキャラ素質:1, 1) == 0
				CONTINUE
			ENDIF
			IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + 6 * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>"
	CASE 132
		LOCALS += "基本知识素质的等级设定基于「空之世界知识」。<br>"
		LOCALS += "对于异世界出身的角色，建议适当降低相应等级。<br>"
		LOCALS += "　兴趣等级为1，专业级/可职业化的等级为2<br>　全空之世界最强级别建议设为3　　<button value='69999'>[设定标准详解]</button><br>"
		LOCALS += "<br>"
		FOR COUNT, 1, 100
			SIF ERDNAME(知識素質, COUNT) == ""
				CONTINUE
			LOCALS:3 '= ERDNAME(知識素質, COUNT)
			SELECTCASE LOCALS:3
				CASE "農業知識"
					LOCALS:3 += @"<br>　　即使在现实世界中，不同地区的农业知识也往往大相径庭。<br>　　气候相似可维持原等级，气候差异大则应降低等级。"
				CASE "経営知識"
					LOCALS:3 += @"<br>　　原则上不降低等级，但需注意空之世界受限于通信<br>　　和运输手段，部分现代商业知识可能不适用。<br>　　奇幻世界出身的角色可保留部分适用性。"
				CASE "高貴"
					LOCALS:3 += @"<br>　　原则上不降低等级，但若原生世界与空之世界<br>　　文化差异过大时，适当降低更为合理。"
				CASE "魔法知識"
					LOCALS:3 += @"<br>　　原则上需要降低等级，但考虑到空之世界的魔法体系<br>　　兼容性广泛，且联动角色通常保留特殊能力，<br>　　若魔法原理具有通用性则可维持原等级。"
				CASE "魔物知識","歴史知識"
					LOCALS:3 += @"<br>　　每个世界都有完全不同的历史和发展，所以除非有非常好的理由，<br>　　否则水平应该大幅降低。"
				CASE "調合知識", "機械知識","錬金知識"
					LOCALS:3 += @"<br>　　由于各个世界技术体系的差异，<br>　　导致应用困难时，建议适当降低等级"
				CASEELSE
					LOCALS:3 += @"<br>　　推测具有跨世界通用性，<br>　　基本无需调整等级"
			ENDSELECT
			LOCALS += @"<nonbutton title='%LOCALS:3%'>"
			LOCALS += @"%ERDNAME(知識素質, COUNT)%<font color='#808080'>(?)</font>"
			LOCALS += " " * (9 - STRLENS(ERDNAME(知識素質, COUNT)))
			LOCALS += "：</nonbutton>"
			FOR LOCAL:1, 0, 4
				IF LOCAL:1 == 0
					LOCALS:2 = 无
				ELSE
					LOCALS:2 = Lv{LOCAL:1}
				ENDIF
				IF 知識素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{60000 + COUNT * 10 + LOCAL:1}' title='%LOCALS:3%'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>"
	CASE 133
		FOR COUNT, 1, 20
			SIF ERDNAME(性癖素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(性癖素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 4
				SELECTCASE LOCAL:1
					CASE 0
						LOCALS:2 = 无
					CASE 1
						LOCALS:2 = 兴趣
					CASE 2
						LOCALS:2 = 爱好
					CASE 3
						LOCALS:2 = 成瘾
				ENDSELECT
				IF 性癖素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{80000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>"
	CASE 134
		LOCALS += "原则上作为不影响平衡的趣味性素质<br>　（例外：偶像、校园偶像）<br>"
		LOCALS += "请根据角色设定灵活运用这些素质参数<br>"
		LOCALS += "<br>"
		FOR COUNT, 0, 10
			LOCALS += @"<button value='{85000 + COUNT}'>专有特质{COUNT}：[%固有素質一時保存:COUNT%]</button><br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>"
	CASE 140
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF ABL一時保存:LOCAL == -1
				BREAK
			LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, 0, 6
				IF ABL数値保存:LOCAL == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{30000 + LOCAL * 10 + LOCAL:1}'>[%TOFULL(TOSTR(LOCAL:1))%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:11 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF
	CASE 150
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF EXP一時保存:LOCAL == -1
				BREAK
			LOCALS += @"<button value='{40000 + LOCAL * 10}'>[%EXPNAME:(EXP一時保存:LOCAL), 18, LEFT%]：{EXP数値保存:LOCAL, 8}</button>"
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:12 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF
	CASE 160
		FOR LOCAL, 1, 23
			IF EQUIPNAME_TIPS(LOCAL) != ""
				LOCALS += @"<nonbutton title='%EQUIPNAME_TIPS(LOCAL)%'>%EQUIPNAME:LOCAL%"
				LOCALS += @"<font color='#808080'>%"(?)", 24 - MIN(24-1, STRLENS(EQUIPNAME:LOCAL)), LEFT%</font>"
				LOCALS += "：</nonbutton>"
			ELSE
				LOCALS += @"%EQUIPNAME:LOCAL, 24, LEFT%："
			ENDIF
			LOCALS:1 = 333333
			LOCALS:2 = 333333
			LOCALS:3 = 333333
			LOCALS:4 = 333333
			LOCALS:(服装数値保存:LOCAL + 1) = C0C0C0
			IF LOCAL == 9
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[不穿胸罩]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[穿着胸罩]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[乳贴]</button></font>"
			ELSEIF LOCAL == 17
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[不穿内裤]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[穿着内裤]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[阴贴]</button></font>"
				LOCALS += @"<font color='#%LOCALS:4%'><button value='{70000 + LOCAL * 10 + 3}'>[兜裆布]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[无]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[有]</button></font>"
			ENDIF
			LOCALS += "<br>"
		NEXT
		LOCALS += "<br>"
		LOCALS:2 = 
		LOCALS:3 '= "　" * 13
		LOCALS:4 = 
		LOCALS += @"%"泳装", 24, LEFT%：" 
		FOR LOCAL, 0, MAX(VARSIZE("泳装一覧"), VARSIZE("男泳装一覧"))
			IF 服装数値保存:30 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF ランダムキャラ素質:1 == 2
				SIF LOCAL >= VARSIZE("男泳装一覧")
					BREAK
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + 30 * 10 + LOCAL}'>[%男泳装一覧:LOCAL%]</button></font>"
			ELSE
				SIF LOCAL >= VARSIZE("泳装一覧")
					BREAK
				IF LOCAL > 1
					LOCALS:4 = \@ LOCAL == 3? 首元から胸下まで切り込みの入ったワンピース# ボトムが短パン\@
				ENDIF
				LOCALS:(2 + LOCAL%2) += @"<font color='#%LOCALS:1%'><button value='{70000 + 30 * 10 + LOCAL}' title='%LOCALS:4%'>[%泳装一覧:LOCAL%]</button></font>"
			ENDIF
		NEXT
		LOCALS += LOCALS:2 + "<br>" + LOCALS:3 + "<br>"
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3506,3675,2812'>"
		LOCALS += @"<button value='71000'>服装名：%ランダムキャラ服装名, 30, LEFT%</button>"
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
	CASE 105
		IF !DT_EXIST("DT_CHARA")
			DT_CREATE "DT_CHARA"
			DT_COLUMN_ADD "DT_CHARA", "NO", 2
			DT_COLUMN_ADD "DT_CHARA", "NAME"
			DT_COLUMN_ADD "DT_CHARA", "VALUE", 1
			FOR LOCAL, 1, キャラクタ数上限
				SIF !EXISTCSV(LOCAL)
					CONTINUE
				;ランダムキャラ排除
				SIF LOCAL == 999
					CONTINUE
				DT_ROW_ADD "DT_CHARA", "NO", LOCAL, "NAME", CSVNAME(LOCAL), "VALUE", 0
			NEXT
		ENDIF
		LOCALS += "■同行候选角色最多可设置100人■<br>"
		LOCAL:1 = DT_ROW_LENGTH("DT_CHARA")
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF LOCAL >= LOCAL:1
				BREAK
			LOCALS += @"<button value='{100000 + LOCAL * 10}'>[{DT_CELL_GET("DT_CHARA", LOCAL, "NO"), LOG10(キャラクタ数上限)+1, RIGHT}] %ADD_STR_SPACE(NAME表示省略(DT_CELL_GETS("DT_CHARA", LOCAL, "NAME")), "]"), 41, LEFT%：{DT_CELL_GET("DT_CHARA", LOCAL, "VALUE"), 3}％</button>"
			LOCALS += "<font color='#606060'>"
			FOR LOCAL:2, 0, VARSIZE("同行確率一覧")
				LOCALS += @"<button value='{100000 + LOCAL * 10 + LOCAL:2 + 1}'>[%同行確率一覧:(LOCAL:2)%]</button>"
			NEXT
			LOCALS += "</font><br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3406,3675,2962'>"
		IF ページ数 > 0
			LOCALS += "<button value='29994'>[|≪]</button>"
		ELSE
			LOCALS += "     "
		ENDIF
		IF ページ数 > 5
			LOCALS += "<button value='29996'> [－５]</button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		LOCALS += @"　　　　　　　{ページ数, 4, RIGHT}/{LOCAL:1 / 20, 4, RIGHT}　　　　　 　　"
		IF ページ数 < LOCAL:1 / 20 - 5
			LOCALS += "<button value='29997'>[＋５] </button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29995'>[≫|]</button>"
		ENDIF
		LOCALS += "<br>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF
	CASE 111
		IF !DT_EXIST("DT_CHARA_REL")
			DT_CREATE "DT_CHARA_REL"
			DT_COLUMN_ADD "DT_CHARA_REL", "NO", 2
			DT_COLUMN_ADD "DT_CHARA_REL", "NAME"
			DT_COLUMN_ADD "DT_CHARA_REL", "REL"
			FOR LOCAL, 1, キャラクタ数上限
				SIF !EXISTCSV(LOCAL)
					CONTINUE
				;ランダムキャラ排除
				SIF LOCAL == 999
					CONTINUE
				DT_ROW_ADD "DT_CHARA_REL", "NO", LOCAL, "NAME", CSVNAME(LOCAL), "REL", ""
			NEXT
		ENDIF
		LOCALS += "■爱慕角色仅限１人，炮友角色最多可设置９９人■<button value='199999'>[详情]</button><br>"
		LOCAL:1 = DT_ROW_LENGTH("DT_CHARA_REL")
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF LOCAL >= LOCAL:1
				BREAK
			LOCALS += @"<button value='{200000 + LOCAL * 10}'>[{DT_CELL_GET("DT_CHARA_REL", LOCAL, "NO"), LOG10(キャラクタ数上限)+1, RIGHT}] %ADD_STR_SPACE(NAME表示省略(DT_CELL_GETS("DT_CHARA_REL", LOCAL, "NAME")), "]"), 41, LEFT%</button>"
			IF DT_CELL_GETS("DT_CHARA_REL", LOCAL, "REL") == "恋慕"
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{200000 + LOCAL * 10}'>[爱慕]</button></font>"
			ELSEIF DT_SELECT("DT_CHARA_REL", "REL = '恋慕'")
				LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[爱慕]</font>"
			ELSE
				LOCALS += @"<button value='{200000 + LOCAL * 10}'>[爱慕]</button>"
			ENDIF
			IF DT_CELL_GETS("DT_CHARA_REL", LOCAL, "REL") == "セフレ"
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{200000 + LOCAL * 10 + 1}'>[炮友]</button></font>"
			ELSEIF DT_SELECT("DT_CHARA_REL", "REL = 'セフレ'") >= 99
				LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[炮友]</font>"
			ELSE
				LOCALS += @"<button value='{200000 + LOCAL * 10 + 1}'>[炮友]</button>"
			ENDIF
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3406,3675,2962'>"
		IF ページ数 > 0
			LOCALS += "<button value='29994'>[|≪]</button>"
		ELSE
			LOCALS += "     "
		ENDIF
		IF ページ数 > 5
			LOCALS += "<button value='29996'> [－５]</button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		LOCALS += @"　　　　　　　{ページ数, 4, RIGHT}/{LOCAL:1 / 20, 4, RIGHT}　　　　　 　　"
		IF ページ数 < LOCAL:1 / 20 - 5
			LOCALS += "<button value='29997'>[＋５] </button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29995'>[≫|]</button>"
		ENDIF
		LOCALS += "<br>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[上一页]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[返回状态界面]</button>　　　　"
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29999'>[下一页]</button>"
		ENDIF
ENDSELECT
LOCALS += "</div>"
IF CSVエディタフラグ && 表示モード < VARSIZE("表示切替") && 表示モード != 3
	LOCALS += "<div rect='2125,3350,3675,312'>"
	FOR LOCAL, 0, VARSIZE("表示切替")
		IF 表示モード == LOCAL
			LOCALS += @"<font color='#606060'>[{LOCAL}] %表示切替:LOCAL%显示</font>　"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %表示切替:LOCAL%显示</button>　"
		ENDIF
		SIF LOCAL % 3 == 2
			LOCALS += "<br>"
	NEXT
	LOCALS += "　　<button value='9'>[9] 更改项目名称颜色</button>"
	LOCALS += "</div>"
ELSE
	IF 表示モード == 0
		LOCALS += "<div rect='3512,3500,3500,312'><button value='1'>[1] 能力・经验显示</button>"
		LOCALS += "　<button value='9'>[9] 更改项目名称颜色</button></div>"
	ELSEIF 表示モード == 1
		LOCALS += "<div rect='3512,3500,3500,312'><button value='0'>[0] 信息・特质显示</button>"
		LOCALS += "　<button value='9'>[9] 更改项目名称颜色</button></div>"
	ENDIF

ENDIF


IF OPTION変数:汎用立ち絵制御
	LOCALS:1 = <div rect='2050,31,1200,1200' padding='6' border='31' bcolor='#C0C0C0'>
	IF 汎用画像キー == ""
		LOCALS:1 += "<button value='170'><img src='顔ノーイメ' height='1125'></button>"
	ELSE
		SPRITEDISPOSE @"短冊グラ3000"
		GDISPOSE 3000
		CALL GCREATE_拡張子(3000, @"汎用グラフィック/顔_%汎用画像キー%")
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF スプライト作成(@"短冊グラ3000", 3000, 0)
			LOCALS:1 += @"<button value='170'><img src='短冊グラ3000'height='1125'></button>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
	ENDIF
	LOCALS:1 += "</div>"
	HTML_PRINT LOCALS:1
	FOR LOCAL, 0, 10
		PRINTL
	NEXT
ENDIF

HTML_PRINT LOCALS
FOR LOCAL, 0, 32
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
;戦闘タイプ--------------------------------------------------------
	CASE 11000 TO 11005
		ランダムキャラ戦闘タイプ:0 = LOCAL - 11000
	CASE 11010 TO 11019
		INVERTBIT ランダムキャラ戦闘タイプ:1, LOCAL - 11010
	CASE 11020 TO 11027
		ランダムキャラ戦闘タイプ:2 = LOCAL - 11020
	CASE 11040 TO 11045
		LOCAL:1 = LOCAL - 11040
		PRINTFORM 请输入\@ LOCAL:1 / 3? 沦陷# 通常\@状态的第%TOFULL(TOSTR(1 + LOCAL:1 % 3))%阶段
		PRINTL 的奥义名称。
		PRINTL （若通常・沦陷状态的第１阶段奥义名称未设置，后续阶段的设置将被忽略）
		SETTEXTBOX ランダムキャラ奥義名:(LOCAL:1)
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ奥義名:(LOCAL:1) = %RESULTS%
	CASE 11100
		表示モード = 0
;肉体情報--------------------------------------------------------
	CASE 12001
		ランダムキャラ素質:0 = 1
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12002
		ランダムキャラ素質:0 = 2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 1
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12003
		ランダムキャラ素質:0 = 3
		ランダムキャラ素質:3 = 3
		肉体特徴数値保存:0 = 1
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		IF ランダムキャラ素質:1 != 2
			ランダムキャラ素質:31 = -1
		ELSEIF ランダムキャラ素質:1 == 2
			ランダムキャラ素質:31 = 2
		ENDIF
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12004
		ランダムキャラ素質:0 = 4
		ランダムキャラ素質:3 = -2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 1
		ランダムキャラ素質:31 = -2
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12005
		ランダムキャラ素質:0 = 5
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12006
		ランダムキャラ素質:0 = 6
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12007
		ランダムキャラ素質:1 = 1
		ランダムキャラ素質:32 = 0
		ランダムキャラ素質:2 = 0
		服装数値保存:30 = 0
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(ランダムキャラ素質:31, 0)
		ENDIF
	CASE 12008
		ランダムキャラ素質:1 = 2
		ランダムキャラ素質:3 = 0
		ランダムキャラ素質:2 = 0
		服装数値保存:30 = 0
		汎用画像キー = 
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MAX(ランダムキャラ素質:31, 1)
		ENDIF
	CASE 12009
		ランダムキャラ素質:1 = 3
		服装数値保存:30 = 0
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(ランダムキャラ素質:31, 0)
		ENDIF
	CASE 12010
		CLEARBIT ランダムキャラ素質:2, 0
	CASE 12011
		SETBIT ランダムキャラ素質:2, 0
	CASE 12012
		CLEARBIT ランダムキャラ素質:2, 1
	CASE 12013
		SETBIT ランダムキャラ素質:2, 1
	CASE 12014
		CLEARBIT Ａ処女保存, 1
	CASE 12015
		SETBIT Ａ処女保存, 1
	CASE 12018 TO 12023
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:3 = MAX(LOCAL - 12020, 2)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:3 = MIN(LOCAL - 12020, -1)
		ELSE
			ランダムキャラ素質:3 = LOCAL - 12020
		ENDIF
		バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12025
		DRAWLINE
		PRINTL 请输入身高（单位毫米）。
		SETTEXTBOX TOSTR(身長保存)
		INPUT
		身長保存 = RESULT
		ランダムキャラ素質:31 = EDIT用体格算出(ランダムキャラ素質:1, 身長保存)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12026
		DRAWLINE
		PRINTL 请输入胸围尺寸（单位毫米）。
		SETTEXTBOX TOSTR(バスト保存)
		INPUT
		バスト保存 = RESULT
		ランダムキャラ素質:3 = EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12028 TO 12033
		ランダムキャラ素質:6 = LOCAL - 12030
	CASE 12039 TO 12042
		ランダムキャラ素質:30 = LOCAL - 12040
	CASE 12047 TO 12052
		IF ランダムキャラ素質:0 == 3 && ランダムキャラ素質:1 != 2
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, 0)
		ELSEIF ランダムキャラ素質:0 == 3 && ランダムキャラ素質:1 == 2
			ランダムキャラ素質:31 = MAX(LOCAL - 12050, 1)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, -1)
		ELSE
			ランダムキャラ素質:31 = LOCAL - 12050
		ENDIF
		身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ウエスト保存 = EDIT用ウエスト算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存)
		ヒップ保存 = EDIT用ヒップ算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存, 下半身保存)
	CASE 12058 TO 12062
		ランダムキャラ素質:32 = LOCAL - 12060
	CASE 12070 TO 12073
		INVERTBIT 肉体特徴数値保存:(LOCAL - 12070), 0
	CASE 12077 TO 12083
		体型保存 = LOCAL - 12080
		ウエスト保存 = EDIT用ウエスト算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存)
	CASE 12089 TO 12091
		下半身保存 = LOCAL - 12090
		ヒップ保存 = EDIT用ヒップ算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存, 下半身保存)
	CASE 12096
		PRINTL 请输入种族名称。
		SETTEXTBOX ランダムキャラ特殊種族
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ特殊種族 = %SUBSTRING(RESULTS, 0, 24)%
	CASE 12097
		ランダムキャラ素質:33 = 0
		DRAWLINE
		PRINTL 请输入年龄。
		SETTEXTBOX TOSTR(年齢保存)
		INPUT
		年齢保存 = RESULT
		ランダムキャラ素質:33 = 年齢層算出処理(年齢保存)
	CASE 12098, 12139
		;年齢設定切り替え及びリロール
		;;年齢保存および年齢区分（ランダムキャラ素質:33）はここではリセットせず、CSV書き出し時に必要な方のみ保存するようにする
		SIF LOCAL == 12098
			INVERTBIT 年齢数値設定フラグ, 0
		IF 年齢数値設定フラグ
			IF INRANGE(ランダムキャラ素質:33, 年齢層_幼い, 年齢層_人外)
				年齢保存 = RAND(年齢ボーダー:(ランダムキャラ素質:33 - 1), MIN(年齢ボーダー:(ランダムキャラ素質:33), 年齢ボーダー:年齢層_老年 * (3 - !(ランダムキャラ素質:34))))
			ELSE
				年齢保存 = RAND(年齢ボーダー:年齢層_成人, 年齢ボーダー:年齢層_大人)
				ランダムキャラ素質:33 = 年齢層算出処理(年齢保存)
			ENDIF
		ELSEIF ランダムキャラ素質:33 != 年齢層_不詳
			ランダムキャラ素質:33 = 年齢層算出処理(年齢保存)
		ENDIF
	CASE 12099 TO 12117
		;年齢層：不詳～老年
		ランダムキャラ素質:33 = LOCAL - 12100
		IF INRANGE(ランダムキャラ素質:33, 年齢層_幼い, 年齢層_人外)
			年齢保存 = RAND(年齢ボーダー:(ランダムキャラ素質:33 - 1), MIN(年齢ボーダー:(ランダムキャラ素質:33), 年齢ボーダー:年齢層_老年 * (3 - !(ランダムキャラ素質:34))))
		ELSEIF ランダムキャラ素質:33 == 年齢層_不詳
			年齢保存 = 0
			CLEARBIT 年齢数値設定フラグ, 0
		ELSE
			年齢保存 = RAND(年齢ボーダー:年齢層_成人, 年齢ボーダー:年齢層_大人)
		ENDIF
	CASE 12120
		ランダムキャラ素質:34 = 0
	CASE 12130 TO 12137
		;外見年齢：幼い～老年
		ランダムキャラ素質:34 = LOCAL - 12120
	CASE 12140
		INVERTBIT OPTION変数:身体項目表示切り替え, 5
	CASE 12990
		DRAWLINE
		PRINTL 请输入腰围尺寸（单位毫米）。
		SETTEXTBOX TOSTR(ウエスト保存)
		INPUT
		ウエスト保存 = RESULT
	CASE 12991
		DRAWLINE
		PRINTL 请输入臀围尺寸（单位毫米）。
		SETTEXTBOX TOSTR(ヒップ保存)
		INPUT
		ヒップ保存 = RESULT
	CASE 12992
		INVERTBIT ウエスト数値設定フラグ, 0
	CASE 12993
		INVERTBIT ヒップ数値設定フラグ, 0
	CASE 12994
		ウエスト保存 = EDIT用ウエスト算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存)
	CASE 12995
		ヒップ保存 = EDIT用ヒップ算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存, 下半身保存)
	CASE 12996
		身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
		ランダムキャラ素質:3 = EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12997
		バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12998
		INVERTBIT 身長数値設定フラグ, 0
	CASE 12999
		INVERTBIT バスト数値設定フラグ, 0
;タレント--------------------------------------------------------
	CASE 20000 TO 29993
		TALENT数値保存:((LOCAL - 20000) / 10) = LOCAL % 10
	CASE 29994
		ページ数 = 0
	CASE 29995
		ページ数 = LOCAL:1 / 20
	CASE 29996
		ページ数 -= 5
	CASE 29997
		ページ数 += 5
	CASE 29998
		ページ数 -= 1
	CASE 29999
		ページ数 += 1
;ABL--------------------------------------------------------
	CASE 30000 TO 39997
		ABL数値保存:((LOCAL - 30000) / 10) = LOCAL % 10
;EXP--------------------------------------------------------
	CASE 40000 TO 49997
		RESULT = 0
		PRINTL 请输入经验值。（最多３位数）
		SETTEXTBOX TOSTR(EXP数値保存:((LOCAL - 40000) / 10))
		INPUT
		EXP数値保存:((LOCAL - 40000) / 10) = MIN(RESULT, 999)
;フレーバー素質----------------------------------------------------
	CASE 50000 TO 50059
		フレーバー素質数値保存:((LOCAL - 50000) / 10) = LOCAL % 10
	CASE 50060 TO 50069
		INVERTBIT フレーバー素質数値保存:((LOCAL - 50000) / 10), LOCAL % 10
;知識素質----------------------------------------------------
	CASE 60000 TO 60209
		知識素質数値保存:((LOCAL - 60000) / 10) = LOCAL % 10
	CASE 69999
		PRINTL 知识素质等级默认基于「空之世界知识」设定。
		PRINTL 异世界出身的角色建议适当降低等级。
		PRINTL
		PRINTL ・等级会大幅下降的知识
		PRINTL 魔物知识、历史知识
		PRINTL
		PRINTL ・等级容易下降的知识
		PRINTL 调合知识、机械知识、炼金知识
		PRINTL
		PRINTL ・等级基本不变的知识
		PRINTL 绘画、垂钓精通、舞者、歌唱技能、厨师、游泳技能
		PRINTL
		PRINTL 视情况而定的知识
		PRINTL 魔法知识：原则上需要降低等级，
		PRINTL 　　　　　但考虑到碧蓝幻想的魔法体系兼容性广泛，且联动角色通常保留特殊能力，
		PRINTL 　　　　　若魔法原理具有通用性则可维持原等级。
		PRINTL
		PRINTL 高贵　　：原则上不降低等级，
		PRINTL 　　　　　但若原生世界与碧蓝幻想世界观文化差异过大时，适当降低更为合理。
		PRINTL
		PRINTL 经营知识：原则上不降低等级，
		PRINTL 　　　　　但需注意碧蓝幻想世界观受限于通信和运输手段，部分现代商业知识可能不适用。
		PRINTL 　　　　　奇幻世界出身的角色可保留部分适用性。
		PRINTL
		PRINTL 农业知识：即使在现实世界中，不同地区的农业知识也往往大相径庭。
		PRINTL 　　　　　气候相似可维持原等级，气候差异大则应降低等级。
		PRINTL
		PRINTW （以上内容可在各知识项的悬浮提示中查看）
;服装----------------------------------------------------
	CASE 70010 TO 70221
		服装数値保存:((LOCAL - 70000) / 10) = LOCAL % 10
		SIF MAX(服装数値保存:3, 服装数値保存:4, 服装数値保存:5, 服装数値保存:6, 服装数値保存:7, 服装数値保存:9) > 0
			服装数値保存:2 = 1
		SIF MAX(服装数値保存:14, 服装数値保存:15, 服装数値保存:18) > 0
			服装数値保存:13 = 1
		IF 服装数値保存:19 > 0
			服装数値保存:2 = 1
			服装数値保存:13 = 1
		ENDIF
		SIF 服装数値保存:21 > 0
			服装数値保存:8 = 1
		SIF 服装数値保存:22 > 0
			服装数値保存:16 = 1
		IF 服装数値保存:20 > 0
			服装数値保存:8 = 1
			服装数値保存:16 = 1
		ENDIF
	CASE 70300 TO 70303
		服装数値保存:30 = LOCAL % 10
	CASE 71000
		PRINTL 请输入服装名称。
		SETTEXTBOX ランダムキャラ服装名
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ服装名 = %RESULTS%
;性癖素質----------------------------------------------------
	CASE 80000 TO 80209
		性癖素質数値保存:((LOCAL - 80000) / 10) = LOCAL % 10
;固有素質----------------------------------------------------
	CASE 85000 TO 85010
		PRINTL 请输入专属特质名称。
		SETTEXTBOX 固有素質一時保存:(LOCAL - 85000)
		CALL 入力処理_HTML記法警告付き()
		固有素質一時保存:(LOCAL - 85000) = %RESULTS%
;同行者候補----------------------------------------------------
	CASE 100000 TO 100000 + キャラクタ数上限 * 10
		LOCAL:2 = DT_CELL_GET("DT_CHARA", (LOCAL - 100000) / 10, "VALUE")
		IF LOCAL % 10 == 0
			PRINTL 请输入同行概率百分比。（０～１００）
			SETTEXTBOX TOSTR(LOCAL:2)
			INPUT
			DT_CELL_SET "DT_CHARA", (LOCAL - 100000) / 10, "VALUE", LIMIT(RESULT, 0, 100)
		ELSE
			LOCAL:2 += TOINT(同行確率一覧:(LOCAL % 10 - 1))
			DT_CELL_SET "DT_CHARA", (LOCAL - 100000) / 10, "VALUE", LIMIT(LOCAL:2, 0, 100)
		ENDIF
	CASE 199999
		PRINTL ■ 默认自动设定８０％同行率，若已手动设定则以手动值为准
		PRINTL ■ 多个CSV文件设定冲突时，编号较小的文件优先
		PRINTL 　（例如角色A和C的CSV都设定与角色B的恋爱关系时，以A的设定优先）
		PRINTL ■ 因此设定CP关系时只需在一方角色文件中设置即可
		WAIT
	CASE 200000 TO 200000 + キャラクタ数上限 * 10 + 1
		SELECTCASE LOCAL % 10
			CASE 0
				;恋慕
				IF DT_CELL_GETS("DT_CHARA_REL", (LOCAL - 200000) / 10, "REL") == "恋慕"
					DT_CELL_SET "DT_CHARA_REL", (LOCAL - 200000) / 10, "REL", ""
				ELSE
					DT_CELL_SET "DT_CHARA_REL", (LOCAL - 200000) / 10, "REL", "恋慕"
				ENDIF
			CASE 1
				;セフレ
				IF DT_CELL_GETS("DT_CHARA_REL", (LOCAL - 200000) / 10, "REL") == "セフレ"
					DT_CELL_SET "DT_CHARA_REL", (LOCAL - 200000) / 10, "REL", ""
				ELSE
					DT_CELL_SET "DT_CHARA_REL", (LOCAL - 200000) / 10, "REL", "セフレ"
				ENDIF
		ENDSELECT
;メイン画面--------------------------------------------------------
	CASE 99
		表示モード = 0
		INVERTBIT 表示モード:1, 0
	CASE 107
		LOCAL:2 = 0
		LOCAL:3 = 0
		FOR LOCAL:1, 0, 999
			SIF EXISTCSV(LOCAL:1)
				LOCAL:2 = LOCAL:1
		NEXT
		FOR LOCAL:1, 1001, キャラクタ数上限
			SIF EXISTCSV(LOCAL:1)
				LOCAL:3 = LOCAL:1
		NEXT
		PRINTFORML 请输入要使用的CSV编号（当前：{登録番号, 4, LEFT}／上限：{キャラクタ数上限-1}）
		PRINTFORML 碧蓝幻想角色可能已占用{LOCAL:2}号前，Ex角色可能已占用{LOCAL:3}号前。
		$INPUT_NUM
		SETTEXTBOX TOSTR(登録番号)
		INPUTS
		LOCAL:4 = TOINT(RESULTS)
		IF LOCAL:4 == 登録番号
		ELSEIF !INRANGE(LOCAL:4, 0, キャラクタ数上限-1)
			PRINTW 输入无效。
			CLEARLINE 2
			GOTO INPUT_NUM
		ELSEIF EXISTCSV(LOCAL:4)
			PRINTW 存在相同编号的CSV文件。
			CLEARLINE 2
			GOTO INPUT_NUM
		ENDIF
		登録番号 = LOCAL:4
	CASE 100
		$INPUT_NAME
		PRINTFORML 请输入名称。（当前：%ランダムキャラネーム%）
		PRINTL ┗[1] 从命名库随机选择　[2] 完全随机生成　[0] 使用当前名称
		SETTEXTBOX ランダムキャラネーム
		CALL 入力処理_HTML記法警告付き()
		IF RESULTS == "1" || RESULTS == "2"
			ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0, ランダムキャラ素質:1)%
			SIF RESULTS == "2"
				ランダムキャラネーム = %ランダムキャラ名ジェネレータ(ランダムキャラ素質:0, ランダムキャラ素質:1)%
			CLEARLINE 3
			GOTO INPUT_NAME
		ELSEIF RESULTS == "0"
		ELSE
			IF STRFIND(RESULTS, " ") >= 0 || STRFIND(RESULTS, "_") >= 0
				PRINTL 因系统限制，名称不可包含半角空格和「_」。
				PRINTW 请使用「-」替代或去除空格。
				GOTO INPUT_NAME
			ELSE
				ランダムキャラネーム = %RESULTS%
			ENDIF
		ENDIF
	CASE 101
		$二つ名ループ
		PRINTL 请输入称号。
		SETTEXTBOX ランダムキャラ二つ名
		CALL 入力処理_HTML記法警告付き()
		IF STRFIND(RESULTS, " ") >= 0 || STRFIND(RESULTS, "_") >= 0
			PRINTL 因系统限制，称号不可包含半角空格和「_」。
			PRINTW 请使用「-」替代。
			GOTO 二つ名ループ
		ELSE
			ランダムキャラ二つ名 = %RESULTS%
		ENDIF
	CASE 102
		PRINTL 请输入昵称。
		SETTEXTBOX ランダムキャラCALLネーム
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラCALLネーム = %RESULTS%
	CASE 103
		PRINTL 请输入作品出处。
		SETTEXTBOX ランダムキャラ出演作品
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ出演作品 = %RESULTS%
	CASE 104
		$好物_LOOP
		DRAWLINE
		PRINTL 请设定礼物偏好属性。
		DRAWLINE
		FOR LOCAL:1, 0, VARSIZE("好物一覧")
			IF GROUPMATCH(好物一覧:(LOCAL:1), "飲食系", "芸術品", "美容系", "実用品")
				PRINTL
				PRINTL 
			ENDIF
			IF 好物一覧:(LOCAL:1) == "娯楽"
				PRINTL 
				PRINTL 
				PRINT ・独立子分类　　　
			ENDIF
			SIF STRCOUNT(好物一時保存, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("黄")
			SIF STRCOUNT(好物一時保存, "飲食系") && MATCH(好物一覧_飲食系, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("グレーアウト")
			SIF STRCOUNT(好物一時保存, "芸術品") && MATCH(好物一覧_芸術品, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("グレーアウト")
			SIF STRCOUNT(好物一時保存, "美容系") && MATCH(好物一覧_美容系, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("グレーアウト")
			SIF STRCOUNT(好物一時保存, "実用品") && MATCH(好物一覧_実用品, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("グレーアウト")
			IF 好物一覧:(LOCAL:1) == "飲食系"
				FOR ループ用, 0, VARSIZE("好物一覧_飲食系")
					IF STRCOUNT(好物一時保存, 好物一覧_飲食系:ループ用)
						SETCOLOR カラーパレット("グレーアウト")
						BREAK
					ENDIF
				NEXT
			ENDIF
			IF 好物一覧:(LOCAL:1) == "芸術品"
				FOR ループ用, 0, VARSIZE("好物一覧_芸術品")
					IF STRCOUNT(好物一時保存, 好物一覧_芸術品:ループ用)
						SETCOLOR カラーパレット("グレーアウト")
						BREAK
					ENDIF
				NEXT
			ENDIF
			IF 好物一覧:(LOCAL:1) == "美容系"
				FOR ループ用, 0, VARSIZE("好物一覧_美容系")
					IF STRCOUNT(好物一時保存, 好物一覧_美容系:ループ用)
						SETCOLOR カラーパレット("グレーアウト")
						BREAK
					ENDIF
				NEXT
			ENDIF
			IF 好物一覧:(LOCAL:1) == "実用品"
				FOR ループ用, 0, VARSIZE("好物一覧_実用品")
					IF STRCOUNT(好物一時保存, 好物一覧_実用品:ループ用)
						SETCOLOR カラーパレット("グレーアウト")
						BREAK
					ENDIF
				NEXT
			ENDIF
			PRINTBUTTON @"%@"[{LOCAL:1, 2}] %好物一覧:(LOCAL:1), 10, LEFT%", 16, LEFT%", LOCAL:1
			PRINT 　
			SIF GROUPMATCH(好物一覧:(LOCAL:1), "飲食系", "芸術品", "美容系", "実用品")
				PRINT ＞　
			RESETCOLOR
		NEXT
		PRINTL
		PRINTL 
		PRINTBUTTON "[999] 返回", 999
		PRINTL
		BINPUT
		SIF RESULT == 999
			GOTO 従業員_LOOP
		IF STRCOUNT(好物一時保存, 好物一覧:RESULT)
			REPLACE 好物一時保存, @"%好物一覧:RESULT%_", ""
			好物一時保存 = %RESULTS%
		ELSE
			好物一時保存 += @"%好物一覧:RESULT%_"
			SELECTCASE 好物一覧:RESULT
				CASE "肉","魚","野菜","お菓子","珍味","酒","茶類"
					IF STRCOUNT(好物一時保存, "飲食系")
						;小カテゴリ選んだら大カテゴリ削除
						REPLACE 好物一時保存, @"飲食系_", ""
						好物一時保存 = %RESULTS%
					ENDIF
				CASE "宝石","絵画","彫刻","詩"
					IF STRCOUNT(好物一時保存, "芸術品")
						;小カテゴリ選んだら大カテゴリ削除
						REPLACE 好物一時保存, @"芸術品_", ""
						好物一時保存 = %RESULTS%
					ENDIF
				CASE "アクセサリ","化粧品"
					IF STRCOUNT(好物一時保存, "美容系")
						;小カテゴリ選んだら大カテゴリ削除
						REPLACE 好物一時保存, @"美容系_", ""
						好物一時保存 = %RESULTS%
					ENDIF
				CASE "文房具","健康器具","日用品","調理器具","工具"
					IF STRCOUNT(好物一時保存, "実用品")
						;小カテゴリ選んだら大カテゴリ削除
						REPLACE 好物一時保存, @"実用品_", ""
						好物一時保存 = %RESULTS%
					ENDIF
				CASE "飲食系"
					;大カテゴリ選んだら小カテゴリ削除
					FOR LOCAL, 1, 8
						IF STRCOUNT(好物一時保存, 好物一覧:LOCAL)
							;小カテゴリ選んだら大カテゴリ削除
							REPLACE 好物一時保存, @"%好物一覧:LOCAL%_", ""
							好物一時保存 = %RESULTS%
						ENDIF
					NEXT
				CASE "芸術品"
					;大カテゴリ選んだら小カテゴリ削除
					FOR LOCAL, 9, 13
						IF STRCOUNT(好物一時保存, 好物一覧:LOCAL)
							;小カテゴリ選んだら大カテゴリ削除
							REPLACE 好物一時保存, @"%好物一覧:LOCAL%_", ""
							好物一時保存 = %RESULTS%
						ENDIF
					NEXT
				CASE "美容系"
					;大カテゴリ選んだら小カテゴリ削除
					FOR LOCAL, 14, 16
						IF STRCOUNT(好物一時保存, 好物一覧:LOCAL)
							;小カテゴリ選んだら大カテゴリ削除
							REPLACE 好物一時保存, @"%好物一覧:LOCAL%_", ""
							好物一時保存 = %RESULTS%
						ENDIF
					NEXT
				CASE "実用品"
					;大カテゴリ選んだら小カテゴリ削除
					FOR LOCAL, 17, 22
						IF STRCOUNT(好物一時保存, 好物一覧:LOCAL)
							;小カテゴリ選んだら大カテゴリ削除
							REPLACE 好物一時保存, @"%好物一覧:LOCAL%_", ""
							好物一時保存 = %RESULTS%
						ENDIF
					NEXT
			ENDSELECT
		ENDIF
		GOTO 好物_LOOP
	CASE 106
		PRINTL 设置游戏初始状态下不可被邀请的角色
		PRINTL 
		PRINTL 招待不可フラグ=0　初始即可邀请
		PRINTL 
		PRINTL 招待不可フラグ=1  出现在ZP商店「添加特殊角色至候选名单」中。
		PRINTL 此时「ZPショップ値段」数值即为售价，若未设定（值为0），则默认为50ZP
		PRINTL 需根据角色背景与フラグ３区分使用
		PRINTL 
		PRINTL 招待不可フラグ=2  不进入ZP商店且不可被邀请
		PRINTL 适用于需通过特殊事件解锁的角色
		PRINTL 
		PRINTL 招待不可フラグ=3  出现在ZP商店「添加特殊角色至候选名单」中。
		PRINTL 但未通关事件［连接「平行世界」的大门］时不会显示。
		PRINTL （通常用于非碧蓝幻想原作角色）
		PRINTL 此时「ZPショップ値段」数值即为售价，若未设定（值为0），则默认为50ZP
		PRINTL 需根据角色背景与フラグ１区分使用
		DRAWLINE
		PRINTFORML 当前禁止邀请标志＝{ランダムキャラ招待不可フラグ}
		FOR LOCAL, 0, 4
			SIF LOCAL == ランダムキャラ招待不可フラグ
				CONTINUE
			PRINTBUTTON @"[{LOCAL}] 将禁止邀请标志设为{LOCAL}", LOCAL
			PRINTL
		NEXT
		PRINTL
		PRINTBUTTON "[999] 返回", 999
		PRINTL
		BINPUT
		SELECTCASE RESULT
			CASE 999
				GOTO 従業員_LOOP
			CASEELSE
				ランダムキャラ招待不可フラグ = RESULT
		ENDSELECT
	CASE 108
		PRINTL 请输入第一人称（角色自称）。
		SETTEXTBOX ランダムキャラ人称
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ人称 = %RESULTS%
	CASE 109
		PRINTL 请输入第二人称（对「你」的称呼）。
		SETTEXTBOX ランダムキャラ人称:1
		CALL 入力処理_HTML記法警告付き()
		ランダムキャラ人称:1 = %RESULTS%
	CASE 110
		表示モード = 110
	CASE 120
		表示モード = 120
	CASE 130
		ページ数 = 0
		表示モード = 130
	CASE 131
		IF !CSVエディタフラグ
			ページ数 = 0
			表示モード = 131
		ELSE
			ページ数 = 0
			表示モード = 132
			INVERTBIT 表示モード:1, 1
		ENDIF
	CASE 132
		ページ数 = 0
		表示モード = 132
	CASE 133
		ページ数 = 0
		表示モード = 133
	CASE 134
		ページ数 = 0
		表示モード = 134
	CASE 140
		ページ数 = 0
		表示モード = 140
	CASE 150
		ページ数 = 0
		表示モード = 150
	CASE 160
		ページ数 = 0
		表示モード = 160
	CASE 105
		ページ数 = 0
		表示モード = 105
	CASE 111
		ページ数 = 0
		表示モード = 111
	CASE 170
		CALL 汎用立ち絵選択処理(汎用画像キー)
	CASE 171
		INVERTBIT OPTION変数:汎用立ち絵制御, 0
	CASE 172
		INVERTBIT OPTION変数:ワーウルフ画像ケモ度, 0
	CASE 200
		IF ランダムキャラネーム == ""
			ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0, ランダムキャラ素質:1)%
		ENDIF

		IF CSVエディタフラグ
			; 作成するCSVファイルの内容表示
			CLEARLINE MAX(LINECOUNT, 1000)
			TWAIT 100, 1
			PRINTFORML 番号,{登録番号}
			PRINTFORML 名前,[%ランダムキャラ二つ名%]%ランダムキャラネーム%
			IF ランダムキャラCALLネーム != ""
				PRINTFORML 呼び名,%ランダムキャラCALLネーム%
			ELSE
				PRINTFORML 呼び名,%ランダムキャラネーム%
			ENDIF
			PRINTL 基礎,体力,1200
			PRINTL 基礎,勃起,1500
			PRINTL 基礎,ムード,1500
			PRINTL 基礎,理性,1000
			PRINTL 基礎,怒り,1000
			HTML_PRINT "<br>"
			PRINTFORML CSTR,一人称,%ランダムキャラ人称:0%
			PRINTFORML CSTR,二人称,%ランダムキャラ人称:1%
			IF ランダムキャラ出演作品 != ""
				PRINTL ;-------------------------------------------------------------------------------------------
				PRINTL ;初始不可邀请角色设置
				PRINTL ;设定游戏初始状态下不可被邀请的角色
				PRINTL ;招待不可フラグ=1  出现在ZP商店「添加特殊角色至候选名单」中。
				PRINTL 		;此时「ZPショップ値段」数值即为售价，若未设定（值为0），则默认为50ZP
				PRINTL 		;需根据角色背景与フラグ３区分使用
				HTML_PRINT "<br>"
				PRINTL ;招待不可フラグ=2  不进入ZP商店且不可被邀请
				PRINTL 		;适用于需通过特殊事件解锁的角色
				HTML_PRINT "<br>"
				PRINTL ;招待不可フラグ=3  出现在ZP商店「添加特殊角色至候选名单」中。
				PRINTL 		;但未通关事件［连接「平行世界」的大门］时不会显示。
				PRINTL 		;此时「ZPショップ値段」数值即为售价，若未设定（值为0），则默认为50ZP
				PRINTL 		;需根据角色背景与フラグ１区分使用
				PRINTL ;-------------------------------------------------------------------------------------------
				PRINTFORML CSTR,出演作品,%ランダムキャラ出演作品%
			ENDIF
			IF ランダムキャラ招待不可フラグ
				PRINTFORML フラグ,招待不可フラグ,{ランダムキャラ招待不可フラグ}
			ENDIF
			HTML_PRINT "<br>"
			SIF 年齢数値設定フラグ
				PRINTFORML 基礎,年齢,{年齢保存}
			SIF 身長数値設定フラグ
				PRINTFORML 基礎,身長,{身長保存}
			IF GETBIT(ランダムキャラ素質:1, 0)
				SIF バスト数値設定フラグ
					PRINTFORML 基礎,バスト,{バスト保存}
				SIF ウエスト数値設定フラグ
					PRINTFORML 基礎,ウエスト,{ウエスト保存}
				SIF ヒップ数値設定フラグ
					PRINTFORML 基礎,ヒップ,{ヒップ保存}
			ENDIF
			HTML_PRINT "<br>"
			PRINTL {
			PRINTL CSTR,知識素質設定,
			PRINTFORML 性知識Lv{ランダムキャラ素質:6}
			FOR LOCAL, 1, 20
				IF ERDNAME(知識素質, LOCAL) != ""
					PRINTFORML %ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			HTML_PRINT "<br>"
			PRINTL {
			PRINTL CSTR,性癖素質設定,
			FOR LOCAL, 1, 20
				IF ERDNAME(性癖素質, LOCAL) != ""
					PRINTFORML %ERDNAME(性癖素質, LOCAL)%Lv{性癖素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			HTML_PRINT "<br>"
			PRINTFORML 素質,種族,{ランダムキャラ素質:0}
			; 特殊種族は種族が[その他]の場合のみ有効
			SIF ランダムキャラ素質:0 == 6 && ランダムキャラ特殊種族 != ""
				PRINTFORML CSTR,特殊種族,%ランダムキャラ特殊種族%
			PRINTFORML 素質,性別,{ランダムキャラ素質:1}
			IF GETBIT(ランダムキャラ素質:2, 0)
				PRINTL 素質,処女,0
			ELSE
				PRINTL 素質,処女,1
			ENDIF
			IF GETBIT(ランダムキャラ素質:2, 1)
				PRINTL 素質,非童貞,1
			ELSE
				PRINTL 素質,非童貞,0
			ENDIF
			IF Ａ処女保存
				PRINTL 素質,Ａ処女,0
			ELSE
				PRINTL 素質,Ａ処女,1
			ENDIF
			SIF GETBIT(ランダムキャラ素質:1, 0) && !バスト数値設定フラグ
				PRINTFORML 素質,バストサイズ,{ランダムキャラ素質:3}
			SIF 肉体特徴数値保存:0
				PRINTFORML 素質,角あり,1
			SIF 肉体特徴数値保存:1
				PRINTFORML 素質,尻尾あり,1
			SIF 肉体特徴数値保存:2
				PRINTFORML 素質,エルーン耳,1
			SIF 肉体特徴数値保存:3
				PRINTFORML 素質,ハーヴィン耳,1
			FOR LOCAL, 0, 300
				SIF TALENT数値保存:LOCAL == 0
					CONTINUE
				SIF TALENT数値保存:LOCAL == 5
					CONTINUE
				PRINTFORML 素質,%TALENTNAME:(TALENT一時保存:LOCAL)%,{TALENT数値保存:LOCAL - 5}
			NEXT
			FOR LOCAL, 0, 10
				SIF 固有素質一時保存:LOCAL == ""
					CONTINUE
				PRINTFORML CSTR,固有素質{LOCAL},%固有素質一時保存:LOCAL%
			NEXT
			SIF !(年齢数値設定フラグ && 年齢保存 > 0)
				PRINTFORML 素質,年齢層,{ランダムキャラ素質:33}
			SIF ランダムキャラ素質:34
				PRINTFORML 素質,外見年齢,{ランダムキャラ素質:34}
			SIF !身長数値設定フラグ
				PRINTFORML 素質,体格,{ランダムキャラ素質:31}
			SIF !ウエスト数値設定フラグ
				PRINTFORML 素質,体型,{体型保存}
			SIF !ウエスト数値設定フラグ || !ヒップ数値設定フラグ
				PRINTFORML 素質,下半身,{下半身保存}
			SIF GETBIT(ランダムキャラ素質:1, 1)
				PRINTFORML 素質,男性器サイズ,{ランダムキャラ素質:32}
			HTML_PRINT "<br>"

			FOR LOCAL, 0, 50
				IF ABL数値保存:LOCAL > 0
					PRINTFORML 能力,%ABLNAME:(ABL一時保存:LOCAL)%,{ABL数値保存:LOCAL}
				ENDIF
			NEXT
			HTML_PRINT "<br>"
			FOR LOCAL, 0, 100
				IF EXP数値保存:LOCAL > 0
					PRINTFORML 経験,%EXPNAME:(EXP一時保存:LOCAL)%,{EXP数値保存:LOCAL}
				ENDIF
			NEXT
			HTML_PRINT "<br>"

			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;礼物偏好
			PRINTL ;可设定主类别或对应子类别。
			PRINTL ;若设定主类别为偏好，其下所有子类别均会被视为喜好，请谨慎操作。
			PRINTL ;礼物可属于多个类别，只要匹配任意一个类别即视为喜好。
			PRINTL ;若礼物属于多个子类别，主类别偏好者仅会匹配第一个符合条件的子类别
			PRINTL ;
			PRINTL ;主类别：饮食类
			PRINTL ;	子类别：肉类、鱼类、蔬菜类、甜点、珍奇美味、酒类、茶类
			PRINTL ;主类别：艺术品
			PRINTL ;	子类别：宝石、绘画、雕塑、诗歌
			PRINTL ;主类别：美容类
			PRINTL ;	子类别：饰品、化妆品
			PRINTL ;主类别：实用品
			PRINTL ;	子类别：文具、健身器材、日用品、厨具、工具
			PRINTL ;独立子类别（不属于任何主类别）
			PRINTL ;	子类别：娱乐、花卉、书籍、玩具、机械
			PRINTL ;
			PRINTL ;设定多个偏好时请用下划线连接，例如「饮食类_娱乐」
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTFORML CSTR,プレゼント好み,%SUBSTRINGU(好物一時保存, 0, STRLENSU(好物一時保存) - 1)%
			HTML_PRINT "<br>"
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;初始角色关系设定
			PRINTL ;需以「角色全名_关系类型」格式填写，全名需包含称号（避免重复）
			PRINTL ;当前关系类型仅支持「恋慕」「セフレ」两种，末尾必须添加%作为分隔符
			PRINTL ;与设定对象的同行率将自动设为80％
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL {
			PRINTL CSTR,初期関係性,
			FOR LOCAL, 0, キャラクタ数上限
				IF DT_CELL_GETS("DT_CHARA_REL", LOCAL, "REL") != ""
					PRINTFORM %DT_CELL_GETS("DT_CHARA_REL", LOCAL, "NAME")%_
					PRINTFORM %DT_CELL_GETS("DT_CHARA_REL", LOCAL, "REL")%
					PRINTL %
				ENDIF
			NEXT
			PRINTL }
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;同行角色设定
			PRINTL ;需以「角色全名_同行概率%」格式填写，全名需包含称号（避免重复）
			PRINTL ;若与初始关系设定冲突，同行率以本设定优先
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL {
			PRINTL CSTR,同行キャラ,
			FOR LOCAL, 0, キャラクタ数上限
				IF DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
					PRINTFORM %DT_CELL_GETS("DT_CHARA", LOCAL, "NAME")%_
					PRINTFORM {DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")}
					PRINTL %
				ENDIF
			NEXT
			PRINTL }
			HTML_PRINT "<br>"
			
			PRINTFORML CSTR,服名称,%ランダムキャラ服装名%
			FOR LOCAL, 1, 23
				PRINTFORML 装着物,%EQUIPNAME:LOCAL%,{服装数値保存:LOCAL}
				SIF EQUIPNAME_TIPS(LOCAL) != ""
					PRINTFORML ;%EQUIPNAME_TIPS(LOCAL)%
			NEXT
			
			IF 服装数値保存:30
				PRINTFORML ;泳装は%泳装一覧:(服装数値保存:30)%
				PRINTFORML フラグ,泳装,{服装数値保存:30}
			ENDIF
			HTML_PRINT "<br>"

			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;战斗状态配置
			PRINTL ;基础值和成长类型需从预设模板中选择。
			PRINTL ;如需精细调整，请通过「角色名_参数设定」中的「@口上側初期パラメータ変更_XXXX」功能修改
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL {
			PRINTFORML CSTR,戦闘基礎ステータス設定,
			PRINTFORML 属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%
			SIF ランダムキャラ戦闘タイプ:1 == 0
				ランダムキャラ戦闘タイプ:1 = 1
			PRINTFORML 得意武器_%得意武器表示文字列(ランダムキャラ戦闘タイプ:1)%
			PRINTFORML 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%
			PRINTL }
			HTML_PRINT "<br>"
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;显示奥义名称
			PRINTL ;设置后可覆盖通用奥义显示名称，无需在剧情脚本中单独设定
			PRINTL ;若在「沦陷奥义设定」中配置，可随攻略进度变更显示名称
			PRINTL ; 使用下划线_分隔可定义+/++等级时的名称。
			PRINTL ;不可仅设定升级后名称。
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINT CSTR,表示奥義設定,
			IF ランダムキャラ奥義名:0 != ""
				PRINTFORM %ランダムキャラ奥義名:0%
				SIF ランダムキャラ奥義名:1 != "" || ランダムキャラ奥義名:2 != ""
					PRINTFORM _%ランダムキャラ奥義名:1%_%ランダムキャラ奥義名:2%
				HTML_PRINT "<br>"
			ELSE
				HTML_PRINT "<br>"
			ENDIF
			PRINT CSTR,陥落奥義設定,
			IF ランダムキャラ奥義名:3 != ""
				PRINTFORM %ランダムキャラ奥義名:3%
				SIF ランダムキャラ奥義名:4 != "" || ランダムキャラ奥義名:5 != ""
					PRINTFORM _%ランダムキャラ奥義名:4%_%ランダムキャラ奥義名:5%
				HTML_PRINT "<br>"
			ELSE
				HTML_PRINT "<br>"
			ENDIF
			HTML_PRINT "<br>"
			OUTPUTLOG @"CSV_EDITFILE/Chara{登録番号} %ランダムキャラネーム%.csv", 1
			PRINTW CSV文件已输出至CSV_EDITFILE文件夹，请移至CSV/角色数据文件夹后重启程序
			DT_RELEASE "DT_CHARA"
			RETURN 0
		ENDIF
		LOCAL:5 = 0
		FOR LOCAL, 0, 従業員部屋数
			IF 従業員部屋割り配列:LOCAL < 1
				LOCAL:5 = 1
				BREAK
			ENDIF
		NEXT
		IF LOCAL:5 == 0
			PRINTW 员工宿舍已满员。
			RETURN 0
		ENDIF
		IF 必要ルピ > MONEY
			PRINTW 卢比不足。
			RETURN 0
		ENDIF
		MONEY -= 必要ルピ
		PRINTFORMW [员工]%ランダムキャラネーム%已被雇用。
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [员工]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = \@ランダムキャラCALLネーム != "" ? %ランダムキャラCALLネーム% # %ランダムキャラネーム%\@
		TALENT:(LOCAL:1):種族 = ランダムキャラ素質:0
		TALENT:(LOCAL:1):性別 = ランダムキャラ素質:1
		
		IF Ａ処女保存
			TALENT:(LOCAL:1):Ａ処女 = 0
		ELSE
			TALENT:(LOCAL:1):Ａ処女 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 0)
			TALENT:(LOCAL:1):処女 = 0
		ELSE
			TALENT:(LOCAL:1):処女 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 1)
			TALENT:(LOCAL:1):非童貞 = 1
		ELSE
			TALENT:(LOCAL:1):非童貞 = 0
		ENDIF
		IF GETBIT(ランダムキャラ素質:1, 0) && !バスト数値設定フラグ
			TALENT:(LOCAL:1):バストサイズ = ランダムキャラ素質:3
		ELSE
			TALENT:(LOCAL:1):バストサイズ = 0
		ENDIF
		
		SIF 汎用画像キー == ""
			汎用画像キー '= 汎用立ち絵取得処理(ランダムキャラネーム, ランダムキャラ素質:0, ランダムキャラ素質:3, ランダムキャラ素質:31)
		CSTR:(LOCAL:1):汎用立ち絵登録文字列 = %汎用画像キー%

		BASE:(LOCAL:1):年齢 = 0
		MAXBASE:(LOCAL:1):年齢 = 0
		BASE:(LOCAL:1):身長 = 0
		MAXBASE:(LOCAL:1):身長 = 0
		BASE:(LOCAL:1):バスト = 0
		MAXBASE:(LOCAL:1):バスト = 0
		BASE:(LOCAL:1):ウエスト = 0
		MAXBASE:(LOCAL:1):ウエスト = 0
		BASE:(LOCAL:1):ヒップ = 0
		MAXBASE:(LOCAL:1):ヒップ = 0
		IF 年齢数値設定フラグ
			BASE:(LOCAL:1):年齢 = 年齢保存
			MAXBASE:(LOCAL:1):年齢 = 年齢保存
		ENDIF
		IF 身長数値設定フラグ
			BASE:(LOCAL:1):身長 = 身長保存
			MAXBASE:(LOCAL:1):身長 = 身長保存
		ENDIF
		IF GETBIT(ランダムキャラ素質:1, 0)
			IF バスト数値設定フラグ
				BASE:(LOCAL:1):バスト = バスト保存
				MAXBASE:(LOCAL:1):バスト = バスト保存
			ENDIF
			IF ウエスト数値設定フラグ
				BASE:(LOCAL:1):ウエスト = ウエスト保存
				MAXBASE:(LOCAL:1):ウエスト = ウエスト保存
			ENDIF
			IF ヒップ数値設定フラグ
				BASE:(LOCAL:1):ヒップ = ヒップ保存
				MAXBASE:(LOCAL:1):ヒップ = ヒップ保存
			ENDIF
		ENDIF

		SIF 肉体特徴数値保存:0
			TALENT:(LOCAL:1):角あり = 1
		SIF 肉体特徴数値保存:1
			TALENT:(LOCAL:1):尻尾あり = 1
		SIF 肉体特徴数値保存:2
			TALENT:(LOCAL:1):エルーン耳 = 1
		SIF 肉体特徴数値保存:3
			TALENT:(LOCAL:1):ハーヴィン耳 = 1
		FOR LOCAL, 0, 300
			IF TALENT数値保存:LOCAL == 5
				CONTINUE
			ENDIF
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:30
		IF !年齢数値設定フラグ
			TALENT:(LOCAL:1):年齢層 = ランダムキャラ素質:33
		ELSE
			TALENT:(LOCAL:1):年齢層 = 0
		ENDIF
		TALENT:(LOCAL:1):外見年齢 = ランダムキャラ素質:34
		IF !身長数値設定フラグ
			TALENT:(LOCAL:1):体格 = ランダムキャラ素質:31
		ELSE
			TALENT:(LOCAL:1):体格 = 0
		ENDIF
		IF !ウエスト数値設定フラグ
			TALENT:(LOCAL:1):体型 = 体型保存
		ELSE
			TALENT:(LOCAL:1):体型 = 0
		ENDIF
		IF !ウエスト数値設定フラグ || !ヒップ数値設定フラグ
			TALENT:(LOCAL:1):下半身 = 下半身保存
		ELSE
			TALENT:(LOCAL:1):下半身 = 0
		ENDIF
		IF GETBIT(ランダムキャラ素質:1, 1)
			TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:32
		ELSE
			TALENT:(LOCAL:1):男性器サイズ = 0
		ENDIF
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		LOCAL = 0
		FOR COUNT, 1, フレーバー素質_項目数 + 1
			SIF フレーバー素質数値保存:COUNT == 0
				CONTINUE
			フレーバー素質:(LOCAL:1):COUNT = フレーバー素質数値保存:COUNT
			LOCAL = 1
		NEXT
		IF LOCAL
			フレーバー素質:(LOCAL:1):素質表示設定 = 1
		ENDIF
		FOR LOCAL, 0, 50
			IF ABL数値保存:LOCAL > 0
				ABL:(LOCAL:1):(ABL一時保存:LOCAL) = ABL数値保存:LOCAL
			ENDIF
		NEXT
		FOR LOCAL, 0, 100
			IF EXP数値保存:LOCAL > 0
				EXP:(LOCAL:1):(EXP一時保存:LOCAL) = EXP数値保存:LOCAL
			ENDIF
		NEXT

		CSTR:(LOCAL:1):プレゼント好み = %SUBSTRING(好物一時保存, 0, STRLENS(好物一時保存) - 1)%
		CFLAG:(LOCAL:1):泳装 = 服装数値保存:30

		;戦闘関連設定
		SIF ランダムキャラ戦闘タイプ:1 == 0
			ランダムキャラ戦闘タイプ:1 = 1
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 =  属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 得意武器_%得意武器表示文字列(ランダムキャラ戦闘タイプ:1)% 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		IF ランダムキャラ奥義名:0 != ""
			表示奥義:(LOCAL:1):0 '= ランダムキャラ奥義名:0
			表示奥義:(LOCAL:1):1 '= ランダムキャラ奥義名:1
			表示奥義:(LOCAL:1):2 '= ランダムキャラ奥義名:2
		ENDIF
		IF ランダムキャラ奥義名:3 != ""
			陥落奥義:(LOCAL:1):0 '= ランダムキャラ奥義名:3
			陥落奥義:(LOCAL:1):1 '= ランダムキャラ奥義名:4
			陥落奥義:(LOCAL:1):2 '= ランダムキャラ奥義名:5
		ENDIF

		CALL ADD_PERSON_FROM_CHARA(LOCAL:1)
		CALL コマンド存在チェック
		従業員追加中フラグ = 1
		CALL キャラクター初期化(LOCAL:1)
		
		;性知識は初期化の後
		知識素質:(LOCAL:1):性知識 = ランダムキャラ素質:6
		
		CFLAG:(LOCAL:1):滞在期間 = 999
		CFLAG:(LOCAL:1):招待不可フラグ = 0
		CFLAG:(LOCAL:1):探索参加許可フラグ = 1
		CALL ステータス記録(LOCAL:1)

		FOR LOCAL:2, 0, 従業員部屋数
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 0
		表示モード = 0
	CASE 1
		表示モード = 1
	CASE 2
		表示モード = 2
	CASE 3
		ページ数 = 0
		表示モード = 3
	CASE 9
		PRINTL 可修改「基本信息・身体信息・身体设定」界面项目名的显示颜色
		PRINTL 除下方预设选项外，支持直接输入十六进制颜色码（000000～FFFFFF）
		LOCALS:2 = 赤青黄緑紫
		FOR LOCAL, 0, STRLENSU(LOCALS:2)
			LOCALS:3 '= カラーパレット_HTML(CHARATU(LOCALS:2, LOCAL))
			LOCALS:1 += @"<button value='%LOCALS:3%'><font color='#%LOCALS:3%'>　[%CHARATU(LOCALS:2, LOCAL)%色]</font></button>"
		NEXT
		LOCALS:1 += @"<button value='C0C0C0'><font color='#C0C0C0'>　[白色]</font></button>"
		LOCALS:1 += @"<button value='666666'><font color='#666666'>　[暗灰色]</font></button>"
		LOCALS:1 += @"<button value='959595'><font color='#959595'>　[亮灰色（默认）]</font></button>"
		HTML_PRINT LOCALS:1
		SETTEXTBOX 見出し文字色
		INPUTS
		IF REGEXPMATCH(RESULTS, "[^0-9a-fA-F]")
			PRINTW 包含非法十六进制字符
		ELSEIF REGEXPMATCH(RESULTS, "[0-9a-fA-F]") != 6
			PRINTW 位数不足/超过6位
		ELSE
			見出し文字色 '= RESULTS
			SETCOLOR TOINT("0x" + RESULTS)
			PRINTW 项目名称显示色已更新
			RESETCOLOR
		ENDIF
	CASE 997
		PRINTL 启动简短评论/解说编辑器（建议提前备份CSV文件）
			CALL CHOICE("是否确认？", "确定", "取消")
			SIF !RESULT
				CALL 一言コメント解説エディタ(登録番号, ランダムキャラネーム, ランダムキャラ人称, ランダムキャラ人称:1)
	CASE 998
		CALL CSVコピー処理
		CSV番号 = RESULT
		IF EXISTCSV(CSV番号)
			;コピー処理
			PRINTFORMW 已复制%CSVNAME(CSV番号)%的数据。
			VARSET RESULTS
			SPLIT CSVNAME(CSV番号), "]", RESULTS
			ランダムキャラ二つ名 = %SUBSTRING(RESULTS:0, 1, -1)%
			ランダムキャラネーム = %REPLACE(CSVNAME(CSV番号), ".*\]", "")%
			ランダムキャラCALLネーム = %CSVCALLNAME(CSV番号)%
			登録番号 = CSV番号

			LOCALS = %CSVCSTR(CSV番号, GETNUM(CSTR, "戦闘基礎ステータス設定"))%
			VARSET RESULTS

			SUBSTRING LOCALS, 1, -1
			SPLIT RESULTS, " ", LOCALS
			;属性
			SPLIT LOCALS:0, "_", CSTR切り分け文字列格納
			ランダムキャラ戦闘タイプ:0 = 属性文字列数値変換(CSTR切り分け文字列格納:1)
			;得意武器
			SPLIT LOCALS:1, "_", CSTR切り分け文字列格納
			VARSET RESULTS
			SPLIT CSTR切り分け文字列格納:1, "・", RESULTS
			LOCAL:1 = 0
			FOR LOCAL, 0, RESULT
				SETBIT LOCAL:1, 得意武器文字列数値変換(RESULTS:LOCAL)
			NEXT
			ランダムキャラ戦闘タイプ:1 = LOCAL:1
			;初期ステータス
			SPLIT LOCALS:2, "_", CSTR切り分け文字列格納
			ランダムキャラ戦闘タイプ:2 = タイプ文字列数値変換(CSTR切り分け文字列格納:1)

			;显示奥义名称
			VARSET LOCALS
			VARSET ランダムキャラ奥義名
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "表示奥義設定")), 0, -1
			IF RESULTS != ""
				SPLIT RESULTS, "_", LOCALS
				ランダムキャラ奥義名:0 '= LOCALS:0
				ランダムキャラ奥義名:1 '= LOCALS:1
				ランダムキャラ奥義名:2 '= LOCALS:2
			ENDIF
			VARSET LOCALS
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "陥落奥義設定")), 0, -1
			IF RESULTS != ""
				SPLIT RESULTS, "_", LOCALS
				ランダムキャラ奥義名:3 '= LOCALS:0
				ランダムキャラ奥義名:4 '= LOCALS:1
				ランダムキャラ奥義名:5 '= LOCALS:2
			ENDIF

			ランダムキャラ出演作品 = %CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品"))%
			ランダムキャラ招待不可フラグ = CSVCFLAG(CSV番号, GETNUM(CFLAG, "招待不可フラグ"))
			ランダムキャラ人称:0 = %CSVCSTR(CSV番号, GETNUM(CSTR, "一人称"))%
			ランダムキャラ人称:1 = %CSVCSTR(CSV番号, GETNUM(CSTR, "二人称"))%


			ランダムキャラ素質:0 = CSVTALENT(CSV番号, GETNUM(TALENT, "種族"))
			ランダムキャラ特殊種族 = %CSVCSTR(CSV番号, GETNUM(CSTR, "特殊種族"))%
			ランダムキャラ素質:1 = CSVTALENT(CSV番号, GETNUM(TALENT, "性別"))
			ランダムキャラ素質:2 = 0
			SIF CSVTALENT(CSV番号, GETNUM(TALENT, "処女")) == 0
				SETBIT ランダムキャラ素質:2, 0
			SIF CSVTALENT(CSV番号, GETNUM(TALENT, "非童貞"))
				SETBIT ランダムキャラ素質:2, 1
			IF CSVTALENT(CSV番号, GETNUM(TALENT, "Ａ処女"))
				Ａ処女保存 = 0
			ELSE
				Ａ処女保存 = 1
			ENDIF
			肉体特徴数値保存:0 = CSVTALENT(CSV番号, GETNUM(TALENT, "角あり"))
			肉体特徴数値保存:1 = CSVTALENT(CSV番号, GETNUM(TALENT, "尻尾あり"))
			肉体特徴数値保存:2 = CSVTALENT(CSV番号, GETNUM(TALENT, "エルーン耳"))
			肉体特徴数値保存:3 = CSVTALENT(CSV番号, GETNUM(TALENT, "ハーヴィン耳"))

			年齢保存 = CSVBASE(CSV番号, GETNUM(BASE, "年齢"))
			IF 年齢保存
				年齢数値設定フラグ = 1
			ELSE
				年齢数値設定フラグ = 0
			ENDIF
			身長保存 = CSVBASE(CSV番号, GETNUM(BASE, "身長"))
			身長数値設定フラグ = (身長保存 != 0)
			ランダムキャラ素質:31 = 身長数値設定フラグ ? EDIT用体格算出(ランダムキャラ素質:1, 身長保存) # CSVTALENT(CSV番号, GETNUM(TALENT, "体格"))
			体型保存 = CSVTALENT(CSV番号, GETNUM(TALENT, "体型"))
			IF GETBIT(ランダムキャラ素質:1, 0)
				バスト保存 = CSVBASE(CSV番号, GETNUM(BASE, "バスト"))
				ウエスト保存 = CSVBASE(CSV番号, GETNUM(BASE, "ウエスト"))
				ヒップ保存 = CSVBASE(CSV番号, GETNUM(BASE, "ヒップ"))
				バスト数値設定フラグ = (バスト保存 != 0)
				ウエスト数値設定フラグ = (ウエスト保存 != 0)
				ヒップ数値設定フラグ = (ヒップ保存 != 0)
				ランダムキャラ素質:3 = バスト保存 && 身長保存 ? EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存)) # CSVTALENT(CSV番号, GETNUM(TALENT, "バストサイズ"))
			ENDIF
			ランダムキャラ素質:33 = CSVTALENT(CSV番号, GETNUM(TALENT, "年齢層"))
			ランダムキャラ素質:34 = CSVTALENT(CSV番号, GETNUM(TALENT, "外見年齢"))
			下半身保存 = CSVTALENT(CSV番号, GETNUM(TALENT, "下半身"))
			IF GETBIT(ランダムキャラ素質:1, 1)
				ランダムキャラ素質:32 = CSVTALENT(CSV番号, GETNUM(TALENT, "男性器サイズ"))
			ENDIF
			好物一時保存 = %CSVCSTR(CSV番号, GETNUM(CSTR, "プレゼント好み"))%_

			;素質
			FOR LOCAL, 0, 300
				SIF TALENT一時保存:LOCAL < 1
					CONTINUE
				TALENT数値保存:LOCAL = CSVTALENT(CSV番号, TALENT一時保存:LOCAL) + 5
			NEXT

			;知識素質
			VARSET LOCALS
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "知識素質設定")), 1, -1
			SPLIT RESULTS, " ", LOCALS
			LOCAL:2 = RESULT - 1
			;性知識
			SPLIT LOCALS:0, "Lv", CSTR切り分け文字列格納
			ランダムキャラ素質:6 = TOINT(CSTR切り分け文字列格納:1)
			FOR LOCAL, 1, LOCAL:2
				SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
				;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
				知識素質数値保存:(GETNUM(知識素質, CSTR切り分け文字列格納:0)) = TOINT(CSTR切り分け文字列格納:1)
			NEXT

			FOR LOCAL, 0, 10
				固有素質一時保存:LOCAL = %CSVCSTR(CSV番号, LOCAL + 80)%
			NEXT

			;性癖素質
			VARSET LOCALS
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "性癖素質設定")), 1, -1
			SPLIT RESULTS, " ", LOCALS
			LOCAL:2 = RESULT - 1
			FOR LOCAL, 0, LOCAL:2
				SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
				;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
				性癖素質数値保存:(GETNUM(性癖素質, CSTR切り分け文字列格納:0)) = TOINT(CSTR切り分け文字列格納:1)
			NEXT

			;能力
			FOR LOCAL, 0, 50
				SIF ABL一時保存:LOCAL < 0
					BREAK
				ABL数値保存:LOCAL = CSVABL(CSV番号, ABL一時保存:LOCAL)
			NEXT

			;経験
			FOR LOCAL, 0, 100
				SIF EXP一時保存:LOCAL < 0
					BREAK
				EXP数値保存:LOCAL = CSVEXP(CSV番号, EXP一時保存:LOCAL)
			NEXT

			;服装
			ランダムキャラ服装名 = %CSVCSTR(CSV番号, GETNUM(CSTR, "服名称"))%
			FOR LOCAL, 1, 23
				服装数値保存:LOCAL = CSVEQUIP(CSV番号, LOCAL)
			NEXT
			服装数値保存:30 = CSVCFLAG(CSV番号, GETNUM(CFLAG, "泳装"))

			;同行者
			SIF DT_EXIST("DT_CHARA")
				DT_RELEASE "DT_CHARA"

			DT_CREATE "DT_CHARA"
			DT_COLUMN_ADD "DT_CHARA", "NO", 2
			DT_COLUMN_ADD "DT_CHARA", "NAME"
			DT_COLUMN_ADD "DT_CHARA", "VALUE", 1

			VARSET LOCALS
			REPLACE CSVCSTR(CSV番号, GETNUM(CSTR, "同行キャラ")), " ", ""
			SPLIT RESULTS, "%", LOCALS
			LOCAL:2 = RESULT - 1
			FOR LOCAL, 1, キャラクタ数上限
				SIF !EXISTCSV(LOCAL)
					CONTINUE
				;ランダムキャラ排除
				SIF LOCAL == 999
					CONTINUE
				DT_ROW_ADD "DT_CHARA", "NO", LOCAL, "NAME", CSVNAME(LOCAL), "VALUE", 0
			NEXT
			FOR LOCAL, 0, LOCAL:2
				SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
				;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
				DT_SELECT "DT_CHARA", @"NAME = '%CSTR切り分け文字列格納:0%'"
				DT_CELL_SET "DT_CHARA", RESULT:1, "VALUE", TOINT(CSTR切り分け文字列格納:1), 1
			NEXT
		ENDIF
	CASE 999
		IF CSVエディタフラグ
			CALL CHOICE("是否退出CSV编辑器？（不会自动保存）", "确定", "取消")
			SIF RESULT
				GOTO 従業員_LOOP
		ENDIF
		RETURN 0
	CASE 1010 TO 1020
		INVERTBIT 表示モード:1, (LOCAL % 1000)
ENDSELECT

GOTO 従業員_LOOP


@ステータス記録(キャラ番号)
;引き継ぎ処理時に使う初期値の記録用
#DIM キャラ番号
#DIM 記録位置

LOCAL:1 = 0
FOR LOCAL, 0, 従業員初期値最大記録数
	IF 従業員初期値記録:LOCAL == ""
		記録位置 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1 == 0
	PRINTW ※因存档槽位已满，该角色无法继承至下周目
	RETURN 0
ENDIF

従業員初期値記録:記録位置 = 

従業員初期値記録:記録位置 += @"キャラ番号_{キャラ番号}_"
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"名前_%CALLNAME:キャラ番号%_"
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 300
	IF TALENT:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"素質_{LOCAL}_{TALENT:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, フレーバー素質_項目数 + 1
	IF フレーバー素質:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"フレーバー素質_{LOCAL}_{フレーバー素質:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 50
	IF ABL:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"ABL_{LOCAL}_{ABL:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 100
	IF EXP:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"EXP_{LOCAL}_{EXP:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"属性_{基礎BATTLE_STATE:キャラ番号:属性}_"
従業員初期値記録:記録位置 += @"得意武器_{基礎BATTLE_STATE:キャラ番号:得意武器}_"
従業員初期値記録:記録位置 += @"タイプ_{基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存}_"

従業員初期値記録:記録位置 += @"性知識_{知識素質:キャラ番号:性知識}_"



@EDIT用バストサイズ表示(バスト保存, アンダーバスト)
#FUNCTIONS
#DIM バスト保存
#DIM アンダーバスト
#DIMS CONST 表記 = "(-)","(AAA)","(AA)","(A)","(B)","(C)","(D)","(E)","(F)","(G)","(H)","(I)","(J)","(K)","(L)","(M)","(N)","(O)","(P)","(Q)","(R)","(S)","(T)","(U)","(V)","(W)","(X)","(Y)","(Z)","(Z+) "

LOCAL = (バスト保存 - アンダーバスト + 10) / 25 - 1

;文字列変数が若干不自由なので無理やり作成
LOCAL = MAX(LOCAL, 1)
LOCAL = MIN(LOCAL, 29)

LOCALS:0 = %表記:(LOCAL)%

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCALS:0
; ENDIF

@EDIT用バストTALENT算出(バスト保存, アンダーバスト)
#FUNCTION
#DIM バスト保存
#DIM アンダーバスト

LOCAL = バスト保存 - アンダーバスト

SELECTCASE LOCAL
	CASE IS < 89
		RETURNF -2
	CASE IS < 139
		RETURNF -1
	CASE IS < 184
		RETURNF 0
	CASE IS < 239
		RETURNF 1
	CASE IS < 314
		RETURNF 2
	CASEELSE
		RETURNF 3
ENDSELECT


@EDIT用バストcm算出(バストサイズ, アンダーバスト)
#FUNCTION
#DIM バストサイズ
#DIM アンダーバスト

;最低サイズ算出
LOCAL = アンダーバスト + 40 + ((バストサイズ + 2) * 50)
SIF バストサイズ == 3
	LOCAL += 25


IF バストサイズ < 2
	;-2~1の時、+4.9cmまで許容
	LOCAL = LOCAL + RAND:50
ELSE
	;それ以上の時、+7.4cmまで許容
	LOCAL = LOCAL + RAND:75
ENDIF

IF バストサイズ == 3 && RAND:10 == 0
	;魔乳時、たまーーに限界突破する
	LOCAL = LOCAL + RAND:75
ENDIF

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCAL
; ENDIF

@EDIT用アンダーバスト算出(性別, 種族, 身長, 体格, 体型)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 種族
#DIM 基礎値
基礎値 = 身長 * (!GETBIT(性別, 0) ? 50 # 43) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.15
	CASE 体格_子供
		TIMES 基礎値, 1.05
	CASE 体格_小柄
		SIF 種族 != 種族_ドラフ
			TIMES 基礎値, 0.95
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 1.05
	CASE 体格_男ドラフ並
		TIMES 基礎値, 1.10
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.90
	CASE 体型_細身
		TIMES 基礎値, 0.94
	CASE 体型_スレンダー
		TIMES 基礎値, 0.97
	CASE 体型_グラマー
		TIMES 基礎値, 1.01
	CASE 体型_豊満
		TIMES 基礎値, 1.05
	CASE 体型_肥満
		TIMES 基礎値, 1.10
ENDSELECT

;夢詰め込んだらドラフカップが大きくなりすぎるので補正
SIF 種族 == 種族_ドラフ && 体格 >= 体格_小柄 && GETBIT(性別, 0)
	基礎値 = 基礎値 * 110 / 100

RETURNF 基礎値

@EDIT用身長算出(性別, 種族, 体格)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 種族
#DIM 基礎値

;今のところ子供は未実装なのでコメントアウト
;IF CFLAG:対象:行動不能状態 == 行動不能_子供
;	基礎値 = MIN(500 + CFLAG:対象:子の成長度 * 20, 1300)
;ELSEIF TALENT:対象:幼児
;	基礎値 = MIN(800 + (CFLAG:対象:子の成長度 - 6) * 50, 1300)
;ELSE
	SELECTCASE 体格
		CASE 体格_ハーヴィン並
			基礎値 = !GETBIT(性別, 0) ? RAND(800, 1000) # RAND(700, 900)
		CASE 体格_子供
			IF 種族 == 種族_ハーヴィン
				基礎値 = !GETBIT(性別, 0) ? RAND(1000, 1400) # RAND(900, 1300)
			ELSE
				基礎値 = !GETBIT(性別, 0) ? RAND(1200, 1400) # RAND(1100, 1300)
			ENDIF
		CASE 体格_小柄
			IF 種族 == 種族_ドラフ && GETBIT(性別, 0) && RAND:4
				基礎値 = RAND(1300, 1400)
			ELSE
				基礎値 = !GETBIT(性別, 0) ? RAND(1400, 1600) # RAND(1300, 1500)
			ENDIF
		CASE 体格_普通
			基礎値 = !GETBIT(性別, 0) ? RAND(1600, 1800) # RAND(1500, 1700)
		CASE 体格_長身
			基礎値 = !GETBIT(性別, 0) ? RAND(1800, 2150) # RAND(1700, 2000)
		CASE 体格_男ドラフ並
			基礎値 = !GETBIT(性別, 0) ? RAND(2150, 2450) # RAND(2000, 2200)
	ENDSELECT
;ENDIF

RETURNF 基礎値

@EDIT用ウエスト算出(性別, 身長, 体格, 体型)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 基礎値

基礎値 = 身長 * (性別 == 2 ? 43 # 38) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.22
	CASE 体格_子供
		TIMES 基礎値, 1.07
	CASE 体格_小柄
		TIMES 基礎値, 1.03
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 0.98
	CASE 体格_男ドラフ並
		TIMES 基礎値, 0.97
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.85
	CASE 体型_細身
		TIMES 基礎値, 0.9
	CASE 体型_スレンダー
		TIMES 基礎値, 0.95
	CASE 体型_グラマー
		TIMES 基礎値, 1.02
	CASE 体型_豊満
		TIMES 基礎値, 1.06
	CASE 体型_肥満
		TIMES 基礎値, 1.10
ENDSELECT

基礎値 = 基礎値 * RAND(9700, 10301) / 10000

RETURNF 基礎値

@EDIT用ヒップ算出(性別, 身長, 体格, 体型, 下半身)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 下半身
#DIM 基礎値
#DIM 補正値

基礎値 = 身長 * (性別 == 2 ? 47 # 54) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.2
	CASE 体格_子供
		TIMES 基礎値, 1.05
	CASE 体格_小柄
		TIMES 基礎値, 1.02
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 0.99
	CASE 体格_男ドラフ並
		TIMES 基礎値, 0.98
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.92
	CASE 体型_細身
		TIMES 基礎値, 0.95
	CASE 体型_スレンダー
		TIMES 基礎値, 0.98
	CASE 体型_グラマー
		TIMES 基礎値, 1.04
	CASE 体型_豊満
		TIMES 基礎値, 1.08
	CASE 体型_肥満
		TIMES 基礎値, 1.12
ENDSELECT

IF 性別 == 2
	補正値 = 100
ELSE
	SELECTCASE 下半身
		CASE -1
			補正値 = 93
		CASE 0
			補正値 = 100
		CASE 1
			補正値 = 108
	ENDSELECT
ENDIF

基礎値 = 基礎値 * 補正値 / 100

基礎値 = 基礎値 * RAND(9600, 10401) / 10000

RETURNF 基礎値


@EDIT用体格算出(性別, 身長)
#FUNCTION
#DIM 性別
#DIM 身長
#DIM 基礎値

;身長と性別をもとに算出する
SELECTCASE 身長
	CASE IS < 900 + !GETBIT(性別, 0)*100
		RETURNF 体格_ハーヴィン並
	CASE IS < 1300 + !GETBIT(性別, 0)*100
		RETURNF 体格_子供
	CASE IS < 1500 + !GETBIT(性別, 0)*100
		RETURNF 体格_小柄
	CASE IS < 1700 + !GETBIT(性別, 0)*100
		RETURNF 体格_普通
	CASE IS < 2000 + !GETBIT(性別, 0)*150
		RETURNF 体格_長身
	CASEELSE
		RETURNF 体格_男ドラフ並
ENDSELECT



@EQUIPNAME_TIPS(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 5
		LOCALS = 无需解开纽扣即可实现胸部暴露？
	CASE 6
		LOCALS = 未脱衣时可否揉捏无胸罩状态的乳头？
	CASE 7
		LOCALS = 未脱衣时能否进行乳交？
	CASE 10
		LOCALS = 不脱衣能否取下胸罩？
	CASE 14
		LOCALS = 虽然说的是裙子，但也包括围裙等
	CASE 15
		LOCALS = 适用于体操服/兔女郎服等高叉服饰
	CASE 18
		LOCALS = 不脱衣能否取下内裤？
	CASEELSE
		LOCALS '= ""
ENDSELECT

RETURNF LOCALS


@汎用立ち絵選択処理(汎用画像キー)
#DIMS REF 汎用画像キー
#DIM F_NUM
#DIM ページ数
#DIM 改行フラグ
#DIMS G_NAME

;汎用立ち絵を全部ずらっと並べてそこから任意のものを選ぶ
;種族や体型・バストなどによる制限はない
VARSET RESULTS
VARSET LOCAL
VARSET TEMP_HTML
SPRITEDISPOSEALL 0
ページ数 = 0

DRAWLINE
LOCALS '= "resources/汎用グラフィック"
F_NUM = ENUMFILES(LOCALS, "顔_*.*", 1)

$表示_LOOP
改行フラグ = 0
TEMP_HTML = 
FOR LOCAL, ページ数 * 40, ページ数 * 40 + 40
	SIF LOCAL >= F_NUM
		BREAK
	G_NAME '= SUBSTRING(RESULTS:LOCAL, 10, -1)
	GDISPOSE LOCAL
	CALL GCREATE_拡張子(LOCAL, G_NAME)
	SIF スプライト作成(TOSTR(LOCAL), LOCAL, 0) == 0
		CONTINUE
	LOCALS = {(LOCAL % 8) * 750}, 0, 750, 750
	TEMP_HTML += @"<div rect='%LOCALS%'><button value='{LOCAL}' title='%ESCAPE(G_NAME)%'><img src='{LOCAL}' width='750' height='750'></button></div>"
	IF (LOCAL + 1) % 8 == 0
		改行フラグ = 0
		TEMP_HTML += "<br><br><br><br><br><br><br>"
	ELSE
		改行フラグ = 1
	ENDIF
NEXT
SIF 改行フラグ
	TEMP_HTML += "<br><br><br><br><br><br><br>"
HTML_PRINT TEMP_HTML

TEMP_HTML = 
DRAWLINE
IF ページ数 > 0
	TEMP_HTML += "<button value='9997'>[9997] 上一页</button>"
ELSE
	TEMP_HTML += "　　　 　　　　　　"
ENDIF
TEMP_HTML += "　　　　　　<button value='9999'>[9999] 返回</button>　　　　　　"
IF F_NUM >= ページ数 * 40 + 40
	TEMP_HTML += "<button value='9998'>[9998] 下一页</button>"
ELSE
	TEMP_HTML += "　　　 　　　　　　"
ENDIF
HTML_PRINT TEMP_HTML
BINPUT
SELECTCASE RESULT
	CASE 9997
		ページ数 -= 1
		GOTO 表示_LOOP
	CASE 9998
		ページ数 += 1
		GOTO 表示_LOOP
	CASE 9999
		RETURN 0
	CASEELSE
		DRAWLINE
		汎用画像キー = %REPLACE(RESULTS:RESULT, ESCAPE("resources\\汎用グラフィック\\顔_"), "")%
		汎用画像キー = %REPLACE(汎用画像キー, ".png", "")%
		汎用画像キー = %REPLACE(汎用画像キー, ".webp", "")%
		HTML_PRINT @"<div rect='0, 0, 750, 750'><img src='{RESULT}' width='750' height='750'></div><br><br><br><br><br><br><br>"
		PRINTFORMW 已选择%汎用画像キー%。
		RETURN 0
ENDSELECT

@CSVコピー処理(ARG=0)
#DIM CSV番号
#DIM 配列番号
#DIM 表示数, 3
#DIM CSVページ数
#DIM 次ページフラグ
#DIM 表示配列, キャラクタ数上限
#DIMS Row_page
#DIMS Row_filter
#DIM フィルタフラグ, 2
#DIMS フィルタ文字列,2;  0:出演作品 1:名前
#DIMS 出演作品, 500
#DIM 出演作品数
#DIM L_COUNT
#DIM 存在ページ数

VARSET 出演作品
出演作品:0 = 碧蓝幻想
出演作品数 = 1
L_COUNT = LINECOUNT

$処理部分
;CLEARLINE L_COUNT - LINECOUNT
L_COUNT = LINECOUNT
表示数 = 0
VARSET 表示配列
FOR CSV番号, 1, キャラクタ数上限
	SIF EXISTCSV(CSV番号) == 0
		CONTINUE
	SIF CSV番号 == 999
		CONTINUE
	IF フィルタフラグ
		SIF STRCOUNT(キャラ一覧フィルタ:性別, @"性別：%GET_TALENTNAME(2, CSVTALENT(CSV番号, GETNUM(TALENT, "性別")))%")
			CONTINUE
		SIF STRCOUNT(キャラ一覧フィルタ:種族, @"種族：%GET_TALENTNAME(200, CSVTALENT(CSV番号, GETNUM(TALENT, "種族")))%")
			CONTINUE
		SIF STRCOUNT(キャラ一覧フィルタ:その他, @"その他：招待不可%TOSTR(CSVCFLAG(CSV番号, GETNUM(CFLAG, "招待不可フラグ")))%")
			CONTINUE
		LOCALS = \@ CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品")) != "" ? %HTML_ESCAPE(CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品")))% # 碧蓝幻想\@
		SIF STRCOUNT(フィルタ文字列, @"出演作品：%LOCALS%")
			CONTINUE
		SIF フィルタ文字列:1 != "" && !STRCOUNT(CSVNAME(CSV番号), フィルタ文字列:1)
			CONTINUE
	ENDIF
	表示配列:表示数 = CSV番号
	IF CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品")) != "" && !MATCH(出演作品, HTML_ESCAPE(CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品"))))
		出演作品:出演作品数 '= HTML_ESCAPE(CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品")))
		出演作品数 ++
	ENDIF
	表示数 ++
NEXT
表示数:1 = 表示数
存在ページ数 = 表示数:1 / 40
SIF 表示数:1 % 40 == 0
	存在ページ数 -= 1


DRAWLINE
IF !ARG
	PRINTL 从现有CSV复制配置，请选择复制对象
ELSE
	PRINTL 将基于现有CSV启动编辑器，请选择编辑对象
ENDIF
DRAWLINE

表示数 = 0
次ページフラグ = 0
FOR 配列番号, CSVページ数 * 40, CSVページ数 * 40 + 40
	SIF 表示配列:配列番号 < 1
		CONTINUE
	PRINTBUTTON @"[{表示配列:配列番号, 4}] %NAME表示省略(CSVNAME(表示配列:配列番号), 60),61,LEFT%", 表示配列:配列番号
	表示数 ++
	SIF 表示数 % 2 == 0
		PRINTL
NEXT
SIF 表示数 % 2
	PRINTL
DRAWLINE
Row_page = 
IF CSVページ数 > 0
	Row_page += "<button value='9997'>[上一页]</button>"
ELSE
	Row_page += "　　　　　　"
ENDIF
Row_page += " " * 22

Row_page += @"{CSVページ数, 4, RIGHT}/{存在ページ数, 4, RIGHT}"
Row_page += " " * 25
IF CSVページ数 < 存在ページ数
	Row_page += "<button value='9998'>[下一页]</button>"
ENDIF
Row_page += "<br>"
IF CSVページ数 > 0
	Row_page += "<button value='9991'>[|≪]</button>"
ELSE
	Row_page += "     "
ENDIF
IF CSVページ数 > 5
	Row_page += "<button value='9992'> [－５]</button>"
ELSE
	Row_page += " 　　　"
ENDIF
Row_page += " " * 24
Row_page += "<button value='9999'> [返回]</button>"
Row_page += " " * 25
IF CSVページ数 <= 存在ページ数 - 5
	Row_page += "<button value='9995'>[＋５] </button>"
ELSE
	Row_page += " 　　　"
ENDIF
IF CSVページ数 < 存在ページ数
	Row_page += "<button value='9996'>[≫|]</button>"
ENDIF
HTML_PRINT Row_page
DRAWLINE

Row_filter '= @"<button value='9000'>□过滤功能（<font color='#\@ フィルタフラグ? B0B0B0'> ON# 666666'>OFF\@</font>）</button>"
IF フィルタフラグ
	Row_filter += "<br>"
	Row_filter += @"　■性別　　　"
	FOR LOCAL, 1, 4
		Row_filter += @"<font color='#\@  STRCOUNT(キャラ一覧フィルタ:性別, @"性別：%GET_TALENTNAME(2, LOCAL)%")? 666666# B0B0B0\@'>"
		Row_filter += @"<button value='性別：%GET_TALENTNAME(2, LOCAL)%'>[%GET_TALENTNAME(2, LOCAL)%]</button></font> "
	NEXT
	Row_filter += @"<font color='#\@  STRCOUNT(キャラ一覧フィルタ:性別, @"性別：%GET_TALENTNAME(2, 0)%")? 666666# B0B0B0\@'>"
	Row_filter += @"<button value='性別：%GET_TALENTNAME(2, 0)%'>[%GET_TALENTNAME(2, 0)%]</button></font> "
	Row_filter += @"／<button value='性別：全て表示'>[显示全部]</button> <button value='性別：全て非表示'>[全部隐藏]</button>"
	Row_filter += "<br>"

	Row_filter += @"　■种族　　　"
	FOR LOCAL, 1, 7
		Row_filter += @"<font color='#\@  STRCOUNT(キャラ一覧フィルタ:種族, @"種族：%GET_TALENTNAME(200, LOCAL)%")? 666666# B0B0B0\@'>"
		Row_filter += @"<button value='種族：%GET_TALENTNAME(200, LOCAL)%'>[%GET_TALENTNAME(200, LOCAL)%]</button></font> "
	NEXT
	Row_filter += @"／<button value='種族：全て表示'>[显示全部]</button> <button value='種族：全て非表示'>[全部隐藏]</button>"
	Row_filter += "<br>"

	Row_filter += @"　■其他　　　"
	FOR LOCAL, 0, 4
		Row_filter += @"<font color='#\@  STRCOUNT(キャラ一覧フィルタ:その他, @"その他：招待不可%TOSTR(LOCAL)%")? 666666# B0B0B0\@'>"
		Row_filter += @"<button value='その他：招待不可%TOSTR(LOCAL)%'>[招待不可フラグ%TOSTR(LOCAL)%]</button></font> "
	NEXT
	Row_filter += @"／<button value='その他：全て表示'>[显示全部]</button> <button value='その他：全て非表示'>[全部隐藏]</button>"
	Row_filter += "<br>"

	Row_filter += @"　■出自作品　"
	Row_filter += @"<font color='#\@  STRCOUNT(フィルタ文字列:0, @"出演作品：%出演作品:0%")? 666666# B0B0B0\@'>"
	Row_filter += @"<button value='出演作品：%出演作品:0%'>[%出演作品:0%]</button></font> "
	Row_filter += @"／<button value='出演作品：全て表示'>[显示全部]</button> <button value='出演作品：全て非表示'>[全部隐藏]</button>"
	Row_filter += @"<button value='9001'>[EX独立筛选显示（<font color='#\@ フィルタフラグ:1? B0B0B0'> ON# 666666'>OFF\@</font>）]</button>"
	IF フィルタフラグ:1
		Row_filter += "<br>"
		LOCALS '= " " * 14
		FOR LOCAL, 1, 出演作品数
			LOCALS:1 '= @"<font color='#\@  STRCOUNT(フィルタ文字列, @"出演作品：%出演作品:LOCAL%")? 666666# B0B0B0\@'>"
			LOCALS:1 += @"<button value='出演作品：%出演作品:LOCAL%'>[%出演作品:LOCAL%]</button></font> "
			IF STRLENS(HTML_TOPLAINTEXT(LOCALS+LOCALS:1)) / (5850/50) > 0
				Row_filter += LOCALS + "<br>"
				LOCALS '= " " * 14
			ENDIF
			LOCALS += LOCALS:1
		NEXT
		Row_filter += LOCALS
	ENDIF
	Row_filter += "<br>"
	Row_filter += "　■名称搜索　"
	Row_filter += @"<font color='#\@  フィルタ文字列:1 == ""? 666666# B0B0B0\@'>"
	Row_filter += @"<button value='直接入力'>\@ フィルタ文字列:1 == ""? （无）# %フィルタ文字列:1%\@</button></font> "
	SIF フィルタ文字列:1 != ""
	Row_filter += @"<button value='入力消去'>[清空输入]</button> "
	Row_filter += "<br>"
ENDIF
HTML_PRINT Row_filter


BINPUTS
IF !ISNUMERIC(RESULTS)
	LOCALS = %RESULTS%
	IF STRCOUNT(LOCALS, "種族：")
		LOCAL = 0
	ELSEIF STRCOUNT(LOCALS, "性別：")
		LOCAL = 7
	ELSEIF STRCOUNT(LOCALS, "その他：")
		LOCAL = 9
	ELSEIF STRCOUNT(LOCALS, "出演作品：")
		LOCAL = 99
	ELSEIF LOCALS == "入力消去"
		フィルタ文字列:1 =
		GOTO 処理部分
	ELSEIF LOCALS == "直接入力"
		PRINTL 请输入要检索的角色名称
		SETTEXTBOX フィルタ文字列:1
		INPUTS
		フィルタ文字列:1 '= RESULTS
		GOTO 処理部分
	ELSE
		GOTO 処理部分
	ENDIF
	IF STRCOUNT(LOCALS, "全て表示")
		IF LOCAL == 99
			フィルタ文字列 = 
		ELSE
			キャラ一覧フィルタ:LOCAL =
		ENDIF
	ELSEIF STRCOUNT(LOCALS, "全て隐藏")
		SELECTCASE LOCAL
			CASE 0
				キャラ一覧フィルタ:種族 = "種族：ヒューマン_種族：エルーン_種族：ドラフ_種族：ハーヴィン_種族：星晶獣_種族：その他_"
			CASE 7
				キャラ一覧フィルタ:性別 = "性別：女性_性別：男性_性別：扶她_性別：その他_"
			CASE 9
				キャラ一覧フィルタ:その他 = "その他：招待不可0_その他：招待不可1_その他：招待不可2_その他：招待不可3_"
			CASE 99
				フィルタ文字列 = 
				FOR LOCAL, 0, 出演作品数
					フィルタ文字列 += @"出自作品：%出演作品:LOCAL%_"
				NEXT
		ENDSELECT
	ELSEIF LOCAL == 99
		IF STRCOUNT(フィルタ文字列, LOCALS)
			REPLACE フィルタ文字列, @"%LOCALS%_", ""
			フィルタ文字列 = %RESULTS%
		ELSE
			フィルタ文字列 += @"%LOCALS%_"
		ENDIF
	ELSEIF STRCOUNT(キャラ一覧フィルタ:LOCAL, LOCALS)
		REPLACE キャラ一覧フィルタ:LOCAL, @"%LOCALS%_", ""
		キャラ一覧フィルタ:LOCAL = %RESULTS%
	ELSE
		キャラ一覧フィルタ:LOCAL += @"%LOCALS%_"
	ENDIF
	GOTO 処理部分
ELSE
	LOCAL = TOINT(RESULTS)
ENDIF
SELECTCASE LOCAL
	CASE 9000
		INVERTBIT フィルタフラグ, 0
		GOTO 処理部分
	CASE 9001
		INVERTBIT フィルタフラグ:1, 0
		GOTO 処理部分
	CASE 9991
		CSVページ数 = 0
		GOTO 処理部分
	CASE 9992
		CSVページ数 -= 5
		GOTO 処理部分
	CASE 9995
		CSVページ数 += 5
		GOTO 処理部分
	CASE 9996
		CSVページ数 = 存在ページ数
		GOTO 処理部分
	CASE 9997
		CSVページ数 --
		GOTO 処理部分
	CASE 9997
		CSVページ数 --
		GOTO 処理部分
	CASE 9997
		CSVページ数 --
		GOTO 処理部分
	CASE 9998
		CSVページ数 ++
		GOTO 処理部分
	CASE 9999
		RETURN -1
	CASEELSE
		CSV番号 = LOCAL
		IF !ARG
			PRINTFORML 确认复制%CSVNAME(CSV番号)%的数据？
		ELSE
			PRINTFORML 确认将%CSVNAME(CSV番号)%设为编辑对象？
		ENDIF
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			RETURN CSV番号
		ELSE
			RESTART
		ENDIF
ENDSELECT
