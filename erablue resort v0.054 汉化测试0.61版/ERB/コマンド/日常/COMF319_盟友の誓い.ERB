@COMNAME319
#FUNCTIONS

TSTR:コマンド名受渡 = 
TRYCALLFORMF 固有非エロ陥落_コマンド名変更_{NO:TARGET}(TARGET)
SIF TSTR:コマンド名受渡 == ""
	TSTR:コマンド名受渡 = 盟友之誓


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_319
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_319
TSTR:ツールチップ受渡 = 
TRYCALLFORM 固有非エロ陥落_フレーバー変更_{NO:TARGET}(TARGET, "取得コマンド解説")
IF TSTR:ツールチップ受渡 == ""
	LOCALS = <br>■盟友之誓<br>
	LOCALS += "与对方缔结深厚信赖关系的指令。<br>好感度要素：信赖值达到一定数值以上时可使用。<br>根据角色不同，指令名称・获得的素质名称可能会产生变动。<br>根据角色不同，可能存在好感度要素：信赖之外的其他条件。"
	LOCALS += "<br>指令类型：日常互动<br>获取来源：无"
	LOCALS += "<br>专属经验值：无<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ENDIF

@COM319

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 1000)
CALL SOURCE_CALC_好感度要素_信頼度UP(TARGET, PLAYER, 1000)
TALENT:非エロ陥落 = 1
初体験日:TARGET:非エロ陥落 = DAY
TRYCALLFORM 固有非エロ陥落_フレーバー変更_{NO:TARGET}(TARGET, "陥落名")
IF CSTR:TARGET:非エロ陥落名称 != ""
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("黄")%'>%CALLNAME:TARGET%被[%CSTR:TARGET:非エロ陥落名称%]攻略</font>","実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("黄")%'>获得攻略特质[%CSTR:TARGET:非エロ陥落名称%]</font>","実績")
ELSE
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("黄")%'>%CALLNAME:TARGET%被[盟友]攻略</font>","実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("黄")%'>获得攻略特质[盟友]</font>","実績")
ENDIF

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE319

;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(319)
	RETURN RESULT

RESULT = 0
TRYCALLFORM 固有非エロ陥落_フレーバー変更_{NO:TARGET}(TARGET, "コマンド条件")
IF RESULT == 0
	;通常条件
	;スケベ陥落してたらダメ
	SIF 陥落チェック_性的(TARGET)
		RETURN 0
	;信頼度一定以上
	SIF CFLAG:信頼度 < 経験値テーブル:79
		RETURN 0
	;二人きりでないとだめ
	SIF GET_TARGETNUM() > 1
		RETURN 0
	;飲み会時はだめ
	SIF TFLAG:調教モード == 3
		RETURN 0
	;馴れ合い強度1
	SIF TCVAR:MASTER:馴れ合い強度 < 1
		RETURN 0
	;すでに非エロ陥落を得ている場合はだめ
	SIF TALENT:非エロ陥落
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠
		RETURN 0
ELSEIF RESULT == 1
	;無条件許可
	RETURN 1
ELSEIF RESULT == -1
	;無条件禁止
	RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM319
CALL 口上変数初期化
TRYCALLFORM 固有非エロ陥落_フレーバー変更_{NO:TARGET}(TARGET, "実行メッセージ")
IF 口上有無確認(0, 1) == 0
	PRINTFORML %CALLNAME:PLAYER%与%CALLNAME:TARGET%被牢固的羁绊所连结……！
	PRINTFORML 两人重新确认了彼此的信赖关系！
	PRINTFORML %CALLNAME:TARGET%获得了［盟友］特质！
ENDIF
