
@COMNAME303
#FUNCTIONS
TSTR:コマンド名受渡 = 搭讪

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_303
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_303
LOCALS = <br>■搭讪<br>
LOCALS += "通过赞美与示好增进亲密的指令。根据概率分为大成功、成功、失败三种结果。<br>短时间内频繁使用会导致话题枯竭而失败。拥有「爱慕」特质的目标可无视此限制。<br>若对方的性知识低于0时效果减弱。"
LOCALS += "<br>指令类型：日常<br>获取来源：爱情・征服"
LOCALS += "<br>专属经验值：对话经验(MASTER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM303
#DIM トランス判定フラグ
;会話

;会話累積値が閾値を超えると失敗
IF TCVAR:話題無しフラグ && !TALENT:恋慕
	PRINTFORML %CALLNAME:PLAYER%试图向%CALLNAME:TARGET%搭话却语塞
	PRINTW 周遭弥漫着尴尬的气氛…
	TIME += 10
	;口上分岐としてはここは大失敗扱いがいいんじゃないかな
	RFLAG:コマンド結果受渡し変数 = -2
	RETURN 0
ENDIF

LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 1
	トランス判定フラグ = 1
	EXP:MASTER:会話経験 ++
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
	トランス判定フラグ = 0
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 0
	;1/2～2/2（会話技能による）の確率でトランス状態が発生
	トランス判定フラグ = (LOCAL <= LOCAL:1 * (10 + ABL:MASTER:話術技能) / 20)
ENDIF

;愛情強度
LOCAL:0 = 300
;征服強度
LOCAL:1 = 100

IF RFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF
SELECTCASE ABL:MASTER:話術技能
	CASE 0
		TIMES LOCAL:0 , 0.20
		TIMES LOCAL:1 , 0.20
	CASE 1
		TIMES LOCAL:0 , 0.40
		TIMES LOCAL:1 , 0.40
	CASE 2
		TIMES LOCAL:0 , 0.70
		TIMES LOCAL:1 , 0.70
	CASE 3
		TIMES LOCAL:0 , 1.00
		TIMES LOCAL:1 , 1.00
	CASE 4
		TIMES LOCAL:0 , 1.50
		TIMES LOCAL:1 , 1.50
	CASEELSE
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
ENDSELECT


CALL SOURCE_CALC_愛情(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, LOCAL:1)


;会話累積値
LOCAL = 200 - (TALENT:TARGET:会話資質 * 50) - (TALENT:PLAYER:会話資質 * 50)
TCVAR:会話累積値 += LOCAL / (3 + ABL:MASTER:話術技能 + TALENT:TARGET:陽気／陰気)

;トランス状態の追加
IF トランス判定フラグ && あなた特殊能力:遊び人 >= 5 && !CFLAG:怒り && !TALENT:恋慕
	RFLAG:コマンド結果受渡し変数 = 2
	TCVAR:トランス残り時間 += 45
ENDIF

TIME += 15
EXP:MASTER:会話経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE303
;会話実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(303)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;口を何かに使ってるなら無理
SIF TEQUIP:PLAYER:口
	RETURN 0
SIF TEQUIP:TARGET:口
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM303
PRINTFORM %CALLNAME:PLAYER%对%CALLNAME:TARGET%
IF TALENT:恋慕
	PRINTDATA
		DATA 倾诉了炽热的爱意
		DATA 耳语了令人沉醉的甜言蜜语
	ENDDATA
ELSE
	PRINTDATA
		DATA 盛赞其美貌
		DATA 称颂其纯洁心灵
		DATA 低语其性感魅力
		DATA 试图拉近亲密距离
	ENDDATA
ENDIF
PRINTL 
IF RFLAG:コマンド結果受渡し変数 == 2
	PRINTFORM 面对%CALLNAME:PLAYER%妖娆热情的攻势，%CALLNAME:TARGET%
	SIF 知識素質:性知識 < 1
		PRINT 虽然未能完全理解
	PRINTL 已然意乱情迷…
ELSEIF 知識素質:性知識 < 1
	PRINTFORML %CALLNAME:TARGET%似乎未能领会其中深意…
ELSEIF RFLAG:コマンド結果受渡し変数 == 1
	IF TALENT:恋慕
		PRINTFORML %CALLNAME:TARGET%欣喜地回应了爱的话语
	ELSE
		PRINTFORML %CALLNAME:TARGET%并未表现出反感
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	IF TALENT:恋慕
		PRINTFORML %CALLNAME:TARGET%展露欢颜
	ELSE
		PRINTFORML %CALLNAME:TARGET%微笑着转移了话题
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORML %CALLNAME:TARGET%对此似乎无动于衷…
ENDIF


