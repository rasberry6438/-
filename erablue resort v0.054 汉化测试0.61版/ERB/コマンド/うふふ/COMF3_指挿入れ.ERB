;-------------------------------------------------
;插入手指
;愛撫系コマンド，レベル1
;-------------------------------------------------
@COMNAME3
#FUNCTIONS
TSTR:コマンド名受渡 = 插入手指
SIF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 同时插入手指





;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
弱点コマンドカテゴリ = V
同時モード許可 = 2
同時モード拡張許可 = 1
オートコマンド許可 = 1


@COM_TOOLTIP_3
LOCALS = <br>■插入手指<br>
LOCALS += "将手指插入对方阴道进行爱抚的指令。<br>对处女对象使用时会造成剧烈痛苦。"
LOCALS += "<br>指令类型：Ｖ系<br>获取来源：快Ｖ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM3
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF !TALENT:処女
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 120)
ELSE
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 30)
ENDIF
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 120)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;苦痛
LOCAL = 40 * PAIN_CHECK_V(TARGET) / 1000
SIF TALENT:処女
	TIMES LOCAL , 1.50

CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

;手淫経験
EXP:PLAYER:手淫経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE3
;插入手指実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(3)
	RETURN RESULT
;Vがない場合駄目
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0

;下半身着衣
;前鎧，めくれない服，タイツ，前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

;指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;膣を何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:膣
	RETURN 0
CALL MODE_部位届く判定(部位_膣, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF EXP:TARGET:Ｖ経験 == 0
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM3
#DIM 同時キャラ番号
;下半身着衣
PRINTFORM %CALLNAME:PLAYER%对
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 的私密处缓缓插入手指…
	RETURN 0
ENDIF

SIF TEQUIP:下半身服あり
		PRINTFORM 将手探入衣物内侧
PRINTFORM 对准%CALLNAME:TARGET%的
;処女
IF TALENT:処女 == 1
	PRINT 未经人事的
;潤滑LV4以上
ELSEIF PALAM:潤滑 >= PALAMLV:4
	PRINT 湿漉漉的
ENDIF
;小柄体型の有無で分岐
IF TALENT:種族 < 0
	PRINT 幼嫩的花蕊
ELSE
	PRINT 秘裂
ENDIF
PRINTFORML 缓缓插入手指…
IF NOWEX:Ｖ絶頂
	IF TALENT:処女 == 1
		PRINTFORML %CALLNAME:TARGET%紧抓着%CALLNAME:PLAYER%，插入的手指不断抽搐收缩…
	ELSE
		PRINTFORML %CALLNAME:TARGET%流着口水，主动扭动腰肢索求快感…
	ENDIF
ELSE
	IF TALENT:処女 == 1
		PRINTFORML %CALLNAME:TARGET%因陌生触感全身僵硬…
	ELSE
		PRINTFORML %CALLNAME:TARGET%因阴道内蠕动的指尖发出甜美娇喘…
	ENDIF
ENDIF

