
@COMNAME_S9_3
#FUNCTIONS
TSTR:コマンド名受渡 = 阴道自慰

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = V
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_3
LOCALS = <br>■阴道自慰<br>
LOCALS += "命令对象在观察者面前将手指插入阴道自慰的指令。"
LOCALS += "<br>指令类型：嘿咻<br>获取来源：暴露・快Ｖ・欢乐(特定条件)・反感(特定条件)"
LOCALS += "<br>专属经验值：自慰经验(自慰执行者)、调教自慰经验(自慰执行者)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_3
;共通処理
CALL Ｖ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("膣オナ", MASTER, (自慰ターゲット保存))
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_3

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_膣オナ
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_膣オナ
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_3")
	RETURN RESULT
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):下腹部前鎧あり
	RETURN 0
;服駄目
SIF TEQUIP:(自慰ターゲット保存):下半身服あり && !TEQUIP:(自慰ターゲット保存):スカートめくり可 && !TEQUIP:(自慰ターゲット保存):ずらし可
	RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):下タイツあり && !TEQUIP:(自慰ターゲット保存):下タイツ破れ
	RETURN 0
;前張り駄目
SIF TEQUIP:(自慰ターゲット保存):下半身下着あり == 2
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｖ感度 == -2
	RETURN 0
;処女駄目
SIF TALENT:(自慰ターゲット保存):処女
	RETURN 0
;膣が無いとダメ
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 0) == 0
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):膣
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 3

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_3

PRINTFORM %CALLNAME:(自慰ターゲット保存)%
IF TEQUIP:(自慰ターゲット保存):下半身服あり && TEQUIP:(自慰ターゲット保存):ずらし可
	PRINT 褪下衣物
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり
	PRINT 褪下内衣
ENDIF
PRINTL 将手指插入阴道开始搅动…


@MESSAGE_COM_S9_3_非口上時
SIF (自慰ターゲット保存)== MASTER
	RETURN 0
SIF TFLAG:オートコマンドフラグ > 0
	RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
	PRINTFORML 似乎还不习惯被人注视着手淫，%CALLNAME:(自慰ターゲット保存)%红着脸瞪视%CALLNAME:MASTER%…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
	PRINTFORML 虽然逐渐适应了些，但%CALLNAME:MASTER%的视线仍让其感到在意…
ELSE
	PRINTFORML %CALLNAME:(自慰ターゲット保存)%在%CALLNAME:MASTER%的注视下进行自慰，竟感到某种异样的兴奋…
ENDIF
