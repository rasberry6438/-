;===================================================
;疑似性交
;===================================================
@COMNAME89
#FUNCTIONS
TSTR:コマンド名受渡 = 模拟性交

;許可時の特別文章はここに記入
IF TFLAG:オートコマンドフラグ >= 4000
	;反転時、つまりあなたが擦りつける方
	許可取り文章 = %CALLNAME:TARGET%想与%CALLNAME:PLAYER%进行[模拟性交]…
ENDIF


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_89
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_女_異性
オートコマンド許可 = 0
反転オート許可 = 3


;===================================================
;ツールチップ文章
;===================================================
@COM_TOOLTIP_89
TSTR:ツールチップ受渡 '= "<br>■模拟性交<br>"
TSTR:ツールチップ受渡 += "通过阴茎与阴唇摩擦互相给予快感的指令。<br>进入模拟性交模式，在切换模式或结束性行为前将持续触发特定效果。"
TSTR:ツールチップ受渡 += "<br>指令类型：嘿咻<br>获取来源：快Ｃ・暴露・接触・不洁"
TSTR:ツールチップ受渡 += "<br>专属经验值：无<br>"


;===================================================
;コマンド実行処理
;===================================================
@COM89
JUMP 疑似性交COM("疑似性交", 200, 150)


@疑似性交COM(モード名, 実行者快Ｃ, 対象者快Ｃ)
#DIMS モード名
#DIM 実行者快Ｃ
#DIM 対象者快Ｃ
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE(モード名, PLAYER, TARGET)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 実行者快Ｃ)
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 対象者快Ｃ)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 150)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET, PLAYER)
	EXP:TARGET:同性愛経験 += 7
SIF 同性愛性癖判定(PLAYER, TARGET)
	EXP:PLAYER:同性愛経験 += 7

RETURN 1


;===================================================
;実行許可判定
;===================================================
@CAN_COM89(ARG)
JUMP 疑似性交CAN_COM(ARG, "疑似性交", 16)


@疑似性交CAN_COM(ARG, コマンド名, 必要実行値)
#DIMS コマンド名
#DIM 必要実行値
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML %コマンド名%(%CALLNAME:TARGET%)

実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 3
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 3})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF GETBIT(TALENT:PLAYER:性別, 1) && ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 1
	PRINTFORM 精液成瘾LV{ABL:精液中毒}({ABL:精液中毒 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:Ｃ感覚
IF ABL:Ｃ感覚
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:Ｃ感覚 * 3
	PRINTFORM Ｃ感觉LV{ABL:Ｃ感覚}({ABL:Ｃ感覚 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 3
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 3})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 3
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 3})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 2
	PRINT 害羞(2)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 5
	PRINT 快感の否定(
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 厌男(12)
	＋表示フラグ = 1
ENDIF
;女嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 2 && !(TALENT:PLAYER:性別 & 2) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 厌女(12)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 爱慕(15)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 炮友(10)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 4
	PRINT 扶她(4)
	＋表示フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 春药(6)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(必要実行値以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;===================================================
;実行可能判定
;===================================================
@COM_ABLE89
;疑似性交実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(89)
	RETURN RESULT

;PLAYER側の判定
SIF !疑似性交COM_ABLE_男性キャラ(PLAYER)
	RETURN 0
;ある程度勃起してないとダメ(PLAYER)
SIF BASE:PLAYER:勃起 < 1000
	RETURN 0
;TARGET側の判定
SIF !疑似性交COM_ABLE_女性キャラ(TARGET)
	RETURN 0
;潤滑が高くないとダメ(TARGET)
SIF PALAM:TARGET:潤滑 < 2000
	RETURN 0

;お風呂場プレイ中は游戏垫が無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:游戏垫 == 0 && NOITEM == 0
		RETURN 0
ENDIF
RETURN 1


@疑似性交COM_ABLE_男性キャラ(判定キャラ番号)
#FUNCTION
#DIM 判定キャラ番号

;ちんこがない場合駄目
SIF !GETBIT(TALENT:判定キャラ番号:性別, 1) || TALENT:判定キャラ番号:Ｃ感度 == -2
	RETURNF 0
;下半身着衣
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:判定キャラ番号:下腹部前鎧あり
	RETURNF 0
SIF TEQUIP:判定キャラ番号:下タイツあり && !TEQUIP:判定キャラ番号:下タイツ破れ
	RETURNF 0
SIF TEQUIP:判定キャラ番号:下半身下着あり == 2
	RETURNF 0
;ペニスを何かに使ってるなら無理
SIF TEQUIP:判定キャラ番号:ペニス
	RETURNF 0
RETURNF 1


@疑似性交COM_ABLE_女性キャラ(判定キャラ番号)
#FUNCTION
#DIM 判定キャラ番号

;ちんこがある場合駄目
SIF GETBIT(TALENT:判定キャラ番号:性別, 1)
	RETURNF 0
;女性器がない場合駄目
SIF !GETBIT(TALENT:判定キャラ番号:性別, 0) || TALENT:判定キャラ番号:Ｖ感度 == -2
	RETURNF 0
;下半身着衣
;前鎧、めくれない服、タイツ、下着があると無理
SIF TEQUIP:判定キャラ番号:下腹部前鎧あり
	RETURNF 0
SIF TEQUIP:判定キャラ番号:下半身服あり && (!TEQUIP:判定キャラ番号:スカートめくり可 && !TEQUIP:判定キャラ番号:ずらし可)
	RETURNF 0
SIF TEQUIP:判定キャラ番号:下タイツあり && !TEQUIP:判定キャラ番号:下タイツ破れ
	RETURNF 0
SIF TEQUIP:判定キャラ番号:下半身下着あり
	RETURNF 0
;クリトリスを何かに使ってるなら無理
SIF TEQUIP:判定キャラ番号:クリトリス
	RETURNF 0
;膣を何かに使ってるなら無理
SIF TEQUIP:判定キャラ番号:膣
	RETURNF 0
RETURNF 1


;===================================================
;実行時メッセージ
;===================================================
@MESSAGE_COM89
PRINTFORM %CALLNAME:PLAYER%将勃起的阴茎抵在仰卧的%CALLNAME:TARGET%
SIF PALAM:TARGET:潤滑 >= PALAMLV:4
	PRINT 湿漉漉的
PRINTL 阴唇上扭腰摩擦…
