;-------------------------------------------------
;挤奶机
;道具使用コマンド，レベル3
;-------------------------------------------------
@COMNAME48
#FUNCTIONS
TSTR:コマンド名受渡 = 挤奶机

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 挤奶机



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_48
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = B_女
同時モード許可 = 999


@COM_TOOLTIP_48
LOCALS = <br>■挤奶机<br>
LOCALS += "安装挤乳装置持续刺激乳腺的指令。<br>进入挤奶机装备状态，在卸除或性行为结束前将持续产生快感。<br>需具备[母乳体质]特质方可使用。<br>需具备[挤奶癖好]性癖特质方可使用。<br>乳头凹陷状态需通过[乳头责弄]勃起后才能使用。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ｂ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)・乳头开发(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM48
#DIM 強さ初期値記録

IF OPTION変数:搾乳機強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■请选择初始强度
	PRINT 　　挤奶机强度： 
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 装备挤奶机", 900
	PRINTBUTTONLC "[999] 返回", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("挤奶机", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("挤奶机", PLAYER, TARGET, , , OPTION変数:搾乳機強さ初期値)
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:乳首開発フラグ = 1
RCVAR:TARGET:苦痛コマンドフラグ = 1
RCVAR:TARGET:道具コマンドフラグ = 1

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:搾乳快楽経験 ++
EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE48
;挤奶机実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(48)
	RETURN RESULT
;すでに使ってるとだめ
SIF MODE_存在判定_ターゲット側("挤奶机", TARGET)
	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:11 == 0 && NOITEM == 0
	RETURN 0
;母乳体質でないと使えない
SIF TALENT:母乳体質 == 0
	RETURN 0
;性癖を持ってないと使えない
SIF 性癖素質:搾乳プレイ < 1
	RETURN 0
;勃起してない陥没乳首はダメ
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:胸
	RETURN 0

;上半身着衣
;鎧，服，タイツ，ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0

;Ｂ感度2以下はダメ
SIF ABL:TARGET:Ｂ感覚 <= 2
	RETURN 0
;ベッドルームでないとダメ
SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM48
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 为全体对象安装挤奶机
	RETURN 0
ENDIF
PRINTFORML 为%CALLNAME:TARGET%安装挤奶机

