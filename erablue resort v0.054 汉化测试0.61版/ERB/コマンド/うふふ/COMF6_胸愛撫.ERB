;-------------------------------------------------
;爱抚胸部
;愛撫系コマンド，レベル2
;-------------------------------------------------
@COMNAME6
#FUNCTIONS
TSTR:コマンド名受渡 = 爱抚胸部
SIF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 同时爱抚胸部



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
弱点コマンドカテゴリ = B_女
同時モード許可 = 2
同時モード拡張許可 = 1
オートコマンド許可 = 1
連続オート許可 = 1


@COM_TOOLTIP_6
LOCALS = <br>■爱抚胸部<br>
LOCALS += "温柔爱抚对方胸部的指令。"
LOCALS += "<br>指令类型：嘿咻or被插入中<br>获取来源：快Ｂ・暴露・接触"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM6
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 5
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 5

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE6
;爱抚胸部実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(6)
	RETURN RESULT
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0

;鎧があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
;指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;胸を何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:胸
	RETURN 0
CALL MODE_部位届く判定(部位_胸, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	
	
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM6
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 的胸部揉捏抚弄…
	RETURN 0
ENDIF

IF MODE_存在判定_順不同("ＶＡ挿入系", PLAYER, TARGET)
	PRINTFORM 在与%CALLNAME:TARGET%保持插入的状态下，对%OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM 对%CALLNAME:TARGET%的%OPPAI_DESCRIPTION(TARGET)%
ENDIF
;欲望のPALAMLV3以上＋
;小柄体型
IF TALENT:種族 < 0
	PRINTL 如同轻抚羽毛般温柔触碰…
ELSE
	PRINTL 进行揉捏爱抚…
ENDIF

