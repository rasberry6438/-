
@COMNAME_S20_1
#FUNCTIONS
TSTR:コマンド名受渡 = 探入舌头

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_20_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中_セクハラ")
弱点コマンドカテゴリ = S
オートコマンド許可 = 1

@COM_TOOLTIP_S20_1
LOCALS = <br>■探入舌头<br>
LOCALS += "接吻模式中将舌头探入对方口腔的指令。<br>进入深吻模式。<br>效果受[舌头灵巧][猫舌]特性影响。"
LOCALS += "<br>指令类型：嘿咻or被插入中or性骚扰<br>获取来源：暴露・接触・快Ｓ・不洁・痛苦(特定条件时)"
LOCALS += "<br>专属经验值：接吻经验(PLAYER＆TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S20_1
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("ディープキス", PLAYER, TARGET)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 250)
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 250)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 250)
CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 150)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF

IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
ENDIF

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 3
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 3


SIF TALENT:恋慕
	EXP:愛情経験 += 1

SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号,@"<font color='#%カラーパレット_HTML("薄ピンク")%'>向%CALLNAME:TARGET%的[嘴唇]献上初吻</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号,@"<font color='#%カラーパレット_HTML("薄ピンク")%'>向%CALLNAME:PLAYER%的[嘴唇]献上初吻</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

RETURN 1


@CAN_COM_S20_1(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 探入舌头(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 侍奉精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 2
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 2})
	＋表示フラグ = 1
ENDIF

;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF

;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 害羞(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 污臭钝感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 1
	PRINT 污臭敏感(1)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 否定快感(1)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 挚爱(10)
	＋表示フラグ = 1
ENDIF
;キス嗜好
IF TALENT:キス嗜好 == 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 10
	PRINT 喜欢接吻(10)
	＋表示フラグ = 1
ELSEIF TALENT:キス嗜好 == 2
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 吻魔(999)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S20_1
;探入舌头実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("20_1")
	RETURN RESULT
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0
;キスモードじゃない時はアウト
SIF !MODE_存在判定_順不同("キス", PLAYER, TARGET)
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S20_1
PRINTFORM %CALLNAME:PLAYER%
IF TALENT:TARGET:恋慕
	PRINTFORML 的舌尖轻触%CALLNAME:TARGET%唇瓣时，
	PRINTFORML %CALLNAME:TARGET%主动启唇缠绕上对方的舌…
ELSE
	PRINTFORML 将舌头挤入%CALLNAME:TARGET%唇缝，强行与对方舌头交缠…
ENDIF

SIF TCVAR:TARGET:泥酔眠姦 && TCVAR:TARGET:言いなり
	PRINTFORM 误把%CALLNAME:PLAYER%当作心上人的醉醺醺%CALLNAME:TARGET%，%TEXTR("甜蜜地/热情地")%纠缠上舌尖…