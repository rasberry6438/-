;-------------------------------------------------
;乳头夹
;道具使用コマンド，レベル2
;-------------------------------------------------
@COMNAME47
#FUNCTIONS
TSTR:コマンド名受渡 = 乳头夹

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 乳头夹


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_47
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = B
同時モード許可 = 999


@COM_TOOLTIP_47
LOCALS = <br>■乳头夹<br>
LOCALS += "安装自动振动器具持续刺激乳头的指令。<br>进入乳首夹装备状态，在卸除或性行为结束前将持续产生快感。<br>乳头凹陷状态需通过[乳头责弄]勃起后才能使用。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ｂ・暴露・接触"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)・乳头开发(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM47
#DIM 強さ初期値記録

IF OPTION変数:乳头夹強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■请选择初始强度
	PRINT 　　乳头夹强度：
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 装备乳头夹", 900
	PRINTBUTTONLC "[999] 返回", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("乳头夹", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("乳头夹", PLAYER, TARGET, , , OPTION変数:乳头夹強さ初期値)
ENDIF
RCVAR:乳首開発フラグ = 1
RCVAR:TARGET:道具コマンドフラグ = 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER,200)
SIF TALENT:陥没乳首
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 120)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE47
;乳头夹実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(47)
	RETURN RESULT
;使用済みだとだめ
;SIF MODE_存在判定_ターゲット側("乳头夹", TARGET)
;	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:10 == 0 && NOITEM == 0
	RETURN 0
;勃起してない陥没乳首はダメ
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0

;上半身着衣
;鎧，差し込めないorはだけない服，タイツ，ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:胸 && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM47
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 为全体对象的乳头安装电动乳首夹
	RETURN 0
ENDIF
PRINTFORM %CALLNAME:TARGET%的乳头
SIF TEQUIP:上半身服あり && TEQUIP:手差し込み可
	PRINTL 隔着衣物缝隙
PRINTL 被夹上震动乳夹


