;-------------------------------------------------
;肛门拉珠
;道具使用コマンド，レベル3
;-------------------------------------------------
@COMNAME46
#FUNCTIONS
TSTR:コマンド名受渡 = 肛门拉珠

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 肛门拉珠



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_46
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系&Ａ系")
弱点コマンドカテゴリ = A
SIF !MODE_存在判定_ターゲット側("肛门拉珠", TARGET) && 初期設定中 == 0
	弱点コマンドカテゴリ = 
同時モード許可 = 999



@COM_TOOLTIP_46
LOCALS = <br>■肛门拉珠<br>
LOCALS += "插入肛门拉珠持续刺激肠道的指令。<br>进入肛门拉珠装备状态，在卸除或性行为结束前将持续产生快感。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ａ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM46
;着脱の処理
CALL MODE_CHANGE("肛门拉珠", PLAYER, TARGET)

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;装着時
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE46
;肛门拉珠実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(46)
	RETURN RESULT
;すでに使ってるとだめ
;SIF MODE_存在判定_ターゲット側("肛门拉珠", TARGET)
;	RETURN 0
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:6 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;後鎧，めくれない服，タイツがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:アナル && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM46
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%将
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 肛门拉珠逐颗推入全体对象的菊穴…
	RETURN 0
ENDIF
PRINTFORML 将肛门拉珠逐颗推入%CALLNAME:TARGET%的菊穴…
