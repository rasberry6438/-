
@COMNAME306
#FUNCTIONS
TSTR:コマンド名受渡 = 身体接触

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_306
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
睡眠中許可 = 1

@COM_TOOLTIP_306
LOCALS = <br>■身体接触<br>
LOCALS += "温柔接触对方身体的指令。<br>执行时可选择接触部位。"
LOCALS += "<br>指令类型：性骚扰<br>获取来源：接触・暴露・欢乐(特定条件)・不快(特定条件)・反感(特定条件)等"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM306
#DIMS CONST 選択先順番制御 = "触摸背部", "触摸耳朵"
#DIM 選択先保存
;身体に触れる

IF RFLAG:サイドボタン受渡し変数
	;サイドボタンから来た時は選択処理挟まず直接コマンド実行に飛ぶ
	選択先保存 = RFLAG:サイドボタン受渡し変数
ELSE
	DRAWLINE
	PRINTL 要接触哪个部位？
	DRAWLINE
	FOR LOCAL, 0, VARSIZE("選択先順番制御")
		CALLFORM 身体に触れる_ABLE_%選択先順番制御:LOCAL%
		IF RESULT
			CALLFORMF COMNAME_S306_{RESULT}
			LOCALS '= TSTR:コマンド名受渡
			PRINTBUTTON @"[{LOCAL}] %LOCALS%", LOCAL
			PRINTL 
		ENDIF
	NEXT
	VARSET RESULTS
	ENUMFUNCBEGINSWITH "身体に触れる_ABLE_"
	FOR LOCAL, 0, RESULT
		CALLFORM %RESULTS:LOCAL%
		IF RESULT
			CALLFORMF COMNAME_S306_{RESULT}
			LOCALS '= TSTR:コマンド名受渡
			SIF MATCH(選択先順番制御, LOCALS)
				CONTINUE
			PRINTBUTTON @"[{LOCAL + 100}] %LOCALS%", LOCAL + 100
			PRINTL 
		ENDIF
	NEXT
	PRINTL 
	PRINTBUTTON "[999] 返回", 999
	PRINTL 
	DRAWLINE
	BINPUT
	LOCAL = RESULT
	IF LOCAL == 999
		RETURN -1
	ELSEIF LOCAL < 100
		CALLFORM 身体に触れる_ABLE_%選択先順番制御:LOCAL%
		選択先保存 = RESULT
	ELSE
		CALLFORM %RESULTS:(LOCAL - 100)%
		選択先保存 = RESULT
	ENDIF
ENDIF

;COM実行処理
;弱点コマンド処理の都合上、CALL_COMを経由させる
RSTR:SELECTCOM_S受け渡し = 306_{選択先保存}
SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%
CALL CALL_COM(TARGET)
RETURN RESULT

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE306
;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(306)
	RETURN RESULT
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM306
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%

@MESSAGE_COM306_非口上時
CALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
