
@COMNAME353
#FUNCTIONS
TSTR:コマンド名受渡 = 请求性行为

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_353
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_353
LOCALS = <br>■请求性行为<br>
LOCALS += "向对象提出性行为请求并将其引导至床上的指令。<br>这是唯一会降低好感度的指令。达成性行为所需次数随角色特质增减。<br>此外当性知识为负值时必定成功。"
LOCALS += "<br>指令类型：性骚扰<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;可視化できるよう別関数化
@COM353_SUB(ARG)
#FUNCTION
VARSET LOCAL
SWAP LOCAL:1, TARGET
TARGET = ARG

SIF BASE:性欲 > 1000
	LOCAL += 10
IF TCVAR:トランス残り時間 > 0
	LOCAL += 15
ELSEIF BASE:酩酊 >= 500
	LOCAL += 15
ELSEIF BASE:酩酊 > 0
	LOCAL += 5
ENDIF
SIF TALENT:処女
	LOCAL -= 20
SIF TALENT:度胸 == -1
	LOCAL -= 5
SIF TALENT:マイペース == -1
	LOCAL += 10
SIF TALENT:態度
	LOCAL -= TALENT:態度 * 5
SIF TALENT:応答
	LOCAL -= TALENT:応答 * 5
SIF TALENT:プライド
	LOCAL -= TALENT:プライド * 10
SIF TALENT:ツンデレ
	LOCAL += 5
SIF TALENT:自制心
	LOCAL -= TALENT:自制心 * 5
SIF TALENT:感情乏しい
	LOCAL += 5
SIF TALENT:性的興味
	LOCAL += TALENT:性的興味 * 10
SIF TALENT:一線越えない
	LOCAL -= 15
SIF TALENT:性欲
	LOCAL += TALENT:性欲 * 5
SIF TALENT:貞操
	LOCAL -= TALENT:貞操 * 15
SIF TALENT:抵抗
	LOCAL -= 10
SIF TALENT:羞恥心
	LOCAL -= TALENT:羞恥心 * 5
SIF TALENT:献身的
	LOCAL += 5
SIF TALENT:倒錯的
	LOCAL += 10
IF TALENT:性別嗜好 == 1 && TALENT:PLAYER:性別 == 2
	LOCAL -= 20
ELSEIF TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1
	LOCAL -= 20
ELSEIF TALENT:性別嗜好 == 3
	LOCAL -= 20
ENDIF
IF TALENT:恋人持ち == 1
	LOCAL -= 20
ELSEIF TALENT:恋人持ち == 2
	LOCAL -= 30
ELSEIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:TARGET:人物番号} Or 対象キャラ２ = {CFLAG:TARGET:人物番号}) And 関係性種別 = '恋慕'")
	LOCAL -= 20
ELSEIF TALENT:恋人持ち == 3
	LOCAL -= 10
ENDIF
IF TALENT:子持ち == 1
	LOCAL -= 10
ENDIF
SIF TALENT:淫乱 == 1
	LOCAL += 999


LOCAL += ABL:欲望 * 5
LOCAL += TALENT:従業員 * 10
LOCAL += TALENT:定住者 * 10

SWAP LOCAL:1, TARGET

RETURNF LOCAL

;请求性行为
@COM353
#DIM 下降基準値
#DIM 今回成功度
#DIM 成功度上限
#DIM 成功度下限

;まず成功度の蓄積量を判定、蓄積100で成功
;想定としては有利素質を盛りに盛って２回目（つまり上限は99）
;不利素質をどれだけ持っていても蓄積可能に（つまり下限は1）、ただ100回交渉するのは現実的じゃないのでZPショップで下限上昇出来るようにする

今回成功度 = CFLAG:好感度レベル / 2
IF CFLAG:TARGET:発情期フラグ < 0
	今回成功度 += 恋慕_性愛比較(TARGET) * 2
ELSE
	今回成功度 += 恋慕_性愛比較(TARGET) / 2
ENDIF
今回成功度 += COM353_SUB(TARGET)
SIF フィールド展開:情欲強化 && 今回成功度 > 0
	今回成功度 = 今回成功度 * 3 / 2
今回成功度 += (ABL:PLAYER:話術技能 - 3) * 5
今回成功度 += COM_353_性交渉回数取得(TARGET) * 30
;遊び人素質による補正 （下限の底上げ）
SELECTCASE あなた特殊能力:遊び人
	CASE IS >= 5
		成功度下限 = 50
	CASE 4
		成功度下限 = 33
	CASE 3
		成功度下限 = 20
	CASE 2
		成功度下限 = 15
	CASE 1
		成功度下限 = 9
	CASEELSE
		成功度下限 = 0
ENDSELECT
;好感度が無二の友未満である場合、大きく成功度と下限が減少
SELECTCASE CFLAG:好感度レベル
	CASE IS <= 関係閾値:2
		;顔見知り以下
		今回成功度 -= 55
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 = -1
	CASE IS <= 関係閾値:3
		;友人
		今回成功度 -= 40
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 /= 8
	CASE IS <= 関係閾値:4
		;親友
		今回成功度 -= 20
		SIF 今回成功度 > 0
			今回成功度 /= 2
		成功度下限 /= 2
ENDSELECT

;二回目以降は一発OKもアリ
IF BASE:酩酊 >= 500
	成功度上限 = 99 + CFLAG:TARGET:お願いックス回数 + CFLAG:TARGET:泥酔ックス回数
ELSE
	成功度上限 = 99 + CFLAG:TARGET:お願いックス回数
ENDIF

RFLAG:コマンド結果受渡し変数２ = 今回成功度
;今回セフレ取得可能な場合は上限判定しない
SIF !COM_353_セフレ取得判定(TARGET)
	RFLAG:コマンド結果受渡し変数２ = MIN(RFLAG:コマンド結果受渡し変数２, 成功度上限)
RFLAG:コマンド結果受渡し変数２ = MAX(RFLAG:コマンド結果受渡し変数２, 1 + 成功度下限)

CFLAG:TARGET:お願いックス蓄積値 += RFLAG:コマンド結果受渡し変数２
;性知識-2の場合、無二の友以上なら基本OK
IF 知識素質:TARGET:性知識 < -1 && CFLAG:好感度レベル > 関係閾値:4
	CFLAG:TARGET:お願いックス蓄積値 = 101
ENDIF

;セフレ不可の場合、問答無用でゼロにする
IF GETBIT(TALENT:TARGET:陥落不可, 1) && BASE:酩酊 < 500 && TCVAR:トランス残り時間 <= 0
	RFLAG:コマンド結果受渡し変数２ = 0
	CFLAG:TARGET:お願いックス蓄積値 = 0
ENDIF

IF CFLAG:TARGET:お願いックス蓄積値 > 99
	RFLAG:コマンド結果受渡し変数 = 1
	CFLAG:TARGET:お願いックス蓄積値 = 0
	;うふふへ移行
	CFLAG:PLAYER:うふふ = 1
	IF BASE:酩酊 >= 500
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:泥酔ックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>灌醉%CALLNAME:TARGET%后得手</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%醉酒后与%CALLNAME:PLAYER%越界</font><img src='えっちハート'>","うふふ")
	ELSEIF TCVAR:トランス残り時間 > 0
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:お願いックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>剥夺%CALLNAME:TARGET%判断力后得手</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>一时意乱情迷与%CALLNAME:PLAYER%越界</font><img src='えっちハート'>","うふふ")
	ELSE
		SIF TCVAR:TARGET:性交渉成功フラグ == 0
			CFLAG:TARGET:お願いックス回数 += 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>恳求%CALLNAME:TARGET%发生关系</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>经%CALLNAME:PLAYER%恳求而献出身体</font><img src='えっちハート'>","うふふ")
	ENDIF
	IF 初体験日取得("初うふふ", TARGET, MASTER) == 0
		IF TALENT:TARGET:恋人持ち > 0 && !TALENT:TARGET:浮気
			TALENT:TARGET:浮気 = 1
			RFLAG:コマンド結果受渡し変数３ = 1
			CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"素質[出轨]关系已建立","うふふ実績")
		ENDIF
		;泥酔時かつ初うふふの場合、翌日釘刺しに来る
		IF BASE:酩酊 >= 500 && 知識素質:TARGET:性知識 > -1
			CSTR:TARGET:特殊起床パターン記録 = 初うふふ泥酔ックス後
		ELSEIF TCVAR:トランス残り時間 > 0 && 知識素質:TARGET:性知識 > -1
			CSTR:TARGET:特殊起床パターン記録 = 初うふふ気の迷いックス後
		ENDIF
	ENDIF
	TCVAR:TARGET:性交渉成功フラグ = 1
	CALL うふふ開始キャラ処理(TARGET, "性交渉お願い")

	IF COM_353_セフレ取得判定(TARGET)
		;セフレ取得処理
		;・セフレ取得済の場合はコマンドが実行できなくなるので二重取得の考慮は不要
		SETBIT CFLAG:1 , 0
		TALENT:TARGET:セフレ = 1
		初体験日:セフレ = DAY
		RFLAG:コマンド結果受渡し変数４ = TALENT:陥落不可
		CLEARBIT TALENT:陥落不可, 1
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%被[炮友]攻略</font><img src='えっちハート'>","うふふ")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>获得攻略特质[炮友]</font><img src='えっちハート'>","うふふ")
	ENDIF
ELSE
	RFLAG:コマンド結果受渡し変数 = 0

	;好感度レベルを下げる。拒否度が高いと更に下がる
	IF CFLAG:TARGET:好感度レベル > 関係閾値:1
		下降基準値 = 20
		SIF RFLAG:コマンド結果受渡し変数２ < 16
			下降基準値 += 10

		;好感度を下降基準値に応じてそれぞれ下降
		;友情信頼は下降基準値通りに低下
		CFLAG:TARGET:友情度 = MIN(CFLAG:TARGET:友情度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - 下降基準値))
		CFLAG:TARGET:信頼度 = MIN(CFLAG:TARGET:信頼度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - 下降基準値))
		;恋心は１．５倍
		CFLAG:TARGET:恋心度 = MIN(CFLAG:TARGET:恋心度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - (下降基準値 * 3 / 2)))
		;性愛は半分
		CFLAG:TARGET:性愛度 = MIN(CFLAG:TARGET:性愛度, 経験値テーブル:MAX(0, CFLAG:TARGET:好感度レベル - (下降基準値  / 2)))
		;固有好感度は変化させない（好感度レベルに関係ないため）

		;好感度レベルの調整
		FOR LOCAL, 0, 100
			IF 経験値テーブル:LOCAL >= MAX(CFLAG:TARGET:友情度, CFLAG:TARGET:信頼度, CFLAG:TARGET:恋心度, CFLAG:TARGET:性愛度)
				;顔見知り未満にはならない
				CFLAG:TARGET:好感度レベル = MAX(LOCAL, 1 + 関係閾値:1)
				BREAK
			ENDIF
		NEXT
	ELSE
		;他人は強制的に下限値
		CFLAG:TARGET:好感度レベル = 1

		;好感度をレベルに応じた値に減少
		CFLAG:TARGET:友情度 = MIN(CFLAG:TARGET:友情度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:信頼度 = MIN(CFLAG:TARGET:信頼度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:恋心度 = MIN(CFLAG:TARGET:恋心度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
		CFLAG:TARGET:性愛度 = MIN(CFLAG:TARGET:性愛度, 経験値テーブル:(CFLAG:TARGET:好感度レベル))
	ENDIF

	;恋慕レベルを下げる。拒否度が高いと更に下がる
	CFLAG:TARGET:恋慕レベル -= 20
	SIF RFLAG:コマンド結果受渡し変数２ < 16
		CFLAG:TARGET:恋慕レベル -= 10
	CFLAG:TARGET:恋慕レベル = MAX(CFLAG:TARGET:恋慕レベル, 0)
	;恋慕度をレベルに応じた値に減少
	CFLAG:TARGET:恋慕度 = MIN(CFLAG:TARGET:恋慕度, 経験値テーブル:(CFLAG:TARGET:恋慕レベル))
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE353
;お願いする実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(353)
	RETURN RESULT
;飲み会時はだめ
SIF TFLAG:調教モード == 3
	RETURN 0
;ハーヴィンオナホ中はだめ
SIF MODE_存在判定_プレイヤー側("ハーヴィンオナホ系", PLAYER)
	RETURN 0
;全裸散歩中はだめ
SIF MODE_存在判定_プレイヤー側("全裸散歩", PLAYER)
	RETURN 0
;二人きりでないとだめ
SIF GET_TARGETNUM() > 1 && フィールド展開:无视嘿咻行为 == 0
	RETURN 0
;友人以上(遊び人素質を持っていれば可能)
SIF !あなた特殊能力:遊び人 && CFLAG:好感度レベル < 1 + 関係閾値:2
	RETURN 0
;既に恋慕・セフレを得ている場合はだめ
SIF 陥落チェック_性的(TARGET)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM353
; RFLAG:コマンド結果受渡し変数    お願い判定結果(1でお願い成功/うふふ移行)
; RFLAG:コマンド結果受渡し変数２  成功度の蓄積量
; RFLAG:コマンド結果受渡し変数３  [出轨]素質の取得有無(1で[出轨]素質取得)
; RFLAG:コマンド結果受渡し変数４  [炮友]素質取得前の陥落不可状態判定

IF RFLAG:コマンド結果受渡し変数
	PRINTFORM %CALLNAME:TARGET%
	IF 知識素質:TARGET:性知識 < 0
		PRINT 带着茫然不解的神情
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 16
		PRINT 露出妥协的姿态
	ENDIF
	PRINTFORML 回应了%CALLNAME:PLAYER%的请求。
	IF 知識素質:TARGET:性知識 < 0
		PRINT 在尚未理解将要发生什么的情况下，
	ELSEIF BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		PRINT 仰卧于床榻，
	ELSEIF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		PRINT 自浴池起身，
	ELSEIF SWIMROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:TARGET:現在位置) && CSTR:TARGET:着せ替え服 == "泳装"
		PRINT 带着湿漉漉的泳装，
	ENDIF
	PRINTFORML %CALLNAME:TARGET%静候着%CALLNAME:PLAYER%的下一步动作…
	IF RFLAG:コマンド結果受渡し変数３
		PRINTFORM %CALLNAME:TARGET%
		SETCOLOR カラーパレット("えっちな色")
		PRINT [出轨]
		RESETCOLOR
		PRINT 特质已激活……
		PRINTL
	ENDIF
	IF TALENT:TARGET:セフレ
		PRINTL
		;淫乱のキャラは1回でセフレになって文章と齟齬が出るので表示しない
		SIF !TALENT:TARGET:淫乱
			PRINTFORML %CALLNAME:TARGET%与%CALLNAME:PLAYER%多次交合后逐渐失去抗拒……
		PRINTFORM %CALLNAME:TARGET%
		SETCOLOR カラーパレット("えっちな色")
		PRINT [炮友]
		RESETCOLOR
		PRINT 关系已建立
		PRINTL
		IF GETBIT(RFLAG:コマンド結果受渡し変数４, 1)
			;
			IF GETBIT(TALENT:TARGET:陥落不可, 0)
				PRINTFORML %CALLNAME:TARGET%的[无法攻略]转变为[无法爱慕]。
			ELSE
				PRINTFORML %CALLNAME:TARGET%的[无法炮友]特质已消除。
			ENDIF
		ENDIF
	ENDIF
ELSE
	PRINTFORM 对于%CALLNAME:PLAYER%的请求，%CALLNAME:TARGET%
	IF RFLAG:コマンド結果受渡し変数２ < 16
		PRINT 斩钉截铁地
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 31
		PRINT 面露无奈地
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 50
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 71
		PRINT 犹豫再三后
	ELSEIF RFLAG:コマンド結果受渡し変数２ < 99
		PRINT 涨红着脸
	ENDIF
	PRINTL 拒绝了
	
	LOCAL = 100
	SIF RFLAG:コマンド結果受渡し変数２ > 0
		LOCAL = (100 - CFLAG:TARGET:お願いックス蓄積値) / RFLAG:コマンド結果受渡し変数２
	IF LOCAL < 1
		;次でイケる
		PRINTL 似乎再坚持一次就能成功…
	ELSEIF LOCAL < 2
		;あと２回
		PRINTL 感觉近期内就会答应请求…
	ELSEIF LOCAL < 4
		;あと３～４回
		PRINTL 需要再坚持数次才能获得同意…
	ELSEIF LOCAL < 10
		;５～１０回
		PRINTL 需要极强的耐心才能说服…
	ELSE
		;それ以上
		PRINTL 若不采取特殊手段，当前方式几乎不可能成功…
	ENDIF

	IF あなた特殊能力:フレンドリーセンス
		PRINTL
		PRINTFORML 请求累积值：{CFLAG:TARGET:お願いックス蓄積値, 2} / 100
		PRINTL ※达到100时请求成功
	ENDIF
ENDIF

@COM_353_性交渉回数取得(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 性交渉回数
性交渉回数  = CFLAG:対象キャラ:お願いックス回数
性交渉回数 += CFLAG:対象キャラ:泥酔ックス回数 / 3
性交渉回数 += EXP:対象キャラ:貸出経験 / 2
RETURNF 性交渉回数

@COM_353_セフレ取得判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
SIF TALENT:対象キャラ:淫乱
	RETURNF 1
SIF COM_353_性交渉回数取得(対象キャラ) >= 4 
	RETURNF 1
RETURNF 0

