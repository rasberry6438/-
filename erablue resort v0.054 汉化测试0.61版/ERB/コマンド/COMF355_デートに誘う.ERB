
@COMNAME355
#FUNCTIONS
TSTR:コマンド名受渡 = 邀请约会

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_355
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_355
LOCALS = <br>■邀请约会<br>
LOCALS += "邀请好感度达到阈值以上的对象进行约会的指令。<br>仅限邀请一人。<br>基础约会时长为360分钟或持续至晚间21:00，隐匿状态及嘿咻期间计时继续。"
LOCALS += "<br>指令类型：日常互动<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM355

CALLFORMF COMNAME355
SELECTCOM_NAME '= TSTR:コマンド名受渡

IF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100
	PRINTFORMW %CALLNAME:TARGET%今日正与%CALLNAME:(ABS(CFLAG:TARGET:デート) - 100)%约会，无法接受邀请
	RETURN -1
ENDIF

;同行者は解除
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	IF CFLAG:(TARGET:LOCAL):同行
		CFLAG:(TARGET:LOCAL):同行 = 0
	ENDIF
NEXT

PRINTFORMW 与%CALLNAME:TARGET%开始约会。
CFLAG:TARGET:デート = PLAYER + 100
CFLAG:PLAYER:デート = TARGET + 100

CFLAG:TARGET:デート残り時間 = 360
CFLAG:TARGET:デートスコア = 0
TFLAG:TARGET変更用 = TARGET
TIME += 1
CALL 初体験日登録処理(TARGET, PLAYER, "初デート", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "", "", 1)
CALL 初体験日登録処理(TARGET, PLAYER, "デート", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "", "", 0)
IF RESULT
	;相手とのデートが初の場合にここに入る
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"初次与%CALLNAME:PLAYER%约会","日常")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "初デート", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "", "", 1)
CALL 初体験日登録処理(PLAYER, TARGET, "デート", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "", "", 0)
IF RESULT
	;相手とのデートが初の場合にここに入る
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"初次与%CALLNAME:TARGET%约会","日常")
ENDIF

;SOURCEまで回さないのでここで口上を表示させる
RESULT = 0
RESULTS =
IF FLAG:7 > 0
	TRYCALLFORM KOJO_COM_{NO:TARGET}_355(TARGET)
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE355
;連れ出す実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(355)
	RETURN RESULT
;TARGETの好感度レベル
SIF CFLAG:好感度レベル < 1 + 関係閾値:3
	RETURN 0
;相手にとってあなたが恋愛対象であること
SIF 恋愛対象外チェック(TARGET, PLAYER)
	RETURN 0
;デート中はダメ
SIF CFLAG:TARGET:デート
	RETURN 0
;２０時半以降はダメ
SIF TIME >= 1230
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------



@デート採点処理(対象キャラ)
#DIM 対象キャラ
#DIM スコア記録

スコア記録 = CFLAG:対象キャラ:デートスコア
DRAWLINE
PRINTW 约会时间结束……
PRINTFORML 本次约会综合评价约为{スコア記録, 6}分。
SELECTCASE スコア記録
	CASE IS < 7500
		PRINTFORML 似乎让%CALLNAME:対象キャラ%感到乏味……
		PRINTL 下次邀请前需要更周密地规划行程
	CASE IS < 15000
		PRINTFORML %CALLNAME:対象キャラ%虽感愉悦，但整体较为平淡
		PRINTL 或许准备礼物能提升体验
	CASE IS < 30000
		PRINTFORML %CALLNAME:対象キャラ%显得相当尽兴
		PRINTL 但不应满足于此，继续追求更高境界！
	CASE IS < 60000
		PRINTFORML %CALLNAME:対象キャラ%看上去很开心！
		PRINTL 如此款待水准已堪称优秀
	CASEELSE
		PRINTFORML %CALLNAME:対象キャラ%呈现完全满足状态！
		PRINTL 称之为极致体验亦不为过！
ENDSELECT
FORCEWAIT
DRAWLINE

DT_ROW_ADD "デートスコアデータベース", "デート相手人物番号", CFLAG:対象キャラ:人物番号, "デートスコア", スコア記録, "デート日時", DAY * 1440 + TIME
CALL デートリセット処理(対象キャラ)

;スコアに応じて逆告白発生、非ダウン時のみ
IF BASE:対象キャラ:体力 > 0
	CALL デート後逆告白処理(対象キャラ, スコア記録)
ENDIF

@デートリセット処理(対象キャラ)
#DIM 対象キャラ

CFLAG:対象キャラ:デート残り時間 = 0
CFLAG:対象キャラ:デートスコア = 0
CFLAG:対象キャラ:デート = 0
CFLAG:MASTER:デート = 0
;デートに使うスコア変数などをリセット


@デートスコア上昇処理(対象キャラ)
#DIM 対象キャラ
#DIM スコア加算値

スコア加算値 = 0

;好感度の上昇量を使う
スコア加算値 += NOWEX:対象キャラ:友情度上昇格納 / 3
スコア加算値 += NOWEX:対象キャラ:信頼度上昇格納 / 2
スコア加算値 += NOWEX:対象キャラ:恋心度上昇格納
スコア加算値 += NOWEX:対象キャラ:性愛度上昇格納 / 3

;コマンドの経過時間で差が出ないよう、消費時間でかける
;10で割るのは数値のインフレ防止
スコア加算値 = スコア加算値 / 10 * TIME_PROGRESS(TFLAG:行動前時刻)

CFLAG:対象キャラ:デートスコア += スコア加算値


@デート履歴表示画面(ソートフラグ)
#DIM イベント件数
#DIM 行カウント
#DIM ページ番号
#DIM 開始ページ
#DIM DYNAMIC 検索リザルトID, 100000
#DIMS フィルタ
#DIM 表示行数 = 20; 履歴の表示行数
;#DIM 履歴表示フィルタ
#DIM L_C
#DIM ソートフラグ
#DIM 表示行保存
#DIMS ソートルール

;データ取得
IF ソートフラグ == 1
	ソートルール = デート相手人物番号 ASC
ELSEIF ソートフラグ == 2
	ソートルール = デートスコア DESC
ELSE
	ソートルール = 
ENDIF

VARSET 検索リザルトID
イベント件数 = DT_SELECT("デートスコアデータベース", , ソートルール, 検索リザルトID)


$表示部分
;ここで立ち絵表示
CALL 画像描画終了
L_C = LINECOUNT
IF GETBIT(画像サイズ切り替えOPTION記憶, 2)
	HTML_PRINT @"<div rect='4062, 37, 1125, 2250'>%短冊グラ表示用文字列関数(MASTER)%</div>", 1
ELSE
	HTML_PRINT @"<div rect='4062, 37, 1125, 1125'>%顔グラ表示用文字列関数(MASTER)%</div>", 1
ENDIF
PRINTFORM ■%NAME表示(MASTER)%
PRINTL
PRINTL
PRINTL □ 约会记录 □---------------------------------------------------------------

表示行保存 = LINECOUNT

FOR 行カウント, 能力表示現在ページ * 表示行数, 能力表示現在ページ * 表示行数 + 表示行数
	SIF 行カウント >= イベント件数
		BREAK
	SIF FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("デートスコアデータベース", 検索リザルトID:行カウント, "デート相手人物番号", 1)) < 0
		CONTINUE
	CALL 任意日付表示(DT_CELL_GET("デートスコアデータベース", 検索リザルトID:行カウント, "デート日時", 1))
	PRINTFORM 　%@"%CALLNAME:FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("デートスコアデータベース", 検索リザルトID:行カウント, "デート相手人物番号", 1))%とのデート", 26, LEFT%
	PRINTFORM 　评分：%NUM_FORMAT(DT_CELL_GET("デートスコアデータベース", 検索リザルトID:行カウント, "デートスコア", 1)), 9%
	PRINTL 
NEXT

IF LINECOUNT - 表示行保存 < 16
	FOR 行カウント, 0, 16 - (LINECOUNT - 表示行保存)
		PRINTL
	NEXT
ENDIF

PRINTL
LOCALS = 
IF イベント件数 > 表示行数
	IF 能力表示現在ページ > 5
		LOCALS += @"<button value='-1'>[0]</button>..."
		開始ページ = 能力表示現在ページ - 5
	ELSE
		LOCALS += @"　　　"
		開始ページ = 0
	ENDIF
	FOR ページ番号, 0, 11
		IF イベント件数 > (ページ番号 + 開始ページ) * 表示行数
			SIF 能力表示現在ページ == ページ番号 + 開始ページ
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>"
			LOCALS += @"<button value='{(ページ番号 + 開始ページ) * -1 - 1}'>[{ページ番号 + 開始ページ}]</button> "
			SIF 能力表示現在ページ == ページ番号 + 開始ページ
				LOCALS += @"</font>"
		ENDIF
	NEXT
	IF イベント件数 > (MAX(能力表示現在ページ, 5) + 5) * 表示行数
		LOCALS += @"<button value='{((イベント件数 - 1) / 表示行数) * -1 - 1}'>...[{(イベント件数 - 1) / 表示行数}]</button> "
	ENDIF
ELSE
	能力表示現在ページ = 0
ENDIF
HTML_PRINT LOCALS, 1
; IF 能力表示現在ページ > 0
; 	PRINTBUTTON "[900] 前ページへ", 900
; ELSE
; 	PRINT 　　　　　　　　
; ENDIF
; PRINT 　　　
; IF イベント件数 > 能力表示現在ページ * 表示行数 + 表示行数
; 	PRINTBUTTON "[901] 次ページへ", 901
; ENDIF
PRINTL 
PRINTL 

IF ソートフラグ == 0
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1014] 按日期排序
	RESETCOLOR
ELSE
	PRINTBUTTON "[1014] 按日期排序", 1014
ENDIF
PRINT  / 
IF ソートフラグ == 1
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1015] 按人物排序
	RESETCOLOR
ELSE
	PRINTBUTTON "[1015] 按人物排序", 1015
ENDIF
PRINT  / 
IF ソートフラグ == 2
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1016] 按评分排序
	RESETCOLOR
ELSE
	PRINTBUTTON "[1016] 按评分排序", 1016
ENDIF


@デート後逆告白処理(対象キャラ, スコア記録)
#DIM 対象キャラ
#DIM スコア記録

;まずオプション
IF OPTION変数:デート後告白禁止
	RETURN 0
ENDIF

;前提条件
;二人きりのみ
SIF GET_TARGETNUM() > 1
	RETURN 0
;恋慕候補のみ
SIF CFLAG:対象キャラ:ゲージ起動分類 != 1
	RETURN 0
;相手にとってあなたが恋愛対象であること
SIF 恋愛対象外チェック(対象キャラ, PLAYER)
	RETURN 0
;既に恋慕はダメ
SIF TALENT:対象キャラ:恋慕
	RETURN 0
;誰かと恋慕でもダメ
SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:対象キャラ:人物番号} Or 対象キャラ２ = {CFLAG:対象キャラ:人物番号}) And 関係性種別 = '恋慕'")
	RETURN 0
;恋慕不可
SIF GETBIT(TALENT:対象キャラ:陥落不可, 0)
	RETURN 0
;人妻系はあっちからは来ない
SIF TALENT:対象キャラ:恋人持ち
	RETURN 0
;フラグを起床時告白と共通して使う
SIF CFLAG:対象キャラ:起床時告白済み
	RETURN 0
;恋慕レベル50で足切り、実際の必要レベルはスコアによって上下
SIF CFLAG:対象キャラ:恋慕レベル <= 50
	RETURN 0

;スコアからの算出
SELECTCASE スコア記録
	CASE IS < 15000
		;平凡以下だと恋慕MAXのみ
		SIF CFLAG:対象キャラ:恋慕レベル < 100
			RETURN 0
	CASE IS < 30000
		;成功時
		SIF CFLAG:対象キャラ:恋慕レベル < 70
			RETURN 0
	CASE IS < 60000
		;大成功時
		SIF CFLAG:対象キャラ:恋慕レベル < 60
			RETURN 0
	CASEELSE
		;最高値なら足切りクリアだけでOK
ENDSELECT
DRAWLINE
;あっちから告白してくる処理

;口上があるならそっちを表示
TRYCCALLFORM KOJO_EVENT_デート後告白_開始時_{NO:対象キャラ}(対象キャラ)
CATCH
	;汎用告白処理
	PRINTFORMW %CALLNAME:MASTER%结束与%CALLNAME:対象キャラ%的约会后，%CALLNAME:対象キャラ%似乎有话要说。
	PRINTL
	PRINTFORMW %CALLNAME:対象キャラ%面泛红潮略显踌躇，向%CALLNAME:MASTER%倾诉爱慕之情。
	PRINTFORMW 因渴望成为恋人，所以在与%CALLNAME:MASTER%约会结束后鼓起勇气告白。
ENDCATCH

PRINTFORML 面对%CALLNAME:対象キャラ%的心意，%CALLNAME:MASTER%……
PRINTBUTTONLC "[0] 回应心意", 0
PRINTBUTTONLC "[1] 请求考虑时间", 1
PRINTL

BINPUT
IF RESULT == 0
	TRYCCALLFORM KOJO_EVENT_デート後告白_成功_{NO:対象キャラ}(対象キャラ)
	CATCH
		PRINTFORMW 当%CALLNAME:MASTER%给予肯定答复，%CALLNAME:対象キャラ%露出笑容欣喜不已。
	ENDCATCH
	SETCOLOR カラーパレット("えっちな色")
	PRINTFORMW %CALLNAME:対象キャラ%获得［爱慕］特质。
	RESETCOLOR
	TALENT:対象キャラ:恋慕 = 1
ELSE
	TRYCCALLFORM KOJO_EVENT_デート後告白_保留_{NO:対象キャラ}(対象キャラ)
	CATCH
		PRINTFORMW %CALLNAME:MASTER%回答说需要时间考虑，%CALLNAME:対象キャラ%表示会耐心等待答复后离去……
	ENDCATCH
	;フラグを起床時告白と共通して使う
	CFLAG:対象キャラ:起床時告白済み = 1
ENDIF



