
@COMNAME414
#FUNCTIONS
TSTR:コマンド名受渡 = 拍摄照片

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_414
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能_うふふ")

@COM_TOOLTIP_414
LOCALS = <br>■拍摄照片<br>
LOCALS += "使用取景镜将场景记录于相纸的指令。<br>根据执行时的情境可拍摄各类照片。"
LOCALS += "<br>指令类型：可单独执行或嘿咻场景<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;-------------------------------------------------
;休憩
;-------------------------------------------------
@COM414
#DIM 覗き許可フラグ

;覗ける状態かどうかを見る
CALL COM_ABLE404
覗き許可フラグ = RESULT
IF CFLAG:TARGET:睡眠
	CALL その他写真文字列算出
	RFLAG:コマンド結果受渡し変数３ = 1
ELSEIF CFLAG:PLAYER:うふふ && CFLAG:TARGET:うふふ
	;カテゴリ１
	;ハメ撮り
	CALL ハメ撮り系写真文字列算出
	RFLAG:コマンド結果受渡し変数３ = 2
ELSEIF 覗き許可フラグ
	;カテゴリ２
	;盗撮
	CALL 盗撮系写真文字列算出
	RFLAG:コマンド結果受渡し変数３ = 3
ELSEIF 現在仕事:TARGET:0 > 0 && CFLAG:TARGET:同行 < 1 && TIME < 1170 && TCVAR:TARGET:休暇中 == 0
	;カテゴリ３
	;仕事中
	CALL 仕事中写真文字列算出
	RFLAG:コマンド結果受渡し変数３ = 4
ELSE
	;カテゴリ４
	;その他
	CALL その他写真文字列算出
	RFLAG:コマンド結果受渡し変数３ = 5
ENDIF
;ハーヴィンオナホ時禁止
SIF MODE_存在判定_ターゲット側("ハーヴィンオナホ系", TARGET)
	RETURN 0

TIME += 1
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE414
;作成中のため封印
;RETURN 0

;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(414)
	RETURN RESULT
;カメラ持ってる？
SIF 景切鏡取得フラグ == 0
	RETURN 0
;覗きが出来ず、TARGETもいないなら不可
CALL COM_ABLE404
SIF RESULT == 0 && TARGET == 0
	RETURN 0
SIF MODE_存在判定_ターゲット側("ハーヴィンオナホ系", TARGET)
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM414
;複雑な分岐結果を一箇所で管理出来るよう、メッセージを文字列で管理する
HTML_PRINT RSTR:コマンド結果受渡し文字列

;口上用のパラメータは追加してある最新の写真データベース行から持ってくる



@ハメ撮り系写真文字列算出
#DIMS 写真タイトル一時記録
#DIMS 撮影日時一時記録
#DIMS ランダム文字列
#DIMS 体位モード一時保存
#DIM 前ターン絶頂の強度
#DIM 配列番号
#DIM 実行者保存
#DIM 対象者保存
#DIMS 実行モード名
#DIMS コマンド履歴保存
#DIMS 表情描写保存
;パターンの優先度は絶頂後＞好感度分岐
;それにプラスしてハメ撮り（V・A）＞コスプレや全裸で分岐

写真タイトル一時記録 = 
表情描写保存 = 
;実行時メッセージを同時に入れ込む
RSTR:コマンド結果受渡し文字列 = %CALLNAME:PLAYER%使用取景镜

;ハメた場所登録
IF CFLAG:PLAYER:隠密
	写真タイトル一時記録 += "在隐蔽处悄然，"
ELSEIF BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	写真タイトル一時記録 += "在床上，"
ELSE
	写真タイトル一時記録 += @"在%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%，"
ENDIF

IF TCVAR:TARGET:前ターン絶頂
	;絶頂時はオートコマンド発生しないので、これで絶頂直後が撮れるはず
	;一番強度の強い部位で文字列を決定、同じ場合はV>A>C>B>Sの優先度
	前ターン絶頂の強度 = MAX(TCVAR:TARGET:前ターンＶ絶頂, TCVAR:TARGET:前ターンＡ絶頂, TCVAR:TARGET:前ターンＣ絶頂, TCVAR:TARGET:前ターンＢ絶頂, TCVAR:TARGET:前ターンＳ絶頂)
	IF TCVAR:TARGET:連続絶頂記録 > 1
		写真タイトル一時記録 += "连续多次"
	ELSEIF TCVAR:TARGET:前ターンＶ絶頂 == 前ターン絶頂の強度
		SIF TALENT:TARGET:Ｖ感度 > 0
			写真タイトル一時記録 += "敏感"
		写真タイトル一時記録 += "阴道"
	ELSEIF TCVAR:TARGET:前ターンＡ絶頂 == 前ターン絶頂の強度
		SIF TALENT:TARGET:Ａ感度 > 0
			写真タイトル一時記録 += "敏感"
		写真タイトル一時記録 += "肛门"
	ELSEIF TCVAR:TARGET:前ターンＣ絶頂 == 前ターン絶頂の強度
		SIF TALENT:TARGET:Ｃ感度 > 0
			写真タイトル一時記録 += "敏感"
		SIF フレーバー素質:TARGET:クリ大きさ == 3
			写真タイトル一時記録 +="硕大的"
		SIF フレーバー素質:TARGET:クリ大きさ == 4
			写真タイトル一時記録 +="如微型阳具般的"
		写真タイトル一時記録 += "阴蒂"
	ELSEIF TCVAR:TARGET:前ターンＢ絶頂 == 前ターン絶頂の強度
		SIF TALENT:TARGET:Ｂ感度 > 0
			写真タイトル一時記録 += "敏感"
		IF TALENT:TARGET:バストサイズ > 1
			写真タイトル一時記録 += @"%TEXTR("巨乳/胸部/大奶子")%"
		ELSEIF TALENT:TARGET:バストサイズ < 0
			写真タイトル一時記録 += @"%TEXTR("娇小胸部/微乳/贫乳")%"
		ELSE
			写真タイトル一時記録 += @"%TEXTR("胸脯/双峰/乳房")%"
		ENDIF
		写真タイトル一時記録 += "で"
	ELSEIF TCVAR:TARGET:前ターンＳ絶頂 == 前ターン絶頂の強度
		SIF TALENT:TARGET:Ｓ感度 > 0
			写真タイトル一時記録 += "敏感"
		写真タイトル一時記録 += "全身被爱抚"
	ENDIF
	写真タイトル一時記録 += @"高潮的%CALLNAME:TARGET%"
	履歴用実績:TARGET:絶頂ハメ撮り回数 += 1
	
	CALL 履歴登録チェック(TARGET, "被写体_ハメ撮り_絶頂時")

	;表情の描写
	STRDATA 表情描写保存
		DATAFORM 喘息着享受高潮余韵的%TEXTR("姿态/面容/表情")%被定格…
		DATAFORM 吐舌挣扎于快感中的%TEXTR("身姿/脸庞/神情")%被捕捉…
		DATAFORM 沉溺于极乐的面容宣告着高潮的%TEXTR("体态/五官/神态")%清晰可见…
		DATAFORM 湿润眼眸与绯红脸颊交织的%TEXTR("样子/容颜/表情")%完整记录…
		DATAFORM 强装镇定却难掩潮红晕染的%TEXTR("仪态/面部/情绪")%尽收镜中…
		DATAFORM 舒展躯体承受快感冲击的%TEXTR("动作/神色/反应")%历历在目…
	ENDDATA
ELSE
	;絶頂後でないなら服装とモードを判定する
	IF CSTR:TARGET:着せ替え服 != ""
		IF CSTR:TARGET:着せ替え服 == "全裸"
			写真タイトル一時記録 += "全裸状态下的"
		ELSE
			IF CSTR:TARGET:着せ替え服 == "浴衣"
				写真タイトル一時記録 += "身着浴袍的"
			ELSE
				写真タイトル一時記録 += @"%CSTR:TARGET:着せ替え服%"
			ENDIF
			IF CSTR:TARGET:着せ替え服追加名 != ""
				写真タイトル一時記録 += @"（%CSTR:TARGET:着せ替え服追加名%）装扮的"
			ELSE
				写真タイトル一時記録 += "装扮的"
			ENDIF
		ENDIF
	ELSE
		写真タイトル一時記録 += "日常服饰的"
	ENDIF
	体位モード一時保存 = 
	詳細文文字列受け渡し変数 = 
	FOR 配列番号, 0, DT_ROW_LENGTH("体位モードデータベース")
		IF DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ") == TARGET
			体位モード一時保存 = %DT_CELL_GETS("体位モードデータベース", 配列番号, "モード名")%
			TRYCALLFORM MODETYPE_%体位モード一時保存%(-1, "写真用文字列_プレイヤー", DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ"), DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ"))
			BREAK
		ELSEIF DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ") == TARGET
			体位モード一時保存 = %DT_CELL_GETS("体位モードデータベース", 配列番号, "モード名")%
			TRYCALLFORM MODETYPE_%体位モード一時保存%(-1, "写真用文字列_ターゲット", DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ"), DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ"))
			BREAK
		ENDIF
	NEXT
	IF 体位モード一時保存 != ""
		IF 詳細文文字列受け渡し変数 != ""
			写真タイトル一時記録 += @"%詳細文文字列受け渡し変数%"
		ELSE
			写真タイトル一時記録 += @"%体位モード一時保存%をする"
		ENDIF
	ELSE
		SIF TCVAR:TARGET:膣内精液量
			写真タイトル一時記録 += @"%TEXTR("残留精液/白浊液/浓稠体液")%从%TEXTR("私密部位/花径入口/秘裂")%缓缓%TEXTR("溢出/流淌")%的"
		SELECTCASE RAND:6
		CASE 0
			写真タイトル一時記録 += @"%TEXTR("绯红浸染/潮红浮现")%脸颊的"
		CASE 1
			IF TALENT:TARGET:応答 == 1
			;生意気
				写真タイトル一時記録 += @"带着挑衅笑容凝视镜头的"
			ELSE
				写真タイトル一時記録 += @"眼眸湿润凝望镜头的"
			ENDIF
		CASE 2
			IF CFLAG:TARGET:発情期フラグ < 0
				写真タイトル一時記録 += @"显露发情期特有的迷离神情的"
			ELSE
				写真タイトル一時記録 += @"隐含期待目光的"
			ENDIF
		CASE 3
			写真タイトル一時記録 += @"%TEXTR("羞涩/赧然")%垂首的"
		CASE 4
			IF TALENT:TARGET:陽気／陰気 == -1
			;陰気
				写真タイトル一時記録 += @"用手臂遮掩面容的"
			ELSE
				写真タイトル一時記録 += @"以手遮眼的"
			ENDIF
		CASE 5
			IF TALENT:TARGET:陽気／陰気 == -1
			;陰気
				写真タイトル一時記録 += @"笨拙摆出%TEXTR("V字手势/僵硬笑容")%的"
			ELSE
				写真タイトル一時記録 += @"随意比划V字手势的"
			ENDIF
		ENDSELECT
	ENDIF
	写真タイトル一時記録 += @"%CALLNAME:TARGET%"
ENDIF

RSTR:コマンド結果受渡し文字列 += @"『%写真タイトル一時記録%』的照片已拍摄。"
;データベース登録
撮影日時一時記録 = %TOSTR(DAY, "00000")%%TOSTR(TIME, "0000")%
;体位モードを全部記録
体位モード一時保存 = 
FOR 配列番号, 0, DT_ROW_LENGTH("体位モードデータベース")
	IF DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ") == TARGET
		実行者保存 = TARGET
		対象者保存 = DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ")
		実行モード名 = %DT_CELL_GETS("体位モードデータベース", 配列番号, "モード名")%
	ELSEIF DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ") == TARGET
		実行者保存 = DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ")
		対象者保存 = TARGET
		実行モード名 = %DT_CELL_GETS("体位モードデータベース", 配列番号, "モード名")%
	ELSE
		CONTINUE
	ENDIF
	IF 系統リスト_アイテム系(実行モード名)
		体位モード一時保存 += @"<font color='#%カラーパレット_HTML("えっちなゲージ有")%'>[%CALLNAME:対象者保存%正在装备%実行モード名%"
	ELSEIF 系統リスト_触手系(実行モード名)
		体位モード一時保存 += @"<font color='#%カラーパレット_HTML("えっちなゲージ有")%'>[触手对%CALLNAME:対象者保存%进行%実行モード名%"
	ELSEIF 系統リスト_自慰系(実行モード名)
		体位モード一時保存 += @"<font color='#%カラーパレット_HTML("えっちなゲージ有")%'>[%CALLNAME:対象者保存%正在%実行モード名%"
	ELSE
		体位モード一時保存 += @"<font color='#%カラーパレット_HTML("えっちなゲージ有")%'>[%CALLNAME:実行者保存%对%CALLNAME:対象者保存%实施%実行モード名%"
	ENDIF
	体位モード一時保存 += "<img src='えっちハート'>"
	体位モード一時保存 += "]</font><br>"
NEXT

;コマンド履歴添付
;これいる？　まあとりあえずつけとく
コマンド履歴保存 = 
コマンド履歴保存 += "＜撮影時コマンド履歴:"
FOR LOCAL, 0, 5
	IF コマンド履歴:LOCAL >= 370 && コマンド履歴:LOCAL <= 374
		SIF LOCAL > 0
			コマンド履歴保存 += " - "
		IF TARGET == コマンド履歴:(LOCAL + 5)
			コマンド履歴保存 += @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]"
		ELSE
			;一旦TARGETが違うなら一旦相手を保存してから固有コマンド表示
			LOCAL:1 = TARGET
			TARGET = コマンド履歴:(LOCAL + 5)
			コマンド履歴保存 += @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]"
			TARGET = LOCAL:1
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 380 && コマンド履歴:LOCAL <= 384
		SIF LOCAL > 0
			コマンド履歴保存 += " - "
		IF CFLAG:MASTER:現在位置 == コマンド履歴:(LOCAL + 10) && CFLAG:MASTER:現在マップ種別 == コマンド履歴:(LOCAL + 15)
			コマンド履歴保存 += @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]"
		ELSE
			;一旦現在位置が違うなら一旦位置を保存してから固有コマンド表示
			LOCAL:1 = CFLAG:MASTER:現在位置
			LOCAL:2 = CFLAG:MASTER:現在マップ種別
			CFLAG:MASTER:現在位置 = コマンド履歴:(LOCAL + 10)
			コマンド履歴保存 += @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]"
			CFLAG:MASTER:現在位置 = LOCAL:1
			CFLAG:MASTER:現在マップ種別 = LOCAL:2
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 0
		SIF LOCAL > 0
			コマンド履歴保存 += " - "
		コマンド履歴保存 += @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]"
	ELSEIF コマンド履歴:LOCAL == -1
		SIF LOCAL > 0
			コマンド履歴保存 += " - "
		コマンド履歴保存 += @"[%TRAIN_COMNAME_S(派生コマンド履歴:LOCAL)%]"
	ENDIF
NEXT
コマンド履歴保存 += "＞"

VARSET RESULTS
TRYCALLFORM 写真添付口上_ハメ撮り系_{NO:TARGET}(TARGET, 写真タイトル一時記録)
SIF RESULTS:1 != ""
	RSTR:表示グラ記録 '= RESULTS:1
SIF RESULTS:2 != ""
	表情描写保存 '= RESULTS:2

{
DT_ROW_ADD "写真データベース", "写真タイトル", 写真タイトル一時記録, "撮影日時", 撮影日時一時記録, 
	"撮影マップ種別", CFLAG:TARGET:現在マップ種別, "撮影場所", CFLAG:TARGET:現在位置, 
	"撮影場所名前", GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置), 
	"撮影対象NAME", NAME:TARGET, "撮影対象NO", NO:TARGET, "撮影シチュカテゴリ", 1, 
	"撮影対象画像リソース", RSTR:表示グラ記録, "撮影時追加文字列１", 体位モード一時保存,
	"撮影時追加文字列２", コマンド履歴保存, "撮影時追加文字列３", 表情描写保存, "写真添付口上", RESULTS
}
履歴用実績:TARGET:被写体回数 += 1
CALL 履歴登録チェック(TARGET, "被写体")
履歴用実績:TARGET:ハメ撮り回数 += 1
CALL 履歴登録チェック(TARGET, "被写体_ハメ撮り")

@盗撮系写真文字列算出
#DIMS 写真タイトル一時記録
#DIMS 撮影日時一時記録
#DIMS ランダム文字列
#DIM 前ターン絶頂の強度
#DIMS 表情描写保存

表情描写保存 = 

写真タイトル一時記録 = 
;実行時メッセージを同時に入れ込む
RSTR:コマンド結果受渡し文字列 = %CALLNAME:PLAYER%正在

IF GETBIT(覗き_ABLE(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), 1) && GETBIT(覗き_ABLE(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), 3)
	DRAWLINE
	PRINTFORM %CALLNAME:PLAYER%试图窥视另一侧
	PRINTL 却感知到可疑的藏匿迹象…
	PRINTBUTTONLC "[1]窥视另一侧", 1
	PRINTBUTTONLC "[3]探查隐蔽处", 3
	PRINTL
	$INPUT_LOOP
	INPUT
	IF RESULT == 1 || RESULT == 3
		RFLAG:コマンド結果受渡し変数 = RESULT
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSE
	FOR LOCAL, 1, 5
		IF GETBIT(覗き_ABLE(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), LOCAL)
			RFLAG:コマンド結果受渡し変数 = LOCAL
			BREAK
		ENDIF
	NEXT
ENDIF

;RFLAG:コマンド結果受渡し変数２に覗き対象が入ってくる
CALL 覗き候補選定

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		;反対側を覗く
		RSTR:表示グラ記録 = 
		RSTR:コマンド結果受渡し文字列 += "使用取景镜偷拍另一侧<br>"
		IF RFLAG:コマンド結果受渡し変数２
			IF CFLAG:PLAYER:現在マップ種別 == 0
				SELECTCASE CFLAG:PLAYER:現在位置
					CASE 7, 8
						写真タイトル一時記録 += "更衣中途的"
					CASE 9, 11
						STRDATA ランダム文字列
							DATA 全身浸入浴缸
							DATA 使用香皂清洁身体
						ENDDATA
						写真タイトル一時記録 += @"%ランダム文字列%"
					CASE 13, 14
						写真タイトル一時記録 += "泳装更换中的"
				ENDSELECT
			ENDIF
		ELSE
			RSTR:コマンド結果受渡し文字列 += "该区域暂无可拍摄对象…"
			RETURN 0
		ENDIF
		写真タイトル一時記録 += @"%CALLNAME:(RFLAG:コマンド結果受渡し変数２)%"
		;履歴用に回数登録
		IF GROUPMATCH(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置, 7, 8, 13, 14)
			;着替え覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):着替え覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_着替え")
		ELSE
			;入浴覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):風呂覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_入浴")
		ENDIF
	CASE 2
		;隠れて覗く
		RSTR:コマンド結果受渡し文字列 += @"潜伏暗处进行偷拍…<br>"
		IF CFLAG:PLAYER:現在マップ種別 == 0
			SELECTCASE CFLAG:PLAYER:現在位置
				CASE 7, 8, 2007, 702, 802
					写真タイトル一時記録 += "更衣中途的"
				CASE 703, 803
					写真タイトル一時記録 += "使用香皂清洁身体"
				CASE 704, 804
					写真タイトル一時記録 += "全身浸入浴缸"
				CASE 705, 805
					写真タイトル一時記録 += "裸体泡在特色汤池的"
				CASE 706, 806
					写真タイトル一時記録 += "在桑拿房里大汗淋漓的"
				CASE 707, 807
					写真タイトル一時記録 += "裸体泡在露天温泉的"
				CASE 708, 808
					写真タイトル一時記録 += "裸体泡在小型浴池的"
				CASE 9, 11, 2008
					STRDATA ランダム文字列
						DATA 全身浸入浴缸
						DATA 使用香皂清洁身体
					ENDDATA
					写真タイトル一時記録 += @"%ランダム文字列%"
				CASE 13, 14
					写真タイトル一時記録 += "泳装更换中的"
			ENDSELECT
		ELSEIF CFLAG:PLAYER:現在マップ種別 == 3
			;海
			SELECTCASE CFLAG:PLAYER:現在位置
				CASE 4, 5
					写真タイトル一時記録 += "泳装更换中的"
			ENDSELECT
		ENDIF
		写真タイトル一時記録 += @"%CALLNAME:(RFLAG:コマンド結果受渡し変数２)%"
		IF GROUPMATCH(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置, 7, 8, 13, 14)
			;着替え覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):着替え覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_着替え")
		ELSE
			;入浴覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):風呂覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_入浴")
		ENDIF
	CASE 3
		;物陰で野外オナニーを見つける
		RFLAG:コマンド結果受渡し変数４ = TCVAR:(RFLAG:コマンド結果受渡し変数２):野外オナニー_部位
		RSTR:コマンド結果受渡し文字列 += @"偷拍隐蔽处的私密行为…<br>"
		IF TCVAR:(RFLAG:コマンド結果受渡し変数２):野外オナニー_絶頂
			;絶頂シーン
			写真タイトル一時記録 += @"%GETPLACENAME(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在マップ種別, CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置)%隐蔽处把玩%PART_DESCRIPTION(RFLAG:コマンド結果受渡し変数２, TCVAR:(RFLAG:コマンド結果受渡し変数２):野外オナニー_部位)%并高潮的%CALLNAME:(RFLAG:コマンド結果受渡し変数２)%"
		ELSE
			;オナニーシーン
			写真タイトル一時記録 += @"%GETPLACENAME(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在マップ種別, CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置)%隐蔽处抚触%PART_DESCRIPTION(RFLAG:コマンド結果受渡し変数２, TCVAR:(RFLAG:コマンド結果受渡し変数２):野外オナニー_部位)%的%CALLNAME:(RFLAG:コマンド結果受渡し変数２)%"
		ENDIF
		;履歴用に回数登録
		履歴用実績:(RFLAG:コマンド結果受渡し変数２):野外オナニー覗かれ回数 += 1
		CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_野外オナニー")
	CASE 4
		;覗き専用場所から覗く
		RSTR:表示グラ記録 = 
		RSTR:コマンド結果受渡し文字列 += @"通过机关偷拍%GETPLACENAME(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在マップ種別, CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置)%…<br>"
		IF RFLAG:コマンド結果受渡し変数２
			IF CFLAG:PLAYER:現在マップ種別 == 0
				SELECTCASE CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置
					CASE 702, 802
						写真タイトル一時記録 += "更衣中途的"
					CASE 703, 803
						写真タイトル一時記録 += "使用香皂清洁身体"
					CASE 704, 804
						写真タイトル一時記録 += "全身浸入浴缸"
					CASE 705, 805
						写真タイトル一時記録 += "裸体泡在特色汤池的"
					CASE 706, 806
						写真タイトル一時記録 += "在桑拿房里大汗淋漓的"
					CASE 707, 807
						写真タイトル一時記録 += "裸体泡在露天温泉的"
					CASE 708, 808
						写真タイトル一時記録 += "裸体泡在小型浴池的"
				ENDSELECT
			ENDIF
		ELSE
			RSTR:コマンド結果受渡し文字列 += "该区域暂无可拍摄对象…"
			RETURN 0
		ENDIF
		写真タイトル一時記録 += @"%CALLNAME:(RFLAG:コマンド結果受渡し変数２)%"
		IF GROUPMATCH(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置, 702, 802)
			;着替え覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):着替え覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_着替え")
		ELSE
			;入浴覗きパターン
			履歴用実績:(RFLAG:コマンド結果受渡し変数２):風呂覗かれ回数 += 1
			CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "覗かれ_入浴")
		ENDIF
ENDSELECT

RSTR:コマンド結果受渡し文字列 += @"查看照片时发现『%写真タイトル一時記録%』已成功拍摄。"
;データベース登録
撮影日時一時記録 = %TOSTR(DAY, "00000")%%TOSTR(TIME, "0000")%

VARSET RESULTS
TRYCALLFORM 写真添付口上_盗撮系_{NO:(RFLAG:コマンド結果受渡し変数２)}(RFLAG:コマンド結果受渡し変数２, 写真タイトル一時記録)
SIF RESULTS:1 != ""
	RSTR:表示グラ記録 '= RESULTS:1
SIF RESULTS:2 != ""
	表情描写保存 '= RESULTS:2

{
DT_ROW_ADD "写真データベース", "写真タイトル", 写真タイトル一時記録, "撮影日時", 撮影日時一時記録, 
	"撮影マップ種別", CFLAG:(RFLAG:コマンド結果受渡し変数２):現在マップ種別, 
	"撮影場所", CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置, 
	"撮影場所名前", GETPLACENAME(CFLAG:(RFLAG:コマンド結果受渡し変数２):現在マップ種別, CFLAG:(RFLAG:コマンド結果受渡し変数２):現在位置), 
	"撮影対象NAME", NAME:(RFLAG:コマンド結果受渡し変数２), "撮影対象NO", NO:(RFLAG:コマンド結果受渡し変数２), 
	"撮影シチュカテゴリ", 2, "撮影対象画像リソース", RSTR:表示グラ記録, "撮影時追加文字列３", 表情描写保存, "写真添付口上", RESULTS
}
履歴用実績:(RFLAG:コマンド結果受渡し変数２):被写体回数 += 1
CALL 履歴登録チェック(RFLAG:コマンド結果受渡し変数２, "被写体")

@仕事中写真文字列算出
#DIMS 写真タイトル一時記録
#DIMS 撮影日時一時記録
#DIMS ランダム文字列
#DIM 前ターン絶頂の強度
#DIMS 画像リソース記録
#DIMS 表情描写保存

表情描写保存 = 

写真タイトル一時記録 = 
;実行時メッセージを同時に入れ込む
RSTR:コマンド結果受渡し文字列 = %CALLNAME:PLAYER%使用取景镜

;仕事内容
IF 現在仕事:TARGET:0 == 2
	;ちゃんと仕事場にいるかで分岐
	IF GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == GETPLACENAME(現在仕事:TARGET:1 / 10000, 現在仕事:TARGET:1 % 10000)
		写真タイトル一時記録 += @"在%GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置)%执行%MAP_GET("常駐業務名", GETPLACENAME(現在仕事:TARGET:1 / 10000, 現在仕事:TARGET:1 % 10000))%的"
	ELSE
		写真タイトル一時記録 += @"前往%MAP_GET("常駐業務名", GETPLACENAME(現在仕事:TARGET:1 / 10000, 現在仕事:TARGET:1 % 10000))%途中的"
	ENDIF
ELSEIF 現在仕事:TARGET:0 == 5
	;ちゃんと仕事場にいるかで分岐
	IF CFLAG:TARGET:現在マップ種別 == 0 && CFLAG:TARGET:現在位置 == 308
		写真タイトル一時記録 += @"在%GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置)%进行训练的"
	ELSE
		写真タイトル一時記録 += @"前往训练场途中的"
	ENDIF
ELSE
	写真タイトル一時記録 += @"在%GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置)%从事%現在仕事数字文字列変換(現在仕事:TARGET:0, 0, TARGET)%的"
ENDIF
;服装
IF CSTR:TARGET:着せ替え服 != ""
	IF CSTR:TARGET:着せ替え服 == "全裸"
		写真タイトル一時記録 += "全裸状态的"
	ELSE
		IF CSTR:TARGET:着せ替え服 == "浴衣"
			写真タイトル一時記録 += "身着温泉浴衣的"
		ELSE
			写真タイトル一時記録 += @"%CSTR:TARGET:着せ替え服%"
			IF CSTR:TARGET:着せ替え服追加名 != ""
				写真タイトル一時記録 += @"（%CSTR:TARGET:着せ替え服追加名%）装扮的"
			ELSE
				写真タイトル一時記録 += "装扮的"
			ENDIF
		ENDIF
	ENDIF
ELSEIF CSTR:TARGET:服名称 == "全裸"
	;普段から全裸だと服装は入れない
ELSE
	写真タイトル一時記録 += "日常服饰的"
ENDIF
写真タイトル一時記録 += @"%CALLNAME:TARGET%"


RSTR:コマンド結果受渡し文字列 += @"『%写真タイトル一時記録%』的照片已拍摄。"
;データベース登録
撮影日時一時記録 = %TOSTR(DAY, "00000")%%TOSTR(TIME, "0000")%

VARSET RESULTS
TRYCALLFORM 写真添付口上_仕事中系_{NO:TARGET}(TARGET, 写真タイトル一時記録)
SIF RESULTS:1 != ""
	RSTR:表示グラ記録 '= RESULTS:1
SIF RESULTS:2 != ""
	表情描写保存 '= RESULTS:2

{
DT_ROW_ADD "写真データベース", "写真タイトル", 写真タイトル一時記録, "撮影日時", 撮影日時一時記録,
	"撮影マップ種別", CFLAG:TARGET:現在マップ種別, "撮影場所", CFLAG:TARGET:現在位置, 
	"撮影場所名前", GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置), 
	"撮影対象NAME", NAME:TARGET, "撮影対象NO", NO:TARGET, "撮影シチュカテゴリ", 3, 
	"撮影対象画像リソース", RSTR:表示グラ記録, "撮影時追加文字列３", 表情描写保存, "写真添付口上", RESULTS
}

履歴用実績:TARGET:被写体回数 += 1
CALL 履歴登録チェック(TARGET, "被写体")


@その他写真文字列算出
#DIMS 写真タイトル一時記録
#DIMS 撮影日時一時記録
#DIMS ランダム文字列
#DIMS 服装一時記録
#DIM 前ターン絶頂の強度
#DIMS 画像リソース記録
#DIMS 表情描写保存

表情描写保存 = 

写真タイトル一時記録 = 
;実行時メッセージを同時に入れ込む
RSTR:コマンド結果受渡し文字列 = %CALLNAME:PLAYER%使用取景镜

;服装を先に取得
服装一時記録 = 
IF CSTR:TARGET:着せ替え服 != ""
	IF CSTR:TARGET:着せ替え服 == "全裸"
		服装一時記録 += "全裸状态的"
	ELSE
		IF CSTR:TARGET:着せ替え服 == "浴衣"
			服装一時記録 += "身着温泉浴衣的"
		ELSE
			服装一時記録 += @"%CSTR:TARGET:着せ替え服%"
		ENDIF
		IF CSTR:TARGET:着せ替え服追加名 != ""
			服装一時記録 += @"（%CSTR:TARGET:着せ替え服追加名%）装扮的"
		ELSE
			服装一時記録 += "装扮的"
		ENDIF
	ENDIF
ELSE
	服装一時記録 += "日常服饰的"
ENDIF

IF CFLAG:TARGET:睡眠
	写真タイトル一時記録 += @"安详沉睡的%服装一時記録%%CALLNAME:TARGET%"
ELSE
	SELECTCASE RAND:3
		CASE 0
			写真タイトル一時記録 += @"%服装一時記録%%CALLNAME:TARGET%与%CALLNAME:PLAYER%的双人合影"
		CASE 1
			IF ENJOYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
				写真タイトル一時記録 += @"在%GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置)%"
				SIF GET_TARGETNUM() > 1
					写真タイトル一時記録 += "与众人"
				写真タイトル一時記録 += "嬉戏的"
				写真タイトル一時記録 += @"%服装一時記録%%CALLNAME:TARGET%"
			ELSE
				写真タイトル一時記録 += @"位于%GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置)%"
				IF GET_TARGETNUM() > 1
					写真タイトル一時記録 += "正与他人交谈的"
				ELSE
					写真タイトル一時記録 += "驻足的"
				ENDIF
				写真タイトル一時記録 += @"%服装一時記録%%CALLNAME:TARGET%"
			ENDIF
		CASE 2
				写真タイトル一時記録 += "察觉镜头的"
				IF TALENT:TARGET:羞恥心 > 0
					STRDATA ランダム文字列
						DATA 羞涩比出V字手势的
						DATA 略带腼腆微笑的
						DATA 下意识躲藏的
					ENDDATA
				ELSEIF TALENT:TARGET:目立ちたがり > 0
					STRDATA ランダム文字列
						DATA 摆出帅气姿势的
						DATA 以专业表情凝视镜头的
						DATA 主动靠近形成特写镜头的
					ENDDATA
				ELSE
					STRDATA ランダム文字列
						DATA 自然比V的
						DATA 向镜头挥手的
						DATA 展露温暖笑容的
					ENDDATA
				ENDIF
				写真タイトル一時記録 += @"%ランダム文字列%%服装一時記録%%CALLNAME:TARGET%"
	ENDSELECT
ENDIF

RSTR:コマンド結果受渡し文字列 += @"『%写真タイトル一時記録%』的照片已拍摄。"
;データベース登録
撮影日時一時記録 = %TOSTR(DAY, "00000")%%TOSTR(TIME, "0000")%

VARSET RESULTS
TRYCALLFORM 写真添付口上_その他系_{NO:TARGET}(TARGET, 写真タイトル一時記録)
SIF RESULTS:1 != ""
	RSTR:表示グラ記録 '= RESULTS:1
SIF RESULTS:2 != ""
	表情描写保存 '= RESULTS:2

{
DT_ROW_ADD "写真データベース", "写真タイトル", 写真タイトル一時記録, "撮影日時", 撮影日時一時記録, 
	"撮影マップ種別", CFLAG:TARGET:現在マップ種別, "撮影場所", CFLAG:TARGET:現在位置, 
	"撮影場所名前", GETPLACENAME(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置), 
	"撮影対象NAME", NAME:TARGET, "撮影対象NO", NO:TARGET, "撮影シチュカテゴリ", 4,
	"撮影対象画像リソース", RSTR:表示グラ記録, "撮影時追加文字列３", 表情描写保存, "写真添付口上", RESULTS
}
履歴用実績:TARGET:被写体回数 += 1
CALL 履歴登録チェック(TARGET, "被写体")

@所持写真一覧表示

IF OPTION変数:写真一覧表示
	;表示がややこしいので別関数に飛ばす
	CALL アルバム式表示()
ELSE
	CALL 撮影順表示()
ENDIF
;表示方式切替時はRESULTに１が返ってくるのでリスタート
SIF RESULT
	RESTART


@撮影順表示
#DIM ページ数
#DIM 表示数カウント
#DIM ボタン記録
#DIM 一括削除モード
#DIM 一括削除件数
#DIMS 削除番号
#DIMS 削除番号配列, 101

DRAWLINE
PRINTL 持有的照片列表如下
IF 一括削除モード
	PRINTL 可批量删除选定照片（上限１００张）
ELSE
	PRINTL 选择单张可查看详情或删除
ENDIF
DRAWLINE

FOR 表示数カウント, ページ数 * 15, ページ数 * 15 + 15
	IF DT_ROW_LENGTH("写真データベース") > 表示数カウント
		SIF 一括削除モード && STRCOUNT(削除番号, @"_{DT_CELL_GET("写真データベース", 表示数カウント, "id")}_")
			SETCOLOR カラーパレット("黄")
		PRINTBUTTON @"[{表示数カウント, 3}] - 『%DT_CELL_GETS("写真データベース", 表示数カウント, "写真タイトル")%』", 表示数カウント
		RESETCOLOR
		PRINTL
	ELSE
		PRINTL
	ENDIF
NEXT

DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[1000] 上一页", 1000
ELSE
	PRINTC  
ENDIF
IF DT_ROW_LENGTH("写真データベース") > ページ数 * 15 + 15
	PRINTBUTTONLC "[1001] 下一页", 1001
ELSE
	PRINTC  
ENDIF
PRINTBUTTONLC "[999] 返回", 999
IF 一括削除モード && 削除番号 != ""
	PRINTBUTTONLC "[998] 确认删除", 998
ELSEIF 一括削除モード
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [998] 确认删除
	PRINT            
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[998] 批量删除", 998
ENDIF
PRINTBUTTONLC "[1002] 切换显示模式", 1002

IF チュートリアルフラグ:写真一覧 == 0
	RESULT = 997
ELSE
	BINPUT
ENDIF
ボタン記録 = RESULT
SELECTCASE ボタン記録
	CASE 997
		CALL 写真一覧チュートリアル()
		RESTART
	CASE 999
		IF 一括削除モード
			一括削除件数 = 0
			一括削除モード = 0
			削除番号 = 
			RESTART
		ENDIF
		ページ数 = 0
		RETURN 0
	CASE 998
		;一括削除へ
		IF 一括削除モード
			VARSET 削除番号配列
			SPLIT 削除番号, "_", 削除番号配列
			FOR 表示数カウント, 1, 一括削除件数 + 1
				DT_ROW_REMOVE "写真データベース", TOINT(削除番号配列:表示数カウント)
			NEXT
			PRINTFORMW 已成功删除{一括削除件数}张照片
			一括削除モード = 0
			一括削除件数 = 0
			削除番号 = 
		ELSE
			一括削除モード = 1
			一括削除件数 = 0
			削除番号 = 
		ENDIF
		RESTART
	CASE 1000
		ページ数 -= 1
		RESTART
	CASE 1001
		ページ数 += 1
		RESTART
	CASE 1002
		INVERTBIT OPTION変数:写真一覧表示, 0
		一括削除モード = 0
		ページ数 = 0
		RETURN 1
	CASEELSE
		IF 一括削除モード
			SIF 一括削除件数 > 99
				RESTART
			IF STRCOUNT(削除番号, @"_{DT_CELL_GET("写真データベース", ボタン記録, "id")}_") == 0
				IF 削除番号 != ""
					削除番号 += @"{DT_CELL_GET("写真データベース", ボタン記録, "id")}_"
				ELSE
					削除番号 += @"_{DT_CELL_GET("写真データベース", ボタン記録, "id")}_"
				ENDIF
				一括削除件数 += 1
			ELSE
				VARSET 削除番号配列
				SPLIT 削除番号, @"_{DT_CELL_GET("写真データベース", ボタン記録, "id")}_", 削除番号配列
				削除番号 = %削除番号配列:0%_%削除番号配列:1%
				一括削除件数 -= 1
			ENDIF
		ELSE
			DO
			LOCAL = 0
			SIF ボタン記録 < DT_ROW_LENGTH("写真データベース") - 1
				LOCAL = 1
			CALL 写真詳細表示(DT_CELL_GET("写真データベース", ボタン記録, "id"), ボタン記録, LOCAL)
			IF RESULT == 0
				RESTART
			ELSE
				ボタン記録 += RESULT
			ENDIF
			LOOP 1
		ENDIF
		RESTART
ENDSELECT


@アルバム式表示()
#DIM ページ数
#DIM キャラ番保存
#DIM 表示数カウント
#DIM 表示キャラ配列, 500
#DIM 枚数登録配列, 500
#DIM ボタン記録

DRAWLINE
PRINTL 持有的照片列表如下
PRINTL 选择角色可查看・管理对应照片

;表示候補を整理
VARSET 表示キャラ配列
VARSET 枚数登録配列
LOCAL = 0
FOR 表示数カウント, 0, DT_ROW_LENGTH("写真データベース")
	;NOと名前から現在のキャラ番号を出す
	キャラ番保存 = 0
	IF DT_CELL_GET("写真データベース", 表示数カウント, "撮影対象NO") == 999
		キャラ番保存 = FINDCHARA(NAME, @"%DT_CELL_GETS("写真データベース", 表示数カウント, "撮影対象NAME")%")
		IF キャラ番保存 == -1
			キャラ番保存 = FINDCHARA(NAME, @"%REPLACE(DT_CELL_GETS("写真データベース", 表示数カウント, "撮影対象NAME"), "[一般人]", "")%")
		ENDIF
	ELSE
		キャラ番保存 = GETCHARA(DT_CELL_GET("写真データベース", 表示数カウント, "撮影対象NO"))
	ENDIF

	IF キャラ番保存 > 0
		IF MATCH(表示キャラ配列, キャラ番保存)
			枚数登録配列:(FINDELEMENT(表示キャラ配列, キャラ番保存)) -= 1
		ELSE
			表示キャラ配列:LOCAL = キャラ番保存
			枚数登録配列:LOCAL = -1
			LOCAL ++
		ENDIF
	ENDIF
NEXT
;登録終わったら枚数で降順ソート
ARRAYMSORT 枚数登録配列, 表示キャラ配列

$表示処理
DRAWLINE
;リスト表示
FOR LOCAL, ページ数 * 15, ページ数 * 15 + 15
	IF 表示キャラ配列:LOCAL < 1
		PRINTL
		CONTINUE
	ENDIF
	PRINTBUTTON @"[{LOCAL, 2}] %NAME表示(表示キャラ配列:LOCAL), 50%　数量：{枚数登録配列:LOCAL * -1}张", LOCAL
	PRINTL
NEXT

DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[1000] 上一页", 1000
ELSE
	PRINTC  
ENDIF
IF 表示キャラ配列:(ページ数 * 15 + 15) > 0
	PRINTBUTTONLC "[1001] 下一页", 1001
ELSE
	PRINTC  
ENDIF
PRINTBUTTONLC "[999] 返回", 999
PRINTBUTTONLC "[1002] 切换显示模式", 1002
IF チュートリアルフラグ:写真一覧 == 0
	RESULT = 997
ELSE
	BINPUT
ENDIF
ボタン記録 = RESULT
SELECTCASE ボタン記録
	CASE 997
		CALL 写真一覧チュートリアル()
		GOTO 表示処理
	CASE 999
		ページ数 = 0
		RETURN 0
	CASE 1000
		ページ数 -= 1
		GOTO 表示処理
	CASE 1001
		ページ数 += 1
		GOTO 表示処理
	CASE 1002
		INVERTBIT OPTION変数:写真一覧表示, 0
		ページ数 = 0
		RETURN 1
	CASEELSE
		DO
		CALL アルバム式個別表示(ボタン記録, 表示キャラ配列)
		IF RESULT == 0
			RESTART
		ELSE
			ボタン記録 += RESULT
		ENDIF
		LOOP 1
ENDSELECT

@アルバム式個別表示(選択キャラ, 表示キャラ配列)
#DIM ボタン記録
#DIM 選択キャラ
#DIM REF 表示キャラ配列, 0
#DIM 写真ID配列, 2000
#DIM 表示数カウント
#DIM ページ数
#DIM 一括削除モード
#DIM 一括削除件数
#DIMS 削除番号
#DIMS 削除番号配列, 101

DRAWLINE
VARSET 写真ID配列
DT_SELECT "写真データベース", @"撮影対象NO = {NO:(表示キャラ配列:選択キャラ)} And 撮影対象NAME = '%NAME表示(表示キャラ配列:選択キャラ)%'", , 写真ID配列

FOR 表示数カウント, ページ数 * 15, ページ数 * 15 + 15
	IF 写真ID配列:表示数カウント < 1
		PRINTL
		CONTINUE
	ENDIF
	SIF 一括削除モード && STRCOUNT(削除番号, @"_{写真ID配列:表示数カウント}_")
		SETCOLOR カラーパレット("黄")
	PRINTBUTTON @"[{表示数カウント, 3}] - 『%DT_CELL_GETS("写真データベース", 写真ID配列:表示数カウント, "写真タイトル", 1)%』", 表示数カウント
	RESETCOLOR
	PRINTL
NEXT

DRAWLINE
IF ページ数 > 0
	PRINTBUTTONLC "[1000] 上一页", 1000
ELSE
	PRINTC  
ENDIF
IF 写真ID配列:(ページ数 * 15 + 15) > 0
	PRINTBUTTONLC "[1001] 下一页", 1001
ELSE
	PRINTC  
ENDIF
PRINTBUTTONLC "[999] 返回", 999
IF 一括削除モード && 削除番号 != ""
	PRINTBUTTONLC "[998] 确认删除", 998
ELSEIF 一括削除モード
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [998] 确认删除
	PRINT            
	RESETCOLOR
ELSE
	PRINTBUTTONLC "[998] 批量删除", 998
ENDIF
PRINTL 
IF 選択キャラ > 0
	PRINTBUTTONLC "[1010] 上一个角色", 1010
ELSE
	PRINTC  
ENDIF
IF 表示キャラ配列:(選択キャラ + 1) > 0
	PRINTBUTTONLC "[1011] 下一个角色", 1011
ENDIF

BINPUT
ボタン記録 = RESULT
SELECTCASE ボタン記録
	CASE 999
		IF 一括削除モード
			一括削除件数 = 0
			一括削除モード = 0
			削除番号 = 
			RESTART
		ENDIF
		ページ数 = 0
		RETURN 0
	CASE 998
		;一括削除へ
		IF 一括削除モード
			VARSET 削除番号配列
			SPLIT 削除番号, "_", 削除番号配列
			FOR 表示数カウント, 1, 一括削除件数 + 1
				DT_ROW_REMOVE "写真データベース", TOINT(削除番号配列:表示数カウント)
			NEXT
			PRINTFORMW 已成功删除{一括削除件数}张照片
			一括削除モード = 0
			一括削除件数 = 0
			削除番号 = 
		ELSE
			一括削除モード = 1
			一括削除件数 = 0
			削除番号 = 
		ENDIF
		RESTART
	CASE 1000
		ページ数 -= 1
		RESTART
	CASE 1001
		ページ数 += 1
		RESTART
	CASE 1010
		RETURN -1
	CASE 1011
		RETURN 1
	CASEELSE
		IF 一括削除モード
			SIF 一括削除件数 > 99
				RESTART
			IF STRCOUNT(削除番号, @"_{写真ID配列:ボタン記録}_") == 0
				IF 削除番号 != ""
					削除番号 += @"{写真ID配列:ボタン記録}_"
				ELSE
					削除番号 += @"_{写真ID配列:ボタン記録}_"
				ENDIF
				一括削除件数 += 1
			ELSE
				VARSET 削除番号配列
				SPLIT 削除番号, @"_{写真ID配列:ボタン記録}_", 削除番号配列
				削除番号 = %削除番号配列:0%_%削除番号配列:1%
				一括削除件数 -= 1
			ENDIF
		ELSE
			DO
			LOCAL = 0
			SIF 写真ID配列:(ボタン記録 + 1) > 0
				LOCAL = 1
			CALL 写真詳細表示(写真ID配列:ボタン記録, ボタン記録, LOCAL)
			IF RESULT == 0
				RESTART
			ELSE
				ボタン記録 += RESULT
			ENDIF
			LOOP 1
		ENDIF
		RESTART
ENDSELECT


@写真詳細表示(ボタン記録, 前フラグ, 次フラグ)
#DIM ボタン記録
#DIM 前フラグ
#DIM 次フラグ
#DIM DAY算出
#DIM TIME算出
#DIM CONST レイヤー番号 = 1500
#DIM size
#DIMS 画像リソース保存

;写真詳細表示
$写真表示
DRAWLINE
PRINTL
PRINTFORML ■%DT_CELL_GETS("写真データベース", ボタン記録, "写真タイトル", 1)%
PRINTL

;グラフィック表示
SPRITEDISPOSE "写真グラ"
GDISPOSE レイヤー番号
;データベースに保存しといた文字列を引っ張ってくる
画像リソース保存 = %DT_CELL_GETS("写真データベース", ボタン記録,  "撮影対象画像リソース", 1)%
VARSET RESULTS
;エフェクト保存分を分割
SPLIT 画像リソース保存, "+++", RESULTS
IF RESULTS:0 != ""
	CALL GCREATE_拡張子(レイヤー番号, RESULTS:0)
	;画像生成に成功したら次へ
	IF RESULT
		LOCAL = 0
		IF STRFIND(RESULTS:0, "短冊_") > -1
			LOCAL = スプライト作成("写真グラ", レイヤー番号, 1)
			size = 2250
		ELSE
			LOCAL = スプライト作成("写真グラ", レイヤー番号, 0)
			size = 1125
		ENDIF
		;スプライト生成に成功したら表示へ
		IF LOCAL
			SIF RESULTS:1 != ""
				LOCAL = スプライト合成("写真グラ", レイヤー番号, RESULTS:1)
			SIF RESULTS:2 != ""
				LOCAL = スプライト合成("写真グラ", レイヤー番号, RESULTS:2)
			SIF RESULTS:3 != ""
				LOCAL = スプライト合成("写真グラ", レイヤー番号, RESULTS:3)
			HTML_PRINT @"　　<img src='写真グラ' width='1125' height='{size}'>"
			FOR LOCAL, 0, size / 100
				PRINTL
			NEXT
		ENDIF
	ENDIF
ENDIF

;フリー表示枠表示
IF DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列１", 1) != ""
	;html形式で保存されているはず
	HTML_PRINT @"　　%REPLACE(DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列１", 1), "<br>", "<br>　　")%"
	PRINTL 
ENDIF
IF DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列２", 1) != ""
	;html形式で保存されているはず
	HTML_PRINT @"　　%REPLACE(DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列２", 1), "<br>", "<br>　　")%"
	PRINTL 
ENDIF
IF DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列３", 1) != ""
	;html形式で保存されているはず
	HTML_PRINT @"　　%REPLACE(DT_CELL_GETS("写真データベース", ボタン記録, "撮影時追加文字列３", 1), "<br>", "<br>　　")%"
	PRINTL 
ENDIF
IF DT_CELL_GETS("写真データベース", ボタン記録, "写真添付口上", 1) != ""
	;html形式で保存されているはず
	HTML_PRINT @"　　%REPLACE(DT_CELL_GETS("写真データベース", ボタン記録, "写真添付口上", 1), "<br>", "<br>　　")%"
	PRINTL 
ENDIF

DAY算出 = TOINT(DT_CELL_GETS("写真データベース", ボタン記録, "撮影日時", 1)) / 10000
TIME算出 = TOINT(DT_CELL_GETS("写真データベース", ボタン記録, "撮影日時", 1)) % 10000
PRINTFORML 　　拍摄时间：第{DAY算出 / 120 + 1}年　%月登録:((DAY算出 / 30) % 4)%　{DAY算出 % 30 + 1}日(%曜日登録:(DAY算出 % 7)%)　{TIME算出 / 60}時{TIME算出 % 60}分
PRINTFORML 　　拍摄地点：%DT_CELL_GETS("写真データベース", ボタン記録, "撮影場所名前", 1)%
PRINTFORML 　　拍摄对象：%DT_CELL_GETS("写真データベース", ボタン記録, "撮影対象NAME", 1)%
IF DT_CELL_GETS("写真データベース", ボタン記録, "写真添付メモ", 1) != ""
	PRINTFORML 　　照片备注：%DT_CELL_GETS("写真データベース", ボタン記録, "写真添付メモ", 1)%
ENDIF
PRINTL

PRINTBUTTONLC "[999] 返回", 999
IF DT_CELL_GETS("写真データベース", ボタン記録, "写真添付メモ", 1) != ""
	PRINTBUTTONLC "[1010] 修改备注", 1010
	PRINTBUTTONLC "[1011] 删除备注", 1011
ELSE
PRINTBUTTONLC "[1010] 添加备注", 1010
ENDIF
PRINTL
PRINTBUTTONLC "[1000] 删除此照片", 1000
PRINTL 
IF 前フラグ
	PRINTBUTTONLC "[4] 上一张照片", 4
ELSE
	PRINTC 　
ENDIF
IF 次フラグ
	PRINTBUTTONLC "[6] 下一张照片", 6
ENDIF

BINPUT
SELECTCASE RESULT
	CASE 1000
		DRAWLINE
		PRINTFORML 是否确认删除『%DT_CELL_GETS("写真データベース", ボタン記録, "写真タイトル", 1)%』？
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		BINPUT
		IF RESULT == 0
			PRINTFORMW 已成功删除『%DT_CELL_GETS("写真データベース", ボタン記録, "写真タイトル", 1)%』。
			DT_ROW_REMOVE "写真データベース", ボタン記録
		ELSE
			GOTO 写真表示
		ENDIF
	CASE 1010
		DRAWLINE
		PRINTL 请输入要添加的备注内容（仅限一行）
		PRINTBUTTONLC "[取消输入]", ""
		INPUTS
		SIF RESULTS == ""
			GOTO 写真表示
		DT_CELL_SET "写真データベース", ボタン記録, "写真添付メモ", RESULTS, 1
		PRINTFORMW 已为照片添加备注『%RESULTS%』
		GOTO 写真表示
	CASE 1011
		PRINTFORML 照片备注：%DT_CELL_GETS("写真データベース", ボタン記録, "写真添付メモ", 1)%
		PRINTL 是否确认删除此备注？
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		BINPUT
		IF RESULT == 0
			DT_CELL_SET "写真データベース", ボタン記録, "写真添付メモ", "", 1
			PRINTW 备注内容已清除
		ENDIF
		GOTO 写真表示
	CASE 4
		RETURN -1
	CASE 6
		RETURN 1
	CASE 999
		RETURN 0
ENDSELECT


