;------------------------------------------------------------------------------
;V挿入時、自分で性感帯を刺激しはじめる
;------------------------------------------------------------------------------
@AUTOCOM31
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｖ(AUTOCOM_実行番号保存, PLAYER, 600)
CALL SOURCE_CALC_露出_反感無し(AUTOCOM_実行番号保存, PLAYER, 80)
CALL SOURCE_CALC_接触_不快無し(AUTOCOM_実行番号保存, PLAYER, 200)
CALL SOURCE_CALC_快Ｃ(PLAYER, AUTOCOM_実行番号保存, 500)

TIME_挿入時反応:AUTOCOM_実行番号保存:0 = TIME

;-------------------------------------------------
;部位決定
;-------------------------------------------------
LOCALS = 
SIF EXP:AUTOCOM_実行番号保存:Ｇスポ開発 > 99
	LOCALS = Ｇスポ開発フラグ
IF EXP:AUTOCOM_実行番号保存:ポルチオ開発 > 99
	IF LOCALS == ""
		LOCALS = ポルチオ開発フラグ
	ELSE
		;両方100以上の場合はランダムで部位決定
		SIF RAND:2
			LOCALS = ポルチオ開発フラグ
	ENDIF
ENDIF

SIF LOCALS != ""
	RCVAR:AUTOCOM_実行番号保存:LOCALS = 4
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:AUTOCOM_実行番号保存:Ｖ性交経験 ++

;同性愛
SIF 同性愛性癖判定(AUTOCOM_実行番号保存,PLAYER)
	EXP:AUTOCOM_実行番号保存:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,AUTOCOM_実行番号保存)
	EXP:PLAYER:同性愛経験 += 4

EXP:AUTOCOM_実行番号保存:愛情経験 += TALENT:AUTOCOM_実行番号保存:恋慕 * 2

RETURN 1

@AUTOCOM_ABLE31(キャラ番号)
#DIM キャラ番号
;V挿入時のみ
SIF !MODE_存在判定_完全一致("Ｖ挿入系", PLAYER, キャラ番号)
	RETURN 0
;30分に１回のみ発動
SIF TIME - TIME_挿入時反応:キャラ番号:0 < 31
	RETURN 0
;欲情PALAMLv4必要
SIF PALAM:キャラ番号:欲情 < PALAMLV:4
	RETURN 0
;無関心はアウト、真面目、恥じらいは性欲必要
SIF TALENT:キャラ番号:自制心 > 0 && BASE:キャラ番号:性欲 < 1500
	RETURN 0
SIF TALENT:キャラ番号:無関心 > 0
	RETURN 0
SIF TALENT:キャラ番号:羞恥心 > 0 && BASE:キャラ番号:性欲 < 1500
	RETURN 0
;技巧2必要
SIF ABL:キャラ番号:技巧 < 2
	RETURN 0
;性感帯開発必須
SIF EXP:キャラ番号:ポルチオ開発 < 100 && EXP:キャラ番号:Ｇスポ開発 < 100
	RETURN 0
;基礎確率1/4、素質により変動
LOCAL = MAX(4 - TALENT:キャラ番号:性的興味 - TALENT:キャラ番号:快感応答 - TALENT:キャラ番号:性欲, 1)
SIF RAND:LOCAL
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM31
PRINTFORM %CALLNAME:AUTOCOM_実行番号保存%
IF 知識素質:AUTOCOM_実行番号保存:性知識 <= 0
	PRINTFORML 本能地扭动腰部，将%CALLNAME:PLAYER%的阴茎抵在自己"舒服"的位置享受快感…
ELSEIF TALENT:AUTOCOM_実行番号保存:羞恥心 > 0
	PRINTFORML 偷偷调整姿势让%CALLNAME:PLAYER%的阴茎抵住自己性感带
	PRINTFORML 当被指出这点时，%CALLNAME:AUTOCOM_実行番号保存%顿时羞红了脸…
ELSE
	PRINTFORML 熟练调整腰部动作，让阴茎精准刺激性感带享受快感…
ENDIF

