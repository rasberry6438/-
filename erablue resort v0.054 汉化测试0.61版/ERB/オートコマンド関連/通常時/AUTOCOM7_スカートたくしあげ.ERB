;------------------------------------------------------------------------------
;合意済みの相手
;性欲一定以上で二人きりだとスカートをたくしあげて誘惑してくる
;チラ見せと時間が共通だが、チラ見せよりも条件がゆるい
;------------------------------------------------------------------------------
@AUTOCOM7
CALL SOURCE_CALC_露出_反感無し(AUTOCOM_実行番号保存, PLAYER, 100)
;チラ見せと時間共通
TIME_胸部チラ見せ:AUTOCOM_実行番号保存:0 = TIME

RETURN 1

@AUTOCOM_ABLE7(対象キャラ)
#DIM 対象キャラ
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(対象キャラ), 0) == 0
	RETURN 0
;ターゲットが陥落済み
SIF 陥落チェック_性的(対象キャラ) == 0
	RETURN 0
;初うふふ済
SIF 初体験日取得("初うふふ", 対象キャラ, MASTER) == 0
	RETURN 0
;二人っきりのみ
SIF GET_TARGETNUM() > 1
	RETURN 0
;スカート必須、鎧はダメ
SIF TEQUIP:対象キャラ:下半身服あり == 0
	RETURN 0
SIF TEQUIP:対象キャラ:スカートめくり可 == 0
	RETURN 0
SIF TEQUIP:対象キャラ:下腹部前鎧あり
	RETURN 0
;うふふ中ではない
SIF CFLAG:対象キャラ:うふふ
	RETURN 0
;２時間に１回のみ発動、チラ見せと時間共通
SIF TIME - TIME_胸部チラ見せ:対象キャラ:0 < 120
	RETURN 0
;性欲一定以上
SIF BASE:対象キャラ:性欲 < 1000
	RETURN 0
;無関心があるとアウト
SIF TALENT:対象キャラ:無関心 > 0
	RETURN 0
;性知識-1必要
SIF 知識素質:対象キャラ:性知識 < -1
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM7
PRINTFORM %CALLNAME:AUTOCOM_実行番号保存%面向%CALLNAME:PLAYER%

IF TALENT:AUTOCOM_実行番号保存:羞恥心 > 0
	PRINT 满脸通红地
ENDIF

PRINT 撩起衣服，

IF TEQUIP:AUTOCOM_実行番号保存:下半身下着あり == 2
	IF PALAM:AUTOCOM_実行番号保存:潤滑 > PALAMLV:3
		PRINT 露出因湿润而半脱落
	ELSE
		PRINT 仅遮盖关键部位的
	ENDIF
	PRINTL 小巧阴贴进行诱惑…
ELSEIF TEQUIP:AUTOCOM_実行番号保存:下半身下着あり == 0
	SIF PALAM:AUTOCOM_実行番号保存:潤滑 > PALAMLV:3
		PRINT 早已湿润的
	PRINTL 主动展示私密部位进行诱惑…
ELSE
	SIF PALAM:AUTOCOM_実行番号保存:潤滑 > PALAMLV:3
		PRINT 完全浸透的
	PRINTFORML %今日の下着文字列生成(AUTOCOM_実行番号保存)%进行诱惑…
ENDIF



