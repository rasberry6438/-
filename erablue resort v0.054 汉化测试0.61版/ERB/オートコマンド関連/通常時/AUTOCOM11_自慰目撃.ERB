;------------------------------------------------------------------------------
;通常時、自慰目撃
;------------------------------------------------------------------------------
@AUTOCOM11
IF MYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	;キャラが自室を訪ねて目撃しちゃう＝あなたに落ち度なし
	CALL SOURCE_CALC_征服(AUTOCOM_実行番号保存, PLAYER, 400)
	IF TALENT:AUTOCOM_実行番号保存:恋慕 || TALENT:AUTOCOM_実行番号保存:セフレ
		IF BASE:AUTOCOM_実行番号保存:性欲 >= 1500 || CFLAG:AUTOCOM_実行番号保存:発情期フラグ < 0
			;陥落済みかつ性欲が溜まってると姿を現してうふふ突入
			CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
			CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
			TARGET = AUTOCOM_実行番号保存
			TARGET:1 = AUTOCOM_実行番号保存
			TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
			CFLAG:PLAYER:うふふ = 1
			CFLAG:AUTOCOM_実行番号保存:同室 = 1
			CALL うふふ開始キャラ処理(AUTOCOM_実行番号保存, "あなたの自慰目撃")
		ENDIF
	ELSEIF 知識素質:AUTOCOM_実行番号保存:性知識 < 0
		;無知や獣並の性知識だと何も気にせず姿を現す
		CFLAG:AUTOCOM_実行番号保存:現在マップ種別 = 0
		CFLAG:AUTOCOM_実行番号保存:現在位置 = CFLAG:PLAYER:現在位置
		CFLAG:AUTOCOM_実行番号保存:同行 = 60
		TARGET = AUTOCOM_実行番号保存
		CFLAG:AUTOCOM_実行番号保存:同室 = 1
		TFLAG:TARGET変更用 = AUTOCOM_実行番号保存
	ENDIF
ELSEIF CFLAG:PLAYER:隠密
	;キャラが隠密あなたを発見しちゃう＝あなたに落ち度ややあり
	CALL SOURCE_CALC_征服(AUTOCOM_実行番号保存, PLAYER, 600)
	IF TALENT:AUTOCOM_実行番号保存:恋慕 || TALENT:AUTOCOM_実行番号保存:セフレ
		IF BASE:AUTOCOM_実行番号保存:性欲 >= 1500 || CFLAG:AUTOCOM_実行番号保存:発情期フラグ < 0
			;陥落済みかつ性欲が溜まってると姿を現してうふふ突入
			CFLAG:AUTOCOM_実行番号保存:隠密 = 1
			CFLAG:AUTOCOM_実行番号保存:同行 = 1
			TARGET = AUTOCOM_実行番号保存
			TARGET:1 = AUTOCOM_実行番号保存
			CFLAG:PLAYER:うふふ = 1
			CALL うふふ開始キャラ処理(AUTOCOM_実行番号保存, "あなたの自慰目撃")
		ENDIF
	ELSEIF 知識素質:AUTOCOM_実行番号保存:性知識 < 0
		;無知や獣並の性知識だと何も気にせず姿を現す
		CFLAG:AUTOCOM_実行番号保存:隠密 = 1
		CFLAG:AUTOCOM_実行番号保存:同行 = 1
		TARGET = AUTOCOM_実行番号保存
		TARGET:1 = AUTOCOM_実行番号保存
	ENDIF
ELSE
	;キャラが隠密中に隠れてないあなたを発見しちゃう＝あなたに落ち度あるが
	;キャラが隠密中＝野外オナニー中なのでどっちもどっち
	CALL SOURCE_CALC_征服(AUTOCOM_実行番号保存, PLAYER, 200)
ENDIF


@AUTOCOM_ABLE11(キャラ番号)
#DIM キャラ番号
;あなたが自慰コマンドした直後のみ
SIF RFLAG:自慰選択フラグ == 0
	RETURN 0
SIF PLAYER != MASTER
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
SIF CFLAG:PLAYER:うふふ
	RETURN 0
;自室かつ隠密だと絶対見つからない
SIF MYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) && CFLAG:PLAYER:隠密
	RETURN 0
;隠密自慰がバレるのは20%
;低い気もするけど、同室者全員分回すのでたくさん居たらほぼ見つかる
IF CFLAG:PLAYER:隠密
	SIF RAND:5 > 1
		RETURN 0
ENDIF
;キャラが隠密・あなたが非隠密時は100%バレるので処理分岐無し
;自室を訪ねてきてバレるパターンは仕様上TARGETが存在しない時に判定するので別処理
RETURN 1

@MESSAGE_AUTOCOM11
PRINTFORM %CALLNAME:AUTOCOM_実行番号保存%
IF MYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	PRINTFORML 拜访%CALLNAME:PLAYER%房间时意外撞见自慰场面…！
	IF (TALENT:AUTOCOM_実行番号保存:恋慕 || TALENT:AUTOCOM_実行番号保存:セフレ) && (BASE:AUTOCOM_実行番号保存:性欲 >= 1500 || CFLAG:AUTOCOM_実行番号保存:発情期フラグ < 0)
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%踏入%CALLNAME:PLAYER%房间表示"与其独自解决，不如…"
		PRINTFORML 并对%CALLNAME:PLAYER%发起邀请……
		PRINTL
		PRINTFORML %CALLNAME:PLAYER%接受邀请后将%CALLNAME:AUTOCOM_実行番号保存%推倒
	ELSEIF 知識素質:AUTOCOM_実行番号保存:性知識 < 0
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%毫不避讳地进入房间询问%CALLNAME:PLAYER%在做什么
		PRINTFORML %CALLNAME:PLAYER%含糊其辞勉强搪塞过去…
	ELSE
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%悄悄退出房间未被%CALLNAME:PLAYER%察觉…
	ENDIF
ELSEIF CFLAG:PLAYER:隠密
	PRINTFORML 意外发现隐匿中的%CALLNAME:PLAYER%并目击自慰行为…！
	IF (TALENT:AUTOCOM_実行番号保存:恋慕 || TALENT:AUTOCOM_実行番号保存:セフレ) && (BASE:AUTOCOM_実行番号保存:性欲 >= 1500 || CFLAG:AUTOCOM_実行番号保存:発情期フラグ < 0)
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%靠近隐匿位置表示"与其独自解决，不如…"
		PRINTFORML 并对%CALLNAME:PLAYER%发起邀请……
		PRINTL
		PRINTFORML %CALLNAME:PLAYER%接受邀请后将%CALLNAME:AUTOCOM_実行番号保存%推倒
	ELSEIF 知識素質:AUTOCOM_実行番号保存:性知識 < 0
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%毫不避讳地靠近询问%CALLNAME:PLAYER%在做什么
		PRINTFORML %CALLNAME:PLAYER%含糊其辞勉强搪塞过去…
	ELSE
		PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%悄悄离开未被%CALLNAME:PLAYER%察觉…
	ENDIF
ELSE
	PRINTFORML 隐匿时目击%CALLNAME:PLAYER%在同一地点开始自慰！
	PRINTFORML %CALLNAME:AUTOCOM_実行番号保存%继续隐匿未被%CALLNAME:PLAYER%发现…
ENDIF


@自宅訪問からの目撃者決定
#DIM 訪問候補者, 1000
#DIM キャラ番号
#DIM 候補番号
;自慰時に誰か来るのは1/3の確率
SIF RAND:3 > 0
	RETURN 0

;候補者リスト作成
候補番号 = 0
FOR キャラ番号, 1, CHARANUM
	;島にいる
	SIF CFLAG:キャラ番号:滞在期間 < 1
		CONTINUE
	;寝てる
	SIF CFLAG:キャラ番号:睡眠
		CONTINUE
	;好感度が一定以上か、従業員のどちらか
	SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2 && !TALENT:キャラ番号:従業員
		CONTINUE
	SIF MODE_存在判定_ターゲット側("移動不可系", キャラ番号)
		CONTINUE
	SIF MODE_存在判定_ターゲット側("全裸散歩", キャラ番号)
		CONTINUE
	訪問候補者:候補番号 = キャラ番号
	候補番号 += 1
	IF TALENT:キャラ番号:セフレ
		;セフレは確率２倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ELSEIF TALENT:キャラ番号:恋慕
		;恋慕は確率３倍
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
		訪問候補者:候補番号 = キャラ番号
		候補番号 += 1
	ENDIF
NEXT

;一人も候補がいないなら戻る
SIF 候補番号 == 0
	RETURN 0

;訪問キャラ決定
AUTOCOM_実行番号保存 = 訪問候補者:(RAND:候補番号)
RETURN 1011
