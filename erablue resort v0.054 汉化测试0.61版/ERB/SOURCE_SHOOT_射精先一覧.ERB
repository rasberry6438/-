;-------------------------------------------------------------------------------------------
;射精先の選択肢
;-------------------------------------------------------------------------------------------
@オート射精先設定
;射精先をいちいち選ばなくてもいいように設定する
DRAWLINE
PRINTL 射精时无需选择直接射精
PRINTL 当预设目标可用时自动选择该目标
PRINTL 多个可用目标时按优先级数字(小)优先选择
DRAWLINE

FOR LOCAL, 0, 31
	IF オート射精先オプション:LOCAL == -1
		PRINTBUTTON @"[{LOCAL}] 设置自动射精目标", LOCAL
		PRINTL
		BREAK
	ELSE
		TSTR:コマンド名受渡 = 
		TRYCALLFORM SHOOT_NAME_{オート射精先オプション:LOCAL}(-1)
		SIF TSTR:コマンド名受渡 == ""
			CONTINUE

		PRINTFORM 优先级{LOCAL}：%TSTR:コマンド名受渡, 20, LEFT%　　
		IF LOCAL > 0
			PRINTBUTTON "[↑]", LOCAL + 100
		ELSE
			PRINT     
		ENDIF
		IF LOCAL < 30 && オート射精先オプション:(LOCAL + 1) > -1
			PRINTBUTTON "[↓]", LOCAL + 200
		ELSE
			PRINT     
		ENDIF
		PRINT 　　
		PRINTBUTTON "[变更]", LOCAL
		PRINT   
		PRINTBUTTON "[解除]", LOCAL + 300
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTBUTTON @"[999] 返回", 999
PRINTL
DRAWLINE
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE IS < 100
		DRAWLINE
		FOR LOCAL:1, 0, 31
			TSTR:コマンド名受渡 = 
			TRYCALLFORM SHOOT_NAME_{LOCAL:1}(-1)
			SIF TSTR:コマンド名受渡 == ""
				CONTINUE
			PRINTBUTTON @"[{LOCAL:1}] %TSTR:コマンド名受渡%", LOCAL:1
			PRINTL
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 返回", 999
		PRINTL
		DRAWLINE
		BINPUT
		IF RESULT != 999
			オート射精先オプション:LOCAL = RESULT
		ENDIF
	CASE IS < 200
		;上に移動
		SWAP オート射精先オプション:(LOCAL - 100), オート射精先オプション:(LOCAL - 101)
	CASE IS < 300
		;下に移動
		SWAP オート射精先オプション:(LOCAL - 200), オート射精先オプション:(LOCAL - 199)
	CASEELSE
		ARRAYSHIFT オート射精先オプション, -1, -1, LOCAL - 300
ENDSELECT

RESTART


;-------------------------------------------------------------------------------------------
;SHOOT_0:膣内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_0(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 阴道内射精
RETURN 1


@SHOOT_PALAM_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｖ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "膣内射精回数")
履歴用実績:対象キャラ:膣内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):膣内射精フラグ = 1

CALL SOURCE_CALC_快Ｖ(対象キャラ, 実行キャラ, 200)
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;妊娠処理用に膣内射精された带着情欲計上
CALL TAKE_SPERM(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)


@SHOOT_ABLE_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;選択肢に出る条件
;Ｖ挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ｖ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＶ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＶ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_0(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
;PRINTFORM %CALLNAME:実行キャラ%在%CALLNAME:(対象キャラ)%的阴道深处で
;以上元部分 以下加筆部分
PRINTFORM %CALLNAME:実行キャラ%
IF CFLAG:対象キャラ:睡眠
	PRINTFORM 睡着了
ELSEIF TCVAR:対象キャラ:泥酔眠姦
	PRINTFORM 醉倒了
ENDIF
PRINTFORM %CALLNAME:(対象キャラ)%的
PRINTDATA
	DATA 阴道深处
;	DATA 最奥
;	DATA 肉穴の最奥
;教えの最奥がチラつく
	DATA 阴道最深处
	DATA 最深的地方
	DATA 最深的位置
ENDDATA
SELECTCASE RAND:2
	CASE 0
		PRINT に
		IF RAND:2 == 0
			PRINTDATA
				DATA 龟头
				DATA 阴茎前端
			ENDDATA
			PRINTDATA
				DATA 紧紧贴住
				DATA 完全贴合
				DATA 不断顶弄
				DATA 反复研磨
				DATA 
			ENDDATA
			PRINT 同时
		ENDIF
	CASEELSE
		PRINT 将
ENDSELECT
IF RFLAG:フィニッシュフラグ > 1
	PRINTDATA
		DATA 大量的
		DATA 浓稠
		DATA 惊人量的
	ENDDATA
ENDIF
PRINTDATA
	DATA 精液
	DATA 种子
;	DATA 種汁
;オラッ！APとBP満タンにしてやる！
	DATA 白浊
ENDDATA
PRINT
PRINTDATA
	DATA 喷涌而出
	DATA 飞溅四散
	DATA 猛烈释放
ENDDATA
PRINT …
PRINTL 
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 1

;-------------------------------------------------------------------------------------------
;SHOOT_1:腸内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_1(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 肠内射精
RETURN 1


@SHOOT_PALAM_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ａ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "腸内射精回数")
履歴用実績:対象キャラ:腸内射精回数 += 1

;Q.なんでフラグを別に作成してるの？
;A.将来的にコンドームとかに対応する用
RCVAR:(対象キャラ):腸内射精フラグ = 1

CALL SOURCE_CALC_快Ａ(対象キャラ, 実行キャラ, 200)
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 400)

;腸内射精された带着情欲計上
CALL TAKE_SPERM_A(対象キャラ, 実行キャラ, RCVAR:実行キャラ:射精量)

@SHOOT_ABLE_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;A挿入系モードじゃない時はアウト
{
SIF !MODE_存在判定_完全一致("Ａ挿入系", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("ハーヴィンオナホＡ", 実行キャラ, 対象キャラ)
	&& !MODE_存在判定_完全一致("挿入歩きＡ", 実行キャラ, 対象キャラ) 
	&& !MODE_存在判定_完全一致("ハーヴィンディルドＡ", 対象キャラ, 実行キャラ)
}
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_1(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%在%CALLNAME:(対象キャラ)%的肠道内将
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
IF 実行キャラ == MASTER
	CALL 挿入アニメ表示(対象キャラ, "_射精")
ENDIF
TCVAR:(対象キャラ):被射精位置 = 2

;-------------------------------------------------------------------------------------------
;SHOOT_2:口
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_2(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 口内射精
RETURN 1


@SHOOT_PALAM_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

EXP:(対象キャラ):Ｓ経験 += 5
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "口内射精回数")
履歴用実績:対象キャラ:口内射精回数 += 1

;ここで精飲するかどうかをRCVAR:(対象キャラ):精飲フラグで渡す
;喜んで飲む=2 なんとか飲み込む=1 吐き出す=0
RCVAR:(対象キャラ):精飲フラグ = 精飲フラグ(対象キャラ)
IF RCVAR:(対象キャラ):精飲フラグ > 0
	EXP:(対象キャラ):精飲経験 += RFLAG:フィニッシュフラグ
ENDIF

;精飲時、絶頂していると精飲快楽経験取得
IF RCVAR:(対象キャラ):絶頂の強度 && RCVAR:(対象キャラ):精飲フラグ
	EXP:(対象キャラ):精飲快楽経験 += RCVAR:(対象キャラ):絶頂の強度
ENDIF

CALL SOURCE_CALC_快Ｓ(対象キャラ, 実行キャラ, 100)
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)


@SHOOT_ABLE_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;フェラチオ系モードじゃない時はアウト
SIF MODE_存在判定_完全一致("フェラチオ系", 対象キャラ, 実行キャラ)
	RETURN 1
;パイズリ系モードなら技巧3以上かつ飲んでくれる場合のみOK
SIF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && 精飲フラグ(対象キャラ) && ABL:(対象キャラ):技巧 > 2 && !MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 1
RETURN 0


@MESSAGE_SHOOT_2(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	PRINTFORML %CALLNAME:(対象キャラ)%察觉射精征兆后，迅速含住%CALLNAME:実行キャラ%的阴茎，
	PRINTFORM %CALLNAME:実行キャラ%随即向%CALLNAME:(対象キャラ)%嘴巴将
ELSE
	PRINTFORM %CALLNAME:実行キャラ%对准%CALLNAME:(対象キャラ)%嘴巴将
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
SELECTCASE RCVAR:(対象キャラ):精飲フラグ
	CASE 0
		IF ABL:対象キャラ:技巧 > 2
			PRINTFORML %CALLNAME:対象キャラ%熟练地将嘴里的精液吐到纸巾上
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%剧烈咳嗽，将嘴里的精液尽数吐出
		ENDIF
	CASE 1
		PRINTFORML %CALLNAME:対象キャラ%略显笨拙地分次吞咽精液
	CASE 2
		IF 知識素質:対象キャラ:性知識 > 1
			PRINTFORM %CALLNAME:対象キャラ%为了证明已经吞下射入口中的精液，
			PRINTL 特意张开嘴展示已完全咽下精液的口腔
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%喉头滚动着将精液全数咽下
		ENDIF
ENDSELECT
TCVAR:(対象キャラ):被射精位置 = 3

;-------------------------------------------------------------------------------------------
;SHOOT_3:乳内
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_3(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 乳内射精
SIF 対象キャラ < 0 || TALENT:対象キャラ:バストサイズ < 1
	TSTR:コマンド名受渡 = 胸部射精
RETURN 1


@SHOOT_PALAM_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_快Ｂ(対象キャラ, 実行キャラ, 10)
CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):Ｂ経験 += 1
EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "乳内射精回数")
履歴用実績:対象キャラ:乳内射精回数 += 1

@SHOOT_ABLE_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_3(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%在%CALLNAME:(対象キャラ)%的
SELECTCASE TALENT:(対象キャラ):バストサイズ
	CASE IS < 1
		PRINT 平坦胸部上将
	CASE 1
		PRINT 乳沟里将
	CASE 2
		PRINT 丰满乳沟里将
	CASE 3
		PRINT 丰硕而深邃乳沟里将
ENDSELECT
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
TCVAR:(対象キャラ):被射精位置 = 4

;-------------------------------------------------------------------------------------------
;SHOOT_5:顔射
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_5(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 颜射
RETURN 1


@SHOOT_PALAM_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ

;履歴登録
RFLAG:履歴用数値保存 = 1
CALL 履歴登録チェック(対象キャラ, "顔射回数")
履歴用実績:対象キャラ:顔射回数 += 1


@SHOOT_ABLE_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;パイズリ系モードでも口戯系モードでもない時はアウト
SIF !MODE_存在判定_完全一致("パイズリ系", 対象キャラ, 実行キャラ) && !MODE_存在判定_完全一致("口戯系", 対象キャラ, 実行キャラ)
	RETURN 0
;縦パイズリは駄目
SIF MODE_存在判定_完全一致("縦パイズリ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_5(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%对准%CALLNAME:(対象キャラ)%的脸将
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
TCVAR:(対象キャラ):被射精位置 = 5

;-------------------------------------------------------------------------------------------
;SHOOT_6:腹射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_6(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 腹部释放
RETURN 1


@SHOOT_PALAM_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;仰向けの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ正常位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ対面立位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_順不同("兜合わせ", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("ボテ腹ズリ", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_6(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%朝着%CALLNAME:(対象キャラ)%的\@PREGNANCY_SIZE(対象キャラ) >= 3?孕肚#肚脐\@将
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
TCVAR:(対象キャラ):被射精位置 = 6

;-------------------------------------------------------------------------------------------
;SHOOT_7:背中射精
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_7(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 背部释放
RETURN 1


@SHOOT_PALAM_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;うつぶせの体位時
LOCAL = 0
SIF MODE_存在判定_完全一致("後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ後背位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面座位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ背面駅弁", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ立ちバック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_7(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%对准%CALLNAME:(対象キャラ)%的背部将
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
TCVAR:(対象キャラ):被射精位置 = 7

;-------------------------------------------------------------------------------------------
;SHOOT_8:大腿射精
;-------------------------------------------------------------------------------------------

@SHOOT_NAME_8(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 大腿射精
SIF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	TSTR:コマンド名受渡 = 大腿内射
RETURN 1


@SHOOT_PALAM_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 350)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;素股、疑似性交と足が閉じてる体位
LOCAL = 0
SIF MODE_存在判定_完全一致("寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ寝バック", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ側位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ａ屈曲位", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("素股", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("疑似性交", 実行キャラ, 対象キャラ)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_8(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

PRINTFORM %CALLNAME:実行キャラ%让%CALLNAME:対象キャラ%
IF 対象キャラ >= 0 && 下半身体型(対象キャラ) >= 2
	PRINT 紧夹丰满的大腿，将
ELSE
	PRINT 张开大腿将
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…
TCVAR:(対象キャラ):被射精位置 = 8

;-------------------------------------------------------------------------------------------
;SHOOT_9:掌心释放
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_9(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 掌心释放
RETURN 1


@SHOOT_PALAM_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;手コキ系モードじゃない時はアウト
LOCAL = 0
SIF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ)
	LOCAL += 1
SIF MODE_存在判定_完全一致("Ｃ愛撫", 対象キャラ, 実行キャラ) && GETBIT(TALENT:実行キャラ:性別, 1)
	LOCAL += 1
SIF LOCAL == 0
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_9(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF MODE_存在判定_完全一致("手コキ系", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2
	PRINTFORM %CALLNAME:(対象キャラ)%察觉射精征兆后，用掌心把%CALLNAME:実行キャラ%的
	PRINTDATA
		DATA 龟头
		DATA 阴茎前端
	ENDDATA
	PRINTL 包裹住
	PRINTFORM %CALLNAME:実行キャラ%直接在%CALLNAME:(対象キャラ)%的掌心将
ELSE
	PRINTFORM %CALLNAME:実行キャラ%向%CALLNAME:(対象キャラ)%的掌心将
ENDIF
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER
	PRINTFORM %CALLNAME:対象キャラ%摊开手掌，
	IF RFLAG:フィニッシュフラグ >= 3
		PRINT 展示着满溢而出的
	ELSEIF RFLAG:フィニッシュフラグ > 1
		PRINT 凝视着粘稠的
	ELSE
		PRINT 铺开的
	ENDIF
	IF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:サド性感 > 2
		PRINTFORML 精液向%CALLNAME:実行キャラ%投去愉悦的眼神
	ELSEIF RFLAG:フィニッシュフラグ > 1 && ABL:対象キャラ:奉仕精神 > 5
		PRINTFORML 精液向%CALLNAME:実行キャラ%露出自豪的微笑
	ELSEIF CFLAG:対象キャラ:発情期フラグ < 0
		IF 知識素質:対象キャラ:性知識 == -2
			PRINT 本能地被精液吸引，
		ELSE
			PRINT 带着情欲
		ENDIF
		PRINTL 用炽热目光注视着掌心
	ELSEIF 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTL 怜爱地轻抚着残留的精液
	ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
		PRINTL 好奇地用指尖拨弄着精液
	ELSE
		PRINT 抽出纸巾
		IF TALENT:対象キャラ:汚臭耐性 < 0
			PRINTDATA
				DATA 反复
				DATA 细致地
				DATA 彻底地
			ENDDATA
		ENDIF
		PRINTL 擦拭干净
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 9

;-------------------------------------------------------------------------------------------
;SHOOT_10:足部释放
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_10(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 足部释放
RETURN 1


@SHOOT_PALAM_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

CALL SOURCE_CALC_接触(対象キャラ, 実行キャラ, 200)

EXP:(対象キャラ):精液経験 += RFLAG:フィニッシュフラグ


@SHOOT_ABLE_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

;選択肢に出る条件
;足コキモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ)
	RETURN 0
RETURN 1


@MESSAGE_SHOOT_10(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
#DIM フィニッシュ反応

フィニッシュ反応 = 0
IF MODE_存在判定_完全一致("足コキ", 対象キャラ, 実行キャラ) && ABL:対象キャラ:技巧 > 2
	IF ABL:対象キャラ:サド性感 > 2
		フィニッシュ反応 = 2
		PRINTFORML %CALLNAME:対象キャラ%察觉射精征兆后，用足底狠狠碾压%CALLNAME:実行キャラ%的阴茎
	ELSEIF ABL:対象キャラ:奉仕精神 > 5
		フィニッシュ反応 = 1
		PRINTFORML %CALLNAME:対象キャラ%察觉射精征兆后，用双足温柔裹住%CALLNAME:実行キャラ%的阴茎
	ENDIF
ENDIF
PRINTFORM %CALLNAME:実行キャラ%\@ フィニッシュ反応 ? 直接 # \@在%CALLNAME:対象キャラ%的足部将
SIF RFLAG:フィニッシュフラグ > 1
	PRINT 大量的
PRINTL 精液喷涌而出…

IF !CFLAG:対象キャラ:睡眠 && 対象キャラ != MASTER
	IF フィニッシュ反応 == 1
		PRINTFORM %CALLNAME:対象キャラ%松开双足，在腿间
		IF RFLAG:フィニッシュフラグ > 1
			PRINT 骄傲展示形成白浊液桥的
		ELSE
			PRINT 呈现扩散状的
		ENDIF
		PRINTFORML 精液给%CALLNAME:実行キャラ%观赏
	ELSEIF フィニッシュ反応 != 2 && 実行キャラ == MASTER && TALENT:対象キャラ:恋慕
		PRINTFORM 当%CALLNAME:実行キャラ%射精时，%CALLNAME:対象キャラ%享受着
		SIF RFLAG:フィニッシュフラグ > 1
			PRINT 大量的
		PRINTL 精液在足部喷涌的触感
	ELSE
		PRINTFORM %CALLNAME:対象キャラ%移开足部，
		IF RFLAG:フィニッシュフラグ > 1
			PRINT 凝视着粘稠的
		ELSE
			PRINT 处理附着的
		ENDIF
		IF フィニッシュ反応 == 2
			PRINTFORML 精液并坏笑着展示给%CALLNAME:実行キャラ%
		ELSEIF (TALENT:対象キャラ:性的興味 > 0 && 知識素質:対象キャラ:性知識 == 0) || 知識素質:対象キャラ:性知識 == -2
			PRINTL 兴致勃勃用双足拨弄精液
		ELSE
			PRINT 抽出纸巾
			IF TALENT:対象キャラ:汚臭耐性 < 0
				PRINTDATA
					DATA 反复
					DATA 细致地
					DATA 彻底地
				ENDDATA
			ENDIF
			PRINTL 擦拭干净
		ENDIF
	ENDIF
ENDIF
TCVAR:(対象キャラ):被射精位置 = 10

;-------------------------------------------------------------------------------------------
;SHOOT_30:他にどこにも射精せない時用
;-------------------------------------------------------------------------------------------
@SHOOT_NAME_30(対象キャラ)
#DIM 対象キャラ
TSTR:コマンド名受渡 = 射到地上
RETURN 1


@SHOOT_PALAM_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ


@SHOOT_ABLE_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ
RETURN 1


@MESSAGE_SHOOT_30(実行キャラ, 対象キャラ)
#DIM 実行キャラ
#DIM 対象キャラ

IF 対象キャラ == -1
	;非うふふ時射精
	IF RFLAG:コマンド結果受渡し変数 == 23 && 実行キャラ == MASTER
		;モブ逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%在陌生女性的阴道内喷射精液的同时迎来高潮…
	ELSEIF RFLAG:コマンド結果受渡し変数 == 24 && 実行キャラ == MASTER
		;モブA逆レイプ
		PRINTFORML %CALLNAME:実行キャラ%在不知名对象的肛门内释放精液时迎来高潮…
	ELSEIF TEQUIP:実行キャラ:下半身下着あり
		PRINTFORML %CALLNAME:実行キャラ%在贴身衣物内倾泻精液的同时迎来高潮…
	ELSEIF TEQUIP:実行キャラ:下半身服あり
		PRINTFORML %CALLNAME:実行キャラ%在外衣内侧喷射精液时迎来高潮…
	ELSE
		PRINTFORML %CALLNAME:実行キャラ%直接从阴茎喷射精液迎来高潮…
	ENDIF
ELSE
	PRINTFORML %CALLNAME:実行キャラ%直接从阴茎喷射精液迎来高潮…
ENDIF
