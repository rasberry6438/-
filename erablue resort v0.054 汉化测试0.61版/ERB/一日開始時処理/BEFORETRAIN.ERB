@EVENTTRAIN0
#DIM 定住フラグ
#DIM 同行配列番号
#DIM デート件数
#DIM 判定値
#DIM 店増加数
#DIMS 属性登録
#DIM 知識番号
#DIM 候補配列, 50


VARSET TARGET
DRAWLINE
CALL 招待キャラ来訪処理
CALL 常連キャラ来訪処理
WAIT
DRAWLINE
;１００以上になった関係を完了させる
CALL 縁結び完了処理()

;初期化・滞在期間処理
FOR LOCAL,0,CHARANUM
	;危険日などを設定する
	CALL SET_PREGNANCY_PALAM(LOCAL)

	;同棲処理
	IF TALENT:LOCAL:定住者 == 1 && RAND:10 == 0
		;同棲を言い出す処理、確率は条件満たしてから１０％
		;今現在同棲していないこと
		IF キャラクター部屋検索(LOCAL) >= 1000 && STRCOUNT(定住者部屋割り配列:(キャラクター部屋検索(LOCAL) - 1000), "_") < 2
			;同行確率を見て一定値以上の相手が定住しているかどうかを見る
			FOR 同行配列番号, 0, 100
				SIF 同行候補キャラ番号:LOCAL:同行配列番号 < 1
					CONTINUE
				SIF 同行候補キャラ番号:LOCAL:同行配列番号 == LOCAL
					CONTINUE
				;相手が定住してないならダメ
				SIF TALENT:(同行候補キャラ番号:LOCAL:同行配列番号):定住者 != 1
					CONTINUE
				;相手の部屋が既に５人いたらダメ
				SIF STRCOUNT(定住者部屋割り配列:(キャラクター部屋検索(同行候補キャラ番号:LOCAL:同行配列番号) - 1000), "_") > 4
					CONTINUE
				;男女の組み合わせなら90以上、そうでないなら50以上が条件
				IF TALENT:LOCAL:性別 == 2 && TALENT:(同行候補キャラ番号:LOCAL:同行配列番号):性別 == 1 || TALENT:LOCAL:性別 == 1 && TALENT:(同行候補キャラ番号:LOCAL:同行配列番号):性別 == 2
					判定値 = 90
				ELSE
					判定値 = 50
				ENDIF
				;恋人同士だと判定値を50に
				SIF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:LOCAL:人物番号} Or 対象キャラ２ = {CFLAG:LOCAL:人物番号}) And (対象キャラ１ = {CFLAG:(同行候補キャラ番号:LOCAL:同行配列番号):人物番号} Or 対象キャラ２ = {CFLAG:(同行候補キャラ番号:LOCAL:同行配列番号):人物番号}) And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')")
					判定値 = 50
				SIF 同行候補キャラ確率:LOCAL:同行配列番号 < 判定値
					CONTINUE
				;素質とかで確率を変動させたいけど、とりあえずは25％くらいで
				IF RAND:4 == 0
					$引っ越し希望
					PRINTFORML %CALLNAME:LOCAL%希望搬离当前房间，与%CALLNAME:(同行候補キャラ番号:LOCAL:同行配列番号)%同住。
					PRINTL 是否同意搬迁？
					PRINTBUTTON "[0] 确定", 0
					PRINTL
					PRINTBUTTON "[1] 取消", 1
					PRINTL
					PRINTBUTTON @"[2] 查看%CALLNAME:LOCAL%的信息", 2
					PRINTL
					PRINTBUTTON @"[3] 查看%CALLNAME:(同行候補キャラ番号:LOCAL:同行配列番号)%的信息", 3
					PRINTL
					BINPUT
					IF RESULT == 0
						PRINTFORML 已批准%CALLNAME:LOCAL%的搬迁请求！
						PRINTFORMW %CALLNAME:LOCAL%开始与%CALLNAME:(同行候補キャラ番号:LOCAL:同行配列番号)%共同生活
						;部屋から削除
						RESULT = キャラクター部屋検索(LOCAL)
						IF STRFIND(定住者部屋割り配列:(RESULT - 1000), @"_{LOCAL}_") > -1
							定住者部屋割り配列:(RESULT - 1000) = %REPLACE(定住者部屋割り配列:(RESULT - 1000), @"_{LOCAL}_", "_")%
						ELSE
							定住者部屋割り配列:(RESULT - 1000) = %SUBSTRING(定住者部屋割り配列:(RESULT - 1000), STRLENS(@"{LOCAL}_"), -1)%
						ENDIF
						;部屋に追加
						RESULT = キャラクター部屋検索(同行候補キャラ番号:LOCAL:同行配列番号)
						定住者部屋割り配列:(RESULT - 1000) += @"{LOCAL}_"
					ELSEIF RESULT == 2
						CALL キャラ簡易紹介(LOCAL)
						WAIT
						GOTO 引っ越し希望
					ELSEIF RESULT == 3
						CALL キャラ簡易紹介(同行候補キャラ番号:LOCAL:同行配列番号)
						WAIT
						GOTO 引っ越し希望
					ELSE
						PRINTFORMW 拒绝了%CALLNAME:LOCAL%的搬迁请求
					ENDIF
					BREAK
				ENDIF
			NEXT
		ENDIF
	ENDIF
	;一日期間を減らす
	IF CFLAG:LOCAL:滞在期間 > 0 && CFLAG:LOCAL:滞在期間 < 999  && !CFLAG:LOCAL:長期雇用
		CFLAG:LOCAL:滞在期間 -= 1
		IF CFLAG:LOCAL:滞在期間 == 0
			定住フラグ = 0
			IF MATCH(開拓エリア開発番号, 大規模番号_居住区エリア) > 0
				IF TALENT:LOCAL:常連 && TALENT:LOCAL:定住者 == 0 && 定住エリア満杯() == 0
					;常連かつ定住者じゃない時、一定確率で定住を申し出る
					;素質とかで確率を変動させたいけど、とりあえずは25％くらいで
					IF OPTION変数:キャラ自動移住 == 2
						;申し出てこない
					ELSEIF OPTION変数:キャラ自動移住 == 1 && 陥落チェック(LOCAL) == 0
						;陥落以外申し出てこない
					ELSEIF RAND:4 == 0
						$定住希望
						PRINTFORML %CALLNAME:LOCAL%希望在此度假岛永久定居
						IF OPTION変数:キャラ自動移住 == 3
							;申し出スキップ
							RESULT = 0
						ELSE
							PRINTL 是否批准定居？
							PRINTFORML 剩余房间数量：{RFLAG:定住枠残りカウント, 3}间
							DRAWLINE
							PRINTBUTTON "[0] 确定", 0
							PRINTL
							PRINTBUTTON "[1] 取消", 1
							PRINTL
							PRINTBUTTON @"[2] 查看%CALLNAME:LOCAL%的信息", 2
							PRINTL
							BINPUT
						ENDIF
						IF RESULT == 0
							PRINTFORML 已批准%CALLNAME:LOCAL%的定居申请！
							PRINTFORMW %CALLNAME:LOCAL%开始在居住区域的住宅区生活
							CALL 滞在者部屋割り配列削除処理(LOCAL)
							TALENT:LOCAL:定住者 = 1
							CFLAG:LOCAL:滞在期間 = 999
							FOR LOCAL:1, 0, 住宅街広さ()
								IF 定住者部屋割り配列:(LOCAL:1) == ""
									定住者部屋割り配列:(LOCAL:1) = {LOCAL}_
									BREAK
								ENDIF
							NEXT
							定住フラグ = 1
						ELSEIF RESULT == 2
							CALL キャラ簡易紹介(LOCAL)
							WAIT
							GOTO 定住希望
						ELSE
							PRINTFORMW 拒绝了%CALLNAME:LOCAL%的定居申请
						ENDIF
					ENDIF
				ENDIF
			ENDIF
			IF 定住フラグ == 0
				;0になったら帰宅し滞在期間をマイナスに
				PRINTFORML %CALLNAME:LOCAL%结束逗留返回原处
				IF CFLAG:LOCAL:リゾート来訪回数 >= 3 && TALENT:LOCAL:常連 == 0
					PRINTFORMW %CALLNAME:LOCAL%频繁造访度假村，成为了［常客］！
					TALENT:LOCAL:常連 = 1
				ENDIF
				CALL 滞在終了時初期化(LOCAL)
				CFLAG:LOCAL:滞在期間 = -1
			ENDIF
		ENDIF
	ENDIF

	;キャラ個別処理があれば呼び出す
	TRYCALLFORM DAYFIRST_CHARAEVENT_{NO:LOCAL}
	;滞在してないキャラはスルー
	IF CFLAG:LOCAL:滞在期間 == -1
		RCVAR:LOCAL:表示カット = 1
		CONTINUE
	ENDIF
	;AUTOCOM変数のリセット
	CALL AUTOCOM用変数リセット()
	;衣装設定
	CALL CLOTHES_TODAY(LOCAL)
	CALL CLOTHES_RESET(LOCAL)
	CALL CLOTHES_RESET_TRAIN(LOCAL)
	;勃起、ムード、理性、怒り
	BASE:LOCAL:勃起 = 0
	BASE:LOCAL:ムード = 0
	BASE:LOCAL:理性 = MAXBASE:LOCAL:11
	BASE:LOCAL:怒り = 0
	CFLAG:LOCAL:勃起度 = 0
	CFLAG:LOCAL:同行 = 0
	CFLAG:LOCAL:酔いすぎ = 0
	;経験表示用
	CALL EXP変動記録処理(LOCAL)
	;お祭り参加履歴
	IF 開催予定日取り == DAY
		IF LOCAL
			TRYCALLFORM MOVE_ROUTINE_%開催予定祭り名%(LOCAL, "参加判定")
			IF RESULT
				SPLIT 開催予定祭り名, "_", LOCALS
				CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, "ーーーーー" + LOCALS:1 + "に参加したーーーーー","日常リゾート")
			ENDIF
		ELSE  ;あなたは主催者
			SPLIT 開催予定祭り名, "_", LOCALS
			CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, "ーーーーー" + LOCALS:1 + "を開催したーーーーー","日常リゾート")
		ENDIF
	ENDIF
	;デートお誘い用の判定を設定
	;平凡以上のデートを経験した事があるキャラは、最大スコアに応じて確率UP
	デート件数 = DT_SELECT("デートスコアデータベース", @"デート相手人物番号 = {CFLAG:LOCAL:人物番号}", "デートスコア DESC")
	IF デート件数
		;デートスコア降順で並んでるはずなので、一個目のスコアを見る
		SELECTCASE DT_CELL_GET("デートスコアデータベース", RESULT:1, "デートスコア", 1)
			CASE IS < 7500
				;失敗のみの場合は来ない
				TCVAR:LOCAL:キャラからデート誘いフラグ = -1
			CASE IS < 15000
				;平凡時10％
				SIF RAND:10
					TCVAR:LOCAL:キャラからデート誘いフラグ = -1
			CASE IS < 30000
				;成功時20％
				SIF RAND:5
					TCVAR:LOCAL:キャラからデート誘いフラグ = -1
			CASE IS < 60000
				;大成功時40％
				SIF RAND:5 > 1
					TCVAR:LOCAL:キャラからデート誘いフラグ = -1
			CASEELSE
				;それ以外は60％
				SIF RAND:5 > 2
					TCVAR:LOCAL:キャラからデート誘いフラグ = -1
		ENDSELECT
	ELSE
		;デートがない場合はデート誘いを封印するためフラグに代入
		TCVAR:LOCAL:キャラからデート誘いフラグ = -1
	ENDIF
NEXT

IF 港改造度:0:0
	;店を持つかどうかは個別に判定
	店増加数 = 0
	FOR LOCAL,1,CHARANUM
		VARSET RESULTS
		属性登録 = 
		;定住してること
		SIF TALENT:LOCAL:定住者 < 1
			CONTINUE
		;従業員はダメ
		IF TALENT:LOCAL:従業員 > 0
			;仕事が商店経営ならヨシ！
			SIF 現在仕事:LOCAL:0 != -1
				CONTINUE
		ENDIF
		;もう店持ってるならダメ
		SIF TALENT:LOCAL:店持ち > 0
			CONTINUE
		;条件達成していること
		VARSET RESULTS
		RESULT = 0
		;特殊条件
		TRYCALLFORM 特殊店舗_建設条件_{NO:LOCAL}(LOCAL)
		属性登録 '= RESULTS:1
		;特殊店舗がまだ条件満たしてないけど汎用建てさせたくない時は-1を返す
		SIF RESULT == -1
			CONTINUE
		IF RESULTS == ""
			;通常条件
			;汎用の店持つ確率は１０％
			SIF RAND:10
				CONTINUE
			VARSET 候補配列
			LOCAL:1 = 0
			FOR 知識番号, 1, 12
				SIF GROUPMATCH(知識番号, GETNUM(知識素質, "魔物知識"), GETNUM(知識素質, "高貴"), GETNUM(知識素質, "踊り子"), GETNUM(知識素質, "歌唱技能"))
					CONTINUE
				IF 知識素質:LOCAL:知識番号 >= 2
					候補配列:(LOCAL:1) = 知識番号
					LOCAL:1 ++
				ENDIF
			NEXT
			;候補がないなら飛ばす
			SIF LOCAL:1 < 1
				CONTINUE
			;候補からランダム
			SELECTCASE 候補配列:(RAND:(LOCAL:1))
				CASE 1
					STRDATA RESULTS
						DATA 薬屋
					ENDDATA
					属性登録 = 薬屋
				CASE 2
					STRDATA RESULTS
						DATA 魔法道具店
						DATA 魔导书店
					ENDDATA
					属性登録 = 魔法店
				CASE 3
					STRDATA RESULTS
						DATA 工坊
						DATA 玩具店
						DATA 武器店
					ENDDATA
					属性登録 = 機械店
				CASE 4
					STRDATA RESULTS
						DATA 炼金工坊
						DATA 工作室
					ENDDATA
					属性登録 = 錬金店
				CASE 6
					STRDATA RESULTS
						DATA 工作室
						DATA 画廊
					ENDDATA
					属性登録 = 絵師店
				CASE 8
					STRDATA RESULTS
						DATA 釣具店
					ENDDATA
					属性登録 = 釣具店
				CASE 11
					STRDATA RESULTS
						DATA 小餐馆
						DATA 餐厅
						DATA 食堂
					ENDDATA
					属性登録 = 食事施設
			ENDSELECT
		ENDIF

		;店を登録
		CALL 商業区店登録処理(LOCAL, RESULTS, 属性登録)

		;配列がいっぱいならこれ以上登録出来ないため外れる
		IF RESULT == 0
			BREAK
		ELSE
			TALENT:LOCAL:店持ち = 1
			SIF 店増加数 == 0
				DRAWLINE
			PRINTFORML 商业区开设了%CALLNAME:LOCAL%的『%RESULTS%』！
		ENDIF
		
		;一日に出来る店は３つまで
		店増加数 ++ 
		SIF 店増加数 >= 3
			BREAK
	NEXT

	;モブ店舗は25%で発生
	IF RAND:4 == 0
		属性登録 = 
		店増加数 ++ 
		LOCAL = RAND(-4, -1)
		STRDATA RESULTS
			DATA 杂货店
			DATA 食堂
			DATA 道具店
		ENDDATA
		IF RESULTS == "食堂"
			属性登録 = 食事施設
		ENDIF
		CALL 商業区店登録処理(LOCAL, RESULTS, 属性登録)
		IF RESULT
			SELECTCASE LOCAL
				CASE -1
					LOCALS = モブおじ
				CASE -2
					LOCALS = モブおばさん
				CASE -3
					LOCALS = モブ青年
				CASE -4
					LOCALS = モブ女性
			ENDSELECT
			SIF 店増加数 == 0
				DRAWLINE
			PRINTFORML 商业区开设了%LOCALS%的『%RESULTS%』！
		ENDIF
	ENDIF
	
	SIF 店増加数
		WAIT
ENDIF

SIF SAVESTR:10 == ""
	SAVESTR:10 = /
;部屋の並び順のために好感度レベルで降順に並び替え
CALL SORT_CHARALIST()
;一日の行動ルーチンの設定・モブ観光客来場
CALL 起床時処理


;15分毎に館内のキャラを移動させる
LOCAL = 1
WHILE LOCAL
	IF TIME + 15 < CFLAG:MASTER:起床予定時刻
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		TIME += 15
		CALL CHARA_MOVEMENT
	ELSE
		FOR LOCAL:1,0,CHARANUM
			;現在位置の保存
			CFLAG:(LOCAL:1):前ターン位置 = CFLAG:(LOCAL:1):現在位置
			CFLAG:(LOCAL:1):前マップ種別 = CFLAG:(LOCAL:1):現在マップ種別
		NEXT
		TIME = CFLAG:MASTER:起床予定時刻
		;経過時間表示用
		TIME:2 = TIME
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		CALL CHARA_MOVEMENT
		BREAK
	ENDIF
WEND
PRINTW 
CALL GOODMORNING

;起床時刻の記録
CFLAG:MASTER:起床時刻 = 1440 * DAY + TIME

;調教者は誰か
PLAYER = MASTER

@GOODMORNING
#DIM 秘書一覧, 100
#DIM 秘書人数
#DIM 対象キャラ
#DIM 告白キャラ
#DIM 起こしに来る秘書
#DIM レイヤー記録
#DIM PLAYER記録

VARSET LOCAL
VARSET 秘書一覧
TARGET = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:LOCAL:現在マップ種別
		TARGET = LOCAL
NEXT
告白キャラ = 起床時告白キャラ判定()
秘書人数 = 0
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	SIF TCVAR:LOCAL:休暇中
		CONTINUE
	IF 現在仕事:LOCAL:0 == 3
		;秘書に従事してるキャラを一覧化
		秘書一覧:(秘書人数) = LOCAL
		秘書人数 += 1
	ENDIF
NEXT
IF FLAG:挿入睡眠相手記録
	TCVAR:(FLAG:挿入睡眠相手記録):挨拶フラグ = 1
	;ハーヴィンオナホとか挿入歩きしたまま寝たパターン
	PRINTFORMW %CALLNAME:MASTER%在睡梦中感觉股间传来温热触感
	IF STRFIND(SAVESTR:挿入睡眠モード記録, "ハーヴィン") > -1
		;ハーヴィンオナホ、ディルドパターン
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:(FLAG:挿入睡眠相手記録):画像フォルダ%/起床時_ハーヴィンオナホ系")
		PRINTFORMW 掀开被子发现%CALLNAME:(FLAG:挿入睡眠相手記録)%整晚保持固定姿势
		PRINTFORMW 因多次高潮而体力耗尽的%CALLNAME:(FLAG:挿入睡眠相手記録)%仍处于昏迷状态…
		PRINTFORMW %CALLNAME:MASTER%解除了%CALLNAME:(FLAG:挿入睡眠相手記録)%的拘束并护送回房

		BASE:(FLAG:挿入睡眠相手記録):体力 = 0
		CFLAG:(FLAG:挿入睡眠相手記録):体力限界 = 1
		CFLAG:(FLAG:挿入睡眠相手記録):睡眠 = 1
		EXP:(FLAG:挿入睡眠相手記録):ダウン経験 += 1
		CFLAG:(FLAG:挿入睡眠相手記録):現在位置 = キャラクター部屋検索(FLAG:挿入睡眠相手記録)
		CFLAG:(FLAG:挿入睡眠相手記録):現在マップ種別 = 部屋検索_マップ種別
		;経験取得表示スキップ用
		TCVAR:(FLAG:挿入睡眠相手記録):436 = EXP:(FLAG:挿入睡眠相手記録):ダウン経験
	ELSE
		;挿入歩きパターン
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:(FLAG:挿入睡眠相手記録):画像フォルダ%/起床時_挿入歩き系")
		PRINTFORMW 身旁可见仍保持插入状态的%CALLNAME:(FLAG:挿入睡眠相手記録)%
		PRINTFORM 先醒来的%CALLNAME:(FLAG:挿入睡眠相手記録)%缓缓从
		IF STRFIND(SAVESTR:挿入睡眠モード記録, "Ｖ") > -1
			PRINT 阴道内
		ELSE
			PRINT 肛穴内
		ENDIF
		PRINTFORMW 退出%CALLNAME:MASTER%的性器官并起身
		PRINTFORMW %CALLNAME:(FLAG:挿入睡眠相手記録)%整理好衣物先行离开房间…
	ENDIF
	FLAG:挿入睡眠相手記録 = 0
	SAVESTR:挿入睡眠モード記録 = 
ELSEIF TARGET
	IF CFLAG:TARGET:睡眠
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:TARGET:画像フォルダ%/起床時_睡眠")
		PRINTFORMW %CALLNAME:MASTER%醒来时身旁的%CALLNAME:TARGET%仍在安睡
		PRINTFORMW %CALLNAME:MASTER%从%CALLNAME:TARGET%的\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? 床铺 # 其身旁 \@轻手轻脚离开，开始准备新的一天
	ELSE
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:TARGET:画像フォルダ%/起床時_同時起床")
		IF 陥落チェック(TARGET)
			PRINTFORMW %CALLNAME:MASTER%醒来时与%CALLNAME:TARGET%四目相对
			PRINTFORMW 与%CALLNAME:TARGET%短暂对视后\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? 离开床铺 # 从其身旁离开 \@，开始新的一天
		ELSE
			PRINTFORMW %CALLNAME:MASTER%醒来时与%CALLNAME:TARGET%四目相对
			PRINTFORMW 虽然疑惑为何%CALLNAME:TARGET%会在\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? 房间内 # 身旁 \@仍保持镇定开始日常准备
		ENDIF
		TRYCALLFORM KOJO_EVENT_逆おはよう_{NO:TARGET}(TARGET)
	ENDIF
ELSEIF 告白キャラ > 0
	CALL 起床時告白処理(告白キャラ)
ELSEIF 秘書人数 > 0
	起こしに来る秘書 = 秘書一覧:(RAND:秘書人数)
	TCVAR:起こしに来る秘書:挨拶フラグ = 1
	;秘書がいるなら起こしに来る、秘書業務なら先に起きてるはずなのでキャラの起床処理は入れない
	TRYCCALLFORM KOJO_EVENT_秘書業務_朝起こし_{NO:起こしに来る秘書}(起こしに来る秘書)
	CATCH
		;朝起こしの口上が無い場合に汎用文章を出す
		IF 陥落チェック_性的(起こしに来る秘書) && GETBIT(TALENT:起こしに来る秘書:性別, 0) && 射精可能(MASTER) && EXP:起こしに来る秘書:膣内射精絶頂経験 && EXP:起こしに来る秘書:Ｖ性交経験 > 10 && 初体験済みチェック("Ｖ挿入初体験", 起こしに来る秘書) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%在睡梦中感到腰部附近有沉重感
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "Ｖ挿入")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				LOCALS '= CSTR:起こしに来る秘書:短冊グラ表示切り替え用文字列
				IF LOCALS == ""
					SIF CSTR:起こしに来る秘書:表示グラフィックカテゴリ == ""
						CSTR:起こしに来る秘書:表示グラフィックカテゴリ = デフォルト
					LOCALS = %"短冊_" + CSTR:起こしに来る秘書:表示グラフィックカテゴリ%
				ENDIF
				SIF 差分抽出(起こしに来る秘書, "短冊_", LOCALS, "騎乗位", "")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			ENDIF
			PRINTFORM 睁眼发现%CALLNAME:起こしに来る秘書%正用\@ PREGNANCY_SIZE(起こしに来る秘書) > 2 && CFLAG:起こしに来る秘書:妊娠発覚 ? 摇晃着隆起的腹部 # \@阴道含住%CALLNAME:MASTER%的
			SELECTCASE TALENT:MASTER:男性器サイズ
				CASE IS <= -1
					PRINT 娇小的
				CASE 0
				CASEELSE
					PRINT 饱满的
			ENDSELECT
			PRINTW 阴茎…
			SIF TALENT:起こしに来る秘書:母乳体質
				PRINTFORMW 其袒露的%OPPAI_DESCRIPTION(起こしに来る秘書)%前端正缓缓滴落乳汁
			PRINTFORMW 作为秘书职责的一部分，正以骑乘位进行晨间唤醒服务
			IF TALENT:起こしに来る秘書:妊娠 && CFLAG:起こしに来る秘書:妊娠発覚 && CFLAG:起こしに来る秘書:子の父親 == MASTER
				PRINTFORMW 提醒孕期中的%CALLNAME:起こしに来る秘書%注意身体时，
				PRINTFORMW %CALLNAME:起こしに来る秘書%\@ TALENT:起こしに来る秘書:恋慕 ? 露出幸福微笑表示宝宝也需要爸爸的爱 # 喃喃说着宝宝也需要爸爸的牛奶 \@，反而更加激烈地摆动腰肢…
			ENDIF
			PRINTFORMW 当%CALLNAME:MASTER%即将射精时，\@ TALENT:起こしに来る秘書:性転換 == 2 && !キャラ一致チェック(起こしに来る秘書, "[开拓的炼金术师]卡莉奥斯特萝") ? 已完全适应女性身体的 # \@%CALLNAME:起こしに来る秘書%更用力地压下腰肢，满脸期待地等待那一刻。
			PRINTFORMW 感受着子宫内精液的灼热，仍沉浸在高潮余韵中的%CALLNAME:起こしに来る秘書%愉悦地眯起了眼睛，在与%CALLNAME:MASTER%连接状态下喘息着汇报今日行程。
			PRINTFORML Ｃ经验 + 10（%CALLNAME:MASTER%）
			PRINTFORML 高潮经验 + 3（%CALLNAME:MASTER%）
			PRINTFORML Ｃ高潮经验 + 3（%CALLNAME:MASTER%）
			PRINTFORML 射精经验 + 3（%CALLNAME:MASTER%）
			PRINTFORML Ｖ经验 + 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML Ｖ高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			IF TALENT:起こしに来る秘書:母乳体質
				PRINTFORML 喷乳经验 + 3（%CALLNAME:起こしに来る秘書%）
				EXP:起こしに来る秘書:噴乳経験 += 3
			ENDIF
			PRINTFORML 精液经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 阴道内射精高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML Ｖ性交经验 + 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 侍奉快乐经验 + 3（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:Ｃ経験 += 5
			EXP:MASTER:絶頂経験 += 3
			EXP:MASTER:Ｃ絶頂経験 += 3
			EXP:MASTER:射精経験 += 3
			EXP:起こしに来る秘書:Ｖ経験 += 5
			EXP:起こしに来る秘書:Ｖ絶頂経験 += 3
			EXP:起こしに来る秘書:絶頂経験 += 3
			EXP:起こしに来る秘書:精液経験 += 3
			EXP:起こしに来る秘書:膣内射精絶頂経験 += 3
			EXP:起こしに来る秘書:Ｖ性交経験 += 5
			EXP:起こしに来る秘書:奉仕快楽経験 += 3
			;履歴登録
			RFLAG:履歴用数値保存 = 3
			CALL 履歴登録チェック(起こしに来る秘書, "膣内射精回数")
			履歴用実績:起こしに来る秘書:膣内射精回数 += 3
			CALL TAKE_SPERM(起こしに来る秘書, MASTER, 最大射精量(MASTER) * 3)
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSEIF 陥落チェック_性的(起こしに来る秘書) && 射精可能(MASTER) && EXP:起こしに来る秘書:腸内射精絶頂経験 && EXP:起こしに来る秘書:Ａ性交経験 > 10 && 初体験済みチェック("Ａ挿入初体験", 起こしに来る秘書) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%在睡梦中感到腰部附近有沉重感
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "Ａ挿入")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				LOCALS '= CSTR:起こしに来る秘書:短冊グラ表示切り替え用文字列
				IF LOCALS == ""
					SIF CSTR:起こしに来る秘書:表示グラフィックカテゴリ == ""
						CSTR:起こしに来る秘書:表示グラフィックカテゴリ = デフォルト
					LOCALS = %"短冊_" + CSTR:起こしに来る秘書:表示グラフィックカテゴリ%
				ENDIF
				SIF 差分抽出(起こしに来る秘書, "短冊_", LOCALS, "Ａ騎乗位", "")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			ENDIF
			PRINTFORM 睁眼发现%CALLNAME:起こしに来る秘書%正用肛穴含住%CALLNAME:MASTER%的
			SELECTCASE TALENT:MASTER:男性器サイズ
				CASE IS <= -1
					PRINT 娇小的
				CASE 0
				CASEELSE
					PRINT 饱满的
			ENDSELECT
			PRINTW 阴茎…
			SIF TALENT:起こしに来る秘書:母乳体質
				PRINTFORMW 其袒露的%OPPAI_DESCRIPTION(起こしに来る秘書)%前端正缓缓滴落乳汁
			PRINTFORMW 作为秘书职责的一部分，正以肛交骑乘位进行晨间唤醒服务
			PRINTFORMW 当%CALLNAME:MASTER%即将射精时，%CALLNAME:起こしに来る秘書%更用力地压下腰肢，满脸期待地等待那一刻
			PRINTFORMW 感受着肠道内精液的灼热，仍沉浸在高潮余韵中的%CALLNAME:起こしに来る秘書%愉悦地眯起了眼睛，在与%CALLNAME:MASTER%连接状态下喘息着汇报今日行程。
			PRINTFORML Ｃ经验 + 10（%CALLNAME:MASTER%）
			PRINTFORML 高潮经验 + 3（%CALLNAME:MASTER%）
			PRINTFORML Ｃ高潮经验 + 3（%CALLNAME:MASTER%）
			PRINTFORML 射精经验 + 5（%CALLNAME:MASTER%）
			PRINTFORML Ａ经验 + 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML Ａ高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			IF TALENT:起こしに来る秘書:母乳体質
				PRINTFORML 喷乳经验 + 3（%CALLNAME:起こしに来る秘書%）
				EXP:起こしに来る秘書:噴乳経験 += 3
			ENDIF
			PRINTFORML 精液经验 + 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 肠内射精高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML Ａ性交经验 + 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 侍奉快乐经验 + 3（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:Ｃ経験 += 5
			EXP:MASTER:絶頂経験 += 3
			EXP:MASTER:Ｃ絶頂経験 += 3
			EXP:MASTER:射精経験 += 5
			EXP:起こしに来る秘書:Ａ経験 += 5
			EXP:起こしに来る秘書:Ａ絶頂経験 += 3
			EXP:起こしに来る秘書:絶頂経験 += 3
			EXP:起こしに来る秘書:精液経験 += 5
			EXP:起こしに来る秘書:腸内射精絶頂経験 += 3
			EXP:起こしに来る秘書:Ａ性交経験 += 5
			EXP:起こしに来る秘書:奉仕快楽経験 += 3
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSEIF 陥落チェック_性的(起こしに来る秘書) && TALENT:起こしに来る秘書:母乳体質 && EXP:起こしに来る秘書:噴乳経験 > 10 && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%在睡梦中尝到类似乳汁的味道
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "授乳")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				LOCALS '= CSTR:起こしに来る秘書:短冊グラ表示切り替え用文字列
				IF LOCALS == ""
					SIF CSTR:起こしに来る秘書:表示グラフィックカテゴリ == ""
						CSTR:起こしに来る秘書:表示グラフィックカテゴリ = デフォルト
					LOCALS = %"短冊_" + CSTR:起こしに来る秘書:表示グラフィックカテゴリ%
				ENDIF
				SIF 差分抽出(起こしに来る秘書, "短冊_", LOCALS, "授乳手コキ", "")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			ENDIF
			PRINTFORMW 睁眼发现%CALLNAME:起こしに来る秘書%正将滴着母乳的%OPPAI_DESCRIPTION(起こしに来る秘書)%前端压在%CALLNAME:MASTER%嘴里。
			PRINTFORMW 作为秘书职责的一部分，正通过晨间哺乳进行唤醒服务
			PRINTFORMW 当%CALLNAME:MASTER%开始吮吸乳头时，%CALLNAME:起こしに来る秘書%神情恍惚地轻轻抱住正在吸奶的%CALLNAME:MASTER%头部。
			PRINTFORMW 充分进行哺乳Play后，%CALLNAME:起こしに来る秘書%微笑着返回工作岗位
			EXP:起こしに来る秘書:Ｂ経験 += 5
			EXP:起こしに来る秘書:Ｂ絶頂経験 += 3
			EXP:起こしに来る秘書:噴乳経験 += 3
			EXP:起こしに来る秘書:搾乳快楽経験 += 3
			PRINTFORML Ｂ经验+ 5（%CALLNAME:起こしに来る秘書%）
			PRINTFORML Ｂ高潮经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 喷乳经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 挤奶快乐经验 + 3（%CALLNAME:起こしに来る秘書%）
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSEIF 陥落チェック_性的(起こしに来る秘書) && EXP:起こしに来る秘書:精飲経験 > 10 && 射精可能(MASTER) && RAND:3 == 0
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "フェラチオ")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				LOCALS '= CSTR:起こしに来る秘書:短冊グラ表示切り替え用文字列
				IF LOCALS == ""
					SIF CSTR:起こしに来る秘書:表示グラフィックカテゴリ == ""
						CSTR:起こしに来る秘書:表示グラフィックカテゴリ = デフォルト
					LOCALS = %"短冊_" + CSTR:起こしに来る秘書:表示グラフィックカテゴリ%
				ENDIF
				SIF 差分抽出(起こしに来る秘書, "短冊_", LOCALS, "フェラチオ", "")
					CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			ENDIF
			PRINTFORMW %CALLNAME:MASTER%在睡梦中感觉股间传来温热触感
			PRINTFORM 掀开被子发现%CALLNAME:起こしに来る秘書%正在
			SELECTCASE TALENT:MASTER:男性器サイズ
				CASE IS <= -1
					PRINTL 用舌头玩弄着%CALLNAME:MASTER%娇小的阴茎……
				CASE 0
					PRINTL 舔舐着%CALLNAME:MASTER%的龟头……
				CASEELSE
					PRINTL 努力吞吐着%CALLNAME:MASTER%硕大的阴茎……
			ENDSELECT
			PRINTFORMW 作为秘书职责的一部分，正通过晨间口交进行唤醒服务
			PRINTFORMW 当%CALLNAME:MASTER%射精时，%CALLNAME:起こしに来る秘書%将精液一滴不漏地咽下后返回工作
			PRINTFORML Ｃ经验 + 10（%CALLNAME:MASTER%）
			PRINTFORML 高潮经验 + 1（%CALLNAME:MASTER%）
			PRINTFORML Ｃ高潮经验 + 1（%CALLNAME:MASTER%）
			PRINTFORML 射精经验 + 1（%CALLNAME:MASTER%）
			PRINTFORML 口交经验 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 饮精经验 + 1（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:Ｃ経験 += 10
			EXP:MASTER:絶頂経験 += 1
			EXP:MASTER:Ｃ絶頂経験 += 1
			EXP:起こしに来る秘書:口淫経験 += 3
			EXP:起こしに来る秘書:精飲経験 += 1
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSEIF GETBIT(陥落チェック_性的(起こしに来る秘書), 1) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%在睡梦中感到全身被快感包裹……
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "ペッティング")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				PLAYER記録 = PLAYER
				PLAYER = 起こしに来る秘書
				CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%コマンド実行時画像名称変換処理(起こしに来る秘書, "愛撫")%")
				PLAYER = PLAYER記録
			ENDIF
			PRINTFORMW 睁眼发现%CALLNAME:起こしに来る秘書%正在爱抚%CALLNAME:MASTER%的身体
			PRINTFORMW 似乎是通过晨间爱抚进行唤醒服务
			PRINTFORMW 察觉到%CALLNAME:MASTER%醒来后，%CALLNAME:起こしに来る秘書%加快爱抚节奏使%CALLNAME:MASTER%达到高潮
			PRINTFORMW 带着职业微笑，%CALLNAME:起こしに来る秘書%返回工作岗位
			LOCAL = 1
			PRINTFORML Ｃ经验 + 10（%CALLNAME:MASTER%）
			PRINTFORML Ｃ高潮经验 + 1（%CALLNAME:MASTER%）
			EXP:MASTER:Ｃ経験 += 10
			EXP:MASTER:Ｃ絶頂経験 += 1
			IF 射精可能(MASTER)
				PRINTFORML 射精经验 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:射精経験 += 1
			ENDIF
			IF GETBIT(TALENT:MASTER:性別, 0)
				PRINTFORML Ｂ经验 + 10（%CALLNAME:MASTER%）
				PRINTFORML Ｂ高潮经验 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:Ｂ経験 += 10
				EXP:MASTER:Ｂ絶頂経験 += 1
				LOCAL += 1
				IF EXP:MASTER:Ｖ経験 > 50
					PRINTFORML Ｖ经验 + 10（%CALLNAME:MASTER%）
					PRINTFORML Ｖ高潮经验 + 1（%CALLNAME:MASTER%）
					EXP:MASTER:Ｖ経験 += 10
					EXP:MASTER:Ｖ絶頂経験 += 1
					LOCAL += 1
				ENDIF
			ENDIF
			IF EXP:MASTER:Ａ経験 > 50
				PRINTFORML Ａ经验 + 10（%CALLNAME:MASTER%）
				PRINTFORML Ａ高潮经验 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:Ａ経験 += 10
				EXP:MASTER:Ａ絶頂経験 += 1
				LOCAL += 1
			ENDIF
			PRINTFORML 高潮经验 + {LOCAL}（%CALLNAME:MASTER%）
				EXP:MASTER:絶頂経験 += LOCAL
			WAIT
			CALL EXP変動記録処理(MASTER)
		ELSEIF 陥落チェック_性的(起こしに来る秘書) && 初体験日取得("キス", MASTER, 起こしに来る秘書) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%在睡梦中感到唇上传来柔软触感
			レイヤー記録 = TFLAG:口上用画像表示番号
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "キス")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			IF レイヤー記録 == TFLAG:口上用画像表示番号
				;専用画像が表示されてないということなので、現在の服のV挿入画像を見る
				CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%コマンド実行時画像名称変換処理(起こしに来る秘書, "キス")%")
			ENDIF
			PRINTFORMW 睁眼发现%CALLNAME:起こしに来る秘書%正在与%CALLNAME:MASTER%接吻
			PRINTFORMW 作为秘书职责的一部分，正通过晨间亲吻进行唤醒服务
			PRINTFORMW 察觉到%CALLNAME:MASTER%醒来后，%CALLNAME:起こしに来る秘書%微笑着返回工作岗位
			PRINTFORML 接吻经验 + 2（%CALLNAME:MASTER%）
			PRINTFORML 接吻经验 + 2（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:キス経験 += 2
			EXP:起こしに来る秘書:キス経験 += 2
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSE
			CALL 汎用一枚絵画像_名称算出(起こしに来る秘書, "秘書朝起こし_", "通常")
			CALL PRINT_STRL(@"IMAGEPATH_%CSTR:起こしに来る秘書:画像フォルダ%/%RESULTS%")
			PRINTFORMW %CALLNAME:MASTER%在睡梦中听到呼唤声
			PRINTFORMW %CALLNAME:起こしに来る秘書%准时前来执行叫醒服务
			PRINTFORMW %CALLNAME:MASTER%起身道谢后，%CALLNAME:起こしに来る秘書%返回工作岗位
		ENDIF
	ENDCATCH
ENDIF

;起床後特殊イベ
FOR 対象キャラ, 1, CHARANUM
	;ついでにここで添い寝解除
	CFLAG:対象キャラ:添い寝フラグ = 0
	IF CFLAG:対象キャラ:滞在期間 < 0
		CSTR:対象キャラ:特殊起床パターン記録 = 
		CONTINUE
	ENDIF
	SIF CSTR:対象キャラ:特殊起床パターン記録 == ""
		CONTINUE
	TRYCALLFORM MORNINGEVENT_%CSTR:対象キャラ:特殊起床パターン記録%(対象キャラ)
	CSTR:対象キャラ:特殊起床パターン記録 = 
NEXT
CALL 手紙お返事処理()
CALL CLOTHES_CHECK(MASTER)


@滞在終了時初期化(ARG)
#DIM ループ用
#DIM 編成番号

CFLAG:ARG:オナニー覚えたて = 0
CFLAG:ARG:現在マップ種別 = 0
CFLAG:ARG:現在位置 = 999
CFLAG:ARG:元気になる薬フラグ = 0
BASE:ARG:体力 = MAXBASE:ARG:体力

IF OPTION変数:編成解除時の装備解除
	;装備解除
	装備ステータス補正保存:ARG:装備証番号 = 0
	IF 装備ステータス補正保存:ARG:装備指輪番号
		DT_CELL_SET "所持指輪データベース", 装備ステータス補正保存:ARG:装備指輪番号, "装備キャラ番号", 0, 1
		装備ステータス補正保存:ARG:装備指輪番号 = 0
	ENDIF
	IF 装備ステータス補正保存:ARG:装備耳飾り番号
		DT_CELL_SET "所持耳飾りデータベース", 装備ステータス補正保存:ARG:装備耳飾り番号, "装備キャラ番号", 0, 1
		装備ステータス補正保存:ARG:装備耳飾り番号 = 0
	ENDIF
	CALL 装備再計算処理(ARG)
ENDIF

FOR ループ用, 0, 14
	SIF ループ用 > 3 && ループ用 < 10
		CONTINUE
	IF BATTLE_STATE:ループ用:キャラ登録番号 == ARG
		BATTLE_STATE:ループ用:キャラ登録番号 = BATTLE_STATE:(ループ用 + 1):キャラ登録番号
		BATTLE_STATE:(ループ用 + 1):キャラ登録番号 = BATTLE_STATE:(ループ用 + 2):キャラ登録番号
		BATTLE_STATE:(ループ用 + 2):キャラ登録番号 = BATTLE_STATE:(ループ用 + 3):キャラ登録番号
		BATTLE_STATE:(ループ用 + 3):キャラ登録番号 = 0
		BREAK
	ENDIF
NEXT

FOR 編成番号, 0, 編成セット最大数
	FOR ループ用, 0, 14
		SIF ループ用 > 3 && ループ用 < 10
			CONTINUE
		IF 編成セット記録配列:編成番号:(ループ用 * 10 + 100) == ARG
			編成セット記録配列:編成番号:(ループ用 * 10 + 100) = 編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 100)
			編成セット記録配列:編成番号:(ループ用 * 10 + 101) = 編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 101)
			編成セット記録配列:編成番号:(ループ用 * 10 + 102) = 編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 102)
			編成セット記録配列:編成番号:(ループ用 * 10 + 103) = 編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 103)
			
			編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 100) = 編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 100)
			編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 101) = 編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 101)
			編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 102) = 編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 102)
			編成セット記録配列:編成番号:((ループ用 + 1) * 10 + 103) = 編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 103)
			
			編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 100) = 編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 100)
			編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 101) = 編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 101)
			編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 102) = 編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 102)
			編成セット記録配列:編成番号:((ループ用 + 2) * 10 + 103) = 編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 103)
			
			編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 100) = 0
			編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 101) = 0
			編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 102) = 0
			編成セット記録配列:編成番号:((ループ用 + 3) * 10 + 103) = 0
			BREAK
		ENDIF
	NEXT
NEXT

CALL 滞在者部屋割り配列削除処理(ARG)
VARSET TCVAR:ARG:0

FOR LOCAL:1,0,200
	TCVAR:ARG:(400 + LOCAL:1) = EXP:ARG:(LOCAL:1)
NEXT

@発情期判定(ARG)
#FUNCTION
SIF TALENT:ARG:発情期あり < 0
	RETURNF 0
SELECTCASE TALENT:ARG:性別
	CASE 0
		RETURNF 0
	CASE 1
		SIF TALENT:ARG:妊娠不可 == 2
			RETURNF 0
	CASE 2
		SIF TALENT:ARG:射精不可 == 2
			RETURNF 0
	CASE 3
		SIF TALENT:ARG:妊娠不可 == 2 && TALENT:ARG:射精不可 == 2
			RETURNF 0
ENDSELECT

;各種設定
SIF TALENT:ARG:種族 == 2 && TALENT:ARG:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 0) == 0
	RETURNF 1
SIF TALENT:ARG:種族 == 2 && TALENT:ARG:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 1) == 0
	RETURNF 1
SIF (TALENT:ARG:種族 == 3|| キャラ一致チェック(ARG, "[六龙之『金』]伽莱翁")) && TALENT:ARG:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 2) == 0
	RETURNF 1
SIF (TALENT:ARG:種族 == 3|| キャラ一致チェック(ARG, "[六龙之『金』]伽莱翁")) && TALENT:ARG:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 3) == 0
	RETURNF 1
SIF TALENT:ARG:発情期あり > 0 && TALENT:ARG:性別 != 2 && GETBIT(OPTION変数:発情期切り替え, 4) == 0
	RETURNF 1
SIF TALENT:ARG:発情期あり > 0 && TALENT:ARG:性別 == 2 && GETBIT(OPTION変数:発情期切り替え, 5) == 0
	RETURNF 1


RETURNF 0





