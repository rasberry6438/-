;-------------------------------------------------
;强夺嘴唇
;コミュニケーション系コマンド、レベル1
;-------------------------------------------------
@RAPE_COMNAME9
#FUNCTIONS
TSTR:コマンド名受渡 = 强夺嘴唇



;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_9
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("うふふ_被挿入中")



@RAPE_COM_TOOLTIP_9
LOCALS = <br>■强夺嘴唇<br>
LOCALS += "强行夺取对方的吻。进入接吻模式。<br>[舌头灵巧][猫舌]特质将影响效果增减。"
LOCALS += "<br>强奸指令类型：嘿咻or被插入中or性骚扰<br>获取来源：暴露・接触・快Ｓ・不洁・痛苦(特定条件时)"
LOCALS += "<br>专属经验值：接吻经验(你・TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@RAPE_COM9
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("キス", PLAYER, TARGET)
;-------------------------------------------------
;RAPE_COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
RCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF !CFLAG:睡眠
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)

	;舌を使う調教では、調教者の舌使いによって、快感のソース増加
	IF TALENT:PLAYER:舌使い
		CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
	ENDIF

	IF TALENT:TARGET:舌使い
		CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
	ENDIF

	;猫舌では減少
	SIF TALENT:PLAYER:猫舌
		CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

	;同性愛　レズなら戻り値1ゲイなら戻り値2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:同性愛経験 += 3
		EXP:PLAYER:同性愛経験 += 3
	ENDIF

	SIF TALENT:恋慕
		EXP:愛情経験 += 1

	SIF TALENT:TARGET:猫舌
		EXP:嗜虐快楽経験 += 1
ENDIF
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｓ(PLAYER, TARGET, 150)
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 150)
ENDIF


;-------------------------------------------------
;経験の処理
;-------------------------------------------------

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	RCVAR:PLAYER:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>用%CALLNAME:TARGET%的[嘴唇]完成初吻</font><img src='えっちハート'>","うふふ")
ENDIF
CALL 初体験日登録処理(PLAYER, TARGET, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 1)
IF RESULT
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>被%CALLNAME:PLAYER%的[嘴唇]被夺走初吻</font><img src='えっちハート'>","うふふ")
	RFLAG:コマンド結果受渡し変数 = 1
	RCVAR:TARGET:ファーストキス中フラグ = 1
ENDIF
CALL 初体験日登録処理(TARGET, PLAYER, "キス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "唇", "", 0)


RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@RAPE_COM_ABLE9
;キスする実行判定
;一括管理
SIF GLOBAL_RAPE_COMABLE(9)
	RETURN RESULT
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;口を何かに使ってるなら無理
IF TEQUIP:PLAYER:口
	RETURN 0
ENDIF
SIF TEQUIP:TARGET:口
	RETURN 0
;キス魔は猫舌無視
SIF TALENT:TARGET:キス嗜好 == 2
	RETURN 1
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM9

PRINTFORM %CALLNAME:PLAYER%

IF CFLAG:TARGET:睡眠
	IF TCVAR:TARGET:泥酔眠姦
		PRINTFORM 醉倒了
	ELSE
		PRINTFORM 睡着了
	ENDIF
ENDIF
IF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	PRINTFORML 在%TEXTR("随心所欲/欲望驱使")%地%TEXTR("侵犯着/抽插着/摆动腰肢侵犯着/")%%CALLNAME:TARGET%的同时，
ELSE
	PRINTFORM 对%CALLNAME:TARGET%的
ENDIF

IF TALENT:TARGET:恋人持ち == 1
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 连恋人也未曾触碰的纯洁
	ELSE
		PRINTFORM 本应属于恋人的
	ENDIF
ELSEIF TALENT:TARGET:恋人持ち == 2
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 连伴侣也未曾触碰的纯洁
	ELSE
		PRINTFORM 本应属于伴侣的
	ENDIF
ELSEIF TALENT:TARGET:恋人持ち == 3
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 连已故伴侣也未曾触碰的纯洁
	ELSE
		PRINTFORM 曾经献给伴侣的
	ENDIF
ELSE
	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM 从未与人接吻的纯洁
	ELSE
		PRINTFORM %TEXTR("柔软的/水润的/诱人的/可爱的")%
	ENDIF
ENDIF

PRINTFORML 嘴唇被%TEXTR("强行夺取/暴力夺取并品尝/强行夺取并享受")%…

