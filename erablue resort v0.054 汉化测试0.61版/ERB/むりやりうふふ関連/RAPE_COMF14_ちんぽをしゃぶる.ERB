;-------------------------------------------------
;为其口交
;奉仕系コマンド、レベル2
;-------------------------------------------------
@RAPE_COMNAME14
#FUNCTIONS
TSTR:コマンド名受渡 = 为其口交


;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_14
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("うふふ")


@RAPE_COM_TOOLTIP_14
LOCALS = <br>■为其口交<br>
LOCALS += "用口腔吮吸爱抚对方阴茎的指令。<br>进入口交模式，在模式变更或性行为结束前持续产生特定效果。<br>乳交中执行会转变为[乳交口交]。"
LOCALS += "<br>强奸指令类型：嘿咻<br>获取来源：暴露・接触・侍奉・不洁"
LOCALS += "<br>专属经验值：口交经验(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM14
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
IF RFLAG:同時モード体位変化フラグ
	CALL MODE_CHANGE("パイズリフェラ", PLAYER, TARGET)
ELSEIF MODE_確認_完全一致("パイズリ", PLAYER, TARGET) < 0
	CALL MODE_CHANGE("フェラチオ", PLAYER, TARGET)
ELSE
	RFLAG:同時モード体位変化フラグ = 1
	CALL MODE_DELETE_プレイヤー側("パイズリ", PLAYER)
	CALL MODE_CHANGE("パイズリフェラ", PLAYER, TARGET)
ENDIF

;-------------------------------------------------
;RAPE_COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_露出(PLAYER, TARGET, 30)
CALL SOURCE_CALC_接触(PLAYER, TARGET, 120)
CALL SOURCE_CALC_奉仕(PLAYER, TARGET, 300)

CALL SOURCE_CALC_不潔(PLAYER, TARGET, 250)

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌使いによって、快感のソース増加
IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 150)
ENDIF

;猫舌では苦痛ちょいある
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
EXP:PLAYER:口淫経験 += 1

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 += 1

;奴隷が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

CALL 初体験日登録処理(PLAYER, TARGET, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "陰茎", "", 1)
IF RESULT
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>将初吻献给%CALLNAME:PLAYER%的[阴茎]</font><img src='えっちハート'>","うふふ")
ENDIF

RETURN 1


;実行判定処理はレイプだからすっ飛ばす。しゃぶる側なので。


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@RAPE_COM_ABLE14
;フェラチオ実行判定
SIF GLOBAL_RAPE_COMABLE(14)
	RETURN RESULT
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:TARGET:性別 & 2)
	RETURN 0
;ちんこがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;下半身着衣
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:TARGET:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:TARGET:下タイツあり && !TEQUIP:TARGET:下タイツ破れ
	RETURN 0
SIF TEQUIP:TARGET:下半身下着あり == 2
	RETURN 0
	
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
;口、ペニスを何かに使ってる・届かないなら無理
IF MODE_確認_完全一致("パイズリ", TARGET, PLAYER) < 0
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
	SIF TEQUIP:PLAYER:口
		RETURN 0
ENDIF

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM14

PRINTFORM %CALLNAME:PLAYER%
IF CFLAG:TARGET:睡眠
	IF TCVAR:TARGET:泥酔眠姦
		PRINTFORM 醉倒了
	ELSE
		PRINTFORM 睡着了
	ENDIF
ENDIF
PRINTFORM %CALLNAME:TARGET%的
	SIF CFLAG:TARGET:発情期フラグ < 0
		PRINTFORM 发情
	
	;GETBITのほうがいいんだろうけど結局のところよくわかってないのでTALENTで
	IF TALENT:非童貞 == 0
		PRINTFORM 童贞
	ELSEIF TALENT:イキやすさ == 1
		PRINTFORM %TEXTR("弱鸡/孱弱")%
	ELSEIF TALENT:Ｃ感度 == 1
		PRINTFORM 敏感
	ENDIF
	
	IF TALENT:男性器サイズ <= -1
		PRINTFORM %TEXTR("鸡鸡/小鸡鸡/迷你鸡巴")%
	ELSEIF TALENT:男性器サイズ == 0
		PRINTFORM %TEXTR("鸡巴/鸡鸡/阴茎")%
	ELSEIF TALENT:男性器サイズ == 1
		PRINTFORM %TEXTR("大肉棒/大鸡巴")%
	ELSEIF TALENT:男性器サイズ == 2
		PRINTFORM %TEXTR("大肉棒/大鸡巴")%
	ENDIF

PRINTFORML 含入%TEXTR("口腔/双唇")%，%TEXTR("用舌头黏腻地舔弄吮吸/享受在口中愉悦跳动的触感/裹着大量唾液仔细侍奉")%


