

;-------------------------------------------------------------------------------------------
;性欲ゲージ関連処理
;上昇量を素質で補正すること
;-------------------------------------------------------------------------------------------

@性欲ゲージ増加(キャラ番号)
#DIM キャラ番号
;性欲無しだと何もしない
SIF TALENT:キャラ番号:性欲 == -2
	RETURN 0

;基礎値=時間経過分/4の性欲（一日360）ABL欲望１につき10%の補正
LOCAL = (10 + ABL:キャラ番号:欲望) * TIME_PROGRESS(TFLAG:行動前時刻) / 40

;性欲素質 1=+20% -1=-20%
LOCAL = LOCAL * (10 + TALENT:キャラ番号:性欲 * 2) / 10

;未精通・初潮前は1/10
SELECTCASE TALENT:キャラ番号:性別
	CASE 0
		LOCAL /= 10
	CASE 1
		SIF TALENT:キャラ番号:妊娠不可 == 2
			LOCAL /= 10
	CASE 2
		SIF TALENT:キャラ番号:射精不可 == 2
			LOCAL /= 10
	CASE 3
		SIF TALENT:キャラ番号:妊娠不可 == 2 && TALENT:キャラ番号:射精不可 == 2
			LOCAL /= 10
ENDSELECT

;乱数で-20%～+20%まで上下
LOCAL = LOCAL * (100 + RAND:40 - 19) / 100

BASE:キャラ番号:性欲 = MIN(BASE:キャラ番号:性欲 + LOCAL, MAXBASE:キャラ番号:性欲)

;発情期は常時最大値の8割保証。エルーン系以外ならば6割保証
IF CFLAG:キャラ番号:発情期フラグ < 0
	IF TALENT:キャラ番号:種族 == 2 || GETBIT(TALENT:キャラ番号:発情期あり, 1) || キャラ一致チェック(キャラ番号, "[六龙之『金』]伽莱翁")
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 8 / 10)
	ELSEIF TALENT:キャラ番号:種族 == 3 || TALENT:キャラ番号:発情期あり
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 6 / 10)
	ENDIF
ENDIF


@性欲ゲージ減少処理(キャラ番号)
#DIM キャラ番号

;うふふ後、満足ゲージに応じて性欲ゲージ減少
;うふふ終了時以外に呼んでも当然なにもならない

BASE:キャラ番号:性欲 = MAX(0, BASE:キャラ番号:性欲 - BASE:キャラ番号:満足)

;ついでに絶頂累積消去
TCVAR:キャラ番号:うふふ中Ｃ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｂ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｖ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ａ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｓ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中消費体力累積 = 0
BASE:キャラ番号:満足 = 0


@満足ゲージ上昇処理(キャラ番号)
#DIM キャラ番号
#DIM 満足係数
#DIM 満足上昇量

;絶頂時、素質に応じて満足ゲージを上げる
;あなたは関係ない
IF キャラ番号 == MASTER
	RETURN 0
ENDIF
;連れ込み中は条件達成しないと上がらない
IF 連れ込みパターン != ""
	RETURN 0
ENDIF
;うふふじゃないなら上げない
IF !CFLAG:キャラ番号:うふふ
	RETURN 0
ENDIF

満足係数 = 1
;各絶頂の強度が高いほど満足
満足係数 += NOWEX:キャラ番号:Ｃ絶頂
満足係数 += NOWEX:キャラ番号:Ｖ絶頂
満足係数 += NOWEX:キャラ番号:Ａ絶頂
満足係数 += NOWEX:キャラ番号:Ｂ絶頂
満足係数 += NOWEX:キャラ番号:Ｓ絶頂

;係数１０倍
満足係数 = 満足係数 * 10
;素質影響
SIF TALENT:キャラ番号:精神的余裕 > 0
	満足係数 += 2
SIF TALENT:キャラ番号:精神的余裕 < 0
	満足係数 -= 2
SIF TALENT:キャラ番号:自制心 < 0
	満足係数 -= 1
SIF TALENT:キャラ番号:性欲 > 0
	満足係数 -= 2
SIF TALENT:キャラ番号:性欲 < 0
	満足係数 += 5
SIF TALENT:キャラ番号:羞恥心 > 0
	満足係数 += 3
SIF TALENT:キャラ番号:快感応答 > 0
	満足係数 += 1
SIF TALENT:キャラ番号:快感応答 < 0
	満足係数 -= 1


;体力消費量に掛ける
満足上昇量 = DOWNBASE:キャラ番号:体力 * 満足係数 / 10

BASE:キャラ番号:満足 = MIN(MAXBASE:キャラ番号:満足, BASE:キャラ番号:満足 + 満足上昇量)


@精力ゲージ_初期化(キャラ番号)
#DIM キャラ番号

MAXBASE:キャラ番号:精力 = 0
BASE:キャラ番号:精力 = 0
CALL 精力ゲージ_最大値変動処理(キャラ番号)


@精力ゲージ_最大値変動処理(キャラ番号)
#DIM キャラ番号
#DIM 変動前数値
#DIM 精力計算値

;-------------------------------------------------------------------------------------------
; 抜かずの3回したら4回目はフルパワー射精じゃなくなる程度（100）を一般人の一日での限界
; 精力は3日で全回復するものとし、100 * 3 = 300が一般人の最大精力とした
; （最大精力を100にする考えもあったが、さすがに1日33≒1射精は少なすぎるし
; 　ファンタジーなんだからもっとあってええやろ、となった
; 　なんならこれでもまだ少ないので、精力を干渉や成長で増やせるようにした ）
;-------------------------------------------------------------------------------------------
精力計算値 = 300
SELECTCASE あなた特殊能力:精力成長の加護
	CASE 1
		精力計算値 += EXP:キャラ番号:空打ち絶頂経験 / 5
	CASE 2
		精力計算値 += EXP:キャラ番号:空打ち絶頂経験 / 3
	CASE 3
		精力計算値 += EXP:キャラ番号:空打ち絶頂経験 / 2
	CASE 4
		精力計算値 += EXP:キャラ番号:空打ち絶頂経験
ENDSELECT

変動前数値 = MAXBASE:キャラ番号:精力
IF キャラ番号 == MASTER
	MAXBASE:キャラ番号:精力 = 9999
ELSEIF TALENT:キャラ番号:絶倫
	;絶倫は3倍
	MAXBASE:キャラ番号:精力 = MIN(精力計算値 + 600, 3000)
ELSE
	MAXBASE:キャラ番号:精力 = MIN(精力計算値, 2400)
ENDIF

;干渉による追加補正（成長上限とは別枠）
IF キャラ番号 != MASTER && 干渉強化変数:キャラ番号:干渉種類 == 11
	MAXBASE:キャラ番号:精力 += (干渉強化変数:キャラ番号:干渉ランク + 1) * 100
ENDIF

IF 変動前数値 >= MAXBASE:キャラ番号:精力
	BASE:キャラ番号:精力 = LIMIT(BASE:キャラ番号:精力, 0, MAXBASE:キャラ番号:精力)
ELSE
	BASE:キャラ番号:精力 = LIMIT(BASE:キャラ番号:精力 + MAXBASE:キャラ番号:精力 - 変動前数値, 0, MAXBASE:キャラ番号:精力)
ENDIF


@精力ゲージ_一日終了時回復処理(キャラ番号)
#DIM キャラ番号
#DIM 精力回復量

CALL 精力ゲージ_最大値変動処理(キャラ番号)
IF キャラ番号 == MASTER || CFLAG:キャラ番号:発情期フラグ < 0
	;あなたと発情期は全回復
	BASE:キャラ番号:精力 = MAXBASE:キャラ番号:精力
ELSE
	;それ以外は1/3回復（3日で全回復）
	精力回復量 = MAXBASE:キャラ番号:精力 * (10 + TALENT:キャラ番号:回復速度) / 30
	BASE:キャラ番号:精力 = MIN(BASE:キャラ番号:精力 + 精力回復量, MAXBASE:キャラ番号:精力)
ENDIF


@精力ゲージ_回復処理_共通(係数)
#DIM 係数
#DIM 精力回復量

SIF BASE:MASTER:精力 == MAXBASE:MASTER:精力
	RETURN

;イベントうふふ時の対応、時間経過しないため回復しない問題の対応
SIF 係数 == 0 && FLAG:イベントうふふ中フラグ
	係数 = 5

;精力が低いときほど大きく回復する
;真・絶倫でさらに回復速度アップ
精力回復量 = (10000 - BASE:MASTER:精力) * 係数 * (あなた特殊能力:真・絶倫 + 2) / 223

BASE:MASTER:精力 = MIN(BASE:MASTER:精力 + 精力回復量, MAXBASE:MASTER:精力)


@精力ゲージ_日中回復処理()
;経過時間(分)依存　半日(720分)で全回復するペース
CALL 精力ゲージ_回復処理_共通(TIME_PROGRESS(TFLAG:行動前時刻))


@精力ゲージ_コマンド外回復処理(ZP獲得量)
#DIM ZP獲得量
;ZP回復量依存
CALL 精力ゲージ_回復処理_共通(ZP獲得量 * 5)


@最小射精量(キャラ番号, 前立腺フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM 前立腺フラグ
#DIM 前立腺開発レベル

SIF !前立腺フラグ
	RETURNF 0

;通常、残り精力が少なくなると射精量も少なくなるが
;十分に開発された前立腺を刺激すれば無理やり吐き出させることができる
前立腺開発レベル = GETEXPLV(EXP:キャラ番号:前立腺開発, 7)
SIF 前立腺開発レベル < 4
	RETURNF 0

;男性器が小さいほど前立腺責めに弱い（唐突なファンタジー要素）
RETURNF (前立腺開発レベル - 3) * (5 - TALENT:キャラ番号:男性器サイズ)


@最大射精量(キャラ番号)
#FUNCTION
#DIM キャラ番号

;男性器がでかいと射精量が若干多くなる（唐突なファンタジー要素）
;ただし精力総量は変化しないので、男性器が小さいほうが射精回数は多くなる
RETURNF 30 * 5 / (5 - TALENT:キャラ番号:男性器サイズ)


@射精量強化適用(射精キャラ, 適用前強度)
#FUNCTION
#DIM 射精キャラ
#DIM 適用前強度
#DIM 能力レベル
#DIM CONST 乗数_粗 = 1, 2, 3, 7, 11, 16
#DIM CONST 乗数_小 = 1, 2, 3, 6, 10, 14
#DIM CONST 乗数_凡 = 1, 2, 3, 6,  9, 12
#DIM CONST 乗数_巨 = 1, 2, 3, 5,  8, 10
#DIM CONST 乗数_超 = 1, 2, 3, 4,  6,  8

IF 射精キャラ == MASTER
	SIF あなた特殊能力:真・絶倫 <= 0
		RETURNF 適用前強度
	能力レベル = あなた特殊能力:真・絶倫

	;あなたは精力が実質無限で、男性器サイズが小さいことによるメリットがないので
	;男性器サイズごとに異なる補正（男性器サイズが小さいほど大きい）をかける
	;絶倫レベルが5に近いほど、男性器サイズに関わらずほぼ同じ精液量になる調整がしてある
	SELECTCASE TALENT:射精キャラ:男性器サイズ
		CASE IS <= -2
			;こどもちんこ
			RETURNF 適用前強度 * 乗数_粗:能力レベル
		CASE -1
			;短小
			RETURNF 適用前強度 * 乗数_小:能力レベル
		CASE 0
			;平凡
			RETURNF 適用前強度 * 乗数_凡:能力レベル
		CASE 1
			;巨根
			RETURNF 適用前強度 * 乗数_巨:能力レベル
		CASE IS >= 2
			;規格外
			RETURNF 適用前強度 * 乗数_超:能力レベル
	ENDSELECT
ELSEIF PLAYER == MASTER && あなた特殊能力_発揮条件_筒枯らし()
	SIF あなた特殊能力:筒枯らし <= 0
		RETURNF 適用前強度
	能力レベル = あなた特殊能力:筒枯らし

	;一般人には同じ係数をかける
	RETURNF 適用前強度 * 乗数_凡:能力レベル
ENDIF
RETURNF 適用前強度


@射精可能(キャラ番号)
#FUNCTION
#DIM キャラ番号

;射精できるなら1、できないなら0を返す
RETURNF GETBIT(TALENT:キャラ番号:性別, 1) && TALENT:キャラ番号:Ｃ感度 != -2 && !TALENT:キャラ番号:射精不可


;RESULT       射精回数(射精経験に加算する数)
;結果受渡し:0 射精量
;結果受渡し:1 空打ちフラグ
@精力消費処理_共通(射精キャラ, 絶頂強度, 前立腺フラグ = 0, 結果受渡し)
#DIM 射精キャラ
#DIM 絶頂強度
#DIM 前立腺フラグ
#DIM REF 結果受渡し, 0
#DIM 最小消費量
#DIM 最大消費量
#DIM 総消費量
#DIM 射精回数
#DIM 消費量

結果受渡し:0 = 0
結果受渡し:1 = 0

SIF 絶頂強度 <= 0
	RETURN 0
SIF GETBIT(TALENT:射精キャラ:性別, 1) == 0 || TALENT:射精キャラ:Ｃ感度 == -2
	RETURN 0
IF TALENT:射精キャラ:射精不可 == 2
	;未精通は空打ち扱い
	SETBIT 結果受渡し:1, 0
	RETURN 0
ELSEIF TALENT:射精キャラ:射精不可
	RETURN 0
ENDIF

絶頂強度 = 射精量強化適用(射精キャラ, 絶頂強度)

最小消費量 = 最小射精量(射精キャラ, 前立腺フラグ)
最大消費量 = 最大射精量(射精キャラ)
総消費量 = 0
射精回数 = 0
DO
	;-------------------------------------------------------------------------------------------
	; 通常時はだいたい精力30くらい消費
	; 精力10≒1mLくらいの想定なので、3mL（現実の平均）くらいになる。
	; ファンタジーなんだからもっと出せばいいじゃんと思ったが、どうせ性行為を繰り返してたら
	; 感度が上がって絶頂強度が上がってたやすく2倍3倍出るようになるし、これでいいよなたぶん
	;-------------------------------------------------------------------------------------------
	消費量 = MAX(BASE:射精キャラ:精力 * 3 / 4, 最小消費量, 0)
	消費量 = MIN(消費量, 最大消費量, BASE:射精キャラ:精力)
	SIF 消費量 <= 0
		BREAK

	BASE:射精キャラ:精力 -= 消費量
	総消費量 += 消費量
	射精回数 += 1
LOOP 射精回数 < 絶頂強度

IF 総消費量 > 0
	結果受渡し:0 = 総消費量
ELSE
	;射精量が0の場合、空打ち
	SETBIT 結果受渡し:1, 0
	;前立腺を刺激されている場合、ドライオーガズム
	SIF 前立腺フラグ
		SETBIT 結果受渡し:1, 1
ENDIF
RETURN 射精回数


@射精処理_コマンド(射精キャラ, 絶頂強度, 前立腺フラグ = 0)
#DIM 射精キャラ
#DIM 絶頂強度
#DIM 前立腺フラグ
#DIM 射精回数
#DIM 結果受渡し, 2

CALL 精力消費処理_共通(射精キャラ, 絶頂強度, 前立腺フラグ, 結果受渡し)
射精回数 = RESULT

IF 射精回数 > 0
	NOWEX:射精キャラ:射精 += 射精回数
	EX:射精キャラ:射精 += 射精回数
	EXP:射精キャラ:射精経験 += 射精回数
	EXP:射精キャラ:精液経験 += 射精回数 - 1
	NOWEX:射精キャラ:スコア += 射精回数
ELSEIF 結果受渡し:1
	EXP:射精キャラ:空打ち絶頂経験 += 絶頂強度
ENDIF
RCVAR:射精キャラ:射精量 = 結果受渡し:0
RCVAR:射精キャラ:空打ちフラグ = 結果受渡し:1
SIF GETBIT(RCVAR:射精キャラ:空打ちフラグ, 1)
	NOWEX:射精キャラ:スコア += 絶頂強度
RETURN 射精回数


@コマンド外精力消費処理(射精キャラ, 絶頂強度, 前立腺フラグ = 0)
#DIM 射精キャラ
#DIM 絶頂強度
#DIM 前立腺フラグ
#DIM 結果受渡し, 2

CALL 精力消費処理_共通(射精キャラ, 絶頂強度, 前立腺フラグ, 結果受渡し)

RESULT:1 = 結果受渡し:0
RESULT:2 = 結果受渡し:1
RETURN RESULT


@精力ゲージ_勃起基準値(キャラ番号)
#FUNCTION
#DIM キャラ番号
#DIM 基準値

;勃起は通常は1500がMAXで、500で半勃ち、1000でフル勃起（挿入可能）
;精力がMAXの1/6（１日あたりの精力の半分）未満になると、だんだん勃起しにくく、陰萎しやすくなっていく
;精力残り1/30までは1500->1000へゆるやかに、1/30を切ると1000->500へ急激に、勃起しにくくなっていく
IF BASE:キャラ番号:精力 * 6 >= MAXBASE:キャラ番号:精力
	基準値 = 1500
ELSEIF BASE:キャラ番号:精力 * 30 >= MAXBASE:キャラ番号:精力
	基準値 = (1500 - 1000) * (30 * BASE:キャラ番号:精力 - MAXBASE:キャラ番号:精力) / MAXBASE:キャラ番号:精力 / 4 + 1000
ELSE
	基準値 = (1000 - 500) * 30 * BASE:キャラ番号:精力 / MAXBASE:キャラ番号:精力 + 500
ENDIF
RETURNF MIN(基準値, MAXBASE:キャラ番号:勃起)



@勃起度上昇(キャラ番号, 上昇量)
#DIM キャラ番号
#DIM 上昇量
#DIM 上昇要素, 2
#DIM 基準値

基準値 = 精力ゲージ_勃起基準値(キャラ番号)

IF BASE:キャラ番号:勃起 + 上昇量 < 基準値
	;上昇前も後も基準値未満
	BASE:キャラ番号:勃起 = MIN(MAXBASE:キャラ番号:勃起, BASE:キャラ番号:勃起 + 上昇量)
ELSEIF BASE:キャラ番号:勃起 < 基準値
	;上昇前後で基準値をまたぐ
	上昇要素:0 = 基準値 - BASE:キャラ番号:勃起
	上昇要素:1 = (BASE:キャラ番号:勃起 + 上昇量 - 基準値) / 8
	BASE:キャラ番号:勃起 = MIN(MAXBASE:キャラ番号:勃起, BASE:キャラ番号:勃起 + 上昇要素:0 + 上昇要素:1)
ELSE
	;上昇前も後も基準値以上
	BASE:キャラ番号:勃起 = MIN(MAXBASE:キャラ番号:勃起, BASE:キャラ番号:勃起 + 上昇量 / 8)
ENDIF


@勃起度下降(キャラ番号, 下降量)
#DIM キャラ番号
#DIM 下降量
#DIM 下降要素, 2
#DIM 基準値

基準値 = 精力ゲージ_勃起基準値(キャラ番号)

IF BASE:キャラ番号:勃起 < 基準値
	;下降前も後も基準値未満
	BASE:キャラ番号:勃起 = MAX(0, BASE:キャラ番号:勃起 - 下降量)
ELSEIF BASE:キャラ番号:勃起 - 下降量 * 3 < 基準値
	;下降前後で基準値をまたぐ
	下降要素:0 = BASE:キャラ番号:勃起 - 基準値
	下降要素:1 = (基準値 - BASE:キャラ番号:勃起 + 下降量 * 3) / 3
	BASE:キャラ番号:勃起 = MAX(0, BASE:キャラ番号:勃起 - 下降要素:0 - 下降要素:1)
ELSE
	;下降前も後も基準値以上
	BASE:キャラ番号:勃起 = MAX(0, BASE:キャラ番号:勃起 - 下降量 * 3)
ENDIF


@勃起度自然変動(キャラ番号)
#DIM キャラ番号
#DIM 変動量
#DIM 快楽値

快楽値 = MAX(PALAM:キャラ番号:快Ｃ, PALAM:キャラ番号:快Ｖ, PALAM:キャラ番号:快Ａ, PALAM:キャラ番号:快Ｂ, PALAM:キャラ番号:快Ｓ)
変動量 = TIME_PROGRESS(TFLAG:行動前時刻) * 20

IF 快楽値 < 7500 && !CFLAG:キャラ番号:うふふ
	CALL 勃起度下降(キャラ番号, 変動量)
ELSE
	CALL 勃起度上昇(キャラ番号, 変動量)
ENDIF


@勃起度下降_射精時(キャラ番号)
#DIM キャラ番号
#DIM 変動量

変動量 = (BASE:キャラ番号:勃起 - 1000) / 2
SIF 変動量 > 0
	CALL 勃起度下降(キャラ番号, 変動量)
