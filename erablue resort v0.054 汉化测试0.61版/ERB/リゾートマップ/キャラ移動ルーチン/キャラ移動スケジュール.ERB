;------------------------------------------------------------------------------
;汎用移動処理
;------------------------------------------------------------------------------
@MOVE_ON_SCHEDULE(キャラ番号, 追従モード = 0)
#DIM キャラ番号
#DIM 追従モード

IF 追従モード == 0
	;まずスケジュール実行
	CALL RUN_SCHEDULE(キャラ番号)
	;追従中は追従モードでしか動かない
	SIF RCVAR:キャラ番号:追従ルーチン中
		RETURN 0
ELSE
	;追従中の対象を見て目的地をセット
	IF RCVAR:キャラ番号:追従ルーチン中
		IF 施設利用性別(キャラ番号) == 性別_女性 && MALEROOM(CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在マップ種別, CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在位置)
			;ここと
		ELSEIF 施設利用性別(キャラ番号) == 性別_男性 && FEMALEROOM(CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在マップ種別, CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在位置)
			;ここの場合は本来のルーチンで動かせば正しく動くはず
		ELSE
			スケジュール状態:キャラ番号:目的マップ = CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在マップ種別
			スケジュール状態:キャラ番号:目的地 = CFLAG:(RCVAR:キャラ番号:追従ルーチン中):現在位置
		ENDIF
	ELSE
		;ここには来ないはずだが一応RETURN
		RETURN 0
	ENDIF
ENDIF

IF スケジュール状態:キャラ番号:目的地 != 998 && CFLAG:キャラ番号:現在位置 == 999 && CFLAG:キャラ番号:現在マップ種別 == 0
		CFLAG:キャラ番号:現在位置 = 1
		CFLAG:キャラ番号:現在マップ種別 = 0
ENDIF

IF CFLAG:キャラ番号:現在マップ種別 == スケジュール状態:キャラ番号:目的マップ
	IF CFLAG:キャラ番号:現在位置 == スケジュール状態:キャラ番号:目的地
		;いるなら処理無し
	ELSEIF スケジュール状態:キャラ番号:目的地 == 998
		;でかけてるパターン
		CFLAG:キャラ番号:現在位置 = 999
		CFLAG:キャラ番号:現在マップ種別 = 0
	ELSE
		;いないならルーチン通りの場所へ向けて移動

		;居住区や商業区の例外処理
		IF スケジュール状態:キャラ番号:目的マップ == 2 && スケジュール状態:キャラ番号:目的地 >= 1000
			CFLAG:キャラ番号:現在位置 = スケジュール状態:キャラ番号:目的地
		ELSEIF スケジュール状態:キャラ番号:目的マップ == 1 && スケジュール状態:キャラ番号:目的地 >= 1000
			CFLAG:キャラ番号:現在位置 = スケジュール状態:キャラ番号:目的地
		ELSE
			;現在地がエリア内なら一旦外に出してからスキップムーブ
			IF CFLAG:キャラ番号:現在マップ種別 == 2 && CFLAG:キャラ番号:現在位置 >= 1000
				CFLAG:キャラ番号:現在位置 = 1
			ELSEIF CFLAG:キャラ番号:現在マップ種別 == 1 && CFLAG:キャラ番号:現在位置 >= 1000
				CFLAG:キャラ番号:現在位置 = 2
			ENDIF
			SIF CFLAG:キャラ番号:現在位置 == スケジュール状態:キャラ番号:目的地
				RETURN 1
			CALL SKIP_MOVE(CFLAG:キャラ番号:現在マップ種別, スケジュール状態:キャラ番号:目的地, キャラ番号)
			CALL 汎用移動処理(キャラ番号, RESULT)
			RETURN 1
		ENDIF
	ENDIF
ELSE
	CALLFORM エリア情報登録_{CFLAG:キャラ番号:現在マップ種別}("エリア移動可能場所")
	LOCAL = RESULT
	IF CFLAG:キャラ番号:現在位置 == LOCAL || CFLAG:キャラ番号:現在マップ種別 == 999
		;マップ種別から違う場合は目的のマップへ移動
		CALL 汎用マップ間移動処理(キャラ番号)
	ELSEIF CFLAG:キャラ番号:現在マップ種別 == 2 && CFLAG:キャラ番号:現在位置 >= 1000
		;居住区や商業区の例外処理
		CALL 汎用移動処理(キャラ番号, LOCAL)
	ELSEIF CFLAG:キャラ番号:現在マップ種別 == 1 && CFLAG:キャラ番号:現在位置 >= 1000
		;居住区や商業区の例外処理
		CALL 汎用移動処理(キャラ番号, LOCAL)
	ELSE
		CALL SKIP_MOVE(CFLAG:キャラ番号:現在マップ種別, LOCAL, キャラ番号)
		CALL 汎用移動処理(キャラ番号, LOCAL)
	ENDIF
	RETURN 1
ENDIF
RETURN 0

@汎用移動処理(キャラ番号, 移動先)
#DIM キャラ番号
#DIM 移動先
;同じ部屋にいるなら移動先PRINT
IF CFLAG:キャラ番号:同室 && !CFLAG:MASTER:睡眠
	PRINTFORML %CALLNAME:キャラ番号%从【%GETPLACENAME(CFLAG:キャラ番号:現在マップ種別, CFLAG:キャラ番号:現在位置)%】移动到了【%GETPLACENAME(CFLAG:キャラ番号:現在マップ種別, RESULT)%】
ENDIF

;移動処理
CFLAG:キャラ番号:現在位置 = 移動先

;同じ部屋に来たならPRINT
SIF CFLAG:キャラ番号:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:MASTER:睡眠 && CFLAG:キャラ番号:現在マップ種別 == CFLAG:MASTER:現在マップ種別 
	PRINTFORML %CALLNAME:キャラ番号%が【%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%】に来た


@汎用マップ間移動処理(キャラ番号)
;仮処理置き
;マップ増やしたらちゃんと汎用化すること！！！！！！！！
#DIM キャラ番号

;同じ部屋にいるなら移動先PRINT
LOCALS = %マップエリア名取得(スケジュール状態:キャラ番号:目的マップ)%
SIF CFLAG:キャラ番号:同室 && !CFLAG:MASTER:睡眠
	PRINTFORMW %CALLNAME:キャラ番号%从【%GETPLACENAME(CFLAG:キャラ番号:現在マップ種別, CFLAG:キャラ番号:現在位置)%】移动到了【%LOCALS%】
;移動処理
CFLAG:キャラ番号:現在位置 = マップ移動初期位置(CFLAG:キャラ番号:現在マップ種別, スケジュール状態:キャラ番号:目的マップ)
IF CFLAG:キャラ番号:現在位置 == 999
	CFLAG:キャラ番号:現在位置 = スケジュール状態:キャラ番号:目的地
ENDIF
CFLAG:キャラ番号:現在マップ種別 = スケジュール状態:キャラ番号:目的マップ
;同じ部屋に来たならPRINT
SIF CFLAG:キャラ番号:現在位置 == CFLAG:MASTER:現在位置 && !CFLAG:MASTER:睡眠 && CFLAG:キャラ番号:現在マップ種別 == CFLAG:MASTER:現在マップ種別 
	PRINTFORMW %CALLNAME:キャラ番号%が【%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%】に来た


@マップ移動初期位置(移動前マップ, 移動後マップ)
#FUNCTION
#DIM 移動前マップ
#DIM 移動後マップ

SELECTCASE 移動後マップ
	CASE 0
		SELECTCASE 移動前マップ
			CASE 999
				RETURNF 6
			CASEELSE
				RETURNF 1
		ENDSELECT
	CASE 1
		RETURNF 1
	CASE 2
		RETURNF 1
	CASE 3
		RETURNF 1
	CASE 100
		CALLFORMF マップ移動初期位置_%開催予定祭り名%(移動前マップ, 移動後マップ)
		RETURNF RESULT
	CASE 999
		RETURNF 999
ENDSELECT


;------------------------------------------------------------------------------
;スケジュール遷移処理
;------------------------------------------------------------------------------
@RUN_SCHEDULE(キャラ番号)
#DIM キャラ番号
#DIM スケジュール番号
#DIM 次スケジュール番号

スケジュール番号 = スケジュール状態:キャラ番号:現在スケジュール
次スケジュール番号 = GET_NEXT_SCHEDULE(キャラ番号)

IF スケジュール番号 == 次スケジュール番号
	;今のスケジュールを続ける処理
	IF GROUPMATCH(スケジュール状態:キャラ番号:状況, スケジュール_未実行, スケジュール_開始中)
		;未実行～開始中なら開始処理の実行。0以外なら実行中状態、0なら開始中状態
		CALLFORM SCHEDULE_%移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール%(キャラ番号, 移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール小分類, "開始処理")
		IF RESULT
			スケジュール状態:キャラ番号:状況 = スケジュール_実行中
		ELSE
			スケジュール状態:キャラ番号:状況 = スケジュール_開始中
		ENDIF
	ENDIF
	IF スケジュール状態:キャラ番号:状況 == スケジュール_実行中
		;実行中なら経過処理の実行
		CALLFORM SCHEDULE_%移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール%(キャラ番号, 移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール小分類, "経過処理")
	ENDIF
ELSE
	;新しいスケジュールへ移行する処理
	IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
		;未実行ならキャンセル
		スケジュール状態:キャラ番号:状況 = スケジュール_キャンセル
	ELSEIF GROUPMATCH(スケジュール状態:キャラ番号:状況, スケジュール_開始中, スケジュール_実行中, スケジュール_終了中)
		;開始中～終了中なら終了処理の実行。0以外なら完了状態、0なら終了中状態
		CALLFORM SCHEDULE_%移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール%(キャラ番号, 移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール小分類, "終了処理")
		IF RESULT
			スケジュール状態:キャラ番号:状況 = スケジュール_完了
		ELSE
			スケジュール状態:キャラ番号:状況 = スケジュール_終了中
		ENDIF
	ENDIF
	IF GROUPMATCH(スケジュール状態:キャラ番号:状況, スケジュール_完了, スケジュール_キャンセル)
		スケジュール状態:キャラ番号:現在スケジュール = 次スケジュール番号
		スケジュール状態:キャラ番号:終了予定時間 = TIME + 移動スケジュール設定:キャラ番号:次スケジュール番号:所要時間
		スケジュール状態:キャラ番号:状況 = スケジュール_未実行
		RESTART
	ENDIF
ENDIF


;------------------------------------------------------------------------------
;次のスケジュールを求める
;------------------------------------------------------------------------------
@GET_NEXT_SCHEDULE(キャラ番号)
#FUNCTION
#DIM キャラ番号
#DIM 次スケジュール番号
#DIM 次優先度

次スケジュール番号 = スケジュール状態:キャラ番号:現在スケジュール
;終了予定時間まではなるべく今のスケジュールを続ける
IF TIME < スケジュール状態:キャラ番号:終了予定時間
	次優先度 = 移動スケジュール設定:キャラ番号:次スケジュール番号:優先度 + 1
ELSE
	次優先度 = __INT_MIN__
ENDIF

FOR LOCAL, (スケジュール状態:キャラ番号:現在スケジュール + 1), 最大スケジュール登録数
	SIF 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール == ""
		BREAK
	;予定時間を前倒しで開始することはない
	SIF TIME < 移動スケジュール設定:キャラ番号:LOCAL:開始予定時間
		BREAK
	;より優先度が同じか高いなら、後のスケジュールを採用する
	IF 次優先度 <= 移動スケジュール設定:キャラ番号:LOCAL:優先度
		次スケジュール番号 = LOCAL
		次優先度 = 移動スケジュール設定:キャラ番号:LOCAL:優先度
	ENDIF
NEXT
RETURNF 次スケジュール番号


;------------------------------------------------------------------------------
;スケジュール初期化処理
;------------------------------------------------------------------------------
@RESET_SCHEDULE(キャラ番号)
#DIM キャラ番号

VARSET スケジュール状態:キャラ番号:0, 0
VARSET 移動スケジュール設定:キャラ番号:0:0, 0
VARSET 移動スケジュール文字列:キャラ番号:0:0, ""


;------------------------------------------------------------------------------
;空のスケジュール枠を探す
;------------------------------------------------------------------------------
@FIND_BLANK_SCHEDULE(キャラ番号)
#FUNCTION
#DIM キャラ番号

FOR LOCAL, 0, 最大スケジュール登録数
	SIF 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール == ""
		RETURNF LOCAL
NEXT
RETURNF 最大スケジュール登録数


;------------------------------------------------------------------------------
;指定時刻より実行できる中で最も新しいスケジュール枠を探す
;------------------------------------------------------------------------------
@FIND_RUNNABLE_SCHEDULE(キャラ番号, 優先度考慮 = 1, 時刻 = -1)
#FUNCTION
#DIM キャラ番号
#DIM 優先度考慮
#DIM 時刻
#DIM 次スケジュール番号
#DIM 次優先度

SIF 時刻 < 0
	時刻 = TIME

次スケジュール番号 = -1
次優先度 = __INT_MIN__
FOR LOCAL, 0, 最大スケジュール登録数
	SIF 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール == ""
		BREAK
	SIF 時刻 < 移動スケジュール設定:キャラ番号:LOCAL:開始予定時間
		BREAK
	IF !優先度考慮 || 次優先度 <= 移動スケジュール設定:キャラ番号:LOCAL:優先度
		次スケジュール番号 = LOCAL
		次優先度 = 移動スケジュール設定:キャラ番号:LOCAL:優先度
	ENDIF
NEXT
RETURNF 次スケジュール番号


;------------------------------------------------------------------------------
;スケジュールセット処理
;------------------------------------------------------------------------------
@SET_SCHEDULE(キャラ番号, スケジュール番号, 開始予定, キー, ARGS:0 = "", ARGS:10 = "", ARGS:1 = "", ARGS:11 = "", ARGS:2 = "", ARGS:12 = "")
#DIM キャラ番号
#DIM スケジュール番号
#DIM 開始予定
#DIMS キー

移動スケジュール設定:キャラ番号:スケジュール番号:開始予定時間 = 開始予定
移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール '= キー
移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール小分類 '= ""
移動スケジュール設定:キャラ番号:スケジュール番号:所要時間 = 0
移動スケジュール設定:キャラ番号:スケジュール番号:優先度 = 0

FOR LOCAL, 0, 3
	SELECTCASE ARGS:LOCAL
		CASE "小分類"
			移動スケジュール文字列:キャラ番号:スケジュール番号:スケジュール小分類 '= ARGS:(LOCAL + 10)
		CASE "所要時間"
			移動スケジュール設定:キャラ番号:スケジュール番号:所要時間 = TOINT(ARGS:(LOCAL + 10))
		CASE "優先度"
			移動スケジュール設定:キャラ番号:スケジュール番号:優先度 = TOINT(ARGS:(LOCAL + 10))
	ENDSELECT
NEXT


;------------------------------------------------------------------------------
;スケジュール割り込み処理
;------------------------------------------------------------------------------
@INTERRUPT_SCHEDULE(キャラ番号, 復帰フラグ = 0, キー, ARGS:0 = "", ARGS:10 = "", ARGS:1 = "", ARGS:11 = "", ARGS:2 = "", ARGS:12 = "")
#DIM キャラ番号
#DIM 復帰フラグ
#DIMS キー
#DIM スケジュール番号
#DIM 実行時間
#DIM 移動数

スケジュール番号 = スケジュール状態:キャラ番号:現在スケジュール
IF 復帰フラグ
	実行時間 = TIME - (スケジュール状態:キャラ番号:終了予定時間 - 移動スケジュール設定:キャラ番号:スケジュール番号:所要時間)
	SIF 実行時間 > 移動スケジュール設定:キャラ番号:スケジュール番号:所要時間
		復帰フラグ = 0
ENDIF

移動数 = 復帰フラグ ? 2 # 1
FOR LOCAL, (最大スケジュール登録数 - 1 - 移動数), (スケジュール番号 - 1), -1
	SIF 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール == ""
		CONTINUE
	移動スケジュール文字列:キャラ番号:(LOCAL + 移動数):スケジュール '= 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール
	移動スケジュール文字列:キャラ番号:(LOCAL + 移動数):スケジュール小分類 '= 移動スケジュール文字列:キャラ番号:LOCAL:スケジュール小分類
	移動スケジュール設定:キャラ番号:(LOCAL + 移動数):開始予定時間 = 移動スケジュール設定:キャラ番号:LOCAL:開始予定時間
	移動スケジュール設定:キャラ番号:(LOCAL + 移動数):所要時間 = 移動スケジュール設定:キャラ番号:LOCAL:所要時間
	移動スケジュール設定:キャラ番号:(LOCAL + 移動数):優先度 = 移動スケジュール設定:キャラ番号:LOCAL:優先度
NEXT

CALL SET_SCHEDULE(キャラ番号, スケジュール番号 + 1, TIME, キー, ARGS:0, ARGS:10, ARGS:1, ARGS:11, ARGS:2, ARGS:12)

IF 復帰フラグ
	移動スケジュール設定:キャラ番号:(スケジュール番号 + 2):開始予定時間 = TIME
	移動スケジュール設定:キャラ番号:(スケジュール番号 + 2):所要時間 = 移動スケジュール設定:キャラ番号:スケジュール番号:所要時間 - 実行時間
	移動スケジュール設定:キャラ番号:スケジュール番号:所要時間 = 実行時間
ENDIF


;------------------------------------------------------------------------------
;同行のためのスケジュールコピー処理
;------------------------------------------------------------------------------
@COPY_SCHEDULE(コピー元キャラ番号, コピー先キャラ番号)
#DIM コピー元キャラ番号
#DIM コピー先キャラ番号
#DIM スケジュール番号

FOR スケジュール番号, 0, 最大スケジュール登録数
	移動スケジュール文字列:コピー先キャラ番号:スケジュール番号:スケジュール '= 移動スケジュール文字列:コピー元キャラ番号:スケジュール番号:スケジュール
	移動スケジュール文字列:コピー先キャラ番号:スケジュール番号:スケジュール小分類 '= 移動スケジュール文字列:コピー元キャラ番号:スケジュール番号:スケジュール小分類
	移動スケジュール設定:コピー先キャラ番号:スケジュール番号:開始予定時間 = 移動スケジュール設定:コピー元キャラ番号:スケジュール番号:開始予定時間
	移動スケジュール設定:コピー先キャラ番号:スケジュール番号:所要時間 = 移動スケジュール設定:コピー元キャラ番号:スケジュール番号:所要時間
	移動スケジュール設定:コピー先キャラ番号:スケジュール番号:優先度 = 移動スケジュール設定:コピー元キャラ番号:スケジュール番号:優先度
NEXT


;------------------------------------------------------------------------------
;目的地にいるか確認
;------------------------------------------------------------------------------
@IS_AT_DESTINATION(キャラ番号)
#FUNCTION
#DIM キャラ番号

RETURNF CFLAG:キャラ番号:現在マップ種別 == スケジュール状態:キャラ番号:目的マップ && CFLAG:キャラ番号:現在位置 == スケジュール状態:キャラ番号:目的地


;------------------------------------------------------------------------------
;休憩
;------------------------------------------------------------------------------
@SCHEDULE_休憩(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		IF 小分類 == "キャンプ" && 施設改造度:3:0
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = 303
		ELSE
			スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(キャラ番号)
			スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
		ENDIF
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;交流
;------------------------------------------------------------------------------
@SCHEDULE_交流(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		スケジュール状態:キャラ番号:目的地 = 2
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;食事
;------------------------------------------------------------------------------
@SCHEDULE_食事(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		IF 小分類 == "バーベキュー" && 施設改造度:3:0
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = 304
		ELSEIF 小分類 == "外食"
			スケジュール状態:キャラ番号:目的マップ = SCHEDULE_外食先決定関数(キャラ番号, 1)
			スケジュール状態:キャラ番号:目的地 = SCHEDULE_外食先決定関数(キャラ番号, 0)
		ELSEIF 小分類 == "海の家"
			スケジュール状態:キャラ番号:目的マップ = 3
			スケジュール状態:キャラ番号:目的地 = 2
		ELSEIF 小分類 == "お弁当" || 小分類 == "屋台"
			スケジュール状態:キャラ番号:目的マップ = CFLAG:キャラ番号:現在マップ種別
			スケジュール状態:キャラ番号:目的地 = CFLAG:キャラ番号:現在位置
		ELSEIF 小分類 == "従業員お昼"
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = 16
		ELSEIF 施設改造度:4:0
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = RAND(403, 406)
		ELSE
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = 4
		ENDIF
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自炊
;------------------------------------------------------------------------------
@SCHEDULE_自炊(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
			IF 小分類 == "同棲"
				スケジュール状態:キャラ番号:目的地 = 2006
				スケジュール状態:キャラ番号:目的マップ = 0
			ELSEIF RCVAR:キャラ番号:追従ルーチン中
				スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(RCVAR:キャラ番号:追従ルーチン中)
				スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
			ELSE
				スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(キャラ番号)
				スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
			ENDIF
		ENDIF
		IF IS_AT_DESTINATION(キャラ番号)
			TCVAR:キャラ番号:移動ルーチンインターバル時間 = LIMIT(スケジュール状態:キャラ番号:終了予定時間 - TIME, 0, 60) / 2
			RETURN 1
		ELSE
			RETURN 0
		ENDIF
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		IF 小分類 == "同棲"
			スケジュール状態:キャラ番号:目的地 = 2005
			スケジュール状態:キャラ番号:目的マップ = 0
		ENDIF
	CASE "終了処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_開始中
			RETURN 1
		ELSE
			RETURN IS_AT_DESTINATION(キャラ番号) && TCVAR:キャラ番号:移動ルーチンインターバル時間 <= 0
		ENDIF
ENDSELECT


;------------------------------------------------------------------------------
;お風呂
;------------------------------------------------------------------------------
@SCHEDULE_お風呂(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
			スケジュール状態:キャラ番号:目的マップ = 0
			IF 小分類 == "同棲"
				スケジュール状態:キャラ番号:目的地 = 2007
			ELSEIF 施設改造度:7:0
				スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 702 # 802
			ELSE
				スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 7 # 8
			ENDIF
			TCVAR:キャラ番号:移動ルーチンインターバル時間 = 15
		ENDIF
		RETURN IS_AT_DESTINATION(キャラ番号)
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = 0
		IF 小分類 == "同棲"
			スケジュール状態:キャラ番号:目的地 = 2008
		ELSEIF 施設改造度:7:0
			スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? (703 + RAND:6) # (803 + RAND:6)
		ELSE
			スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? (9 + RAND:2) # (11 + RAND:2)
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(30, 60)
	CASE "終了処理"
		SELECTCASE スケジュール状態:キャラ番号:状況
			CASE スケジュール_開始中
				RETURN 1
			CASE スケジュール_実行中
				スケジュール状態:キャラ番号:目的マップ = 0
				IF 小分類 == "同棲"
					スケジュール状態:キャラ番号:目的地 = 2007
				ELSEIF 施設改造度:7:0
					スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 702 # 802
				ELSE
					スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 7 # 8
				ENDIF
				TCVAR:キャラ番号:移動ルーチンインターバル時間 = 15
				RETURN 0
			CASE スケジュール_終了中
				RETURN IS_AT_DESTINATION(キャラ番号) && TCVAR:キャラ番号:移動ルーチンインターバル時間 <= 0
		ENDSELECT
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自室入浴
;------------------------------------------------------------------------------
@SCHEDULE_自室入浴(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(キャラ番号)
		スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
		RETURN 1
	CASE "経過処理"
		CFLAG:キャラ番号:風呂 = 0
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;プールで遊ぶ
;------------------------------------------------------------------------------
@SCHEDULE_プールで遊ぶ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 13 # 14
			TCVAR:キャラ番号:移動ルーチンインターバル時間 = 15
		ENDIF
		RETURN IS_AT_DESTINATION(キャラ番号)
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = 0
		IF 施設改造度:15:0
			;プールサイド、メインプール、ボールプール、流れるプールの４択
			スケジュール状態:キャラ番号:目的地 = 1500 + (RAND:4 + 3)
			;ハーヴィンはメインプールに行かない
			SIF TALENT:キャラ番号:体格 == 体格_ハーヴィン並 && スケジュール状態:キャラ番号:目的地 == 1504
				スケジュール状態:キャラ番号:目的地 = 1505
		ELSE
			スケジュール状態:キャラ番号:目的地 = 15
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		SELECTCASE スケジュール状態:キャラ番号:状況
			CASE スケジュール_開始中
				RETURN 1
			CASE スケジュール_実行中
				スケジュール状態:キャラ番号:目的マップ = 0
				スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 13 # 14
				TCVAR:キャラ番号:移動ルーチンインターバル時間 = 15
				RETURN 0
			CASE スケジュール_終了中
				RETURN IS_AT_DESTINATION(キャラ番号) && TCVAR:キャラ番号:移動ルーチンインターバル時間 <= 0
		ENDSELECT
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;外で遊ぶ
;------------------------------------------------------------------------------
@SCHEDULE_外で遊ぶ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = 0
		IF 施設改造度:3:0
			;メインテント、バーベキューエリア、渓流エリア、サウナテントの４択
			LOCAL = 4
			;訓練場増築済みだと5択
			SIF 施設改造度:3:8
				LOCAL += 1
			スケジュール状態:キャラ番号:目的地 = 300 + (RAND:LOCAL + 3)
			;釣り好きはキャンプ場にいると絶対渓流に行く
			SIF 知識素質:キャラ番号:釣り好き
				スケジュール状態:キャラ番号:目的地 = 305
		ELSE
			スケジュール状態:キャラ番号:目的地 = 3
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;遊戯室で遊ぶ
;------------------------------------------------------------------------------
@SCHEDULE_遊戯室で遊ぶ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = 0
		IF 施設改造度:5:0
			;バーエリア、ボードゲームエリア、卓球場の３択
			LOCAL = 3
			;図書館増築済みだと4択
			SIF 施設改造度:5:6
				LOCAL += 1
			スケジュール状態:キャラ番号:目的地 = 500 + (RAND:LOCAL + 3)
		ELSE
			スケジュール状態:キャラ番号:目的地 = 5
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;空艇港でショッピング
;------------------------------------------------------------------------------
@SCHEDULE_空艇港でショッピング(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = SCHEDULE_ショッピング先決定関数(キャラ番号, 1)
		スケジュール状態:キャラ番号:目的地 = SCHEDULE_ショッピング先決定関数(キャラ番号)
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_ショッピング先決定関数(キャラ番号, マップ種別取得フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM マップ種別取得フラグ
#DIM 店舗候補配列, 6
#DIM 候補数

SIF マップ種別取得フラグ
	RETURNF 1

VARSET 店舗候補配列
候補数 = 0
FOR LOCAL, 3, 9
	IF 空艇港店舗状況:LOCAL:0 > 0 && 空艇港店舗状況:LOCAL:0 != 3
		店舗候補配列:候補数 = LOCAL
		候補数 += 1
	ENDIF
NEXT
IF 候補数
	RETURNF 店舗候補配列:(RAND:候補数)
ELSE
	RETURNF 2
ENDIF

@SCHEDULE_外食先決定関数(キャラ番号, マップ種別取得フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM マップ種別取得フラグ
#DIM 店舗候補配列, 6
#DIM 候補数

VARSET 店舗候補配列
候補数 = 0
FOR LOCAL, 3, 9
	IF 空艇港店舗状況:LOCAL:0 == 3
		SIF マップ種別取得フラグ
			RETURNF 1
		店舗候補配列:候補数 = LOCAL
		候補数 += 1
	ENDIF
NEXT
IF 候補数
	RETURNF 店舗候補配列:(RAND:候補数)
ELSEIF マップ種別取得フラグ
	RETURNF 0
ELSE
	IF 施設改造度:4:0
		RETURNF RAND(403, 406)
	ELSE
		RETURNF 4
	ENDIF
ENDIF


;------------------------------------------------------------------------------
;雑務
;------------------------------------------------------------------------------
@SCHEDULE_雑務(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = SCHEDULE_雑務先決定関数(キャラ番号, 1)
		スケジュール状態:キャラ番号:目的地 = SCHEDULE_雑務先決定関数(キャラ番号)
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_雑務先決定関数(キャラ番号, マップ種別取得フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM マップ種別取得フラグ
#DIM 雑務先候補配列, 200
#DIM 候補数

SIF マップ種別取得フラグ
	RETURNF 0

VARSET 雑務先候補配列
候補数 = 0
FOR LOCAL, 1, マップ部屋番号上限
	IF 存在しない移動先(0, LOCAL) == 0
		IF 施設利用性別(キャラ番号) == 性別_男性 && FEMALEROOM(0, LOCAL)
			CONTINUE
		ELSEIF 施設利用性別(キャラ番号) == 性別_女性 && MALEROOM(0, LOCAL)
			CONTINUE
		ENDIF
		;隠し通路
		SIF LOCAL == 810 || LOCAL == 710
			CONTINUE
		;自宅
		SIF LOCAL == 20 || LOCAL / 100 == 20
			CONTINUE
		;マッサージ室には入らない
		SIF LOCAL == 811 || LOCAL == 711
			CONTINUE
		雑務先候補配列:候補数 = LOCAL
		候補数 += 1
	ENDIF
NEXT

RETURNF 雑務先候補配列:(RAND:候補数)


;------------------------------------------------------------------------------
;常駐業務
;------------------------------------------------------------------------------
@SCHEDULE_常駐業務(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 現在仕事:キャラ番号:1 / 10000
		スケジュール状態:キャラ番号:目的地 = 現在仕事:キャラ番号:1 % 10000
		;シーガイドだけ例外処理
		IF 現在仕事:キャラ番号:1 == 30104
			スケジュール状態:キャラ番号:目的地 = 104 + RAND:2
		ENDIF
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;宣伝
;------------------------------------------------------------------------------
@SCHEDULE_宣伝(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(キャラ番号)
		スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;訓練
;------------------------------------------------------------------------------
@SCHEDULE_訓練(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		スケジュール状態:キャラ番号:目的地 = 308
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自動探索前
;------------------------------------------------------------------------------
@SCHEDULE_自動探索前(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		スケジュール状態:キャラ番号:目的地 = 1
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自動探索
;------------------------------------------------------------------------------
@SCHEDULE_自動探索(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		スケジュール状態:キャラ番号:目的地 = 998
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT

;------------------------------------------------------------------------------
;居住区各施設
;------------------------------------------------------------------------------
@SCHEDULE_居住区でのんびり(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = SCHEDULE_居住区移動先決定関数(キャラ番号, 1)
		スケジュール状態:キャラ番号:目的地 = SCHEDULE_居住区移動先決定関数(キャラ番号)
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_居住区移動先決定関数(キャラ番号, マップ種別取得フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM マップ種別取得フラグ

SIF マップ種別取得フラグ
	RETURNF 2

RETURNF RAND(2, 7)

;------------------------------------------------------------------------------
;海エリア関連
;------------------------------------------------------------------------------
@SCHEDULE_海入口(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 3
		スケジュール状態:キャラ番号:目的地 = 1
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT

@SCHEDULE_海更衣室、泳装へ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
			スケジュール状態:キャラ番号:目的マップ = 3
			スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 4 # 5
		ENDIF
		RETURN IS_AT_DESTINATION(キャラ番号)
	CASE "経過処理"
		TCVAR:キャラ番号:泳装着替え状態 = 1
	CASE "終了処理"
		TCVAR:キャラ番号:泳装着替え状態 = 1
		RETURN 1
ENDSELECT


@SCHEDULE_海更衣室、普段着へ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		IF スケジュール状態:キャラ番号:状況 == スケジュール_未実行
			スケジュール状態:キャラ番号:目的マップ = 3
			スケジュール状態:キャラ番号:目的地 = (施設利用性別(キャラ番号) == 性別_男性) ? 4 # 5
		ENDIF
		RETURN IS_AT_DESTINATION(キャラ番号)
	CASE "経過処理"
		TCVAR:キャラ番号:泳装着替え状態 = 0
	CASE "終了処理"
		TCVAR:キャラ番号:泳装着替え状態 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_海で遊ぶ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		スケジュール状態:キャラ番号:目的マップ = 3
		スケジュール状態:キャラ番号:目的地 = SCHEDULE_海で遊ぶ先決定関数(キャラ番号)
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_海で遊ぶ先決定関数(キャラ番号)
#FUNCTION
#DIM キャラ番号
;海の家、海岸、桟橋の三択

SELECTCASE RAND:3
	CASE 2
		RETURNF 2
	CASE 1
		RETURNF 3
	CASE 0
		RETURNF 6
ENDSELECT

@SCHEDULE_遊覧船に乗る(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 3
		スケジュール状態:キャラ番号:目的地 = 104
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT

@SCHEDULE_海底散歩をする(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 3
		スケジュール状態:キャラ番号:目的地 = 105
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自宅
;------------------------------------------------------------------------------
@SCHEDULE_あなた自宅でのんびり(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN

		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(60, 120)
		スケジュール状態:キャラ番号:目的マップ = SCHEDULE_あなた自宅移動先決定関数(キャラ番号, 1)
		スケジュール状態:キャラ番号:目的地 = SCHEDULE_あなた自宅移動先決定関数(キャラ番号)
	CASE "終了処理"
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
ENDSELECT

@SCHEDULE_あなた自宅移動先決定関数(キャラ番号, マップ種別取得フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM マップ種別取得フラグ

SIF マップ種別取得フラグ
	RETURNF 0

SELECTCASE RAND:7
	CASE 6
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 10
		RETURNF 2003
	CASE 5
		RETURNF キャラクター部屋検索(キャラ番号)
	CASE 4
		RETURNF 2009
	CASE 3
		RETURNF 2008
	CASE 2
		RETURNF 2006
	CASE 1
		RETURNF 2005
	CASE 0
		RETURNF 2004
ENDSELECT

;------------------------------------------------------------------------------
;美術館
;------------------------------------------------------------------------------
@SCHEDULE_美術鑑賞(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 2
		スケジュール状態:キャラ番号:目的地 = 102
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT

;------------------------------------------------------------------------------
;商業区
;------------------------------------------------------------------------------
@SCHEDULE_商業区店番(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 1
		スケジュール状態:キャラ番号:目的地 = 店持ちキャラ店検索(キャラ番号) + 1000
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;学校で勉強
;------------------------------------------------------------------------------
@SCHEDULE_学校で勉強(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 2
		スケジュール状態:キャラ番号:目的地 = 13
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		;たぶんここに知識経験値を増やす処理を入れればいい……はず……
		CALL 自動学習処理(キャラ番号)
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;学校に滞在
;------------------------------------------------------------------------------
@SCHEDULE_学校に滞在(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 2
		スケジュール状態:キャラ番号:目的地 = 13
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		;シンプルに学習処理を削除しただけの学校滞在
		;教師などに使う予定
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;食堂業務
;------------------------------------------------------------------------------
@SCHEDULE_食堂業務(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		IF 施設改造度:4:0
			スケジュール状態:キャラ番号:目的地 = RAND(403, 406)
		ELSE
			スケジュール状態:キャラ番号:目的地 = 4
		ENDIF
		RETURN 1
	CASE "経過処理"
		IF 施設改造度:4:0
			SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
				RETURN

			TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(15, 30)
			スケジュール状態:キャラ番号:目的マップ = 0
			スケジュール状態:キャラ番号:目的地 = RAND(403, 406)
		ENDIF
	CASE "終了処理"
		;シンプルに食堂に滞在するだけ
		RETURN 1
ENDSELECT

@SCHEDULE_調理業務(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		IF 施設改造度:4:0
			スケジュール状態:キャラ番号:目的地 = 406
		ELSE
			スケジュール状態:キャラ番号:目的地 = 4
		ENDIF
		RETURN 1
	CASE "経過処理"
	CASE "終了処理"
		;シンプルに食堂に滞在するだけ
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;自室でうふふ
;------------------------------------------------------------------------------
@SCHEDULE_自室でうふふ(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容
#DIM 攻めキャラ
#DIM 受けキャラ

IF CFLAG:キャラ番号:デート < 0
	;あなた以外とデート中は追従
	RCVAR:キャラ番号:追従ルーチン中 = ABS(CFLAG:キャラ番号:デート) - 100
ENDIF

SELECTCASE 処理内容
	CASE "開始処理"
		IF CFLAG:キャラ番号:デート < 0
			スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(ABS(CFLAG:キャラ番号:デート) - 100)
			スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
		ELSE
			スケジュール状態:キャラ番号:目的地 = キャラクター部屋検索(キャラ番号)
			スケジュール状態:キャラ番号:目的マップ = 部屋検索_マップ種別
		ENDIF
		IF CFLAG:キャラ番号:デート
			IF CFLAG:キャラ番号:現在位置 == CFLAG:(ABS(CFLAG:キャラ番号:デート) - 100):現在位置 && CFLAG:キャラ番号:現在マップ種別 == CFLAG:(ABS(CFLAG:キャラ番号:デート) - 100):現在マップ種別
				CFLAG:キャラ番号:キャラ同士うふふ = 1
				CALL CLOTHES_CHANGE(キャラ番号, "全裸")
			ENDIF
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		RETURN 1
	CASE "経過処理"
		IF CFLAG:キャラ番号:デート
			IF CFLAG:キャラ番号:現在位置 == CFLAG:(ABS(CFLAG:キャラ番号:デート) - 100):現在位置 && CFLAG:キャラ番号:現在マップ種別 == CFLAG:(ABS(CFLAG:キャラ番号:デート) - 100):現在マップ種別
				CFLAG:キャラ番号:キャラ同士うふふ = 1
				CALL CLOTHES_CHANGE(キャラ番号, "全裸")
				;ランダムにモードをセット、モードの切り替えはインターバル時間を流用
				;時間を共通させるため、追従キャラの時は処理しない
				IF CFLAG:キャラ番号:デート > 0 && TCVAR:キャラ番号:移動ルーチンインターバル時間 < 1
					;まずモードリセット
					CALL 特定キャラモードリセット(キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
					;PLAYERが観測してないなら文章表示しない
					SIF CFLAG:キャラ番号:現在位置 != CFLAG:PLAYER:現在位置 || CFLAG:キャラ番号:現在マップ種別 != CFLAG:PLAYER:現在マップ種別
						SKIPDISP 1
					;以下、ランダムでモードを設定
					;キス
					IF RAND:2 == 0 || TALENT:キャラ番号:キス嗜好 || TALENT:(ABS(CFLAG:キャラ番号:デート) - 100):キス嗜好
						IF RAND:3 == 0 || TALENT:キャラ番号:キス嗜好 || TALENT:(ABS(CFLAG:キャラ番号:デート) - 100):キス嗜好
							CALL MODE_CHANGE("ディープキス", キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
						ELSE
							CALL MODE_CHANGE("キス", キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
						ENDIF
					ENDIF
					;アイテム・自慰系、それぞれにどれか一個しかセットされない
					IF RAND:2
						CALL MODE_CHANGE(ランダム自慰・アイテムモード算出(キャラ番号), ABS(CFLAG:キャラ番号:デート) - 100, キャラ番号)
					ENDIF
					IF RAND:2
						CALL MODE_CHANGE(ランダム自慰・アイテムモード算出(ABS(CFLAG:キャラ番号:デート) - 100), キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
					ENDIF
					;愛撫系、それぞれにどれか一個しかセットされない
					CALL MODE_CHANGE(ランダム愛撫モード算出(キャラ番号), ABS(CFLAG:キャラ番号:デート) - 100, キャラ番号)
					CALL MODE_CHANGE(ランダム愛撫モード算出(ABS(CFLAG:キャラ番号:デート) - 100), キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
					;挿入系、受け攻めを見て算出
					IF RAND:2
						CALL 受け攻め関係性算出(キャラ番号, ABS(CFLAG:キャラ番号:デート) - 100)
						IF RESULT >= 0
							攻めキャラ = RESULT:10
							受けキャラ = RESULT:11

							CALL ランダム挿入モードセット(攻めキャラ, 受けキャラ)
						ENDIF
					ENDIF
					TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(30, 60)
					SKIPDISP 0
				ENDIF
			ENDIF
		ENDIF
	CASE "終了処理"
		CFLAG:キャラ番号:キャラ同士うふふ = 0
		CALL CLOTHES_RESET_TRAIN(キャラ番号)
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		IF !CFLAG:キャラ番号:うふふ
			;うふふじゃないなら全てのモードを消す
			CALL 特定キャラモードリセット(キャラ番号)
		ENDIF
		RETURN 1
ENDSELECT


;------------------------------------------------------------------------------
;被炉
;------------------------------------------------------------------------------
@SCHEDULE_被炉でまったり(キャラ番号, 小分類, 処理内容)
#DIM キャラ番号
#DIMS 小分類
#DIMS 処理内容

SELECTCASE 処理内容
	CASE "開始処理"
		スケジュール状態:キャラ番号:目的マップ = 0
		スケジュール状態:キャラ番号:目的地 = 2004
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 15
		RETURN 1
	CASE "経過処理"
		;確率で眠ることがある
		SIF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
			RETURN
		SIF CFLAG:キャラ番号:現在マップ種別 != 0
			RETURN
		SIF CFLAG:キャラ番号:現在位置 != 2004
			RETURN

		CFLAG:キャラ番号:睡眠 = 0
		TCVAR:キャラ番号:日中睡眠 = 0
		IF !RAND:10
			;とりあえず暫定で10%
			CFLAG:キャラ番号:睡眠 = 1
			TCVAR:キャラ番号:日中睡眠 = 1
		ENDIF
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(30, 60)
	CASE "終了処理"
		CFLAG:キャラ番号:睡眠 = 0
		TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
		TCVAR:キャラ番号:日中睡眠 = 0
		RETURN 1
ENDSELECT
