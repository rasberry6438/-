
;-------------------------------------------------
;プールで一起游玩
;-------------------------------------------------
@COMNAME_PLACE_380_3_3
#FUNCTION
TSTR:コマンド名受渡 = 一起游玩
IF 施設改造度:15:0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_3_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_380_3_3
LOCALS = <br>■一起游玩<br>
LOCALS += "与在场所有角色在海水浴场共同游玩的指令。根据概率分为大成功、成功、失败三种结果。<br>随机选择普通玩耍、竞速游泳、浮圈漂浮三种模式。"
LOCALS += "<br>指令类型：日常<br>获取来源：欢乐・征服"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM380_3_3

;成功度の判定
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 10
	CALL 同室者縁結び進行処理(RAND:3 + 1)
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 1
	CALL 同室者縁結び進行処理(1)
ENDIF

;遊びの種類をランダム決定
;１：普通に遊ぶ
;２：泳ぎで競争
;３：浮き輪でのんびり浮かぶ
;４：ビーチバレーで遊ぶ
;５：スイカ割りで遊ぶ
RFLAG:コマンド結果受渡し変数３ = RAND:5 + 1
RFLAG:コマンド結果受渡し変数 = RFLAG:コマンド結果受渡し変数 * RFLAG:コマンド結果受渡し変数３

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):隠密
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM380_3_3_追加処理
NEXT
TARGET = LOCAL:1

TIME += 60
RETURN 1

@COM380_3_3_追加処理



SELECTCASE RFLAG:コマンド結果受渡し変数３
	CASE 1
		;普通に遊ぶ
		
		;歓楽強度
		LOCAL:0 = 500
		;征服強度
		LOCAL:1 = 100
	CASE 2
		;競争
		
		LOCAL:0 = 300
		LOCAL:1 = 500
	CASE 3
		;浮き輪で浮かぶ
		LOCAL:0 = 700
		LOCAL:1 = 10
	CASE 4
		;ビーチバレー
		LOCAL:0 = 550
		LOCAL:1 = 200
	CASE 5
		;スイカ割り
		LOCAL:0 = 700
		LOCAL:1 = 10
ENDSELECT

IF RFLAG:コマンド結果受渡し変数 <= -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 <= 9
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)

;関係性上昇(基本15分で+1、大成功で1.5倍、失敗時は半減)
LOCAL:2 = RFLAG:コマンド結果受渡し変数 > 0 ? RFLAG:コマンド結果受渡し変数 / 10# -1
CALL 関係性一括上昇(TARGET, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_3_3
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_3_3

PRINTFORM %CALLNAME:PLAYER%与%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 们 # \@
SELECTCASE RFLAG:コマンド結果受渡し変数３
	CASE 1
		;普通に遊ぶ
		PRINT 在浪花边缘戏水
	CASE 2
		;競争
		PRINT 比赛谁游得最快
	CASE 3
		;浮き輪で浮かぶ
		PRINT 悠闲地躺在游泳圈上随波荡漾
	CASE 4
		;ビーチバレー
		PRINT 分组进行沙滩排球对决
	CASE 5
		;スイカ割り
		PRINT 蒙眼享受劈西瓜的乐趣
ENDSELECT

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE IS <= -1
		;失敗時
		PRINTL ，但
		PRINTW 气氛有些冷场…
	CASE IS <= 9
		;成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 大家 # 两人\@共度了愉快时光…
	CASEELSE
		;大成功時
		PRINTL 
		PRINTFORMW \@ GET_TARGETNUM() > 1 ? 大家 # 两人\@玩得非常尽兴！
ENDSELECT


