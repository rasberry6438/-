
;-------------------------------------------------
;兵装開発
;-------------------------------------------------
@COMNAME_PLACE_382_1_11
#FUNCTION
TSTR:コマンド名受渡 = 开发兵装
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_382_1_11
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_382_1_11
LOCALS = <br>■开发兵装<br>
LOCALS += "设计新型机体或搭载武器的指令。在场全员可共同参与开发"
LOCALS += "<br>当进度与灵感达标时可获得新设计图"
LOCALS += "<br>COM类型：可单独执行<br>获取来源：欢乐・征服(特定条件)"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM382_1_11

CALL 兵装開発
;設計結果　0=未設計 1=設計中 2=完成 -1=うまくいかなかった
RFLAG:コマンド結果受渡し変数 = RESULT
SIF RFLAG:コマンド結果受渡し変数 == 2
	RSTR:コマンド結果受渡し文字列 '= RESULTS

SIF RFLAG:コマンド結果受渡し変数 == 0
	RETURN 0

;縁結びの進行
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		;設計中
		CALL 同室者縁結び進行処理(1)
	CASE 2
		;完成
		CALL 同室者縁結び進行処理(3)
ENDSELECT

;同室の全員にSOURCE
IF TARGET > 0
	FOR LOCAL, 1, VARSIZE("TARGET")
		SIF TARGET:LOCAL <= 0
			BREAK
		SIF !COMABLEキャラ個別_382_1_11(TARGET:LOCAL)
			CONTINUE

		CALL COM382_1_11_追加処理(TARGET:LOCAL)
	NEXT
ENDIF

TIME += 60
RETURN 1

@COM382_1_11_追加処理(ARG)
#LOCALSIZE 3

VARSET LOCAL, 0

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -1
		;うまくいかなかった
		;歓楽
		LOCAL:0 = 50
		;関係性補正 半減
		LOCAL:2 = -1
	CASE 1
		;設計中
		;歓楽
		LOCAL:0 = 500
	CASE 2
		;完成
		;歓楽
		LOCAL:0 = 1000
		;征服
		LOCAL:1 = 200
		;関係性補正 2倍
		LOCAL:2 = 2
ENDSELECT

CALL SOURCE_CALC_歓楽(ARG, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(ARG, PLAYER, LOCAL:1)
CALL SOURCE_CALC_好感度要素_友情度UP(ARG, PLAYER, LOCAL:0 / 2)

CALL 関係性一括上昇(ARG, 4, LOCAL:2)

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE382_1_11
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(382)
	RETURN RESULT
SIF TARGET > 0 && !COMABLEキャラ個別_382_1_11(TARGET)
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;進捗などの計算で何度も使うので共通関数化
@COMABLEキャラ個別_382_1_11(ARG)
#FUNCTION

;睡眠中
SIF CFLAG:ARG:睡眠
	RETURNF 0
;隠密中はダメ
SIF CFLAG:ARG:隠密
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM382_1_11

;設計してない
SIF RFLAG:コマンド結果受渡し変数 == 0
	RETURN

IF TARGET > 0
	PRINTFORML %CALLNAME:PLAYER%与%CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 们 # \@共同开发新型兵装

	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE -1
			;うまくいかなかった
			IF 兵装開発_進捗 > 0
				PRINTL 但开发进度不如预期
				PRINTL 或许需要更熟悉机械或魔法的人员协助…
			ELSE
				PRINTL 但未能想出优秀方案
				PRINTL 可能需要寻找更有创意的人选…
			ENDIF
		CASE 1
			;設計中
			SELECTCASE 兵装開発_進捗
				CASE IS < 兵装開発_案出し
					PRINTFORML %CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 等 # \@正兴致勃勃地提出各种构想…
				CASE IS < 兵装開発_仕様決め
					PRINTFORML \@ GET_TARGETNUM() > 1 ? 大家以 # 两人以 \@提案为基础确定具体方案…
				CASEELSE
					PRINTFORML %CALLNAME:TARGET%\@ GET_TARGETNUM() > 1 ? 等 # \@正专注地将方案转化为设计图…
			ENDSELECT
		CASE 2
			;完成
			PRINTFORML \@ GET_TARGETNUM() > 1 ? 众人合作 # 两人合力 \@完成新型兵装「%RSTR:コマンド結果受渡し文字列%」设计图！
	ENDSELECT
ELSE
	PRINTFORML %CALLNAME:PLAYER%独自进行兵装开发

	SELECTCASE RFLAG:コマンド結果受渡し変数
		CASE -1
			;うまくいかなかった
			IF 兵装開発_進捗 > 0
				PRINTL 但开发进度不如预期
				PRINTL 或许需要更熟悉机械或魔法的人员协助…
			ELSE
				PRINTL 但未能想出优秀方案
				PRINTL 可能需要寻找更有创意的人选…
			ENDIF
		CASE 1
			;設計中
			SELECTCASE 兵装開発_進捗
				CASE IS < 兵装開発_案出し
					PRINTL 各种构思在脑海中不断浮现又消失…
				CASE IS < 兵装開発_仕様決め
					PRINTL 以核心构想为基础逐步完善具体方案…
				CASEELSE
					PRINTFORML %CALLNAME:PLAYER%全神贯注地绘制设计图…
			ENDSELECT
		CASE 2
			;完成
			PRINTFORML 新型兵装「%RSTR:コマンド結果受渡し文字列%」设计图完成！
	ENDSELECT
ENDIF

WAIT
