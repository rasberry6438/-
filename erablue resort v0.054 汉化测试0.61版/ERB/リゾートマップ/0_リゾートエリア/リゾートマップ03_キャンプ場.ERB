

@マップ紹介_3
;とりあえず5行
PRINTFORML ※介绍文案征集中※
PRINTFORML 
PRINTFORML 
PRINTFORML 
PRINTFORML 


@MAP_RESORT_0_3(AA)
#DIMS REF AA, 0


AA:00 = 山山山山山山山木木木木川川川木木木木木木山山山山山木木木木木木木木木木木木木
AA:01 = 山山山山山木木木木　　川川川　　　　木木木山山山木木木木木木　　　　　木木木
AA:02 = 山山木木木木木　　　川川川川　　　　　　木木木木木　　　　　　　　　　　　木
AA:03 = 山木木木木木　　　　川川川　　　　　　　　　木木　　　　　　　　　　　　　
AA:04 = 木木木木　　　　　　川川川──┐　　　　　　　　　　　　　┌─────┐　→02
AA:05 = 木木木　　　　　　川川川川　　│　　　　　　　　　　　　　│　　　　　│
AA:06 = 木木　　　　　　　川川川　　　│　　　　　　　　　　　　　│　　03　　│
AA:07 = 木　　　　　　　　川川川　05　│　　　　　　　　　　　　　│　　　　　│
AA:08 = 木　　　　　　　　川川川　　　│　　　　　　　　　　　　　└─────┘
AA:09 = 木　　　　　　　　川川川───┘　　　　　　　　　　　　　　　　　　　　　木
AA:10 = 　　　　　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木
AA:11 = 　　　　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木木
AA:12 = 　　　　　　　　川川川　　　　　　　　　　　　　┌─┐　　　　　　　　　木木
AA:13 = 　　　　　　　　川川川　　　　　　　　　　　　　│07│　　　　　　　　　　木
AA:14 = 　　　　　　　川川川川川　　　　　　　　　　　　└─┘　　　　　　　　　　木
AA:15 = 　　　　　湖湖湖湖湖湖湖湖湖　　┌─┐
AA:16 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖│06│
AA:17 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖└─┘
AA:18 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖
AA:19 = 　　　　湖湖湖湖湖湖湖湖湖湖湖　　　　　　　┌─────────┐
AA:20 = 　　　　　湖湖湖湖湖湖湖湖湖　　　　　　　　│　　　　　　　　　│　　　　木
AA:21 = 木　　　　　　湖湖湖湖湖　　　　　　　　　　│　　　　04　　　　│　　　　木
AA:22 = 木　　　　　　　川川川　　　　　　　　　　　│　　　　　　　　　│　　　木木
AA:23 = 木　　　　　　川川川川　　　　　　　　　　　└─────────┘　　木木木
AA:24 = 木木　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　木木木木木木
AA:25 = 木木木　　　川川川川　　　　　　　　　　　　　　　　　　　　　木木木木木木木
AA:26 = 
AA:27 = 二：前往大厅　　　　　三：主帐篷　　　　　　四：烧烤区　　　　　　
AA:28 = 五：溪流区　　　　　　六：桑拿帐篷　　　　　七：篝火营地　　　　　

IF 施設改造度:3:8 > 0
	AA:10 = 　┌────┐　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木
	AA:11 = 　│　　　　│　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木木
	AA:12 = 　│　08　　│　川川川　　　　　　　　　　　　　┌─┐　　　　　　　　　木木
	AA:13 = 　│　　　　│　川川川　　　　　　　　　　　　　│07│　　　　　　　　　　木
	AA:14 = 　└────┘川川川川川　　　　　　　　　　　　└─┘　　　　　　　　　　木

	AA:29 = 八：训练场
ENDIF

@GETPLACENAME_0_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 302
		TSTR:場所名受渡 = 大厅
	CASE 303
		TSTR:場所名受渡 = 主帐篷
	CASE 304
		TSTR:場所名受渡 = 烧烤区
	CASE 305
		TSTR:場所名受渡 = 溪流区
	CASE 306
		TSTR:場所名受渡 = 桑拿帐篷
	CASE 307
		TSTR:場所名受渡 = 篝火营地
	CASE 308
		IF 施設改造度:3:8 > 0
			TSTR:場所名受渡 = 训练场
		ENDIF
ENDSELECT


@DRAW_MAP_BG_0_3(SLICED, 表示文字列)
#DIMS REF 表示文字列
#DIMS SLICED
SELECTCASE SLICED
;色つけたいときはこんな感じ
CASE "木", "竹"
	表示文字列 += @"<font color='#90EE90'>%SLICED%</font>"
CASE "山"
	表示文字列 += @"<font color='#5C985C'>%SLICED%</font>"
CASE "川", "湖"
	表示文字列 += @"<font color='#5C5CFF'>%SLICED%</font>"
;違う文字を表示させる例
;下部一覧の数字を三桁表示しつつボタン化させる
CASE "二"
	表示文字列 += @"<button value='302'>302</button>"
CASE "三"
	表示文字列 += @"<button value='303'>303</button>"
CASE "四"
	表示文字列 += @"<button value='304'>304</button>"
CASE "五"
	表示文字列 += @"<button value='305'>305</button>"
CASE "六"
	表示文字列 += @"<button value='306'>306</button>"
CASE "七"
	表示文字列 += @"<button value='307'>307</button>"
CASE "八"
	表示文字列 += @"<button value='308'>308</button>"
;ふつうの
CASEELSE
	表示文字列 += @"%SLICED%"
ENDSELECT

;桑拿帐篷にいる場合の特殊イベント
@COM_END_PLACEEVENT_0_306

;別の場所からサウナに来たならまず熱気ゲージリセット
IF CFLAG:MASTER:前ターン位置 != CFLAG:MASTER:現在位置
	PRINTL 桑拿房内弥漫着令人窒息的热浪
	PRINTW 看来不宜在此久留
	熱気ゲージ = 0
	RETURN 0
ENDIF

;特定のコマンドの場合実行しない（移動、覗く、川で冷やした場合）
SIF SELECTCOM == 400 || SELECTCOM == 404 || SELECTCOM == 380
	RETURN 0

;オートコマンド時は発動しない
SIF TFLAG:オートコマンドフラグ
	RETURN 0

;サウナにいるキャラ全員の歓楽と露出と苦痛を少量増加させる。水で冷やしたターンは増加しない。
;その場に居るTARGET全員で回す
IF 熱気ゲージ >= 0
	LOCAL:1 = TARGET
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):睡眠
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):隠密
			CONTINUE
		TARGET = TARGET:LOCAL
		
		CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 70)
		CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
	NEXT
	TARGET = LOCAL:1
ENDIF

;熱気ゲージを増加させ、値により反応を返す。水で冷やしたターンは増加しない。
;具体的な数値は表示しない隠しパラメータとする

熱気ゲージ += RAND:15 + 20
DRAWLINE
SELECTCASE 熱気ゲージ
	CASE IS < 20
		;0～19
		PRINTL 目前状态良好，还能继续享受桑拿。
		RETURN 0
	CASE IS < 40
		;20～39
		PRINTL 身体开始感到舒适的暖意。
		RETURN 0
	CASE IS < 60
		;40～59
		PRINTL 体温明显升高，也许是时候冷却下来了。
		RETURN 0
	CASE IS < 80
		;60～79
		PRINTL 好热啊！
		PRINTL 意识开始变得朦胧…
		RETURN 0
	CASE IS < 100
		;80～99
		;サウナに弱いだとギブアップ
		IF TALENT:MASTER:サウナ耐性 == -1
		ELSE
			PRINTL 已经到达极限了！
			PRINTL 有什么东西可以给身体降温吗？
			RETURN 0
		ENDIF
	CASEELSE
		;100～
		;通常時ギブアップ
		IF TALENT:MASTER:サウナ耐性 == 1 && 熱気ゲージ < 120
			PRINTL 已经到达极限了！
			PRINTL 有什么东西可以给身体降温吗？
			RETURN 0
		ENDIF
ENDSELECT


PRINTL 不行了！
PRINTFORML %CALLNAME:MASTER%无法忍受高温，从桑拿房中夺门而出！
PRINTL 在其他区域经过充分休整后，身体的灼热感终于消退…

TIME += 30
CFLAG:MASTER:現在位置 = 303
;隠密時解除
CFLAG:MASTER:隠密 = 0
CFLAG:TARGET:隠密 = 0
CALL うふふ終了処理("サウナ限界")
RETURN 1


@庭系列改造メニュー()
#FUNCTIONS
;100~199と999は大本のメニュー操作で使うため、使用しないこと

LOCALS = 改造度假村连接的户外区域。<br><br>
IF 施設改造度:3:0 < 1
	LOCALS += @"■<button value='200'>[营地改造]</button> Lv{施設改造度:3:0}>{施設改造度:3:0 + 1} （需要150,000卢比）<br>"
ELSE
	LOCALS += @"■[营地改造] Lv{施設改造度:3:0}：上限<br>"
ENDIF
IF 施設改造度:3:0 > 0 && 施設改造度:3:8 < 1
	LOCALS += @"└■<button value='201'>[扩建训练场]</button> （需要80,000卢比）<br>"
ELSEIF 施設改造度:3:8 > 0
	LOCALS += @"└■[训练场扩建]已完成<br>"
ENDIF

RETURNF LOCALS

@庭系列改造実行関数(ARG)

SELECTCASE ARG
	CASE 200
		DRAWLINE
		PRINTL 庭院将改造为露营地，新增以下设施：
		PRINTBUTTON "■解锁露营地专属地图", 300
		PRINTL 
		PRINTBUTTON "■搭建主帐篷", 301
		PRINTL 
		PRINTBUTTON "■开辟烧烤区", 302
		PRINTL 
		PRINTBUTTON "■开发溪流区", 303
		PRINTL 
		PRINTBUTTON "■架设桑拿帐篷", 304
		PRINTL 
		PRINTBUTTON "■布置篝火堆", 305
		PRINTL 
		PRINTL 
		PRINTL 是否将庭院改造为露营地？（需要:150,000卢比）
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		BINPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 150000
					PRINTW 卢比不足。
					RETURN
				ENDIF
				施設改造度:3:0 = 1
				MONEY -= 150000
				PRINTL 庭院已成功改造为露营地！
				PRINTW (可停留角色上限＋５)
				CALL 滞在可能キャラ数再計算
				CALL RESET_GRAPH_0
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号,@"将庭院改造为Lv1露营地","実績")
				RETURN
			CASE 1
				RETURN
			CASE 300
				DRAWLINE
				PRINTL 解锁地图后，进入露营地时将切换至专属场景。
				PRINTL 随着改造深入，可扩展地图并升级内部设施。（暂未实装）
				PRINTW
			CASE 301
				DRAWLINE
				PRINTL 作为露营核心的大型帐篷。
				PRINTL 可围坐篝火享受悠闲时光。
				PRINTL 夜间露营角色将在此就寝。
				PRINTW
			CASE 302
				DRAWLINE
				PRINTL 配备肉类与鱼类的烧烤区域。
				PRINTL 支持烹饪与饮酒，露营角色将在此享用午晚餐。
				PRINTL 夜间可能有角色在此举办宴会。（暂未实装）
				PRINTW 
			CASE 303
				DRAWLINE
				PRINTL 可享受清澈溪流的亲水区域。
				PRINTL 可进行悠闲垂钓或戏水活动。
				PRINTW 
			CASE 304
				DRAWLINE
				PRINTL 配备桑拿功能的帐篷设施。
				PRINTL 附带可跃入的清凉湖泊。
				PRINTW 
			CASE 305
				DRAWLINE
				PRINTL 巨型篝火架与燃烧装置。
				PRINTL 18:00前篝火不会点燃。
				PRINTL 入夜后将成为角色聚集地。
				PRINTW 
		ENDSELECT
		RESTART
	CASE 201
		DRAWLINE
		PRINTL 将在露营地外围区域搭建训练场。
		PRINTL 引入训练场后，长期雇佣角色可在每日结束时获取经验值。
		PRINTL 同时员工任务分配中将新增「训练」选项，执行该任务的角色亦可获得经验。
		PRINTL 注：所有角色等级上限不得超过「当前队伍最高角色等级」。
		PRINTL 
		PRINTL 是否建造训练场？（需要:80,000卢比）
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 80000
					PRINTW 卢比不足。
					RETURN
				ENDIF
				施設改造度:3:8 = 1
				MONEY -= 80000
				PRINTL 训练场已成功建造！
				PRINTW (可停留角色上限＋５)
				CALL 滞在可能キャラ数再計算
				CALL RESET_GRAPH_0
				RETURN
			CASE 1
				RETURN
		ENDSELECT
		RESTART
ENDSELECT







