

;-------------------------------------------------
;男性泳装挑战
;-------------------------------------------------
@COMNAME_PLACE_381_0_15
#FUNCTION
TSTR:コマンド名受渡 = 男性泳装挑战
IF 施設改造度:15:0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_381_0_15
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")

@COM_TOOLTIP_381_0_15
LOCALS = <br>■男性泳装挑战<br>
LOCALS += "让女性或扶她角色穿着男式泳装进行隐蔽挑战的指令。<br>可能成功或失败。"
LOCALS += "<br>指令类型：性骚扰<br>获取来源：暴露"
LOCALS += "<br>专属经验值：暴露快感经验(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM381_0_15
#DIM 成功閾値

;成功度の判定
LOCAL = RAND:100
成功閾値 = 0
IF TALENT:TARGET:バストサイズ == -2
	成功閾値 += 50
ELSEIF TALENT:TARGET:バストサイズ == -1
	成功閾値 += 25
ENDIF
SIF TALENT:TARGET:度胸 > 0
	成功閾値 += 5
SIF TALENT:TARGET:感情乏しい > 0
	成功閾値 += 10
SIF TALENT:TARGET:目立ちたがり > 0
	成功閾値 += 5
成功閾値 -= TALENT:TARGET:羞恥心 * 5
成功閾値 += TALENT:TARGET:豪快 * 5
成功閾値 += ABL:TARGET:露出癖 * 3

IF LOCAL >= 成功閾値
	;失敗
	RFLAG:コマンド結果受渡し変数 = -1
	SIF 知識素質:TARGET:性知識 > -1
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 800)
ELSE
	;成功
	RFLAG:コマンド結果受渡し変数 = 1
	SIF 知識素質:TARGET:性知識 > -1
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 500)
ENDIF

TIME += 30
RETURN 1


@CAN_COM381_0_15(ARG)
#DIM CONST 必要実行値 = 60
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 男性泳装挑战(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 暴露癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 10
	PRINT 害羞(10)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 厌男(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 炮友(3)
	＋表示フラグ = 1
ENDIF
;性知識不足
IF 知識素質:TARGET:性知識 > -1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 15
	PRINT 性知识不足(15)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(60以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE381_0_15
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(381)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;男はダメ
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;貧乳以下
SIF TALENT:TARGET:バストサイズ > -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM381_0_15

PRINTFORM %CALLNAME:PLAYER%让%CALLNAME:TARGET%穿上男式泳装，要求绕场一周进行挑战。


@MESSAGE_COM381_0_15_非口上時

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -1
		;失敗時
		PRINTL 
		PRINTL ……周围游客投来怀疑的目光。
		PRINTFORML 隐约听到有人窃窃私语%CALLNAME:TARGET%可能是女性……
		IF 知識素質:TARGET:性知識 < 0
			PRINTFORML %CALLNAME:TARGET%无视这些视线，昂首阔步回到%CALLNAME:PLAYER%身边
		ELSE
			PRINTFORML %CALLNAME:TARGET%强忍质疑的目光红着脸，勉强回到%CALLNAME:PLAYER%身边
		ENDIF
	CASE 1
		;成功時
		PRINTL 
		PRINTL 
		PRINTL ……似乎成功瞒过众人返回
		IF 知識素質:TARGET:性知識 < 0
			PRINTFORML %CALLNAME:TARGET%歪着头不解这个行为的意义
		ELSE
			PRINTFORML %CALLNAME:TARGET%忐忑不安地快速换回原泳装
		ENDIF
ENDSELECT


