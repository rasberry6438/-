
;-------------------------------------------------
;ポーカー処理関連
;-------------------------------------------------
;あなた勝利時は１
;代入番号キャラ勝利時は-１
;平局時は０を返り値とする
;-------------------------------------------------
		
@簡易ポーカー(キャラ番号)
#DIM 手持ちカード, 5
#DIM 交換カード, 5
#DIM 相手カード強さ
#DIM 自己カード強さ
#DIM キャラ番号
#DIM ループ用
#DIM 被りチェック
#DIM カード交換, 5
#DIM 同花？
#DIM ペア系？
#DIM 顺子？

VARSET 手持ちカード
VARSET 交換カード
VARSET 相手カード強さ
VARSET 自己カード強さ
VARSET ループ用
VARSET 被りチェック
VARSET カード交換
VARSET 同花？
VARSET ペア系？
VARSET 顺子？

DRAWLINE

;簡易版のため相手の役は単純な確率で算出
;皇家同花顺・五条・同花顺は確率が0.1%未満のため省略

LOCAL = RAND:1000

;ギャンブラーは強いので交換アリ
SIF TALENT:キャラ番号:ギャンブラー > 0
	LOCAL = MAX(LOCAL, RAND:1000)

SELECTCASE LOCAL
	CASE IS < 400
		;散牌：４０％
		相手カード強さ = 0
	CASE IS < 840
		;一对：４４％
		相手カード強さ = 1
	CASE IS < 920
		;两对：８％
		相手カード強さ = 2
	CASE IS < 970
		;三条：５％
		相手カード強さ = 3
	CASE IS < 990
		;顺子：２％
		相手カード強さ = 4
	CASE IS < 993
		;同花：０．３％
		相手カード強さ = 5
	CASE IS < 998
		;葫芦：０．５％
		相手カード強さ = 6
	CASEELSE
		;四条：０．２％
		相手カード強さ = 7
ENDSELECT


;自分の手持ちカード配布
FOR ループ用, 0, 5
	$被りループ
	;53種
	手持ちカード:ループ用 = RAND:53
	;カード被りが無いか
	FOR 被りチェック, 0, ループ用
		SIF 手持ちカード:被りチェック == 手持ちカード:ループ用
			GOTO 被りループ
	NEXT
NEXT

;手札表示
$表示ループ
FOR ループ用, 0, 5
	SELECTCASE 手持ちカード:ループ用
		CASE 52
			PRINT [JOKER] 
		CASE 0 TO 12
			PRINT [
			PRINT_IMG "スペード"
			PRINTPLAINFORM {手持ちカード:ループ用 + 1}]  
			SIF 手持ちカード:ループ用 < 10
				PRINT  
		CASE 13 TO 25
			PRINT [
			PRINT_IMG "クローバー"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 26 TO 38
			PRINT [
			PRINT_IMG "ハート"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 39 TO 51
			PRINT [
			PRINT_IMG "ダイヤ"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
	ENDSELECT
NEXT
PRINTL 

;交換フェイズ
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		PRINTBUTTON "[交换]", ループ用
	ELSEIF カード交換:ループ用 == 1
		SETCOLOR 0xFFD400
		PRINTBUTTON "[保留]", ループ用
		RESETCOLOR
	ENDIF
	PRINT   
NEXT
PRINTL 

PRINTBUTTON "[999]执行交换", 999
PRINTL
PRINTBUTTON "[说明]", 998

INPUT

SELECTCASE RESULT
	CASE 0 TO 4
		INVERTBIT カード交換:RESULT, 0
		CLEARLINE 5
		GOTO 表示ループ
	CASE 999
	CASE 998
		DRAWLINE
		PRINTL 这是通过交换初始手牌组成牌型决胜负的扑克小游戏，牌型强度从低到高依次为：
		PRINTL 散牌、一对、两对、三条、顺子
		PRINTL 同花、葫芦、四条、同花顺
		PRINTL 五条、皇家同花顺
		PRINTL 
		PRINTW 特殊情况下可与对手进行赌博，可设定卢比或惩罚规则
		GOTO 表示ループ
	CASEELSE
		GOTO 表示ループ
ENDSELECT

;交換用カードが手持ちとも他の交換とも被りがないかチェック
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		$被りループ２
		;53種
		交換カード:ループ用 = RAND:53
		;カード被りが無いか
		FOR 被りチェック, 0, 5
			SIF 手持ちカード:被りチェック == 交換カード:ループ用
				GOTO 被りループ２
		NEXT
		FOR 被りチェック, 0, ループ用
			SIF 交換カード:被りチェック == 交換カード:ループ用
				GOTO 被りループ２
		NEXT
	ELSEIF カード交換:ループ用 == 1
		交換カード:ループ用 = 0
	ENDIF
NEXT

;被りチェックを突破したら手持ちと交換
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		手持ちカード:ループ用 = 交換カード:ループ用
	ELSEIF カード交換:ループ用 == 1
	ENDIF
NEXT

DRAWLINE

;交換後再表示
FOR ループ用, 0, 5
	SELECTCASE 手持ちカード:ループ用
		CASE 52
			PRINT [JOKER] 
		CASE 0 TO 12
			PRINT [
			PRINT_IMG "スペード"
			PRINTPLAINFORM {手持ちカード:ループ用 + 1}]  
			SIF 手持ちカード:ループ用 < 10
				PRINT  
		CASE 13 TO 25
			PRINT [
			PRINT_IMG "クローバー"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 26 TO 38
			PRINT [
			PRINT_IMG "ハート"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 39 TO 51
			PRINT [
			PRINT_IMG "ダイヤ"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
	ENDSELECT
NEXT
PRINTL 

;役判定
;まずは同花系の判定
FOR ループ用, 1, 5
	IF 手持ちカード:0 == 52
		;初手ジョーカーの場合２枚目で判定する
		SIF (手持ちカード:1 / 13) == (手持ちカード:ループ用 / 13)
			同花？ += 1
	ELSE
		;通常一枚目で照合する
		IF (手持ちカード:0 / 13) == (手持ちカード:ループ用 / 13)
			同花？ += 1
		ELSEIF 手持ちカード:ループ用 == 52
			同花？ += 1
		ENDIF
	ENDIF
NEXT

;ペア系の判定
;まずは手札を全て数字化
FOR ループ用, 0, 5
	SIF 手持ちカード:ループ用 == 52
		;ジョーカーが計算後-1になるように
		手持ちカード:ループ用 = -2
	手持ちカード:ループ用 = 手持ちカード:ループ用 % 13 + 1
NEXT

;同花成立なら同花？＝４になっているはず
;その場合ペア系はありえないので顺子までGOTO
SIF 同花？ == 4
	GOTO 顺子判定

;一枚目照合
FOR ループ用, 1, 5
	SIF 手持ちカード:0 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;二枚目照合
FOR ループ用, 2, 5
	SIF 手持ちカード:1 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;三枚目照合
FOR ループ用, 3, 5
	SIF 手持ちカード:2 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;四枚目照合
SIF 手持ちカード:3 == 手持ちカード:4
	ペア系？ += 1

SELECTCASE ペア系？
	CASE 1
		;一对
		ペア系？ = 1
	CASE 2
		;两对
		ペア系？ = 2
	CASE 3
		;三条
		ペア系？ = 3
	CASE 4
		;葫芦
		ペア系？ = 6
	CASE 6
		;四条
		ペア系？ = 7
ENDSELECT

;ジョーカーの処理
IF MIN(手持ちカード:0, 手持ちカード:1, 手持ちカード:2, 手持ちカード:3, 手持ちカード:4) == -1
	SELECTCASE ペア系？
		CASE 0
			;散牌＞一对
			ペア系？ = 1
		CASE 1
			;一对＞三条
			ペア系？ = 3
		CASE 2
			;两对＞葫芦
			ペア系？ = 6
		CASE 3
			;三条＞四条
			ペア系？ = 7
		CASE 7
			;四条＞五条
			ペア系？ = 9
	ENDSELECT
ENDIF

$顺子判定

;手持ちを小さい順にソート
ARRAYSORT 手持ちカード, FORWARD

;２番目～５番目のカードが１差であることを判定
FOR ループ用, 1, 4
	SELECTCASE 手持ちカード:(ループ用 + 1) - 手持ちカード:ループ用
		CASE 1
			顺子？ += 1
		CASE 2
			;ジョーカー処理
			IF 手持ちカード:0 == -1
				顺子？ += 2
				;二重処理されないように数変更
				手持ちカード:0 = -10
			ENDIF
		
	ENDSELECT
NEXT

IF 顺子？ == 3
	IF 手持ちカード:0 > 0
		SIF 手持ちカード:1 - 手持ちカード:0 == 1
			顺子？ += 1
	ELSEIF 手持ちカード:0 == -1
		;端っこジョーカーのフォロー
		顺子？ += 1
	ENDIF
ENDIF

;ロイヤル系の顺子のフォロー
;１を１４にしてから再判定
IF 顺子？ != 4 && (手持ちカード:0 == 1 || 手持ちカード:1 == 1)
	FOR ループ用, 0, 5
		SIF 手持ちカード:ループ用 == 1
			手持ちカード:ループ用 = 14
	NEXT
	顺子？ = 0
	GOTO 顺子判定
ENDIF



;役の強さに変換
IF 顺子？ == 4
	自己カード強さ = 4
	;ストフラ
	IF 同花？ == 4
		自己カード強さ += 4
		;ロイフラ？
		SIF 手持ちカード:4 == 14
			自己カード強さ += 2
		SIF 手持ちカード:1 == 10 && 手持ちカード:0 < 0
			自己カード強さ += 2
	ENDIF
ELSEIF 同花？ == 4
	自己カード強さ = 5
ELSEIF ペア系？
	自己カード強さ = ペア系？
ENDIF

自己カード強さ = MIN(自己カード強さ, 10)

;役名表示
PRINT 我方牌型：
CALL 役名表示(自己カード強さ)
PRINTL 
PRINT 对手牌型：
CALL 役名表示(相手カード強さ)
PRINTL 
SIF 自己カード強さ == 10
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"达成皇家同花顺！！","実績")
SIF 相手カード強さ == 10
	CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"达成皇家同花顺！！","実績")

IF 自己カード強さ > 相手カード強さ
	PRINTFORMW %CALLNAME:MASTER%获胜！
	履歴用実績:MASTER:ポーカー勝利 += 1
	CALL 履歴登録チェック(MASTER, "ポーカー勝利")
	RETURN 1
ELSEIF 自己カード強さ < 相手カード強さ
	PRINTFORMW %CALLNAME:キャラ番号%获胜！
	履歴用実績:キャラ番号:ポーカー勝利 += 1
	CALL 履歴登録チェック(キャラ番号, "ポーカー勝利")
	RETURN -1
ELSE
	PRINTW 平局
	RETURN 0
ENDIF


@役名表示(ARG)

SELECTCASE ARG
	CASE 0
		PRINT 散牌
	CASE 1
		PRINT 一对
	CASE 2
		PRINT 两对
	CASE 3
		PRINT 三条
	CASE 4
		PRINT 顺子
	CASE 5
		PRINT 同花
	CASE 6
		PRINT 葫芦
	CASE 7
		PRINT 四条
	CASE 8
		PRINT 同花顺
	CASE 9
		PRINT 五条
	CASE 10
		PRINT 皇家同花顺
ENDSELECT



