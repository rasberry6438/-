;-------------------------------------------------
;腹股沟淋巴按摩
;-------------------------------------------------
@MASSAGE_COMNAME22
#FUNCTIONS
TSTR:コマンド名受渡 = 腹股沟淋巴按摩


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_22
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_22
LOCALS = <br>■腹股沟淋巴按摩<br>
LOCALS += "通过指压与掌压手法刺激腹股沟至比基尼线区域的淋巴节点"
LOCALS += "<br>因涉及敏感部位，需提前解除对象的警戒状态"
LOCALS += "<br>指令类型：情色按摩<br>获取来源：快Ｃ・快Ｖ・暴露・接触・欢乐"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM22
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


;鼠径部からビキニラインにかけて素肌の上からの刺激なので快Ｖはほんの気持ち程度
SIF TALENT:TARGET:Ｖ感度 >= -1
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 30)
	
;対象者がオトコ、半人半妖、ふたなりの場合はＣ感。外性器なのでＶより刺激が強い
SIF GETBIT(TALENT:TARGET:性別, 1) && TALENT:TARGET:Ｃ感度 != -2
	CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 50)

CALL SOURCE_CALC_露出(TARGET, PLAYER, 75)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 75)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)


IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -50)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ELSE
;局部に直接触れるわけではないので、警戒は少し上がるが理性は削れる。性欲も煽られる。なまごろし～！
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 20)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

IF TARGET == MASTER
	CALL ちんイラ増加処理(PLAYER, 75)
ELSE
	CALL ちんイラ増加処理(TARGET, 150)
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE22
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(22)
	RETURN RESULT

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;実行するだけならそこそこ緩い。警戒はされるけど。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1500
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM22
PRINTFORM %CALLNAME:PLAYER%用沾满按摩精油的掌心对%CALLNAME:TARGET%
	IF 下半身体型(TARGET) >= 1
	;肉感的以上
		PRINTFORM 丰腴饱满的
	ELSEIF 下半身体型(TARGET) <= -2
	;細身以下
		PRINTFORM 脂肪稀薄的
	ENDIF
PRINTFORML %TEXTR("腹股沟/比基尼线")%进行%TEXTR("细致入微地/轻柔舒缓地/缓慢渐进地")%揉捏按摩。

