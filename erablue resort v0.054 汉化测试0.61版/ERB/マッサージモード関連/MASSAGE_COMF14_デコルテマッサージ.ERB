;-------------------------------------------------
;颈胸区按摩
;-------------------------------------------------
@MASSAGE_COMNAME14
#FUNCTIONS
TSTR:コマンド名受渡 = 颈胸区按摩


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_14
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("微エロマッサージ")

@MASSAGE_COM_TOOLTIP_14
LOCALS = <br>■颈胸区按摩<br>
LOCALS += "通过按摩颈部至锁骨及前胸区域改善淋巴循环"
LOCALS += "<br>促进面部血流使轮廓清晰，优化颈部线条"
LOCALS += "<br>指令类型：轻度情色按摩<br>获取来源：快Ｓ・接触・欢乐・暴露"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM14
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -40)
	BASE:TARGET:性欲 += 20
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 60)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 30)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 30)
ELSE
;際どい位置に触れるため、少しだけ警戒される。理性もあんまり減らないし性欲も少ししか煽れない。……これといった強み、なくね？
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 60)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 30)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 30)
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -40)
	BASE:TARGET:性欲 += 20
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 10)
ENDIF

;血行促進で基礎体温が上がったりするらしいので、歓楽強め。リラックス効果も強め。
;触る場所が胸元に近いので一応微エロ枠に。露出がちょっと増える。
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 75)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)

IF TARGET == MASTER
	CALL ちんイラ増加処理(PLAYER, 35)
ELSE
	CALL ちんイラ増加処理(TARGET, 70)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE14
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(14)
	RETURN RESULT

;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0

;女性限定。男は大胸筋マッサージの範囲内にこれが入ってるでしょって感じ
SIF TALENT:TARGET:性別 == 2
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;警戒が900以下でないとダメ
SIF BASE:TARGET:警戒 > 900
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM14

PRINTFORM %CALLNAME:PLAYER%
	;今後浴衣とかバスローブみたいな施術着を着ないとも限らないため
	SIF TEQUIP:TARGET:上半身服あり == 1 && TEQUIP:TARGET:はだけ可 == 1
		PRINTFORM 将%CALLNAME:TARGET%穿着的理疗服前襟解开后，
PRINTFORML 用沾满按摩精油的掌心对%CALLNAME:TARGET%的锁骨下方敏感区域进行按摩。

