;-------------------------------------------------
;乳头舒缓按摩
;-------------------------------------------------
@MASSAGE_COMNAME23
#FUNCTIONS
TSTR:コマンド名受渡 = 乳头舒缓按摩
SIF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	TSTR:コマンド名受渡 = 内陷乳头唤醒护理


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_23
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_23
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	LOCALS = <br>■内陷乳头唤醒护理<br>
	LOCALS += "对深藏于乳房中『紧绷』的内陷乳头进行牵引唤醒"
	LOCALS += "<br>指令类型：情色按摩<br>获取来源：快Ｂ・暴露・接触・欢乐"
	LOCALS += "<br>专属经验值：无<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ELSE
	LOCALS = <br>■乳头舒缓按摩<br>
	LOCALS += "通过按摩缓解因快感刺激而『紧绷』的乳头"
	LOCALS += "<br>本应放松却愈发坚挺的现象实属情趣所在"
	LOCALS += "<br>指令类型：情色按摩<br>获取来源：快Ｂ・暴露・接触・欢乐"
	LOCALS += "<br>专属经验值：无<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ENDIF
@MASSAGE_COM23
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:乳首開発フラグ = 1

;Ｂ愛撫より全体的に強め。勿論です、プロですから。
;性交ではなくマッサージの中での行為のため露出強め。エロマッサージなので歓楽は通常より弱め
CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 175)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)

;陥没乳首をほじり出すときは刺激が強い。
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
ENDIF

;せっかくだから男性相手にはさらに効果が増すように。
;男への性感マッサージコマンドは数が少なめだから……
SIF TALENT:TARGET:性別 == 2
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)

;陥没乳首をほじり出す場合のみ、上半身下着を脱がす。
IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ && TEQUIP:上半身下着あり
	TEQUIP:上半身下着あり = 0
	TEQUIP:上半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:下半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF


;乳首をいじるため性欲を煽る。
BASE:TARGET:性欲 += 80

;警戒は理性の倍回復する。

IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -50)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒140で理性が70回復するも、煽られる性欲と与えられる刺激で理性が80削られて理性変動は-10。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒56で理性28回復、そこから理性80低下でこの数字となる。理性と警戒の合計値はほんの少しだけ上がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -52)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 56)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF BASE:TARGET:理性 <= 200
;理性がグズグズに煮詰まっちゃってると、警戒及び理性の回復は半分になる。理性200警戒800とかの場合これ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 35)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 70)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF TALENT:TARGET:バストサイズ <= -1
;相手が貧乳以下の場合、実行可能であるならば「これもおっぱい大きくするため……？」ってしぶしぶ受け入れるイメージ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 50)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 100)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSE
;基本値
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 70)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 140)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDIF

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 100)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE23
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(23)
	RETURN RESULT

;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0

;乳首が勃起するくらいの欲情が必要
SIF PALAM:欲情 <= PALAMLV:3
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;理性警戒の合計が1000以下じゃないとダメ
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM23

IF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	PRINTFORM %CALLNAME:PLAYER%
		;胸の施術着があった場合
		SIF RFLAG:コマンド結果受渡し変数 == 1
			PRINTFORM 褪去%CALLNAME:TARGET%的胸部理疗服后，
	PRINTFORML 用手指将深埋在乳房中的内陷乳头缓缓牵引而出。
ELSE
	PRINTFORM %CALLNAME:PLAYER%
		;胸の施術着がある場合
		SIF TEQUIP:上半身下着あり
			PRINTFORM 隔着理疗服也能清晰感受到
	PRINTFORML 用指尖细致揉捏%CALLNAME:TARGET%%TEXTR("傲然挺立的/硬如小石的")%乳头。
ENDIF

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%%TEXTR("双眸迷离地/满脸陶醉地张着樱唇/神情恍惚地")%
	CASE 1
		PRINTFORM %CALLNAME:TARGET%从微张的唇间%TEXTR("溢出甜腻呻吟/发出愉悦娇喘/漏出淫靡声响")%
	ENDSELECT
	PRINTFORML 任由%CALLNAME:PLAYER%%TEXTR("的『按摩』/的技法/带来的快感")%%TEXTR("玩弄着乳头/沉醉其中/予取予求/全盘接受/甘之如饴")%…
ENDIF
