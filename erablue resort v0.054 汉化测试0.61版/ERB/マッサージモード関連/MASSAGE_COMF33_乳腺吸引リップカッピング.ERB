;-------------------------------------------------
;乳腺吸引唇部护理
;-------------------------------------------------
@MASSAGE_COMNAME33
#FUNCTIONS
TSTR:コマンド名受渡 = 乳腺吸引唇部护理


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_33
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_33
LOCALS = <br>■乳腺吸引唇部护理<br>
LOCALS += "不进入吸乳模式的单次指令"
LOCALS += "<br>用双唇『吸出』堆积在顾客乳头深处乳腺的老废物质"
LOCALS += "<br>注：拔罐疗法本是使用类似挤奶器的杯具吸附腹部"
LOCALS += "<br>将淤积在血管内的血液引导至体表的实际存在疗法"
LOCALS += "<br>当然不会用嘴实施，细节无需深究"
LOCALS += "<br>指令类型：情色按摩<br>获取来源：快Ｂ・暴露・接触・欢乐"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM33
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

RCVAR:乳首開発フラグ = 1
;基盤ができたあとで理性を削る処理を入れる
;強く吸うと乳首コリほぐしを参考に調整

CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 250)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)

;せっかくだから男性相手にはさらに効果が増すように。
;男への性感マッサージコマンドは数が少なめだから……
SIF TALENT:TARGET:性別 == 2
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)

;母乳体質ならさらに快感アップ
IF TALENT:TARGET:母乳体質
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
	CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
ENDIF

;上半身下着を脱がす。
IF TEQUIP:上半身下着あり
	TEQUIP:上半身下着あり = 0
	TEQUIP:上半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:下半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF

;乳首をいじるため性欲を煽る。
BASE:TARGET:性欲 += 100

;警戒は理性の倍回復する。

IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;コリほぐしより理性は削るが警戒削りはやや低くなる
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -60)
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒140で理性が70回復するも、煽られる性欲と与えられる刺激で理性が100削られて理性変動は-30。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒56で理性28回復、そこから理性80低下でこの数字となる。理性と警戒の合計値はほんの少しだけ上がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -72)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 56)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF BASE:TARGET:理性 <= 200
;理性がグズグズに煮詰まっちゃってると、警戒及び理性の回復は半分になる。理性200警戒800とかの場合これ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 35)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 70)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSEIF TALENT:TARGET:バストサイズ <= -1
;相手が貧乳以下の場合、実行可能であるならば「これもおっぱい大きくするため……？」ってしぶしぶ受け入れるイメージ。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 50)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 100)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)

ELSE
;基本値
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 70)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 140)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ENDIF

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 100)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE33
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(33)
	RETURN RESULT

;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0

;乳首が勃起するくらいの欲情が必要
SIF PALAM:欲情 <= PALAMLV:3
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;陥没乳首なら露出させてからでないとダメ
SIF TALENT:TARGET:陥没乳首 && !TCVAR:TARGET:陥没乳首露出フラグ
	RETURN 0

;理性警戒の合計が1000以下じゃないとダメ
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1000
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM33
PRINTFORM %CALLNAME:PLAYER%将%CALLNAME:TARGET%的
SIF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORM 胸部部理疗服褪去后，
PRINTFORML 把%TEXTR("傲然挺立的/硬如小石的")%%CALLNAME:TARGET%的乳头%TEXTR("含入口中/用唇轻衔/双唇包裹/舌尖挑弄")%，%TEXTR("发出橡胶挤压声/蠕动着面颊与舌头/从乳晕处挤压式吮吸/连乳腺堵塞物一同")%将乳头细致吸出。

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 700
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%%TEXTR("双眸迷离地/满脸陶醉地张着樱唇/神情恍惚地")%
	CASE 1
		PRINTFORM %CALLNAME:TARGET%从微张的唇间%TEXTR("溢出甜腻呻吟/发出愉悦娇喘/漏出淫靡声响")%
	ENDSELECT
	PRINTFORML 向%CALLNAME:PLAYER%%TEXTR("的『护理』/的口技/带来的快感")%%TEXTR("献出乳头/沉醉其中/予取予求/全盘接受/甘之如饴")%…
ENDIF
