;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――スタイル変化関連
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;●使い方
;--------------------------------------------------
;「1147」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;「<スタイル名>」を任意のスタイル名に変更する
;--------------------------------------------------

;--------------------------------------------------
;●スタイル変更基礎関数
;--------------------------------------------------
;必須関数。
;スタイル変更を追加する場合、必ず作成すること
;この関数はいくつ別スタイル作ってもキャラ毎に一個でよい
;--------------------------------------------------
@スタイル変更_1147(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード

	SELECTCASE 動作モード
		CASE "スタイル名取得"
			;ここは触らない
			;--------------------------------------------------
			VARSET RESULTS
			RESULTS:0 = %NAME:対象キャラ%
			RESULTS:10 = ノーマル
			;--------------------------------------------------

			;以下、追加スタイル。
			;RESULTS:Xに名前全体を、RESULTS:X+10にリミテッドや泳装など通称(？)を入れる
			;X+10の、通称？　強化スタイル種別？　については、これ自体に意味はない
			;例：
			; RESULTS:1 = [強化状態二つ名]テスト太郎
			; RESULTS:11 = 強化スタイル
			IF 陥落チェック(対象キャラ)
				RESULTS:1 = [花曇の魔女]パチュリー・ノーレッジ
				RESULTS:11 = リミテッド
			ENDIF

			; 複数スタイルを作りたい場合はRESULTS:2、RESULTS:12のように数字をふやしていく

			;このように書けばよい――はずだが、封印。
			;たぶんここをコメントアウトすれば封印できるはず……
			;『レスリング』ネタだからね……嫌いな人普通にいるだろうし。
			;じゃあどうして作ったんだい？　って言えば、『第三のスタイル』を作りたかったからです

;			RESULTS:2 = [無救]マチョリー・ノーレッジ
;			;『アナザーワン』なのは、ジョジョ四部から（神主はジョジョネタが結構好きみたい）（俺も好き）
;			RESULTS:12 = アナザーワン

		CASE @"スタイル解説_%NAME:対象キャラ%"
			TSTR:スタイル解説 = 
			TSTR:スタイル解説 += @"■%NAME:対象キャラ%<br>"
			TSTR:スタイル解説 += "<br>"
			TSTR:スタイル解説 += "通常のスタイル[七曜の魔女]。<br>"
			TSTR:スタイル解説 += "自身の属性に応じて使用できるアビリティ・奥技が変化する。<br>"
			TSTR:スタイル解説 += "鈍足低耐久の、もやし・ザ・紫。<br>"
			TSTR:スタイル解説 += "<br>"
			;このCALLはそのままで良い
			CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
		CASE "スタイル解説_[花曇の魔女]パチュリー・ノーレッジ"
			TSTR:スタイル解説 = 
			TSTR:スタイル解説 += "■[花曇の魔女]パチュリー・ノーレッジ<br>"
			TSTR:スタイル解説 += "<br>"
			TSTR:スタイル解説 += "東方緋想天、非想天則をモチーフにしたスタイル。<br>"
			TSTR:スタイル解説 += "ノーマルスタイルでは使用しないスペルカードを使用する。<br>"
			TSTR:スタイル解説 += "属性が火に変化する。<br>"
			TSTR:スタイル解説 += "ステータス配分がマイルドになる。<br>"
			TSTR:スタイル解説 += "<br>"
			;このCALLはそのままで良い、戦闘アビが変動しない場合は消しても良い
			CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[花曇の魔女]パチュリー・ノーレッジ")
		CASE "スタイル解説_[無救]マチョリー・ノーレッジ"
			TSTR:スタイル解説 = 
			TSTR:スタイル解説 += "■[無救]マチョリー・ノーレッジ<br>"
			TSTR:スタイル解説 += "<br>"
			TSTR:スタイル解説 += "世紀末覇者……ちがう。違います。喘息を克服したパッチェさんです。<br>"
			TSTR:スタイル解説 += "病弱であった体質が、なんということでしょう。<br>"
			TSTR:スタイル解説 += "病原菌をねじ伏せるは筋肉にしてマッスル。<br>"
			;※全部「むきゅう」と読んでね※
			TSTR:スタイル解説 += "無救。されど無窮。そして無泣。<br>"
			TSTR:スタイル解説 += "その健康さはきっと、空に響き渡る。救いってねえの？<br>"
			TSTR:スタイル解説 += "あ、ステータス配分も、きちんとムキムキになるよ。<br>"
			TSTR:スタイル解説 += "属性が光になるのは汗のきらめき。<br>"
			TSTR:スタイル解説 += "<br>"
			;このCALLはそのままで良い、戦闘アビが変動しない場合は消しても良い
			CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[無救]マチョリー・ノーレッジ")

		CASE "変更後処理"
			;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
			;一人称や二人称、素質の変化などの調教側ステータスの変更を想定
			;戦闘ステータスは別の関数

			;ここに来た時点で：マチョリー時に体格変動してたのを戻す
			;マチョリー体格変化をコメントアウトしてるのでこちらもコメントアウト
;			TALENT:対象キャラ:体格 = 0

			SELECTCASE CSTR:対象キャラ:スタイル管理文字列
				CASE ""
					;通常時に戻す処理
				CASE "[花曇の魔女]パチュリー・ノーレッジ"
					;スタイル変更時の処理

				CASE "[無救]マチョリー・ノーレッジ"
					;スタイル変更時の処理
					;体格を筋肉質とかに変えてもいいかな…………

					;男ドラフ並みの体格……にするのはさすがに良くないよな……！　ということでコメントアウト
;					TALENT:対象キャラ:体格 = 2
			ENDSELECT
	ENDSELECT


;--------------------------------------------------
;スタイル変更時、戦闘ステータス変動
;--------------------------------------------------
;関数名に[]が使えないため、しょうがなく[]排除時の文字列をキーに
;例えば[強化状態二つ名]テスト太郎というスタイルなら、
;@スタイルステータス補正_強化状態二つ名_テスト太郎　という関数名にすること
;--------------------------------------------------
;装備再計算時にこの関数は呼ばれる
;補正の順番は「スタイル＞指輪＞耳飾り＞料理など＞パーティキャラ補正」
;補正可能なステータスはERB\探索\装備データ下、「装備ステータス補正保存.ERD」を参照のこと
;--------------------------------------------------
@スタイルステータス補正_花曇の魔女_パチュリー・ノーレッジ(対象キャラ)
#DIM 対象キャラ
#DIM 現在レベル

	;属性の変更、火とか水とか文字を変えるだけでOK
	装備ステータス補正保存:対象キャラ:基礎属性変更 = 属性文字列数値変換("火") + 10

	;ステータスの変更
	;　普段は鈍足・低耐久・高火力の砲台タイプ
	;　これを、「鈍足低耐久高火力」の傾向はそのままに、もう少しマイルドにする
	;　疑似的に「別の成長曲線で成長してきた場合」のステータスに変更（初期値の関係で同じタイプのキャラよりちょっといびつなステータスになるかな？）
	;　本当は割合で変動させた方がいい気がするが……

	現在レベル = 基礎BATTLE_STATE:対象キャラ:Lv

	;それぞれの成長率を、疑似的に、下記の通りに変更。
	;モデルはマギサ。まあだいぶ普通のキャラに近づく。
	;　最大ＨＰ ：24→29
	;　最大ＭＰ ：30→29
	;　攻撃力   ：28→24
	;　防御力   ：15→22
	;　回復力   ：24→23
	;　行動速度 ：14→16
	装備ステータス補正保存:対象キャラ:最大ＨＰ += 現在レベル * 5
	装備ステータス補正保存:対象キャラ:最大ＭＰ -= 現在レベル * 1
	装備ステータス補正保存:対象キャラ:攻撃力   -= 現在レベル * 4
	装備ステータス補正保存:対象キャラ:防御力   += 現在レベル * 7
	装備ステータス補正保存:対象キャラ:回復力   -= 現在レベル * 1
	装備ステータス補正保存:対象キャラ:行動速度 += 現在レベル * 2

@スタイルステータス補正_無救_マチョリー・ノーレッジ(対象キャラ)
#DIM 対象キャラ
#DIM 現在レベル

	;属性の変更、火とか水とか文字を変えるだけでOK
	装備ステータス補正保存:対象キャラ:基礎属性変更 = 属性文字列数値変換("光") + 10

	;ステータスの変更
	;　普段は鈍足・低耐久・高火力の砲台タイプ
	;　これを重戦士タイプに変更する

	現在レベル = 基礎BATTLE_STATE:対象キャラ:Lv

	;それぞれの成長率を、疑似的に、下記の通りに変更。
	;　最大ＨＰ ：24→69（むきゅー）
	;　最大ＭＰ ：30→15
	;　攻撃力   ：28→23
	;　防御力   ：15→28
	;　回復力   ：24→10
	;　行動速度 ：14→15
	装備ステータス補正保存:対象キャラ:最大ＨＰ += 現在レベル * 45
	装備ステータス補正保存:対象キャラ:最大ＭＰ -= 現在レベル * 15
	装備ステータス補正保存:対象キャラ:攻撃力   -= 現在レベル * 5
	装備ステータス補正保存:対象キャラ:防御力   += 現在レベル * 13
	装備ステータス補正保存:対象キャラ:回復力   -= 現在レベル * 14
	装備ステータス補正保存:対象キャラ:行動速度 += 現在レベル * 1




;--------------------------------------------------
;スタイル変更時、使用可能アビリティの変動
;--------------------------------------------------
;テンプレート「固有アビリティ_キャラ名.ERB」に記述する
;「@固有アビ_XXXX_任意の文字列(ARGS, キャラ番号)」の上部にある分岐を修正する

; IF スタイル判定関数(キャラ番号, "デフォルト") == 0
; を以下のように修正
; IF スタイル判定関数(キャラ番号, "<スタイル名>") == 0

;こうすることで、そのアビは<スタイル名>になっている時のみ使用可能という意味になる


;--------------------------------------------------
;スタイル変更時、使用可能奥義の変動
;--------------------------------------------------
;奥義はアビのように複数種類を使うことを想定していないため、別関数を呼ぶことを推奨。
;奥義テンプレにある以下の分岐のうち、任意のスタイル名の部分を編集した上で
;奥義テンプレをコピーして作成した関数に貼り付け、中身を記述する。

; IF CSTR:キャラ番号:スタイル管理文字列 == "任意のスタイル名"
; 	CALL 任意のスタイル名固有奥義(ARGS, キャラ番号)
; 	RETURN RESULT
; ENDIF


