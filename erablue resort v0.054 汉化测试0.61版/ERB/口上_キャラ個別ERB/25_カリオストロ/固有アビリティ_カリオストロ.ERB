@固有アビ_25_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果成功率
#DIMS アビ名
#DIM Lv閾値 =  1, 13, 20, 33

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = 坍缩＋＋
	消費ＭＰ = 45
	効果数値 = 120
	効果数値２ = 30
	効果成功率 = 100
	SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = 核合成
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = 坍缩＋
	消費ＭＰ = 35
	効果数値 = 120
	効果数値２ = 20
	効果成功率 = 100
ELSE
	アビ名 = 坍缩
	消費ＭＰ = 20
	効果数値 = 80
	効果数値２ = 10
	効果成功率 = 80
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>对敌方全体造成［攻击力✕{効果数値}％］的自身属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ｂ槽・3回合）的防御力下降效果。成功率{効果成功率}％"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>赋予［1］层减速效果。成功率{効果成功率}％"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>当自身属性刻印≥3时，消耗1层强化效果并消除［1］层，发动2次。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
	FOR LOCAL, 0, 1+(刻印現在数確認(属性刻印名())>2)
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:効果量 = 1
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
	NEXT
	IF 刻印現在数確認(属性刻印名()) > 2
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = %属性刻印名()%
		CALL アビテンプレート_刻印減少(1)
	ENDIF
ENDSELECT
RETURN RESULT


@固有アビ_25_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果数値２
#DIMS 枠名 = "幻觉成像"
#DIM Lv閾値 =  5, 16, 24, 40

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = 幻觉成像＋＋
	消費ＭＰ = 80
	効果数値 = 30
	効果数値２ = 25
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = 神秘演化
		枠名 = 神秘演化
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = 幻觉成像＋
	消費ＭＰ = 70
	効果数値 = 30
	効果数値２ = 15
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = 幻觉成像
	消費ＭＰ = 50
	効果数値 = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体（3回合・不可驱散）的%枠名%效果。
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体（3回合）的%枠名%效果。
		ENDIF
		SWAP TARGET, キャラ番号
		詳細文文字列受け渡し変数 += @"<br>赋予［累积1层 最大5层］的%属性刻印名()%效果。"
		SWAP TARGET, キャラ番号
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体（3回合・不可驱散）的%枠名%效果。
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体（3回合）的%枠名%效果。
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値}％］的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値}％］的防御力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値}％］的DA率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値/2}％］的TA率提升效果。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値２*2}％］的暴击率提升效果。"
			詳細文文字列受け渡し変数 += @"<br>＞赋予［{効果数値２+5}％］的暴击伤害提升效果。"
		ENDIF
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>赋予（1次）驱散防御效果"
		詳細文文字列受け渡し変数 += @"<br>赋予［累积1层 最大5层］的%属性刻印名()%效果。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = %枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = %枠名%
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:特殊表示オプション = 1
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット


		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 防御力_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = DA率_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値 / 2
		アビ汎用文字列:バフ・デバフ枠 = TA率_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 効果数値２ * 2
			アビ汎用文字列:バフ・デバフ枠 = CT率_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 効果数値２ + 5
			アビ汎用文字列:バフ・デバフ枠 = CT倍率_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ディスペルガード_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)
		ENDIF

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ対象能力 = %属性刻印名()%
		CALL アビテンプレート_刻印追加(1)
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_幻觉成像(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("CT率_幻觉成像", 隊列番号)
PRINTFORML 幻觉成像
PRINTFORML 提升攻击力・防御力・DA率・TA率\@ LOCAL > -1 ? ・暴击率・暴击伤害 # \@
PRINTFORML 持续回合：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

@バフ・デバフ特殊表示_神秘演化(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("ディスペルガード_神秘演化", 隊列番号)
SIF LOCAL > -1
	LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "持続回数")
PRINTFORML 神秘演化
PRINTFORML 提升攻击力・防御力・DA率・TA率・暴击率・暴击伤害
PRINTFORML 无效受到的下一次驱散效果
PRINTFORM 持续回合： {DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}ターン
PRINTFORML 　／　驱散防御剩余次数： {LOCAL:1}次

@固有アビ_25_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果成功率
#DIMS アビ名
#DIM Lv閾値 = 8, 18, 29, 42

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = 补强＋＋
	効果数値 = 80
	消費ＭＰ = 50
	SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = 圣铜蛇
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = 补强＋
	効果数値 = 60
	消費ＭＰ = 35
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = 补强
	効果数値 = 35
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>为我方全体回复［回复力✕{効果数値}％］的ＨＰ。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			詳細文文字列受け渡し変数 += "<br>赋予［1］个减益消除。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += "<br>赋予［回复力✕10％（上限150）］（独立槽・7回合）的再生效果。"
			詳細文文字列受け渡し変数 += "<br>赋予［100％］（独立槽・1次）的受伤害减少效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理_MAXでも回復
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			CALL アビテンプレート_有利効果_デバフ消去
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用文字列:バフ・デバフ枠 = 再生_ﾈﾌｼｭﾀﾝ
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			アビ汎用変数:効果割合 = 0
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = MIN(RESULT * 10 / 100, 150)
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 被ダメ無効_ﾈﾌｼｭﾀﾝ
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:効果量 = 0
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
RETURN RESULT

@固有アビ_25_11(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = 辉誉术
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 10
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 10
	ENDIF
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}(最大ＭＰ的１０％)
		詳細文文字列受け渡し変数 += "<br>赋予我方全体（不可驱散）的永恒乌洛波洛斯效果。"
		詳細文文字列受け渡し変数 += "<br>此技能每场战斗仅可使用一次。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}(最大ＭＰ的１０％)
		詳細文文字列受け渡し変数 += "<br>赋予我方全体（不可驱散）永恒乌洛波洛斯效果。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［100％］的DA率提升效果。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［30％］的TA率提升效果。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［100％］的奥义伤害提升效果。"
		詳細文文字列受け渡し変数 += "<br>＞<b>赋予［35％］的奥义值上升量<font color='#FF3030'>下降</font>效果。</b>"
		詳細文文字列受け渡し変数 += "<br>此技能每场战斗仅可使用一次。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = エターナル・ウロボロス
		アビ汎用文字列:バフ・デバフ対象能力 = エターナル・ウロボロス
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = DA率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 30
		アビ汎用文字列:バフ・デバフ枠 = TA率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ枠 = 奥義D_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 35
		アビ汎用文字列:バフ・デバフ枠 = GE率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("エターナル・ウロボロス")

		戦闘中カウント:キャラ隊列検索(GETCHARA(25)):0 = 1
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_エターナル・ウロボロス(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
PRINTL エターナル・ウロボロス
HTML_PRINT "DA率・TA率・奥义伤害提升，但<b>奥义槽积累速度<font color='#FF3030'>降低</font>的状态</b>（永久・不可驱散）"
PRINTL

@固有アビ_25_12(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM CONST 使用可能ターン = 40

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = 恩底弥翁
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 2
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 2
	ENDIF
ENDIF
IF キャラ隊列検索(GETCHARA(25)) > -1
	判定値 = 探索中全体カウント:累積ターン数 + 現在ターン数
	FOR LOCAL, 0, 10
		判定値 += 戦闘中カウント:LOCAL:奥義回数 + 探索中カウント:LOCAL:奥義回数
	NEXT
	SIF 探索中カウント:キャラ隊列検索(GETCHARA(25)):2 || 判定値 < 使用可能ターン
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}(最大ＭＰ的５０％)　赋予我方全体武器爆发效果。
		詳細文文字列受け渡し変数 += "<br>赋予［全部］的ＭＰ回复。"
		詳細文文字列受け渡し変数 += "<br>赋予［100％］（独立槽）的自动复活效果。"
		詳細文文字列受け渡し変数 += @"<br>此技能可在探索累计回合数＋队伍全体奥义发动次数≥{使用可能ターン}（現在{判定値}）时使用。"
		詳細文文字列受け渡し変数 += "<br><b>此技能每次探索仅可使用一次</b>。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL アビテンプレート_有利効果_ウェポンバースト

		アビ汎用変数:効果割合 = 10000
		CALL アビテンプレート_ＭＰ回復処理(1)

		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 恩底弥翁
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
		CALL アビテンプレート_有利効果_バフ効果セット

		;アビ効果→MP消費の順に適応されるため、カリオストロのMPが半分になるのを防ぐため消費を0に
		消費MP一時保存 = 0
		探索中カウント:キャラ隊列検索(GETCHARA(25)):2 = 1
ENDSELECT
RETURN RESULT

@固有アビ_25_13(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = 几何学之路
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 20
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 20
	ENDIF
ENDIF

IF キャラ隊列検索(GETCHARA(25)) > -1
	判定値 = ( 戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 ? MAX(0, 6 + 戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 - 現在ターン数) # 0)
	SIF 判定値
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}(最大ＭＰ的５％)
		詳細文文字列受け渡し変数 += "<br>赋予敌方全体（6回合・不可驱散）的几何学之路效果。"
		詳細文文字列受け渡し変数 += @"<br>此技能会产生［6回合］冷却时间。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"剩余{判定値}回合。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}(最大ＭＰ的５％)
		詳細文文字列受け渡し変数 += "<br>赋予敌方全体（6回合・不可驱散）的几何学之路效果。成功率200％。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［100％］的DA率下降效果。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［100％］的TA率下降效果。"
		詳細文文字列受け渡し変数 += "<br>＞赋予［30％］的命中率下降效果。"
		詳細文文字列受け渡し変数 += "<br>赋予［100％］（1回合・不可驱散・穿透弱化抗性）的封印效果。"
		詳細文文字列受け渡し変数 += @"<br>此技能会产生［6回合］冷却时间。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"剩余{判定値}回合。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 几何学之路
		アビ汎用文字列:バフ・デバフ対象能力 = 几何学之路
		アビ汎用変数:基礎成功確率 = 200
		アビ汎用変数:持続ターン = 6
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = DA率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("几何学之路")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = TA率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("几何学之路")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 30
		アビ汎用文字列:バフ・デバフ枠 = 命中率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("几何学之路")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:効果量 = 0
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ枠 = 封印_几何学之路
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		CALL アビテンプレート_不利効果_デバフ効果セット

		戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 = 現在ターン数
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_几何学之路(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
PRINTFORML 几何学之路
PRINTFORML DA率・TA率・命中率下降的状态
PRINTFORML 持续回合： {DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}ターン
