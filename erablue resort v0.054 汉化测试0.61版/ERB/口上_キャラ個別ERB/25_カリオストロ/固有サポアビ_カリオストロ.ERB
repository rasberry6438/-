
;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_25(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

IF ダンジョン表示モード != "" && INRANGE(キャラ隊列検索(対象キャラ), 0, 3)
	LOCAL = BATTLE_STATE:キャラ隊列検索(対象キャラ):属性
ELSEIF 装備ステータス補正保存:対象キャラ:属性
	LOCAL = 装備ステータス補正保存:対象キャラ:属性 - 10
ELSE
	LOCAL = 基礎BATTLE_STATE:対象キャラ:属性
ENDIF
LOCAL:1 = 有利属性変換(LOCAL)

IF TALENT:対象キャラ:恋慕
	KSTR:(K++) = □ 特性：『真理具象化』 □------------------------------------------------------------------
	KSTR:(K++) = 　友方发动奥义时，赋予自身［累积1层 最大8层］的真理Lv效果。
	KSTR:(K++) = 　自身发动奥义时，消耗全部真理Lv使奥义威力提升［真理等级Lv✕30％］。
	KSTR:(K++) = 　
	KSTR:(K++) = □ 特性：『卡莉奥斯特萝是无敌哒☆』 □--------------------------------------------------
	KSTR:(K++) = 　友方使用驱散效果时，对目标敌人造成［攻击力✕188％］的无属性伤害。
	KSTR:(K++) = 　赋予［1］层减速效果。
	KSTR:(K++) = 　赋予自身［累积1层 最大8层］的真理Lv效果。
	KSTR:(K++) = 　
ENDIF
KSTR:(K++) = □ 特性：『可爱的真谛』 □---------------------------------------------------------------
KSTR:(K++) = 　当友方持有属性刻印时，根据刻印数值提升暴击率、被回复上限、_COLOR_{属性数値文字色変換(LOCAL:1)}_%属性数値文字列変換(LOCAL:1)%抗性_。
KSTR:(K++) = 　%属性刻印名(LOCAL, , 2)%效果更强。
KSTR:(K++) = 　且当%属性刻印名(LOCAL, , 2)%数值≥3时，回合结束时回复［50✕（%属性刻印名(LOCAL, , 2)%数值-3）］HP。
KSTR:(K++) = 　（_COLOR_{属性数値文字色変換(LOCAL:1)}_%属性数値文字列変換(LOCAL:1)%_及%属性刻印名(LOCAL, , 2)%属性会随卡莉奥斯特萝当前属性变化）
KSTR:(K++) = 　自身属性刻印提升时，随机对敌人造成［攻击力✕100％］的自身属性伤害，共{MIN(2+(基礎BATTLE_STATE:対象キャラ:Lv/39), 3)}次。
KSTR:(K++) = 　

KSTR:(K++) = □ 特性：『备用躯体』 □-----------------------------------------------------------------
KSTR:(K++) = 　战斗开始时，赋予自身［50％］的自动复活效果。
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)

;------------------------------------------------------------------------
;　恋慕時（原作リミテッドVer）限定のサポアビ
;------------------------------------------------------------------------

@固有奥義後処理_バフ_真理具象化(バフ・デバフ行数, 全滅フラグ)
#DIM バフ・デバフ行数
#DIM 全滅フラグ
#DIM カリオストロ隊列

SIF 全滅フラグ
	RETURN 0

カリオストロ隊列 = キャラ隊列検索(キャラ検索("[开拓的炼金术师]卡莉奥斯特萝"))

IF INRANGE(カリオストロ隊列, 0, 3)
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(カリオストロ隊列)
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 真理Lv
	アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
	アビ汎用変数:持続回数 = 1
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:効果割合 = 30
	アビ汎用変数:累積上限_割合 = 240
	LOCAL = バフ番号取得_枠("真理Lv", カリオストロ隊列)
	IF LOCAL > -1
		LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "効果量_割合")
	ELSE
		LOCAL:1 = 0
	ENDIF
	アビ汎用文字列:実行時メッセージ１ = カリオストロの真理Lv\@ LOCAL:1 < アビ汎用変数:累積上限_割合 ? が1上がった！ # は既に最大値だ \@
	CALL アビテンプレート_有利効果_累積バフ効果セット
ENDIF

@固有行動直後処理_バフ・デバフ_真理具象化(効果番号)
#DIM 効果番号
#DIM 隊列番号
#DIM 対象番号保存, アビ対象最大数
VARSET LOCAL
VARSET 対象番号保存, -1

;その行動でディスペルを使用していないなら戻す
SIF STRFIND(アビテンプレート用_付与デバフ保存, "ディスペル") < 0 
    RETURN 0

;カリオストロが前衛にいないなら戻す
隊列番号 = キャラ隊列検索(キャラ検索("[开拓的炼金术师]卡莉奥斯特萝"))
SIF 隊列番号 < 0 || 隊列番号 > 3
    RETURN 0

;一時的に戦闘行動キャラをカリオストロに渡す
SWAP 戦闘行動キャラ, 隊列番号

;ディスペルの対象に無属性ダメージ
アビテンプレート用_アビ名 = 卡莉奥斯特萝是无敌哒☆

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 188
CALL アビテンプレート_ダメージ処理_無属性
アビ汎用変数:効果割合 = 0
アビ汎用変数:効果量 = 1
アビ汎用変数:基礎成功確率 = 100
CALL アビテンプレート_不利効果_スロウ

;元の行動の対象を保存
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(戦闘行動キャラ)
アビ汎用変数:消去不可オプション = 1
アビ汎用文字列:バフ・デバフ枠 = 真理Lv
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
アビ汎用変数:持続回数 = 1
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:効果割合 = 30
アビ汎用変数:累積上限_割合 = 240
LOCAL = バフ番号取得_枠("真理Lv", 戦闘行動キャラ)
IF LOCAL > -1
	LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "効果量_割合")
ELSE
	LOCAL:1 = 0
ENDIF
アビ汎用文字列:実行時メッセージ１ = カリオストロの真理Lv\@ LOCAL:1 < アビ汎用変数:累積上限_割合 ? が1上がった！ # は既に最大値だ \@
CALL アビテンプレート_有利効果_累積バフ効果セット

;戦闘行動キャラと元の行動の対象を戻す
SWAP 戦闘行動キャラ, 隊列番号
FOR LOCAL, 0, アビ対象最大数
	アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
NEXT

@バフ・デバフ特殊表示_真理Lv(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
PRINTFORML 真理Lv{LOCAL/30}
PRINTFORML 奥义威力提升［真理Lv✕30（{LOCAL}）％］，奥义发动时全部消耗。

;------------------------------------------------------------------------
;　通常（各属性・季節限定ごちゃまぜ）のサポアビ
;------------------------------------------------------------------------

@固有ターン終了時処理_バフ_可爱的真谛(バフ・デバフ行数)
#DIM バフ・デバフ行数

LOCAL = 刻印現在数確認(属性刻印名(DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")))
SIF LOCAL < 3
	RETURN 0

CALL アビ汎用変数文字列リセット
アビテンプレート用_アビ名表示済フラグ = 1
アビテンプレート用_アビ名 = 可爱的真谛
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 50 * (LOCAL-2)
CALL アビテンプレート_回復処理_MAXでも回復


@固有行動直後処理_バフ・デバフ_可爱的真谛(効果番号)
#DIM 効果番号
#DIM 隊列番号
#DIM 対象番号保存, アビ対象最大数
VARSET LOCAL
VARSET 対象番号保存, -1

[SKIPSTART]
;--- 刻印の増減による効果の増減に関する記述---
;その行動で属性刻印を増減していないなら戻す
FOR LOCAL, 0, 6
	SIF STRFIND(アビテンプレート用_付与バフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:1 ++
	SIF STRFIND(アビテンプレート用_付与デバフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:2 ++
NEXT
SIF !(LOCAL:1 + LOCAL:2)
	RETURN 0

;一時的に元の行動の対象を保存する
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

FOR 隊列番号, 0, 4
	;対象でないなら飛ばす
	SIF !MATCH(アビテンプレート用_対象保存, 隊列番号) || BATTLE_STATE:隊列番号:キャラ登録番号 < 1
		CONTINUE
	FOR LOCAL, 0, 6
		;属性刻印の最大値を算出する。カリオストロと同一属性刻印は2倍換算する。
		LOCAL:3 = MAX(LOCAL:3, 刻印現在数確認(属性刻印:LOCAL, 隊列番号))
		SIF LOCAL == BATTLE_STATE:キャラ隊列検索(キャラ検索("[开拓的炼金术师]卡莉奥斯特萝")):属性
			LOCAL:3 = MAX(LOCAL:3, 刻印現在数確認(属性刻印:LOCAL, 隊列番号)*2)
	NEXT
	LOCAL:4 = バフ番号取得_枠("刻印バフ_カリオストロ", 隊列番号)
	IF LOCAL:4 >= 0
	;効果値としては最大値の半分（端数切り上げ）他属性刻印でも多少は効果でるように。
		DT_CELL_SET "戦闘効果データベース", LOCAL:4, "効果量_固定値", (LOCAL:3 + 1) / 2
	ELSE
		;ないことだとは思うが、刻印バフが消えていた場合再設定
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(隊列番号)
		アビテンプレート用_アビ名 = 刻印バフ_カリオストロ
		アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_カリオストロ
		アビ汎用文字列:バフ・デバフ対象能力 = 刻印バフ_カリオストロ
		アビ汎用変数:効果量 = (LOCAL:3 + 1) / 2
		CALL アビテンプレート_有利効果_付随バフ効果セット("可爱的真谛")
	ENDIF
	
NEXT
[SKIPEND]
;--- 刻印の増加による自動ダメアビに関する記述---
;カリオストロが前衛にいない、または元の行動の対象にカリオストロが含まれていないなら保存しておいた対象を戻して返す

FOR LOCAL, 0, 6
	SIF STRFIND(アビテンプレート用_付与バフ保存, 属性刻印:LOCAL) >= 0 
		LOCAL:1 ++
NEXT
;一時的に元の行動の対象を保存する
FOR LOCAL, 0, アビ対象最大数
	対象番号保存:LOCAL = アビテンプレート用_対象保存:LOCAL
NEXT

隊列番号 = キャラ隊列検索(キャラ検索("[开拓的炼金术师]卡莉奥斯特萝"))
IF INRANGE(隊列番号, 0, 3) && MATCH(対象番号保存, 隊列番号) && LOCAL:1 >= 0
ELSE
	FOR LOCAL, 0, アビ対象最大数
		アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
	NEXT
	RETURN 0
ENDIF
;一時的に戦闘行動キャラをカリオストロに渡す
SWAP 戦闘行動キャラ, 隊列番号

;カリオストロの属性刻印が上昇した場合、追撃２～３回
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵ランダムＸ体(MIN(2+(BATTLE_STATE:隊列番号:Lv/39), 3))
アビテンプレート用_アビ名 = 可爱的真谛
アビ汎用変数:効果割合 = 100
CALL アビテンプレート_ダメージ処理_自属性

;戦闘行動キャラと対象を戻す
SWAP 戦闘行動キャラ, 隊列番号
FOR LOCAL, 0, アビ対象最大数
	アビテンプレート用_対象保存:LOCAL = 対象番号保存:LOCAL
NEXT

@バフ・デバフ特殊表示_可爱的真谛(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIMS 刻印名
VARSET LOCAL

;LOCAL:0 = バフ番号取得_枠("刻印バフ_カリオストロ", 隊列番号)
LOCAL:1 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
刻印名 = %属性刻印名(LOCAL:1, , 1)%

LOCALS = 可爱的真谛
LOCALS += @"<br>当卡莉奥斯特萝在队伍中存活时，根据自身属性刻印数值提升"
LOCALS += @"<br>暴击率・造成伤害・被回复量・"
LOCALS += @"获得<font color='#%属性数値文字色変換_HTML(有利属性変換(LOCAL:1))%'>%属性数値文字列変換(有利属性変換(LOCAL:1))%抗性</font>提升状态。"
LOCALS += @"<br>且当%刻印名%数值≥3时，回合结束时回复［50✕（%刻印名%数值-2）］ＨＰ。"
HTML_PRINT LOCALS
[IF_DEBUG]
LOCAL:2 = バフ番号取得_枠("刻印バフ_CT率_カリオストロ", 隊列番号)
LOCAL:3 = バフ番号取得_枠("刻印バフ_属性耐性_カリオストロ", 隊列番号)
LOCAL:4 = バフ番号取得_枠("刻印バフ_被回復_カリオストロ", 隊列番号)
LOCAL:5 = DT_CELL_GET("戦闘効果データベース", LOCAL:0, "効果量_固定値")
;PRINTFORML ＞［{LOCAL:5}］分の属性刻印の影響で
PRINTFORML ＞暴击率提升［{付随依存変動算出(LOCAL:2, 隊列番号, "固定値")}％］
PRINTFORML ＞被回复量提升［{付随依存変動算出(LOCAL:4, 隊列番号, "固定値")}］
PRINTFORML ＞%属性数値文字列変換(有利属性変換(LOCAL:1))%抗性提升［{付随依存変動算出(LOCAL:3, 隊列番号, "固定値")}％］
[ENDIF]
;@付随依存変動算出(判定行, 判定キャラ, 判定種別)

;--------------------------------------------------
;戦闘能力の開始時処理
;--------------------------------------------------
@固有戦闘開始時処理_キャラ_25
;戦闘開始時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM カリオストロ隊列

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

カリオストロ隊列 = キャラ隊列検索(キャラ検索("[开拓的炼金术师]卡莉奥斯特萝"))

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 备用躯体
アビ汎用文字列:バフ・デバフ対象能力 = 自动复活
アビ汎用変数:効果割合 = 50
アビ汎用変数:持続回数 = 1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体
アビテンプレート用_アビ名 = 可爱的真谛
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 可爱的真谛
アビ汎用文字列:バフ・デバフ対象能力 = 可爱的真谛
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:効果量 = 1
アビ汎用変数:効果割合 = BATTLE_STATE:カリオストロ隊列:属性
CALL アビテンプレート_有利効果_パッシブバフ効果セット

[SKIPSTART]
CALL アビ汎用変数文字列リセット
アビテンプレート用_アビ名 = 刻印バフ_カリオストロ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_カリオストロ
アビ汎用文字列:バフ・デバフ対象能力 = 刻印バフ_カリオストロ
アビ汎用変数:効果量 = 0
アビ汎用変数:効果割合 = BATTLE_STATE:カリオストロ隊列:属性
CALL アビテンプレート_有利効果_付随バフ効果セット("可爱的真谛")
[SKIPEND]

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_CT率_カリオストロ
アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
アビ汎用変数:付随量 = 5
アビ汎用文字列:参照先設定 = 付随元属性刻印
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("可爱的真谛")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_被回復_カリオストロ
アビ汎用文字列:バフ・デバフ対象能力 = 被回復量
アビ汎用変数:付随量 = 30
アビ汎用文字列:参照先設定 = 付随元属性刻印
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("可爱的真谛")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 刻印バフ_属性耐性_カリオストロ
アビ汎用文字列:バフ・デバフ対象能力 = %属性数値文字列変換(有利属性変換(BATTLE_STATE:カリオストロ隊列:属性))%耐性
アビ汎用変数:付随量 = 4
アビ汎用文字列:参照先設定 = 付随元属性刻印
アビ汎用文字列:付随先_隊列設定 = {カリオストロ隊列}
CALL アビテンプレート_有利効果_付随バフ効果セット("可爱的真谛")

IF TALENT:キャラ検索("[开拓的炼金术师]卡莉奥斯特萝"):恋慕
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:適用範囲 = 味方全体
	CALL アビ対象選択テンプレート_味方全体
	アビテンプレート用_アビ名 = 真理具象化
	アビ汎用文字列:バフ・デバフ枠 = 真理具象化
	アビ汎用文字列:バフ・デバフ対象能力 = 真理具象化
	アビ汎用変数:特殊表示オプション = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF

;----------------------------------------------------------------------------------
;『真理Lv』戦闘画面上表示処理
@探索欄表示_キャラ_25(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号
#DIM バフ・デバフ行数
バフ・デバフ行数 = バフ番号取得_枠("真理Lv", 隊列番号)
SIF バフ・デバフ行数 < 0
	RETURN

GDRAWTEXT レイヤー番号, "真理Lv", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")/30}", 200, 90

;サポアビ案
;「真理具象化」PTメンバーが奥義発動時、真理Lv+1
;「卡莉奥斯特萝是无敌哒☆」他キャラがディスペル使用時、敵に88万8888ダメージ、スロウ効果、真理Lv+1
;「ビーチスタイルだよっ☆」「世界で一番可愛いっ☆」「イ・タ・ズ・ラしちゃうぞ☆」
;		刻印の数に応じてクリティカル率UP（倍率30％/発動率5*刻印数）刻印上昇時に100％のアビリティダメージｘ3回、ダメージ上限UP（2*刻印数）
;「备用躯体」戦闘開始時に自动复活効果（50％）
;「リポルションボディ」刻印の数に応じて被回復上限上昇、ターン終了時に回復
