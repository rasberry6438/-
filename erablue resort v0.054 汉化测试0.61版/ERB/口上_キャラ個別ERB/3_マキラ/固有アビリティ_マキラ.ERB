
@固有アビ_3_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果数値２
#DIM 効果上限
#DIM 対象キャラ
#DIM 鼓レベル
#DIM 対象カウント

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 鼓舞激励＋＋
	消費ＭＰ = 60
	効果数値 = 15
	効果数値２ = 5
	効果上限 = 5
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = 鼓舞激励＋
	消費ＭＰ = 50
	効果数値 = 10
	効果数値２ = 4
	効果上限 = 4
ELSE
	アビ名 = 鼓舞激励
	消費ＭＰ = 40
	効果数値 = 5
	効果数値２ = 3
	効果上限 = 3
ENDIF

IF ダンジョン表示モード == "戦闘画面"
	戦闘中カウント:隊列算出_味方(GETCHARA(3)):0 = 効果上限

	対象カウント = 0
	FOR 対象キャラ, 0, 4
		IF バフ番号取得_枠("鼓の音", 対象キャラ) == -1
			対象カウント ++
		ENDIF
	NEXT
	SIF 対象カウント == 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}　赋予我方全体［Lv1］（独立槽・不可驱散）的鼓音效果。
		詳細文文字列受け渡し変数 += @"<br>鼓音Lv每进行５次普通攻击便提升一级，并提供各类强化效果。"
		詳細文文字列受け渡し変数 += @"<br>鼓音Lv最高可提升至{効果上限}级。"

		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}　赋予我方全体［Lv1］（独立槽・不可驱散）的鼓音效果。
		詳細文文字列受け渡し変数 += @"<br>＞赋予［鼓音Lv✕{効果数値}％］的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［鼓音Lv✕{効果数値}％］的防御力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［鼓音Lv✕{効果数値}％］的连续攻击率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>＞赋予［鼓音Lv✕{効果数値２}％］的奥义伤害提升效果。"
		詳細文文字列受け渡し変数 += @"<br>鼓音Lv每进行５次普通攻击即提升一级。（SA计为１次，DA计为２次，TA计为３次）"
		詳細文文字列受け渡し変数 += @"<br>鼓音Lv最高可提升至{効果上限}级。"
	CASE "アビ効果"
		;鼓が無い味方にLv1で張る
		対象カウント = 0
		FOR 対象キャラ, 0, 4
			IF バフ番号取得_枠("鼓の音", 対象キャラ) == -1
				CALL アビ対象選択テンプレート_指定(対象キャラ, 対象カウント)
				対象カウント ++
			ENDIF
		NEXT
		IF 対象カウント
			アビ汎用変数:特殊表示オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 鼓の音
			アビ汎用文字列:バフ・デバフ対象能力 = 鼓の音
			CALL アビテンプレート_刻印追加(1)

			;付随バフを貼る
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 鼓の音_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:付随割合 = 効果数値
			CALL アビテンプレート_有利効果_付随バフ効果セット("鼓の音")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 鼓の音_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:付随割合 = 効果数値
			CALL アビテンプレート_有利効果_付随バフ効果セット("鼓の音")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 鼓の音_連続攻撃率
			アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
			アビ汎用変数:付随割合 = 効果数値
			CALL アビテンプレート_有利効果_付随バフ効果セット("鼓の音")

			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 鼓の音_奥義ダメージ補正
			アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
			アビ汎用変数:付随割合 = 効果数値２
			CALL アビテンプレート_有利効果_付随バフ効果セット("鼓の音")
		ENDIF
ENDSELECT


@バフ・デバフ特殊表示_鼓の音(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORM 鼓音增益
PRINTFORML ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORM 鼓音Lv：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"), 18, LEFT}
PRINTFORML 普通攻击次数：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合"), 4}
PRINTFORML 持续回合：永久　　　　　　　鼓音Lv上限：{戦闘中カウント:戦闘行動キャラ:0}


@固有通常攻撃後処理_バフ_鼓の音(バフ・デバフ行数, 全滅フラグ, 連撃数)
;通常攻撃後処理関数から飛んでくる
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM バフ番号
#DIM 攻撃前数値
#DIM レベル上限

レベル上限 = 戦闘中カウント:隊列算出_味方(GETCHARA(3)):0

SIF 全滅フラグ
	RETURN 0

攻撃前数値 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
DT_CELL_SET "戦闘効果データベース", バフ・デバフ行数, "効果量_割合", 攻撃前数値 + 連撃数
IF レベル上限 <= (攻撃前数値 + 5) / 5
	;既にLv上限の場合何もしない
ELSE
	;上限でない場合、攻撃前値と攻撃後数値を比較する
	IF (攻撃前数値 + 5) / 5 == (攻撃前数値 + 連撃数 + 5) / 5
		;Lvが上がらない場合は何もしない
	ELSE
		;Lvが上がる場合はバフを貼りなおす
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(戦闘行動キャラ)

		アビ汎用文字列:バフ・デバフ対象能力 = 鼓の音
		アビ汎用変数:累積上限_固定値 = レベル上限
		CALL アビテンプレート_刻印追加(1)
	ENDIF
ENDIF

@固有アビ_3_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = 飞花落叶＋＋
	効果数値 = 250
	効果数値２ = 25
	消費ＭＰ = 30
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 飞花落叶＋
	効果数値 = 200
	効果数値２ = 15
	消費ＭＰ = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 飞花落叶
	効果数値 = 200
	効果数値２ = 10
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的自身属性伤害。
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ｂ槽・3回合）的防御力下降效果。成功率85％"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:基礎成功確率 = 85
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@固有アビ_3_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果数値２

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 花鸟风月＋＋
	消費ＭＰ = 70
	効果数値 = 30
	効果数値２ = 25
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 花鸟风月＋
	消費ＭＰ = 50
	効果数値 = 20
	効果数値２ = 15
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 花鸟风月
	消費ＭＰ = 30
	効果数値 = 20
	効果数値２ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv < 24
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方单体
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体
		ENDIF
		詳細文文字列受け渡し変数 += @"［{効果数値}％］（Ａ槽・3回合）的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（Ａ槽・3回合）的追击效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv < 24
			CALL アビ対象選択テンプレート_味方単体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_味方全体_生者のみ
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
