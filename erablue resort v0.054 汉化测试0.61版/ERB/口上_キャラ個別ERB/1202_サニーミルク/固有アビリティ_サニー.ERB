@固有アビ_1202_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 2

;効果が強くなっても名前は同じ
アビ名 = アグレッシブライト
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	消費ＭＰ = 25
	効果数値:0 = 180
	効果数値:1 = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	消費ＭＰ = 15
	効果数値:0 = 110
	効果数値:1 = 65
ELSE
	消費ＭＰ = 10
	効果数値:0 = 90
	効果数値:1 = 50
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 日符「アグレッシブライト」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に光属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>対象に3ターン暗闇［{効果数値:1}％］効果を与える。基礎成功率80％。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += "<br>味方全体の光属性攻撃を3ターン［+50％/A枠］強化する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_光属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:効果割合 = 効果数値:1
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 属性与ダメージ_光
			アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_光
			アビ汎用変数:効果量 = 50
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1202_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 3
#DIM ループ用

;効果が強くなっても名前は同じ
アビ名 = クロスディフュージョン
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	消費ＭＰ = 30
	効果数値:0 = 3
	効果数値:1 = 35
	効果数値:2 = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	消費ＭＰ = 20
	効果数値:0 = 2
	効果数値:1 = 35
	効果数値:2 = 60
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	消費ＭＰ = 15
	効果数値:0 = 1
	効果数値:1 = 20
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 光精「クロスディフュージョン」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += "<br>敵単体に自属性の［攻撃力✕130％］ダメージを"
		IF 効果数値:0 >= 2
			詳細文文字列受け渡し変数 += @"{効果数値:0}回与える。"
		ELSE
			詳細文文字列受け渡し変数 += "与える。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>対象の防御力を5ターン［{効果数値:1}％/B枠］減少させる。基礎成功率90％。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
			詳細文文字列受け渡し変数 += "<br>対象の与ダメージを5ターン［20％/B枠］減少させる。基礎成功率80％。"
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			詳細文文字列受け渡し変数 += @"<br>対象の命中率を5ターン［-20％/B枠］する。基礎成功率{効果数値:2}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 130
		FOR ループ用, 0, 効果数値:0
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値:1
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 与ダメ減少B
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 命中率B
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = 5
			アビ汎用変数:基礎成功確率 = 効果数値:2
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1202_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 4
#DIM 攻撃対象一時退避
#DIM 味方番号
#DIM 対象数

;効果が強くなっても名前は同じ
アビ名 = ブルーディフレクション
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	消費ＭＰ = 30
	効果数値:0 = 120
	効果数値:1 = 200
	効果数値:2 = 50
	効果数値:3 = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	消費ＭＰ = 25
	効果数値:0 = 100
	効果数値:1 = 150
	効果数値:2 = 30
	効果数値:3 = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	消費ＭＰ = 20
	効果数値:0 = 80
	効果数値:1 = 100
	効果数値:3 = 25
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
アビ汎用文字列:実行時メッセージ２ = 光符「ブルーディフレクション」！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>敵全体に火属性の［攻撃力✕{効果数値:0}％］ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>その後、敵単体に光属性の［攻撃力✕{効果数値:1}％］ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			詳細文文字列受け渡し変数 += @"<br>蒼い空の世界の民、および妖精の攻撃力を2ターン［+{効果数値:2}％/A枠］する。"
			詳細文文字列受け渡し変数 += @"<br>さらに連続攻撃率を2ターン［+{効果数値:3}％/A枠］する。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>蒼い空の世界の民、および妖精の連続攻撃率を2ターン［+{効果数値:3}％/A枠］する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		SIF アビテンプレート用_キャンセルフラグ
			RETURN -1
		攻撃対象一時退避 = アビテンプレート用_対象保存:0

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_火属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(攻撃対象一時退避)
		アビ汎用変数:効果割合 = 効果数値:1
		CALL アビテンプレート_ダメージ処理_光属性

		対象数 = 0
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
				CONTINUE
			SELECTCASE CSTR:(BATTLE_STATE:味方番号:キャラ登録番号):出演作品
				CASE ""
					;出典なし＝グラブルキャラとする
				CASE "东方Project"
					SIF CSTR:(BATTLE_STATE:味方番号:キャラ登録番号):特殊種族 != "妖精"
						CONTINUE
				CASEELSE
					CONTINUE
			ENDSELECT
			CALL アビ対象選択テンプレート_指定(味方番号, 対象数)
			対象数 ++
		NEXT
		IF 対象数 > 0
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
				CALL アビ汎用変数文字列リセット
				アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果量 = 効果数値:2
				アビ汎用変数:持続ターン = 2
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
			アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
			アビ汎用変数:効果量 = 効果数値:3
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@固有アビ_1202_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値, 1
#DIM バフ番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
	アビ名 = 賑やかな無力＋
	消費ＭＰ = 10
	効果数値:0 = 100
ELSE
	アビ名 = 賑やかな無力
	消費ＭＰ = 5
	効果数値:0 = 60
ENDIF

アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%は光を屈折させる程度の能力を使った！
アビ汎用文字列:実行時メッセージ２ = %アビ名%！

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		詳細文文字列受け渡し変数 += @"<br>味方全体に回避率が［+{効果数値:0}％］される効果を与える。"
		詳細文文字列受け渡し変数 += "<br>この効果は攻撃を回避するたびに効果が小さくなる。"
		詳細文文字列受け渡し変数 += "<br>自身の次の行動がターンの最後になる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 賑やかな無力
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_回避時
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		バフ番号 = バフ番号取得_枠("少女待機中", 戦闘行動キャラ)
		IF バフ番号 >= 0
			DT_CELL_SET "戦闘効果データベース", バフ番号, "持続行動回数", 2
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 少女待機中
			アビ汎用文字列:バフ・デバフ対象能力 = 少女待機中
			アビ汎用文字列:バフ・デバフ名 = 少女待機中
			アビ汎用変数:持続行動回数 = 2
			アビ汎用変数:消去不可オプション = 1
			アビ汎用変数:特殊表示オプション = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@固有バフ・デバフ処理_バフ_賑やかな無力_回避率(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

RETURN 補正前数値 + DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")

@特殊反応_回避時_賑やかな無力(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号
#DIM 効果量
#DIM CONST 減衰量 = 20

効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
IF 効果量 > 減衰量
	DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 効果量 - 減衰量
ELSE
	;持続回数1をセットしてこのあとの処理ですぐ消す
	DT_CELL_SET "戦闘効果データベース", バフ番号, "持続回数", 1
ENDIF

@バフ・デバフ特殊表示_賑やかな無力(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

PRINTFORML 回避率バフ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 効果量：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値"), 4}　　

@固有バフ・デバフ処理_バフ_少女待機中_行動速度(隊列番号, バフ・デバフ行数, 補正前数値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM 補正前数値

RETURN 1


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1202(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『光の三妖精』 □-------------------------------------------------------------------------------
KSTR:(K++) = 　ルナおよびスターが同一ターンに先に奥義を発動していると、特殊な奥義が使用できる。

CALL 口上変数表示(対象キャラ, 1)
