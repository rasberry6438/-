;アビリティメモ
;■基本設計について
;刻印アビリティを利用する。
;『分解試薬』を用いて自身の錬金術（分解）の効力を高める戦い方。所持は最大５個まで。
;『分解試薬』を消費する際、持っているモノは必ず全て消費する設計。
;■サポアビについて
;サポアビは陥落状態で倍に強化され、アビリティの使用感が大幅に向上される設計
;サポアビ１『新分解理论』：最初から解禁。毎ターン開始ごとに『分解試薬』を１つ獲得。陥落状態では２個取得。（陥落の先が実装された場合は３個取得）
;サポアビ２『三元質定理』：レベル１５で解禁。ダメージを受けると５０％の確率で『分解試薬』をひとつ得る。陥落状態では１００％化。（陥落の先が実装された場合は得られる『分解試薬』がふたつに）
;サポアビ３『哲学的結婚』：レベル３０で解禁。３０％の確率で『分解試薬』を消費しなくなる。陥落状態では７０％化。（陥落の先が実装された場合は１００％）
;■アビリティについて
;アビ１『炼金术三原质・盐』：単体連続攻撃。発動ごとに『分解試薬』をひとつ獲得。使用時『分解試薬』は消費しない。奥義ゲージも上昇する。『分解試薬』が増加するごとに攻撃回数が１回増える。５個保持中では更に例外的に１０回攻撃。モチーフは錬金術三大要素の一つ『塩』
;アビ２『炼金术三原质・硫磺』：基本的にデバフ。１個消費で防御力低下。２個消費で攻撃力低下追加。３個消費で列化＆回避率低下追加。４個消費で効力強化＋味方命中率バフ。５個で全体化＋更に効力強化＋味方命中率バフ強化。モチーフは錬金術三大要素の一つ『硫黄』
;アビ３『炼金术三原质・水银』：ランダム複数攻撃。１個消費で敵全体の中からランダム３回複数攻撃。２個消費でランダム４回攻撃。３個消費で全体化。４個消費で威力強化＋敵命中率低下デバフ。５個消費で更に威力強化＋敵命中率低下デバフ強化。モチーフは錬金術三大要素の一つ『水銀』

;----------------------------------------------------------------------------------
;アビリティ１処理
;----------------------------------------------------------------------------------	
@固有アビ_169_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 攻撃回数
#DIM 奥義ゲージ量
#DIM 分解試薬数
#DIM 効果数値

隊列番号 = キャラ隊列検索(GETCHARA(169))
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
消費ＭＰ = 35
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	アビ名 = 炼金术三原质・盐+
	奥義ゲージ量 = 分解試薬数 + 25
	効果数値 = 分解試薬数 + 105
ELSE
	アビ名 = 炼金术三原质・盐
	奥義ゲージ量 = 分解試薬数 + 15
	効果数値 = 分解試薬数 + 90
ENDIF

;分解試薬の回数分だけ攻撃回数増加処理
IF 分解試薬数 == 5
	攻撃回数 = 分解試薬数 + 5
ELSE
	攻撃回数 = 分解試薬数 + 4
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「让你见识炼金术三原质之一『盐』的本质」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『盐』是连接硫磺与水银的以太物质。若能引出其力量…」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的自身属性伤害{攻撃回数}次。
		詳細文文字列受け渡し変数 += @"<br>赋予自身［{奥義ゲージ量}］的奥义槽增加。"
		詳細文文字列受け渡し変数 += @"<br>赋予［1］份分解试剂。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		FOR LOCAL, 0, 攻撃回数
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 奥義ゲージ量
		CALL アビテンプレート_有利効果_奥義ゲージUP

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
		CALL アビテンプレート_刻印追加(1)
ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ２処理
;----------------------------------------------------------------------------------	
@固有アビ_169_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 防御低下値
#DIM 攻撃低下値
#DIM 回避低下値
#DIM 命中上昇値
#DIM 成功率
#DIM プロメティア番号
#DIM 分解試薬数
#DIM 効果ターン

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = 炼金术三原质・硫磺+
	消費ＭＰ = 50
	IF 分解試薬数 == 5
		防御低下値 = 35
		攻撃低下値 = 35
		回避低下値 = 35
		命中上昇値 = 35
		成功率 = 150
		効果ターン = 5
	ELSEIF 分解試薬数 >= 4
		防御低下値 = 25
		攻撃低下値 = 25
		回避低下値 = 25
		命中上昇値 = 25
		成功率 = 120
		効果ターン = 4
	ELSEIF 分解試薬数 >= 3
		防御低下値 = 15
		攻撃低下値 = 15
		回避低下値 = 15
		成功率 = 100
		効果ターン = 3
	ELSEIF 分解試薬数 >= 2
		防御低下値 = 15
		攻撃低下値 = 15
		成功率 = 80
		効果ターン = 3
	ELSE
		防御低下値 = 15
		成功率 = 80
		効果ターン = 3
	ENDIF
ELSE
	アビ名 = 炼金术三原质・硫磺
	消費ＭＰ = 50
	IF 分解試薬数 == 5
		防御低下値 = 25
		攻撃低下値 = 25
		回避低下値 = 25
		命中上昇値 = 25
		成功率 = 120
		効果ターン = 4
	ELSEIF 分解試薬数 >= 4
		防御低下値 = 15
		攻撃低下値 = 15
		回避低下値 = 15
		命中上昇値 = 15
		成功率 = 110
		効果ターン = 3
	ELSEIF 分解試薬数 >= 3
		防御低下値 = 10
		攻撃低下値 = 10
		回避低下値 = 10
		成功率 = 90
		効果ターン = 3
	ELSEIF 分解試薬数 >= 2
		防御低下値 = 10
		攻撃低下値 = 10
		成功率 = 80
		効果ターン = 3
	ELSE
		防御低下値 = 10
		成功率 = 80
		効果ターン = 3
	ENDIF
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「请体验炼金术三原质之一、金属之父『硫磺』的威能…」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『硫磺』属『阳』之相。用『分解』之力祛除其活性特质即可…」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 分解試薬数 >= 4
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予敌方全体［{防御低下値}％］（独立槽・{効果ターン}回合）的防御力下降效果。
			詳細文文字列受け渡し変数 += @"<br>赋予［{攻撃低下値}％］（独立槽・{効果ターン}回合）的攻击力下降效果。"
			詳細文文字列受け渡し変数 += @"<br>赋予［{回避低下値}％］（独立槽・{効果ターン}回合）的回避率下降效果。"
			詳細文文字列受け渡し変数 += @"<br>赋予我方全体［{命中上昇値}％］（独立槽・{効果ターン}回合）的命中率提升效果。"
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ELSEIF 分解試薬数 >= 3
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予敌方纵列［{防御低下値}％］（独立槽・{効果ターン}回合）的防御力下降效果。
			詳細文文字列受け渡し変数 += @"<br>赋予［{攻撃低下値}％］（独立槽・{効果ターン}回合）的攻击力下降效果。"
			詳細文文字列受け渡し変数 += @"<br>赋予［{回避低下値}％］（独立槽・{効果ターン}回合）的回避率下降效果。"
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ELSEIF 分解試薬数 >= 2
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予敌方单体［{防御低下値}％］（独立槽・{効果ターン}回合）的防御力下降效果。
			詳細文文字列受け渡し変数 += @"<br>赋予［{攻撃低下値}％］（独立槽・{効果ターン}回合）的攻击力下降效果。"
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予敌方单体［{防御低下値}％］（独立槽・{効果ターン}回合）的防御力下降效果。
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 分解試薬数 >= 4
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 回避低下値
			アビ汎用文字列:バフ・デバフ枠 = 回避率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 命中上昇値
			アビ汎用文字列:バフ・デバフ枠 = 命中率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:持続ターン = 効果ターン
			CALL アビテンプレート_有利効果_バフ効果セット
		
		ELSEIF 分解試薬数 >= 3
			;敵縦列は頭に1回のみでよい
			CALL アビ対象選択テンプレート_敵縦列
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 回避低下値
			アビ汎用文字列:バフ・デバフ枠 = 回避率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSEIF 分解試薬数 >= 2
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSE
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ENDIF

		;サポアビ３の使用判定
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			IF 陥落チェック(キャラ番号)
				;確率６０％
				IF RAND:10 < 6
					WSTR:(K++) = 特性：由于『哲学婚姻』的效果，不消耗『分解试剂』！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ELSE
				;確率３０％
				IF RAND:10 < 3
					WSTR:(K++) = 特性：由于『哲学婚姻』的效果，不消耗『分解试剂』！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ENDIF
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
			CALL アビテンプレート_刻印減少(分解試薬数)
		ENDIF

ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ３処理
;----------------------------------------------------------------------------------	
@固有アビ_169_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 命中低下値
#DIM 成功率
#DIM アビダメ倍率
#DIM アビダメ回数
#DIM L_CNT
#DIM 分解試薬数
#DIM 攻撃対象数
#DIM 効果ターン

隊列番号 = キャラ隊列検索(GETCHARA(169))
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 炼金术三原质・水银+
	消費ＭＰ = 60
	IF 分解試薬数 == 5
		アビダメ倍率 = 145
		アビダメ回数 = 8
		命中低下値 = 35
		成功率 = 120
		効果ターン = 5
	ELSEIF 分解試薬数 >= 4
		アビダメ倍率 = 145
		アビダメ回数 = 6
		命中低下値 = 25
		成功率 = 100
		効果ターン = 4
	ELSEIF 分解試薬数 >= 3
		アビダメ倍率 = 135
		アビダメ回数 = 5
	ELSEIF 分解試薬数 >= 2
		アビダメ倍率 = 125
		アビダメ回数 = 4
		攻撃対象数 = 4
	ELSE
		アビダメ倍率 = 115
		アビダメ回数 = 3
		攻撃対象数 = 3
	ENDIF
ELSE
	アビ名 = 炼金术三原质・水银
	消費ＭＰ = 45
	IF 分解試薬数 == 5
		アビダメ倍率 = 130
		アビダメ回数 = 6
		命中低下値 = 25
		成功率 = 100
		効果ターン = 4
	ELSEIF 分解試薬数 >= 4
		アビダメ倍率 = 130
		アビダメ回数 = 4
		命中低下値 = 15
		成功率 = 80
		効果ターン = 3
	ELSEIF 分解試薬数 >= 3
		アビダメ倍率 = 120
		アビダメ回数 = 4
	ELSEIF 分解試薬数 >= 2
		アビダメ倍率 = 110
		アビダメ回数 = 4
		攻撃対象数 = 4
	ELSE
		アビダメ倍率 = 100
		アビダメ回数 = 3
		攻撃対象数 = 3
	ENDIF
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「炼金术三原质之母『水银』的暴烈痛楚，请好好感受」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「属『阴』之相的『水银』。若将其如静水般的本质分解…！」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 分解試薬数 >= 4
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成［攻击力✕{アビダメ倍率}％］的自身属性伤害{アビダメ回数}次。
			詳細文文字列受け渡し変数 += @"<br>赋予［{命中低下値}％］（独立槽・{効果ターン}回合）的回避率下降效果。"
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ELSEIF 分解試薬数 >= 3
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成［攻击力✕{アビダメ倍率}％］的自身属性伤害{アビダメ回数}次。
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>随机对{攻撃対象数}个敌人造成［攻击力✕{アビダメ倍率}％］的自身属性伤害{アビダメ回数}次。
			詳細文文字列受け渡し変数 += @"<br>使用此技能时，消耗［{分解試薬数}］份分解试剂。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 分解試薬数 >= 4
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = アビダメ倍率
			FOR L_CNT, 0, アビダメ回数
				CALL アビテンプレート_ダメージ処理_自属性
			NEXT

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 命中低下値
			アビ汎用文字列:バフ・デバフ枠 = 命中率_ハイドラ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSEIF 分解試薬数 >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = アビダメ倍率
			FOR L_CNT, 0, アビダメ回数
				CALL アビテンプレート_ダメージ処理_自属性
			NEXT
		ELSE
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(アビダメ回数)
			アビ汎用変数:効果割合 = アビダメ倍率
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF

		;サポアビ３の使用判定
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			IF 陥落チェック(キャラ番号)
				;確率６０％
				IF RAND:10 < 6
					WSTR:(K++) = 特性：由于『哲学婚姻』的效果，不消耗『分解试剂』！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ELSE
				;確率３０％
				IF RAND:10 < 3
					WSTR:(K++) = 特性：由于『哲学婚姻』的效果，不消耗『分解试剂』！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ENDIF
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
			CALL アビテンプレート_刻印減少(分解試薬数)
		ENDIF

ENDSELECT

;----------------------------------------------------------------------------------
;サポアビ処理
;----------------------------------------------------------------------------------
@バフ・デバフ特殊表示_三元質定理(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM プロメティア番号

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)

PRINTFORML 支援增益：三原质定理
PRINTFORML 效果槽：三原质定理
IF 陥落チェック(プロメティア番号)
	PRINTFORML 效果：受到伤害时１００％概率获得１个『分解试剂』
ELSE
	PRINTFORML 效果：受到伤害时５０％概率获得１个『分解试剂』
ENDIF
PRINTFORML 持续回合：永久

;----------------------------------------------------------------------------------
;『分解試薬』戦闘画面上表示処理
@探索欄表示_キャラ_169(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "分解試薬:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"Ū{刻印現在数確認("分解試薬", 隊列番号)}", 200, 90

;----------------------------------------------------------------------------------
;サポアビ１『新分解理论』処理
@固有行動開始時処理_キャラ_169
#DIM プロメティア番号

プロメティア番号 = GETCHARA(169)

SELECTCASE RAND:4
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「要上了。做好觉悟吧」
CASE 1
	アビ汎用文字列:実行時メッセージ１ = 「请用身体感受崩解之力的片段」
CASE 2
	アビ汎用文字列:実行時メッセージ１ = 「分解准备……！」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「我虽无克拉莉丝那般才能…！」
ENDSELECT

アビテンプレート用_アビ名 = 新分解理论

IF 陥落チェック(プロメティア番号)
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(2)
ELSE
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(1)
ENDIF

;----------------------------------------------------------------------------------
;サポアビ２『三元質定理』処理
@固有戦闘開始時処理_キャラ_169
;戦闘開始時処理関数から呼び出し
#DIM プロメティア番号

プロメティア番号 = GETCHARA(169)

;サポアビ２呼び出し処理
IF 基礎BATTLE_STATE:プロメティア番号:Lv >= 15
	アビテンプレート用_アビ名 = 三元質定理

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 三元質定理
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF

@特殊反応_被ダメ時_三元質定理(バフ番号, 隊列番号, 被ダメ記録順)
#DIM 被ダメ記録順
#DIM 隊列番号
#DIM バフ番号
#DIM プロメティア番号

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)

SELECTCASE RAND:2
CASE 0
	KSTR:(K++) = 「以眼还眼，以牙还牙，以痛还痛！」
CASE 1
	KSTR:(K++) = 「唔…！将攻击能量的残渣用『盐』之力化合转化…！」
ENDSELECT

IF 陥落チェック(プロメティア番号)
	;陥落時、成功率１００％
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(1)
ELSE
	;陥落前、成功率５０％
	IF RAND:2 == 0
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
		CALL アビテンプレート_刻印追加(1)
	ELSE
		KSTR:(K++) = 「…！炼金术的控制系统…！」
		WSTR:(K++) = 『三原质定理』促使普罗米西娅试图生成『分解试剂』但失败了…
	ENDIF
ENDIF

;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_169(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『新分解理论』 □-----------------------------------------------------------------
IF 陥落チェック(対象キャラ)
	KSTR:(K++) = 　每回合开始时获得２个『分解试剂』
	KSTR:(K++) = 　发动技能２・３及奥义时，消耗所有『分解试剂』提升威力・效力・范围
	KSTR:(K++) = 　
ELSE
	KSTR:(K++) = 　每回合开始时获得１个『分解试剂』
	KSTR:(K++) = 　发动技能２・３及奥义时，消耗所有『分解试剂』提升威力・效力・范围
	KSTR:(K++) = 　
ENDIF

IF 基礎BATTLE_STATE:対象キャラ:Lv >= 15
	KSTR:(K++) = □ 特性：『三原质定理』 □-----------------------------------------------------------------
	IF 陥落チェック(対象キャラ)
		KSTR:(K++) = 　受到伤害时１００％概率获得１个『分解试剂』
		KSTR:(K++) = 　
	ELSE
		KSTR:(K++) = 　受到伤害时５０％概率获得１个『分解试剂』
		KSTR:(K++) = 　
	ENDIF
ENDIF

IF 基礎BATTLE_STATE:対象キャラ:Lv >= 30
	KSTR:(K++) = □ 特性：『哲学婚姻』 □-----------------------------------------------------------------
	IF 陥落チェック(対象キャラ)
		KSTR:(K++) = 　消耗『分解试剂』时６０％概率不消耗
		KSTR:(K++) = 　
	ELSE
		KSTR:(K++) = 　消耗『分解试剂』时３０％概率不消耗
		KSTR:(K++) = 　
	ENDIF
ENDIF

CALL 口上変数表示(対象キャラ, 1)
