@固有奥義_1205(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 命中補正
#DIM ループ用
#DIM 一時保存用_回避フラグ
#DIM 命中回数

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 74 && 陥落チェック(キャラ番号)
	奥義名 = アポロ捏造説
	奥義威力 = 550
	命中補正 = -20

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 「アポロ捏造説」！
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
	奥義名 = フェイクアポロ
	奥義威力 = 450
	命中補正 = -40

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 「フェイクアポロ」！
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 26
	奥義名 = 地獄の蝕
	奥義威力 = 450

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄符「地獄の蝕」！
ELSE
	奥義名 = ヘルエクリプス
	奥義威力 = 400

	アビ汎用文字列:実行時メッセージ１ = %CALLNAME:キャラ番号%のスぺルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = 獄符「ヘルエクリプス」！
ENDIF


SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵単体に自属性のダメージを
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += "3回与える。複数回命中しにくい。"
		ELSE
			詳細文文字列受け渡し変数 += "与える。"
		ENDIF
		RETURN 奥義威力
	CASE "奥義前効果"
	CASE "奥義追加効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 38 && 陥落チェック(キャラ番号)
			一時保存用_回避フラグ = アビテンプレート用_回避フラグ
			命中回数 = 0
			FOR ループ用, 0, 2
				IF アビテンプレート用_回避フラグ
					アビテンプレート用_回避フラグ = 0
				ELSE
					命中回数 ++
				ENDIF

				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果割合 = 奥義威力
				アビ汎用変数:命中率補正値 = 命中補正 * 命中回数
				アビ汎用文字列:ダメージ種類 = 奥義ダメージ
				CALL アビテンプレート_ダメージ処理_自属性
				一時保存用_回避フラグ = (一時保存用_回避フラグ || アビテンプレート用_回避フラグ)
			NEXT
			アビテンプレート用_回避フラグ = 一時保存用_回避フラグ
		ENDIF
ENDSELECT
