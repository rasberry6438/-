@スタイル変更_31(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード
;この関数はいくつ別スタイル作ってもキャラ毎に一個

SELECTCASE 動作モード
	CASE "スタイル名取得"
		VARSET RESULTS
		;RESULTS:Xに肩書を、RESULTS:X+10に限定や泳装など通称(？)を入れる
		RESULTS:0 = %NAME:対象キャラ%
		RESULTS:10 = 常规

		IF 陥落チェック(対象キャラ)
			RESULTS:1 = [摇曳的想姬]娜尔梅亚
			RESULTS:11 = 限定
		ENDIF

		;RESULTS:2 = [爽涼の斬姫]ナルメア
		;RESULTS:12 = 泳装
	CASE @"スタイル解説_%NAME:対象キャラ%"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += @"■%NAME:対象キャラ%<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "常规形态。无属性变动。暗属性。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
	CASE "スタイル解説_[摇曳的想姬]娜尔梅亚"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += "■[摇曳的想姬]娜尔梅亚<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "以原作限定版为原型设计的形态。风属性。<br>"
		TSTR:スタイル解説 += "必定发动三重攻击＋普通攻击不会提升奥义槽。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[摇曳的想姬]娜尔梅亚")
	CASE "変更後処理"
		;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
		;一人称や二人称、素質の変化などを想定、今回は何もなし
		SELECTCASE CSTR:対象キャラ:スタイル管理文字列
			CASE ""
			CASE "[摇曳的想姬]娜尔梅亚"
			;CASE RESULTS:2
			;CASE RESULTS:3
		ENDSELECT
ENDSELECT

@スタイルステータス補正_摇曳的想姬_娜尔梅亚(対象キャラ)
#DIM 対象キャラ
;関数名に[]が使えないため、しょうがなく[]排除時の文字列をキーに

装備ステータス補正保存:対象キャラ:基礎属性変更 = 属性文字列数値変換("風") + 10
装備ステータス補正保存:対象キャラ:TA率 += 1000


@固有アビ_31_10(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[摇曳的想姬]娜尔梅亚") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	;原作での＋前後の違いが不明だったのでとりあえず変化なしで
	アビ名 = 槿花泡影+
	消費ＭＰ = 80
ELSE
	アビ名 = 槿花泡影
	消費ＭＰ = 100
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>赋予自身［攻击力✕100％］（独立槽・4回合）的追击效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予（常规槽・4回合）的慧眼效果。"
		詳細文文字列受け渡し変数 += @"<br>该技能将触发［6回合］的冷却时间。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値, 2}回合）"
		詳細文文字列受け渡し変数 += @"<br>此技能的冷却时间与技能「寂寞无为」共享。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		;追撃{効果数値}4T
		アビ汎用文字列:バフ・デバフ枠 = 槿花泡影_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 慧眼
		アビ汎用文字列:バフ・デバフ対象能力 = 慧眼
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_31_11(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[摇曳的想姬]娜尔梅亚") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 寂寞无为＋
	消費ＭＰ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 寂寞无为
	消費ＭＰ = 120
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予自身（常规槽・1回合）的全体援护效果。
		詳細文文字列受け渡し変数 += @"<br>赋予［攻击力✕250％］（常规槽・5回合）的反击效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［50％］（常规槽・5回合或5次）的反射效果。该技能将触发［6回合］的冷却时间。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値, 2}回合）"
		詳細文文字列受け渡し変数 += @"<br>此技能的冷却时间与技能「槿花泡影」共享。使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 全体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 全体かばう
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 250
		アビ汎用変数:持続回数 = 5
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 反射
		アビ汎用文字列:バフ・デバフ対象能力 = 反射
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続回数 = 5
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有アビ_31_12(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[摇曳的想姬]娜尔梅亚") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 花风・薄红舞
	消費ＭＰ = 150
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		;原作は50%だけど別枠乗算なので100に補正
		詳細文文字列受け渡し変数 += @"<br>赋予自身［100％］（独立槽・1回合）的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［100％］（Ａ槽・3回合）的暴击率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［10％］（独立槽・3回合）的暴击伤害率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>该技能将触发［6回合］的冷却时间。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（剩余{判定値, 2}回合）"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 花风・薄红舞_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 花风・薄红舞_CT倍率
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:効果量 = 10
		アビ汎用変数:持続ターン = 3

		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@揺らぎの想姫固有戦闘開始時処理_キャラ
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 胡蝶泡沫装
アビ汎用文字列:バフ・デバフ対象能力 = 追撃
アビ汎用変数:効果割合 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 天司
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_通常攻撃ダメージ
アビ汎用変数:効果割合 = 20
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 通常攻撃_ゲージ上昇率
アビ汎用文字列:バフ・デバフ対象能力 = 通常攻撃_ゲージ上昇率
アビ汎用変数:効果割合 = 100
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_不利効果_パッシブデバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

@揺らぎの想姫固有戦闘不能時処理_キャラ
#DIM 対象番号
#DIM 胡蝶泡沫装

;FOR 対象番号, 0, 4
;	胡蝶泡沫装 = バフ番号取得_枠("胡蝶泡沫装", 対象番号)
;	CALL 戦闘効果削除(胡蝶泡沫装)
;NEXT

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@揺らぎの想姫口上_ステータス画面_固有特性解説(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『万里鹏翼』 □---------------------------------------------------------------
KSTR:(K++) = 　虽然普通攻击不提升能量槽，但具有以下效果：普通攻击伤害提升/
KSTR:(K++) = 　必定发动三重攻击/强化效果不可被无效化
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『蝴蝶泡沫装』 □-----------------------------------------------------------------
KSTR:(K++) = 　赋予我方全体追击效果。

CALL 口上変数表示(対象キャラ, 1)


