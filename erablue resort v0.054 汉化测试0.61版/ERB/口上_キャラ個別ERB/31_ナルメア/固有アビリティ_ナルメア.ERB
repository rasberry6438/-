@固有アビ_31_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果数値３
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
	IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
		アビ名 = 蝴蝶刃・神乐
	ELSE
		アビ名 = 蝴蝶刃・源氏
	ENDIF
	消費ＭＰ = 20
	効果数値 = 10
	効果数値２ = 70
	効果数値３ = 20
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 源氏
;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 神楽

IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
	IF RAND:2
		アビ汎用文字列:実行時メッセージ１ = 「起舞吧蝴蝶。利刃翩跹，如神乐之舞…」
	ELSE
		アビ汎用文字列:実行時メッセージ１ = 「刃锋低吟，如神乐之仪…」
	ENDIF
ELSE
	IF RAND:2
		アビ汎用文字列:実行時メッセージ１ = 「起舞吧蝴蝶。利刃翻飞，如源氏之武…」
	ELSE
		アビ汎用文字列:実行時メッセージ２ = 「刃锋诉说，如源氏之魂…」
	ENDIF
ENDIF

IF キャラ隊列検索(キャラ番号) >= 0
	SIF 現在ターン数 == 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}　　　切换为「神乐架式」。
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}　　　切换为「源氏架式」。
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>赋予自身［累积{効果数値}％上限50％］（独立槽・4回合）的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［攻击力✕{効果数値２}％］（独立槽・4回合）的追击效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値３}％］（独立槽・3回合）的防御力下降效果。"
		詳細文文字列受け渡し変数 += @"<br>若自身存在蝴蝶双舞效果时，赋予自身［10％］的奥义槽增加效果。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			戦闘中カウント:戦闘行動キャラ:0 = 1
		ELSE
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
		CALL アビ対象選択テンプレート_自己のみ
		IF バフ番号取得_枠("蝴蝶双舞" , 戦闘行動キャラ) > -1
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
			CALL アビ汎用変数文字列リセット
		ENDIF
		;追撃{効果数値２}4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		;攻撃累積{効果数値}上限50%4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:累積上限_割合 = 50
		CALL アビテンプレート_有利効果_累積バフ効果セット
		;自分の防御ダウン{効果数値３}3T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値３
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		CALL アビ汎用変数文字列リセット
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 = 現在ターン数
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_31_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIMS アビ名
#DIM 攻撃対象一時退避

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = 泡沫梦幻＋＋
	効果数値 = 400
	消費ＭＰ = 65
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 泡沫梦幻＋
	効果数値 = 400
	消費ＭＰ = 55
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 泡沫梦幻
	効果数値 = 300
	消費ＭＰ = 45
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的自身属性伤害。
		詳細文文字列受け渡し変数 += @"<br>赋予蝴蝶相克效果（4回合・不可驱散）。"
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>根据当前架式发动追加效果。"
		ELSE
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>赋予自身［200％］（独立槽・3回合）的TA率提升效果。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>赋予自身［200％］（独立槽・3回合）的DA率提升效果。"
					詳細文文字列受け渡し変数 += @"<br>赋予［50％］（独立槽・3回合）的TA率提升效果。"
				ENDIF
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>赋予［25％］（Ｂ槽・3回合）的防御力下降效果。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>赋予［15％］（Ｂ槽・3回合）的防御力下降效果。"
				ENDIF
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的自身属性伤害。
		詳細文文字列受け渡し変数 += @"<br>赋予蝴蝶相克效果（4回合・不可驱散）。"
		詳細文文字列受け渡し変数 += @"<br>（蝴蝶相克："
		詳細文文字列受け渡し変数 += @"<br>　单独存在时不产生效果的减益状态。）"
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>根据当前架式发动追加效果。"
		ELSE
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>赋予自身［200％］（独立槽・3回合）的TA率提升效果。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>赋予自身［200％］（独立槽・3回合）的DA率提升效果。"
					詳細文文字列受け渡し変数 += @"<br>赋予［50％］（独立槽・3回合）的TA率提升效果。"
				ENDIF
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
					詳細文文字列受け渡し変数 += @"<br>赋予［25％］（Ｂ槽・3回合）的防御力下降效果。"
				ELSE
					詳細文文字列受け渡し変数 += @"<br>赋予［15％］（Ｂ槽・3回合）的防御力下降效果。"
				ENDIF
			ENDIF
		ENDIF
	CASE "アビ効果"
		;自属性ダメージ
		CALL アビ対象選択テンプレート_敵単体
		攻撃対象一時退避 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		;胡蝶相剋付与
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶相剋
		アビ汎用文字列:バフ・デバフ対象能力 = 胡蝶相剋
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
		;蝴蝶双舞があれば両方、なければ構えに応じた効果
		IF バフ番号取得_枠("蝴蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア2アビ源氏処理(キャラ番号)
			CALL ナルメア2アビ神楽処理(キャラ番号,攻撃対象一時退避)
			CALL 戦闘効果削除(戦闘行動キャラ)
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			;源氏ならTA率上昇
			CALL ナルメア2アビ源氏処理(キャラ番号)
		ELSE
			;神楽なら防御ダウン
			CALL ナルメア2アビ神楽処理(キャラ番号,攻撃対象一時退避)
		ENDIF
ENDSELECT

@ナルメア2アビ源氏処理(ARG)
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			IF 基礎BATTLE_STATE:ARG:Lv >= 28
				アビ汎用文字列:バフ・デバフ枠 = 泡沫梦幻_TA率
				アビ汎用文字列:バフ・デバフ対象能力 = TA率
				アビ汎用変数:効果量 = 200
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ELSE
				アビ汎用文字列:バフ・デバフ枠 = 泡沫梦幻_DA率
				アビ汎用文字列:バフ・デバフ対象能力 = DA率
				アビ汎用変数:効果量 = 200
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
				アビ汎用文字列:バフ・デバフ枠 = 泡沫梦幻_TA率
				アビ汎用文字列:バフ・デバフ対象能力 = TA率
				アビ汎用変数:効果量 = 50
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF

@ナルメア2アビ神楽処理(ARG,デバフ対象)
#DIM デバフ対象
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(デバフ対象)
			アビ汎用文字列:バフ・デバフ枠 = 防御力B
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			IF 基礎BATTLE_STATE:ARG:Lv >= 28
				アビ汎用変数:効果割合 = 25
			ELSE
				アビ汎用変数:効果割合 = 15
			ENDIF
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット

@固有アビ_31_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 镜花水月
	消費ＭＰ = 35
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>根据当前架式发动效果。"
		ELSE
			IF	戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>对敌方单体施加［1］个增益消除。"
				詳細文文字列受け渡し変数 += @"<br>赋予［1］层减速效果。成功率80％"
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("蝴蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>当存在携带蝴蝶相克减益的敌人时，赋予自身［280％］（独立槽・1回合）的攻击力提升效果。"
				詳細文文字列受け渡し変数 += @"<br>赋予自身［50％］（Ａ槽・3回合）的暴击率提升效果。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF バフ番号取得_枠("蝴蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア3アビ源氏処理
			CALL ナルメア3アビ神楽処理
			CALL 戦闘効果削除(戦闘行動キャラ)
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			CALL ナルメア3アビ源氏処理
		ELSE
			CALL ナルメア3アビ神楽処理
		ENDIF
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 反射
		アビ汎用文字列:バフ・デバフ対象能力 = 反射
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@ナルメア3アビ源氏処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵単体
	CALL アビテンプレート_不利効果_バフ消去()
	アビ汎用変数:効果量 = 1
	アビ汎用変数:基礎成功確率 = 80
	CALL アビテンプレート_不利効果_スロウ

@ナルメア3アビ神楽処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	;アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	;アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	;アビ汎用変数:効果割合 = 20
	;アビ汎用変数:持続回数 = 1
	;CALL アビテンプレート_有利効果_バフ効果セット
	;CALL アビ汎用変数文字列リセット
	FOR LOCAL, 10, 20
		IF デバフ番号取得_枠("胡蝶相剋", LOCAL) != -1
			アビ汎用文字列:バフ・デバフ枠 = 镜花水月_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 280
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			BREAK
		ENDIF
	NEXT
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
	アビ汎用変数:効果量 = 50
	アビ汎用変数:持続ターン = 3
	CALL アビテンプレート_有利効果_バフ効果セット

@固有アビ_31_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM アビリティ使用フラグ
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = 蝴蝶双舞
	効果数値 = 60
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

アビ汎用文字列:実行時メッセージ１ = 「大智若渊，小智若隙」

SIF 現在ターン数 == アビリティ使用フラグ && アビテンプレート用_アビ封印用フラグ != 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予自身蝴蝶双舞效果。
		詳細文文字列受け渡し変数 += @"<br>（蝴蝶双舞："
		詳細文文字列受け渡し変数 += @"<br>　单独存在时不产生效果的增益状态。）"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値}％］（独立槽・1回合）的奥义伤害提升效果。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 蝴蝶双舞
		アビ汎用文字列:バフ・デバフ対象能力 = 蝴蝶双舞
		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 蝴蝶双舞_奥義
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		アビリティ使用フラグ = 現在ターン数
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有戦闘開始時処理_キャラ_31
;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:GETCHARA(31):スタイル管理文字列 == "[摇曳的想姬]娜尔梅亚"
	TRYCALL 揺らぎの想姫固有戦闘開始時処理_キャラ()
	RETURN
ENDIF

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット


@固有戦闘不能時処理_キャラ_31
;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:GETCHARA(31):スタイル管理文字列 == "[摇曳的想姬]娜尔梅亚"
	TRYCALL 揺らぎの想姫固有戦闘不能時処理_キャラ()
	RETURN
ENDIF


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_31(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:対象キャラ:スタイル管理文字列 == "[摇曳的想姬]娜尔梅亚"
	TRYCALL 揺らぎの想姫口上_ステータス画面_固有特性解説()
	RETURN
ENDIF

KSTR:(K++) = □ 特性：『质实刚健』 □---------------------------------------------------------------
KSTR:(K++) = 　强化效果不可被无效化
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『锷迫合』 □-----------------------------------------------------------------
KSTR:(K++) = 　使用「镜花水月」时，赋予自身1次反射效果

CALL 口上変数表示(対象キャラ, 1)

