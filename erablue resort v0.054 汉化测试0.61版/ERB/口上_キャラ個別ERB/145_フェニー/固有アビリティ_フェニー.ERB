
@固有アビ_145_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM バフ行数
#DIM 対象番号
#DIM 効果ターン

隊列番号 = キャラ隊列検索(GETCHARA(145))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = マスダル・ショーラ+
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 100
	ENDIF
ELSE
	アビ名 = マスダル・ショーラ
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 100
	ENDIF
ENDIF

IF 隊列番号 >= 0
	バフ・デバフ行数 = バフ番号取得_枠("神秘の光焔", 隊列番号)
	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20 && バフ・デバフ行数 > -1
		効果ターン = 6
	ELSE
		効果ターン = 3
	ENDIF
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（3ターン・消去不可）の天陽の種火効果を与える。
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、対象を味方全体に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、持続ターンを（6ターン）に変更する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（3ターン・消去不可）の天陽の種火効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞［50％］の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［30％］のTA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［10％］の通常攻撃ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［10％］のアビリティダメージ上昇効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、対象を味方全体に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、持続ターンを（6ターン）に変更する。"
		ENDIF
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20 && バフ・デバフ行数 > -1
			CALL アビ対象選択テンプレート_味方全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_自己のみ
		ENDIF
		
		アビ汎用文字列:バフ・デバフ枠 = 天陽の種火
		アビ汎用文字列:バフ・デバフ対象能力 = 天陽の種火
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 天陽の種火_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("天陽の種火")
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 天陽の種火_TA率
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 30
		CALL アビテンプレート_有利効果_付随バフ効果セット("天陽の種火")
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 天陽の種火_通常補正
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_通常攻撃ダメージ
		アビ汎用変数:効果割合 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("天陽の種火")
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 天陽の種火_アビ補正
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果割合 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("天陽の種火")
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有アビ_145_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数

隊列番号 = キャラ隊列検索(GETCHARA(145))
;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = リーシュ・ヤフタレク+
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	アビ名 = リーシュ・ヤフタレク
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 200
	ENDIF
ENDIF

SIF 隊列番号 >= 0
	バフ・デバフ行数 = バフ番号取得_枠("神秘の光焔", 隊列番号)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕150％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>味方全体に［150］のＨＰ回復を与える。"
		詳細文文字列受け渡し変数 += @"［1］のデバフ解除を与える。"
		詳細文文字列受け渡し変数 += @"<br>ターン終了時に瀕死の味方がいる時、このアビリティを自動発動する。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、効果を［攻撃力✕300％］に変更する。"
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、敵全体に［1］のバフ消去を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30 && バフ・デバフ行数 > -1
			アビ汎用変数:効果割合 = 150
		ELSE
			アビ汎用変数:効果割合 = 300
		ENDIF
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 150
		CALL アビテンプレート_回復処理_MAXでも回復

		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_有利効果_デバフ消去()
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30 && バフ・デバフ行数 > -1
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			CALL アビテンプレート_不利効果_バフ消去()
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有アビ_145_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数

隊列番号 = キャラ隊列検索(GETCHARA(145))

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ名 = シャムス・ヤハーミ+
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 300
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = シャムス・ヤハーミ
	IF 隊列番号 >= 0 && 戦闘中カウント:隊列番号:0 == 1
		消費ＭＰ = 0
	ELSE
		消費ＭＰ = 300
	ENDIF
ENDIF
バフ・デバフ行数 = バフ番号取得_枠("神秘の光焔", 隊列番号)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（1ターン・消去不可）の炎々たる燦翼効果を与える。
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、自身に［50％］（独自枠・2ターン）の与ダメージ上昇効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（1ターン・消去不可）の炎々たる燦翼効果を与える。
		詳細文文字列受け渡し変数 += @"<br>（炎々たる燦翼："
		詳細文文字列受け渡し変数 += @"<br>　全体かばう効果を含む。"
		詳細文文字列受け渡し変数 += @"<br>　被ターゲット時、攻撃してきた対象に［攻撃力✕300％］の火属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>　［25％］（独自枠・2ターン）の攻撃力減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>　［累積50最大500］（通常枠）の火傷効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 += @"<br>自身に神秘の光焔効果が存在する時、自身に［50％］（独自枠・2ターン）の与ダメージ上昇効果を与える。"
		ENDIF
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 炎々たる燦翼
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被タゲ時
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 炎々たる燦翼_全体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 全体かばう
		CALL アビテンプレート_有利効果_付随バフ効果セット("炎々たる燦翼")
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50 && バフ・デバフ行数 > -1
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 50
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 炎々たる燦翼_与ダメ大幅増加
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		IF 戦闘中カウント:戦闘行動キャラ:0 == 1
			アビテンプレート用_行動消費無しフラグ = 1
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
ENDSELECT



@固有戦闘開始時処理_キャラ_145
;戦闘開始時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 不死鳥の神秘
アビ汎用文字列:バフ・デバフ対象能力 = 不死鳥の神秘
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死鳥の神秘_自動復活
アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
アビ汎用変数:効果割合 = 100
アビ汎用変数:持続回数 = 1
CALL アビテンプレート_有利効果_付随バフ効果セット("不死鳥の神秘")

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死鳥の神秘_防御力
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:効果割合 = 50
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_不利効果_パッシブデバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 不死鳥の神秘_再生
アビ汎用文字列:バフ・デバフ対象能力 = 再生
アビ汎用変数:特殊表示オプション = -1
アビ汎用変数:効果量 = 100
CALL アビテンプレート_有利効果_付随バフ効果セット("不死鳥の神秘_防御力")


@固有戦闘不能時処理_キャラ_145
#DIM 隊列番号
#DIM 不死鳥の神秘

隊列番号 = キャラ隊列検索(GETCHARA(145))

不死鳥の神秘 = バフ番号取得_枠("不死鳥の神秘", 隊列番号)
CALL 戦闘効果削除(不死鳥の神秘)

IF 戦闘中カウント:隊列番号:1 == 0
	WSTR:(K++) = フェニーの不死鳥の神秘！

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 神秘の光焔
	アビ汎用文字列:バフ・デバフ対象能力 = 神秘の光焔
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 神秘の光焔_追撃
	アビ汎用文字列:バフ・デバフ対象能力 = 追撃
	アビ汎用変数:効果割合 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("神秘の光焔")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 神秘の光焔_回復力
	アビ汎用文字列:バフ・デバフ対象能力 = 回復力
	アビ汎用変数:効果割合 = 20
	CALL アビテンプレート_有利効果_付随バフ効果セット("神秘の光焔")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 神秘の光焔_防御力
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:効果割合 = 200
	CALL アビテンプレート_有利効果_付随バフ効果セット("神秘の光焔")

	戦闘中カウント:隊列番号:0 = 1
	戦闘中カウント:隊列番号:1 = 1
ENDIF



@固有ターン終了時処理_キャラ_145
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM フェニー
#DIM 瀕死判定

フェニー = GETCHARA(145)
隊列番号 = キャラ隊列検索(フェニー)

バフ・デバフ行数 = バフ番号取得_枠("神秘の光焔", 隊列番号)
IF バフ・デバフ行数 > -1
	IF 現在ターン数 % 5 == 0
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 基礎BATTLE_STATE:隊列番号:最大ＨＰ
		CALL アビテンプレート_回復処理_MAXでも回復
		
		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_有利効果_ウェポンバースト
	ENDIF
ENDIF

瀕死判定 = 0
FOR LOCAL, 0, 4
	IF BATTLE_STATE:LOCAL:ＨＰ < BATTLE_STATE:LOCAL:最大ＨＰ / 4 && BATTLE_STATE:LOCAL:ＨＰ > 0
		瀕死判定 = 1
		BREAK
	ENDIF
NEXT

IF 瀕死判定 == 1 && BATTLE_STATE:隊列番号:ＨＰ > 0
	WSTR:(K++) = 「それーっ！」
	WSTR:(K++) = フェニーのリーシュ・ヤフタレク！

	CALL アビ対象選択テンプレート_敵全体_生者のみ
	IF 基礎BATTLE_STATE:フェニー:Lv >= 30 && バフ・デバフ行数 > -1
		アビ汎用変数:効果割合 = 150
	ELSE
		アビ汎用変数:効果割合 = 300
	ENDIF
	CALL アビテンプレート_ダメージ処理_火属性
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果量 = 150
	CALL アビテンプレート_回復処理_MAXでも回復
	
	IF 基礎BATTLE_STATE:フェニー:Lv >= 30 && バフ・デバフ行数 > -1
		CALL アビ汎用変数文字列リセット
		CALL アビテンプレート_有利効果_デバフ消去()
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		CALL アビテンプレート_不利効果_バフ消去()
	ENDIF
ENDIF



@特殊反応_被タゲ時_炎々たる燦翼(バフ番号, 攻撃者隊列, 被タゲ記録順)
#DIM 被タゲ記録順
#DIM 攻撃者隊列
#DIM バフ番号

CALL アビ対象選択テンプレート_指定(攻撃者隊列)

アビテンプレート用_アビ名 = 炎々たる燦翼

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 300
CALL アビテンプレート_ダメージ処理_火属性

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 25
アビ汎用文字列:バフ・デバフ枠 = 攻撃力_炎々たる燦翼
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = 2
CALL アビテンプレート_不利効果_デバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 火傷
アビ汎用文字列:バフ・デバフ対象能力 = 火傷
アビ汎用変数:効果量 = 50
アビ汎用変数:累積上限_固定値 = 500
アビ汎用変数:持続ターン = -1
アビ汎用変数:基礎成功確率 = 100
CALL アビテンプレート_不利効果_累積デバフ効果セット



@バフ・デバフ特殊表示_天陽の種火(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 天陽の種火
PRINTFORML 火属性攻撃力・TA率・通常攻撃与ダメージ・アビリティ与ダメージが上昇している状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_炎々たる燦翼(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 炎々たる燦翼
PRINTFORML 全体攻撃を含む敵の攻撃を味方の代わりに受ける/被ターゲット時、対象に火属性ダメージ・攻撃力ダウン効果・火傷効果を与える状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_不死鳥の神秘(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 不死鳥の神秘
PRINTFORML 戦闘不能時、1度だけ復活/復活時、自身に神秘の光焔効果を付与する状態［消去不可］
PRINTFORML 持続ターン：永続



@バフ・デバフ特殊表示_神秘の光焔(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 神秘の光焔
PRINTFORML 通常攻撃時、追撃を行う/回復力・防御力が上昇/5の倍数ターン終了時、HPを全回復・即座に奥義発動可能/奥義・アビリティが強化された状態［消去不可］
PRINTFORML 持続ターン：永続

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_145(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『不死鳥の神秘』 □------------------------------------------------------------
KSTR:(K++) = 　防御力が非常に低いが、毎ターンHP回復/戦闘開始時に自動復活効果
KSTR:(K++) = 　自動復活時、自身に神秘の光焔効果/1度だけアビリティ発動時MP・行動回数を消費しない
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『巡り合いし幸せ』 □----------------------------------------------------------
KSTR:(K++) = 　ターン終了時に瀕死状態の味方がいる時、リーシュ・ヤフタレクが自動発動

CALL 口上変数表示(対象キャラ, 1)



