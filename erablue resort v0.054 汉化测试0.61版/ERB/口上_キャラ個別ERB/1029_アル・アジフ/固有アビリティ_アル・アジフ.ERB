
@固有アビ_1029_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = バルザイの偃月刀＋＋
	消費ＭＰ = 50
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = バルザイの偃月刀＋
	消費ＭＰ = 50
ELSE
	アビ名 = バルザイの偃月刀
	消費ＭＰ = 50
ENDIF


;下の４つの文字列に文章を入れると、アビ使用時の文章を変化させることが出来る。
;これらを使わない場合は「（キャラ名）の（アビ名）！」と表示される。
SELECTCASE RAND:3
	CASE 0
		アビ汎用文字列:実行時メッセージ１ = アル・アジフはバルザイの偃月刀を握った！
	CASE 1
		アビ汎用文字列:実行時メッセージ１ = アル・アジフはバルザイの偃月刀を呼び出した！
	CASEELSE
		アビ汎用文字列:実行時メッセージ１ = アル・アジフはバルザイの偃月刀を振るった！
ENDSELECT


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>バルザイの偃月刀を召喚する
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			詳細文文字列受け渡し変数 += @"<br>自身に［40％］（独自枠・5ターン）の追撃効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［40％］（独自枠・5ターン）の通常攻撃ダメージ上昇効果を与える。"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
			詳細文文字列受け渡し変数 += @"<br>自身に［35％］（独自枠・5ターン）の追撃効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［35％］（独自枠・5ターン）の通常攻撃ダメージ上昇効果を与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>自身に［30％］（独自枠・5ターン）の追撃効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［30％］（独自枠・5ターン）の通常攻撃ダメージ上昇効果を与える。"
		ENDIF
	CASE "アビ効果"
		;バルザイの偃月刀がレムリア・インパクトを強化してしまうのはおかしいので
		;通常攻撃強化・追撃付与にしています
		CALL アビ対象選択テンプレート_自己のみ
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
			アビ汎用変数:効果割合 = 40
			アビ汎用変数:持続ターン = 5
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
			アビ汎用変数:効果割合 = 35
			アビ汎用変数:持続ターン = 4
		ELSE
			アビ汎用変数:効果割合 = 30
			アビ汎用変数:持続ターン = 4
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 通常攻撃力_偃月刀
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_通常攻撃ダメージ
		CALL アビテンプレート_有利効果_バフ効果セット
		
		アビ汎用文字列:バフ・デバフ枠 = 追撃_偃月刀
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		CALL アビテンプレート_有利効果_バフ効果セット
		
ENDSELECT

@固有アビ_1029_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 旧き印＋
	消費ＭＰ = 40
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 旧き印
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF


;下の４つの文字列に文章を入れると、アビ使用時の文章を変化させることが出来る。
;これらを使わない場合は「（キャラ名）の（アビ名）！」と表示される。
アビ汎用文字列:実行時メッセージ１ = 宙に刻まれし歪んだ五芒星が光を放つ！
;アビ汎用文字列:実行時メッセージ２ = 
;アビ汎用文字列:実行時メッセージ３ = 
;アビ汎用文字列:実行時メッセージ４ = 


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［回復力✕100％］（通常枠）のバリア効果を与える。
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［回復力✕70％］（通常枠）のバリア効果を与える。
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
			アビ汎用変数:効果量 = 基礎BATTLE_STATE:キャラ番号:回復力
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
			アビ汎用変数:効果量 = (基礎BATTLE_STATE:キャラ番号:回復力 * 7 / 10)
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@固有アビ_1029_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = ド・マリニーの時計
	消費ＭＰ = 80
ELSE
	アビ名 = 
ENDIF


;下の４つの文字列に文章を入れると、アビ使用時の文章を変化させることが出来る。
;これらを使わない場合は「（キャラ名）の（アビ名）！」と表示される。
;アビ汎用文字列:実行時メッセージ１ = 
;アビ汎用文字列:実行時メッセージ２ = 
;アビ汎用文字列:実行時メッセージ３ = 
;アビ汎用文字列:実行時メッセージ４ = 


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>少しの間、時間を遅延させる。
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に［500％］（独自枠・1ターン）のTA率上昇効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 500
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = TA率_時計
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_1029_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名


IF 基礎BATTLE_STATE:キャラ番号:Lv >= 43
	アビ名 = クトゥグア＝イタクァ＋＋
	効果数値 = 150
	消費ＭＰ = 90
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 36
	アビ名 = クトゥグア＝イタクァ＋
	効果数値 = 120
	消費ＭＰ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = クトゥグア＝イタクァ
	効果数値 = 100
	消費ＭＰ = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE RAND:2
	CASE 0
	アビ汎用文字列:実行時メッセージ１ = 二挺拳銃が歌い、炎と氷の弾丸が乱舞する！
	CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 炎と氷、相反する二つの力が二挺拳銃から迸る！
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕{効果数値}％］の自属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>［攻撃力✕{効果数値}％］の火属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［攻撃力✕{効果数値}％］の水属性ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性
	ENDSELECT

;---------------------------

@固有バフ・デバフ処理_キャラ_1029_与ダメージ(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象の種族に外宇宙存在があるなら
IF 戦闘行動対象 >= 10 && STRFINDU(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "外宇宙存在") >= 0
	;ダメージ１．３倍
	RETURN ダメージ値 * 13 / 10
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値

@バフ・デバフ特殊表示_魔を断つ剣(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 魔を断つ剣
PRINTFORML 外宇宙存在に対して与えるダメージが1.3倍になる
PRINTFORML 持続ターン：永続

;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1029(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化
;外宇宙存在＝エイリアンやクトゥルフ系エネミーを想定。現時点（25年2月23日）ではフレーバー状態
KSTR:(K++) = □ 特性：『魔を断つ剣』 □-------------------------------------------------------------------------------
KSTR:(K++) = 外宇宙存在に対して与えるダメージが1.3倍になる
