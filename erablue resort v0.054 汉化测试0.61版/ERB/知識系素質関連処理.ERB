
@知識系素質の取得(ARG)
;話術、釣り技能もここで成長
IF ARG == MASTER
	SELECTCASE EXP:MASTER:会話経験
		CASE IS >= 5000
			IF ABL:MASTER:話術技能 <= 9
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了10级！
				ABL:MASTER:話術技能 = 10
			ENDIF
		CASE IS >= 2500
			IF ABL:MASTER:話術技能 <= 8
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了9级！
				ABL:MASTER:話術技能 = 9
			ENDIF
		CASE IS >= 1500
			IF ABL:MASTER:話術技能 <= 7
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了8级！
				ABL:MASTER:話術技能 = 8
			ENDIF
		CASE IS >= 1000
			IF ABL:MASTER:話術技能 <= 6
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了7级！
				ABL:MASTER:話術技能 = 7
			ENDIF
		CASE IS >= 750
			IF ABL:MASTER:話術技能 <= 5
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了6级！
				ABL:MASTER:話術技能 = 6
			ENDIF
		CASE IS >= 500
			IF ABL:MASTER:話術技能 <= 4
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了5级！
				ABL:MASTER:話術技能 = 5
			ENDIF
		CASE IS >= 300
			IF ABL:MASTER:話術技能 <= 3
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了4级！
				ABL:MASTER:話術技能 = 4
			ENDIF
		CASE IS >= 100
			IF ABL:MASTER:話術技能 <= 2
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了3级！
				ABL:MASTER:話術技能 = 3
			ENDIF
		CASE IS >= 30
			IF ABL:MASTER:話術技能 <= 1
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了2级！
				ABL:MASTER:話術技能 = 2
			ENDIF
		CASE IS >= 5
			IF ABL:MASTER:話術技能 <= 0
				PRINTFORML %CALLNAME:MASTER%的话术技能达到了1级！
				ABL:MASTER:話術技能 = 1
			ENDIF
	ENDSELECT
	SELECTCASE EXP:MASTER:釣り経験
		CASE IS >= 1000
			IF ABL:MASTER:釣り技能 <= 9
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了10级！
				ABL:MASTER:釣り技能 = 10
			ENDIF
		CASE IS >= 800
			IF ABL:MASTER:釣り技能 <= 8
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了9级！
				ABL:MASTER:釣り技能 = 9
			ENDIF
		CASE IS >= 650
			IF ABL:MASTER:釣り技能 <= 7
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了8级！
				ABL:MASTER:釣り技能 = 8
			ENDIF
		CASE IS >= 500
			IF ABL:MASTER:釣り技能 <= 6
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了7级！
				ABL:MASTER:釣り技能 = 7
			ENDIF
		CASE IS >= 350
			IF ABL:MASTER:釣り技能 <= 5
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了6级！
				ABL:MASTER:釣り技能 = 6
			ENDIF
		CASE IS >= 200
			IF ABL:MASTER:釣り技能 <= 4
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了5级！
				ABL:MASTER:釣り技能 = 5
			ENDIF
		CASE IS >= 100
			IF ABL:MASTER:釣り技能 <= 3
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了4级！
				ABL:MASTER:釣り技能 = 4
			ENDIF
		CASE IS >= 50
			IF ABL:MASTER:釣り技能 <= 2
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了3级！
				ABL:MASTER:釣り技能 = 3
			ENDIF
		CASE IS >= 20
			IF ABL:MASTER:釣り技能 <= 1
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了2级！
				ABL:MASTER:釣り技能 = 2
			ENDIF
		CASE IS >= 5
			IF ABL:MASTER:釣り技能 <= 0
				PRINTFORML %CALLNAME:MASTER%的钓鱼技能达到了1级！
				ABL:MASTER:釣り技能 = 1
			ENDIF
	ENDSELECT
ENDIF
;マッサージ
SELECTCASE EXP:ARG:マッサージ施術経験
	CASE IS >= 1000
		IF ABL:ARG:マッサージ技能 <= 4
			PRINTFORML %CALLNAME:ARG%的按摩技能达到了5级！
			ABL:ARG:マッサージ技能 = 5
		ENDIF
	CASE IS >= 650
		IF ABL:ARG:マッサージ技能 <= 3
			PRINTFORML %CALLNAME:ARG%的按摩技能达到了4级！
			ABL:ARG:マッサージ技能 = 4
		ENDIF
	CASE IS >= 350
		IF ABL:ARG:マッサージ技能 <= 2
			PRINTFORML %CALLNAME:ARG%的按摩技能达到了3级！
			ABL:ARG:マッサージ技能 = 3
		ENDIF
	CASE IS >= 100
		IF ABL:ARG:マッサージ技能 <= 1
			PRINTFORML %CALLNAME:ARG%的按摩技能达到了2级！
			ABL:ARG:マッサージ技能 = 2
		ENDIF
	CASE IS >= 20
		IF ABL:ARG:マッサージ技能 <= 0
			PRINTFORML %CALLNAME:ARG%的按摩技能达到了1级！
			ABL:ARG:マッサージ技能 = 1
		ENDIF
ENDSELECT



;ここから知識系素質
FOR LOCAL, 1, 100
	;知識マイナスの場合は通常成長しない
	SIF 知識素質:ARG:LOCAL < 0
		CONTINUE
	LOCAL:1 = POWER(10, 知識素質:ARG:LOCAL) * 100
	IF 知識素質:ARG:(LOCAL + 100) >= LOCAL:1
		知識素質:ARG:LOCAL += 1
		PRINTFORMW %CALLNAME:ARG%已习得能力[%ERDNAME(知識素質, LOCAL)%Lv{知識素質:ARG:LOCAL}]！
		;知識成長口上
		TRYCALLFORM KOJO_SKILLUP_知識素質_{NO:ARG}(ARG, LOCAL)
		;経験値を0に戻す
		知識素質:ARG:(LOCAL + 100) = 0
	ENDIF
NEXT

;買春経験が10を超えた場合
IF EXP:PLAYER:買春経験 >= 10 && あなた特殊能力:ルピえっち交渉術 < 1
	PRINTFORML 搭话的方式、不被讨厌的话术引导、套出渴望之词的技巧……
	PRINTFORMW 至今已让众多员工为卢比做下流事的%CALLNAME:PLAYER%，切实感受到自己已逐渐熟悉这类交涉……
	PRINTL
	PRINTFORM %CALLNAME:PLAYER%掌握了
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORM ルピえっち交渉術
	RESETCOLOR
	PRINTFORML ！
	PRINTFORMW 这样一来，即便是非员工角色，或许也能通过卢比交涉让他们答应做下流事了！
	あなた特殊能力:ルピえっち交渉術 = 1
ENDIF

;ここからあなた特殊能力の成長処理
IF ARG == MASTER
	;ハーレム素質成長処理
	SELECTCASE あなた恋慕人数()
		CASE IS >= 10
			IF あなた特殊能力:ハーレム < 4
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%领悟了后宫的极致！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(1, あなた特殊能力:ハーレム)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(1, 4)%]！
				あなた特殊能力:ハーレム = 4
			ENDIF
		CASE IS >= 5
			IF あなた特殊能力:ハーレム < 3
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%精通了后宫管理！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(1, あなた特殊能力:ハーレム)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(1, 3)%]！
				あなた特殊能力:ハーレム = 3
			ENDIF
		CASE IS >= 3
			IF あなた特殊能力:ハーレム < 2
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%掌握了后宫诀窍！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(1, あなた特殊能力:ハーレム)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(1, 2)%]！
				あなた特殊能力:ハーレム = 2
			ENDIF
	ENDSELECT

	;遊び人素質成長処理
	SELECTCASE あなたセフレ人数()
		CASE IS >= 10
			IF あなた特殊能力:遊び人 < 4
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%领悟了花花公子的极致！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(11, あなた特殊能力:遊び人)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(11, 4)%]！
				あなた特殊能力:遊び人 = 4
			ENDIF
		CASE IS >= 5
			IF あなた特殊能力:遊び人 < 3
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%精通了花花公子的技巧！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(11, あなた特殊能力:遊び人)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(11, 3)%]！
				あなた特殊能力:遊び人 = 3
			ENDIF
		CASE IS >= 3
			IF あなた特殊能力:遊び人 < 2
				DRAWLINE
				PRINTFORML %CALLNAME:MASTER%掌握了花花公子的诀窍！
				PRINTFORMW 能力[%GET_あなた特殊能力NAME(11, あなた特殊能力:遊び人)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(11, 2)%]！
				あなた特殊能力:遊び人 = 2
			ENDIF
	ENDSELECT

	;触手技能成長処理
	IF EXP:MASTER:触手操作経験 > 5000 && あなた特殊能力:触手変化術 < 5
		DRAWLINE
		PRINTFORML %CALLNAME:MASTER%彻底掌握了触手的操控！
		PRINTW 触手变化术等级提升至５级！
		あなた特殊能力:触手変化術 = 5
	ELSEIF EXP:MASTER:触手操作経験 > 2500 && あなた特殊能力:触手変化術 < 4
		DRAWLINE
		PRINTFORML %CALLNAME:MASTER%熟练掌握了触手的操控！
		PRINTW 触手变化术等级提升至４级！
		あなた特殊能力:触手変化術 = 4
	ELSEIF EXP:MASTER:触手操作経験 > 1000 && あなた特殊能力:触手変化術 < 3
		DRAWLINE
		PRINTFORML %CALLNAME:MASTER%对触手操控已颇有心得！
		PRINTW 触手变化术等级提升至３级！
		あなた特殊能力:触手変化術 = 3
	ELSEIF EXP:MASTER:触手操作経験 > 300 && あなた特殊能力:触手変化術 < 2
		DRAWLINE
		PRINTFORML %CALLNAME:MASTER%领悟了触手操控的诀窍！
		PRINTW 触手变化术等级提升至２级！
		あなた特殊能力:触手変化術 = 2
	ENDIF

	;ギャンブラー成長
	IF EXP:PLAYER:ギャンブル経験 > 50 && あなた特殊能力:ギャンブラー < 2
		DRAWLINE
		PRINTFORML %CALLNAME:MASTER%掌握了赌徒的气质！
		PRINTFORMW 能力[%GET_あなた特殊能力NAME(2, あなた特殊能力:ギャンブラー)%]获得成长，觉醒为[%GET_あなた特殊能力NAME(2, 2)%]！
		あなた特殊能力:ギャンブラー = 2
	ENDIF
ENDIF


@性知識段階表示(ARG, ARG:1, ツールチップフラグ = 0)
#FUNCTIONS
#DIM ツールチップフラグ
;ARGは性知識レベル、ARG:1はキャラ番号
;普段キャラ番号は使わないが、耳年増算出のために引っ張ってくる

SELECTCASE ARG
	CASE -2
		IF ツールチップフラグ
			RETURNF "<br>知识等级：负值<br>对性行为毫无认知、完全不了解的状态。即所谓的无知状态。<br>除非使用特殊手段，否则等级不会提升。<br>"
		ENDIF
		RETURNF "无知"
	CASE -1
		IF ツールチップフラグ
			RETURNF "<br>知识等级：负值<br>将性行为等同于动物交配的认知状态。虽知道恋爱等概念的存在，但无法真正理解。<br>除非使用特殊手段，否则等级不会提升。<br>"
		ENDIF
		RETURNF "动物认知"
	CASE 0
		IF ARG:1 > 0 && TALENT:(ARG:1):性的興味 > 0
			IF ツールチップフラグ
				RETURNF "<br>知识等级：０<br>性兴趣强烈，道听途说的知识很多但准确知识很少。<br>"
			ENDIF
			RETURNF "半懂不懂"
		ELSE
			IF ツールチップフラグ
				RETURNF "<br>知识等级：０<br>性知识贫乏，经验也稀缺的状态。<br>处女或未成年人多属此类。<br>"
			ENDIF
			RETURNF "懵懂无知"
		ENDIF
	CASE 1
		IF ツールチップフラグ
			RETURNF "<br>知识等级：１<br>具备正确性知识，拥有享受恋爱基础的状态。<br>非处女或成年人多属此类。<br>"
		ENDIF
		RETURNF "基本认知"
	CASE 2
		IF ツールチップフラグ
			RETURNF "<br>知识等级：２<br>精通核心性知识，具备详细解说能力的状态。<br>"
		ENDIF
		RETURNF "知识专家"
	CASE 3
		IF ツールチップフラグ
			RETURNF "<br>知识等级：３<br>对性相关事项近乎全知的状态。<br>"
		ENDIF
		RETURNF "身经百战"
ENDSELECT


@自動学習処理(対象キャラ)
#DIM 対象キャラ
#DIM 入手経験値
#DIM 学習対象
#DIM 経験値保存
#DIM DYNAMIC ランダム配列, 100

学習対象 = 0
経験値保存 = 0

;業務の場合は指定されてるのでそれ読んで学習処理へ
IF 現在仕事:対象キャラ:0 == 7 && TCVAR:対象キャラ:休暇中 == 0
	学習対象 = 現在仕事:対象キャラ:1
	GOTO 学習処理
ENDIF

;何を学習するかを決定
FOR LOCAL, 1, 100
	SIF ERDNAME(知識素質, LOCAL) == ""
		BREAK
	;まずはLv1の知識を優先
	IF 知識素質:対象キャラ:LOCAL == 1 && 経験値保存 < 知識素質:対象キャラ:(LOCAL + 100)
		学習対象 = LOCAL
		経験値保存 = 知識素質:対象キャラ:(LOCAL + 100)
	ENDIF
NEXT
IF 経験値保存 == 0
	;Lv1がないならLv0を対象
	FOR LOCAL, 1, 100
		SIF ERDNAME(知識素質, LOCAL) == ""
			BREAK
		IF 経験値保存 < 知識素質:対象キャラ:(LOCAL + 100)
			学習対象 = LOCAL
			経験値保存 = 知識素質:対象キャラ:(LOCAL + 100)
		ENDIF
	NEXT
ENDIF
IF 経験値保存 == 0
	;ここまで来ても０ということは全部の知識が同条件なのでランダムで算出
	VARSET ランダム配列
	LOCAL:1 = 0
	FOR LOCAL, 1, 100
		SIF ERDNAME(知識素質, LOCAL) == ""
			BREAK
		;Lv2以上は対象にならない
		IF 知識素質:対象キャラ:LOCAL < 2
			ランダム配列:(LOCAL:1) = LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
	IF LOCAL:1 == 0
		;それでも対象がないということは知識全部２以上なので戻す
		RETURN 0
	ELSE
		学習対象 = ランダム配列:(RAND:(LOCAL:1))
	ENDIF
ENDIF

$学習処理
IF STRFIND(学校利用者:学習対象, @"_{対象キャラ}_") < 0 && STRFIND(学校利用者:学習対象, @"{対象キャラ}_") != 0
	学校利用者:学習対象 += @"{対象キャラ}_"
	学校利用者:0 = 表示
ENDIF
;学習対象が決定したら入手経験値を決定
;既存の知識が多いほど経験値が減る
LOCAL:1 = 0
FOR LOCAL, 1, 100
	SIF ERDNAME(知識素質, LOCAL) == ""
		BREAK
	;種類だけカウント
	IF 知識素質:対象キャラ:LOCAL
		LOCAL:1 ++
	ENDIF
NEXT

入手経験値 = MAX(1, 27 - (LOCAL:1 * 3 / 2))
SELECTCASE TALENT:対象キャラ:習得速度
	CASE -1
		入手経験値 -= 5
	CASE 1
		入手経験値 += 5
	CASE 2
		入手経験値 += 25
ENDSELECT
;教師がいる場合
IF 教師レベル保存:学習対象 > 0
	入手経験値 += 教師レベル保存:学習対象
ENDIF

知識素質:対象キャラ:(学習対象 + 100) += MAX(1, 入手経験値)
