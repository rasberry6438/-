@服ERB生成処理
#DIM 服構造保存, 30
#DIM 表示番号, 20
#DIMS 服名保存
#DIM 服種別設定保存
#DIM 入手手段設定保存
#DIM 編集モード
#DIMS 説明文章, 10
#DIMS 出典元
#DIM 販売価格 = 10000
#DIMS 連動衣装
#DIMS CONST 服種別 = "汎用衣装", "エロ衣装", "条件付き汎用", "条件付きエロ"
#DIMS CONST 入手手段 = "別途設定", "初期制服", "購入", "他の衣装に連動"

LOCALS = 
;左ウインドウ表示
LOCALS += "<div rect='81,375,1937,2812' padding='75' border='31' bcolor='#C0C0C0'>"
LOCALS += "<button value='100'>[100] 设定名称</button><br>"
LOCALS += "<button value='101'>[101] 设定服装类别</button><br>"
LOCALS += "<button value='102'>[102] 设定获取方式</button><br>"
IF 入手手段設定保存 == 2
	LOCALS += "<button value='103'>[103] 设定售价</button><br>"
ELSE
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[103] 设定售价</font><br>"
ENDIF
IF 入手手段設定保存 >= 2
	LOCALS += "<button value='104'>[104] 设定联动服装</button><br>"
ELSE
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[104] 设定联动服装</font><br>"
ENDIF
LOCALS += "<button value='105'>[105] 设定出处</button><br>"
LOCALS += "<button value='20'>[ 20] 编辑说明文</button><br>"
LOCALS += "<br>"
IF 服名保存 == ""
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 无法生成（未设定名称）</font><br>"
ELSEIF MAXARRAY(服構造保存) < 1
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 无法生成（全裸状态）</font><br>"
ELSEIF ENUMFUNCENDSWITH(@"CLOTHES_CHANGE_%服名保存%") > 0
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[998] 无法生成（存在同名服装）</font><br>"
ELSE
	LOCALS += "<button value='998'>[998] 以此设定生成</button><br>"
ENDIF
LOCALS += "<button value='999'>[999] 不生成并返回</button><br>"
LOCALS += "</div>"

;右ウインドウ表示
LOCALS += "<div rect='2050,375,3875,2812' padding='75' border='31' bcolor='#C0C0C0'>"
LOCALS += @"%"■服名称", 24, LEFT%："
IF 服名保存 == ""
	LOCALS += "（未设定）　　　　　　　　　　"
ELSE
	LOCALS += @"%服名保存, 30, LEFT%"
ENDIF
LOCALS += "　　<button value='100'>[变更]</button><br>"
LOCALS += @"%"■服種別設定", 24, LEFT%：%服種別:服種別設定保存, 30, LEFT%"
LOCALS += "　　<button value='101'>[变更]</button><br>"

LOCALS += @"%"■入手手段", 24, LEFT%：%入手手段:入手手段設定保存, 30, LEFT%"
LOCALS += "　　<button value='102'>[变更]</button><br>"
LOCALS += "-------------------------------------------------------------------------<br>"

SELECTCASE 編集モード
	CASE 0
		LOCALS += "■上半身设定<br>"
		VARSET 表示番号
		表示番号 = 1,2,3,4,5,6,7,8,9,10,19,20,21
	CASE 1
		LOCALS += "■下半身设定<br>"
		VARSET 表示番号
		表示番号 = 11,12,13,14,15,16,17,18,19,20,22
	CASE 2
		LOCALS += "■销售・解说设定<br>"
		VARSET 表示番号
		LOCALS += @"<button value='103'>%"　販売価格", 24, LEFT%：<font color='#\@ 入手手段設定保存 != 2 ? 808080 # C0C0C0 \@'> {販売価格, 8, LEFT}</font>ルピ</button><br>"
		LOCALS += @"<button value='104'>%"　連動衣装", 24, LEFT%：<font color='#\@ !STRLENS(連動衣装) ? 808080'>（なし）# C0C0C0'>%連動衣装% \@</font></button><br>"
		LOCALS += @"<button value='105'>%"　出典元", 24, LEFT%：<font color='#\@ !STRLENS(出典元) ? 808080'>（なし）# C0C0C0'>%出典元% \@</font></button><br>"
		LOCALS += @"%"　説明文", 24, LEFT%<br>"
		FOR LOCAL, 0, VARSIZE("説明文章")
			LOCALS += @"<button value='{20 + LOCAL}'>[{20 + LOCAL}] "
			IF 説明文章:LOCAL == ""
				LOCALS += @"<font color='#808080'>\@ LOCAL ? （行を増やす） # （未设定） \@</font></button><br>"
				BREAK
			ELSE
				LOCALS += @"%説明文章:LOCAL%</button><br>"
			ENDIF
		NEXT
ENDSELECT

FOR LOCAL, 1, 23
	SIF !MATCH(表示番号, LOCAL)
		CONTINUE
	IF EQUIPNAME_TIPS(LOCAL) != ""
		LOCALS += @"<nonbutton title='%EQUIPNAME_TIPS(LOCAL)%'>%EQUIPNAME:LOCAL%"
		LOCALS += @"<font color='#808080'>%"(?)", 24 - STRLENS(EQUIPNAME:LOCAL), LEFT%</font>"
		LOCALS += "：</nonbutton>"
	ELSE
		LOCALS += @"%EQUIPNAME:LOCAL, 24, LEFT%："
	ENDIF
	IF LOCAL == 9
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[不穿胸罩]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[穿着胸罩]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 2}'>[乳贴]</button></font>"
	ELSEIF LOCAL == 17
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[不穿内裤]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[穿着内裤]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 2 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 2}'>[阴贴]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 3 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 3}'>[兜裆布]</button></font>"
	ELSE
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 0 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 0}'>[无]</button></font>"
		LOCALS += @"<font color='#\@ 服構造保存:LOCAL == 1 ? C0C0C0 # 333333\@'><button value='{1000 + LOCAL * 10 + 1}'>[有]</button></font>"
	ENDIF
	LOCALS += "<br>"
NEXT
LOCALS += "</div>"
LOCALS += "<div rect='2250,2975,3875,2812'>"
FOR LOCAL, 0, 3
	LOCALS += @"<font color='#\@ 編集モード == LOCAL ? 666666'> # C0C0C0'><button value='{300 + LOCAL}'> \@[{300 + LOCAL}] "
	SELECTCASE LOCAL
		CASE 0
			LOCALS += "上半身設定へ"
		CASE 1
			LOCALS += "下半身設定へ"
		CASE 2
			LOCALS += "販売・解説設定へ"
	ENDSELECT
	LOCALS += @"</font>\@ 編集モード == LOCAL ? # </button> \@　　"
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS
FOR LOCAL, 0, 28
	PRINTL
NEXT

BINPUT
SELECTCASE RESULT
	CASE 100
		DRAWLINE
		PRINTL 请输入服装名称。
		PRINTBUTTON "[不修改并返回]", "変更せず戻る"
		PRINTL
		DRAWLINE
		INPUTS
		IF RESULTS != "変更せず戻る"
			服名保存 = %RESULTS%
			PRINTFORMW 已将服装名称变更为「%服名保存%」。
		ENDIF
	CASE 101
		DRAWLINE
		PRINTL 请设定服装类别。
		DRAWLINE
		FOR LOCAL, 0, VARSIZE("服種別")
			IF 服種別設定保存 == LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAINFORM [{LOCAL}] %服種別:LOCAL%
				RESETCOLOR
				PRINTL
			ELSE
				PRINTBUTTON @"[{LOCAL}] %服種別:LOCAL%", LOCAL
				PRINTL
			ENDIF
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 返回", 999
		PRINTL
		BINPUT
		IF RESULT != 999
			服種別設定保存 = RESULT
			PRINTFORMW 已将服装类别变更为「%服種別:RESULT%」。
			SIF RESULT >= 2
				PRINTFORMW ERB输出后，请重新填写穿着条件
		ENDIF
	CASE 102
		DRAWLINE
		PRINTL 请设定服装获取方式。
		DRAWLINE
		FOR LOCAL, 0, VARSIZE("入手手段")
			IF 入手手段設定保存 == LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAINFORM [{LOCAL}] %入手手段:LOCAL%
				RESETCOLOR
				PRINTL
			ELSE
				PRINTBUTTON @"[{LOCAL}] %入手手段:LOCAL%", LOCAL
				PRINTL
			ENDIF
		NEXT
		PRINTL
		PRINTBUTTON @"[999] 返回", 999
		PRINTL
		BINPUT
		IF RESULT != 999
			入手手段設定保存 = RESULT
			PRINTFORMW 已将获取方式变更为「%入手手段:RESULT%」。
			SIF !入手手段設定保存
				PRINTFORMW 请注意若不设定任何获取方式，该服装将不会在游戏中出现。
		ENDIF
	CASE 103
		SETTEXTBOX TOSTR(販売価格)
		PRINTL 请输入销售价格
		INPUT
		販売価格 = RESULT
	CASE 104
		SETTEXTBOX 連動衣装
		PRINTL 请输入需要联动的服装名称
		IF 入手手段設定保存 == 2
			PRINTL （持有对应服装时可解锁购买）
		ELSE
			PRINTL （若对应服装被设定为初始制服时将自动可用
			PRINTL 　若联动服装本身具有获取方式，请在该服装的
			PRINTFORML 　『CALL \@ 服種別設定保存 % 2 ? 情趣服装 # 制服 \@登録処理("%服名保存%")』字段后追加说明）
			PRINTL 　
		ENDIF
		INPUTS
		LOCALS:1 '= RESULTS
		IF RESULTS != "" && ENUMFUNCENDSWITH(@"CLOTHES_CHANGE_%RESULTS%") < 1
			CALL CHOICE("その名前の衣装はまだ存在しませんが、よろしいですか？", "确定", "取消")
			SIF RESULT
				RESTART
		ENDIF
		連動衣装 '= LOCALS:1
	CASE 105
		SETTEXTBOX 出典元
		PRINTL 请输入服装出处
		INPUTS
		出典元 '= RESULTS
	CASE 20 TO 29
		編集モード = 2
		LOCAL = RESULT - 20
		SETTEXTBOX 説明文章:LOCAL
		PRINTL 请输入说明文
		INPUTS
		説明文章:LOCAL '= RESULTS
	CASE 300 TO 302
		編集モード = RESULT - 300
	CASE 999
		CALL CHOICE("要退出服装ERB编辑器吗？（正在编辑的数据将会丢失）", "确定", "取消")
		SIF RESULT
			RESTART
		GOTO 終了処理
	CASE 998
		CALL CHOICE("是否要输出服装ERB并退出？", "确定", "取消")
		SIF RESULT
			RESTART
		;ERBファイル生成
		CLEARLINE LINECOUNT
		;記述データPRINT

		IF 入手手段設定保存 == 1 || 入手手段設定保存 == 3
			PRINTFORML @CLOTHES_初期所持関数_%服名保存%()
			IF 入手手段設定保存 == 3 && 連動衣装 != ""
				PRINTFORML SIF STRCOUNT(所持制服文字列, @"汎用/%連動衣装%_") || STRCOUNT(所持エロ衣装文字列, @"エロ/%連動衣装%_")
			ENDIF
			PRINTFORML CALL \@ 服種別設定保存 % 2 ? エロ衣装 # 制服 \@登録処理("%服名保存%")
			HTML_PRINT "<br>"
		ELSEIF 入手手段設定保存 == 2 || 説明文章:0 != ""
			PRINTFORML @CLOTHES_販売説明_%服名保存%()
			PRINTL #DIMS 説明文章
			PRINTL #DIMS 出典元
			PRINTL #DIM 販売価格
			PRINTL #DIMS 販売分類
			HTML_PRINT "<br>"
			PRINTL ;単位はルピ
			PRINTFORML 販売価格 = { 入手手段設定保存 == 2 ? 販売価格 # 0 }
			PRINTL ;通常衣装、もしくはエロ衣装のどちらか
			PRINTFORML 販売分類 = \@ 服種別設定保存 % 2 ? エロ衣装 # 通常衣装 \@
			PRINTL ;販売させない服はこれに1を入れる
			IF 連動衣装 != ""
				PRINTFORML ;%連動衣装%を入手後に販売開始
				PRINTFORML 販売禁止フラグ = !(STRCOUNT(所持制服文字列, @"汎用/%連動衣装%_") + STRCOUNT(所持エロ衣装文字列, @"エロ/%連動衣装%_"))
			ELSE
				PRINTL 販売禁止フラグ = 0
			ENDIF
			HTML_PRINT "<br>"
			PRINTL 説明文章 = 
			FOR LOCAL, 0, VARSIZE("説明文章")
				IF 説明文章:LOCAL == ""
					BREAK
				ELSE
					PRINTFORML 説明文章 += "%説明文章:LOCAL%<br>"
				ENDIF
			NEXT
			HTML_PRINT "<br>"
			IF 出典元 != ""
				PRINTL ;若存在引用来源请注明出处
				PRINTFORML 出典元 = %出典元%
				HTML_PRINT "<br>"
			ENDIF
			PRINTL RESULTS = %説明文章%
			PRINTL RESULTS:1 = %出典元%
			PRINTL RESULTS:2 = %販売分類%
			HTML_PRINT "<br>"
			PRINTL RETURN 販売価格
			HTML_PRINT "<br>"
		ENDIF
		PRINTFORML @CLOTHES_CHANGE_%服名保存%(対象キャラ)
		PRINTL #DIM 対象キャラ
		HTML_PRINT "<br>"
		PRINTL ;保留前一套服装
		PRINTFORML SIF CSTR:対象キャラ:着せ替え服 != "%服名保存%"
		PRINTL 	CSTR:対象キャラ:脱ぐ前の服 = %CSTR:対象キャラ:着せ替え服%
		HTML_PRINT "<br>"

		PRINTFORML CSTR:対象キャラ:着せ替え服 = %服名保存%
		PRINTFORML CSTR:対象キャラ:着せ替え服追加名 = 
		FOR LOCAL, 1, 23
			PRINTFORML TEQUIP:対象キャラ:%EQUIPNAME:LOCAL% = {服構造保存:LOCAL}
		NEXT
		HTML_PRINT "<br>"
		IF 服種別設定保存 >= 2
			PRINTFORML @CLOTHES_着用条件_%服名保存%(対象キャラ)
			PRINTL #DIM 対象キャラ
			HTML_PRINT "<br>"
			PRINTL ;不满足条件时NG
			PRINTL ;SIF NG条件
			PRINTL ;	RETURN 0
			HTML_PRINT "<br>"
			PRINTL ;满足条件时OK
			PRINTL ;SIF OK条件
			PRINTL ;	RETURN 1
			HTML_PRINT "<br>"
			PRINTL ;通过条件审查的角色视为OK
			PRINTL RETURN 1
			HTML_PRINT "<br>"
		ENDIF
		HTML_PRINT "<br>"

		OUTPUTLOG @"ERB_EDITFILE/CLOTHES_%服名保存%.ERB", 1
		CLEARLINE LINECOUNT
		PRINTL 已输出至ERB_EDITFILE文件夹。请移动至ERB文件夹后重新启动程序
		WAIT
		GOTO 終了処理
	CASEELSE
		服構造保存:((RESULT - 1000) / 10) = RESULT % 10
ENDSELECT

RESTART

$終了処理
VARSET 服構造保存
VARSET 説明文章
服名保存 = 
服種別設定保存 = 0
入手手段設定保存 = 0
編集モード = 0
出典元 = 
販売価格 = 10000
連動衣装 = 
RETURN 0
