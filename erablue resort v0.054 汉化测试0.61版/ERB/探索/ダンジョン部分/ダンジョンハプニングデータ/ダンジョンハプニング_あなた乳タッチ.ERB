@ダンジョンハプニング_あなた乳タッチ(対象キャラ番号)
#DIM 対象キャラ番号

WSTR:(K++) = %CALLNAME:対象キャラ番号%被障碍物绊倒！
KSTR:(K++) = 所幸%CALLNAME:PLAYER%及时搀扶，
SELECTCASE TALENT:PLAYER:バストサイズ
	CASE IS <= 0
		KSTR:(K++) = 惯性作用下手碰到了平坦的胸口。_PAGE
	CASEELSE
		KSTR:(K++) = 惯性作用下将丰满的胸部抓了个正着。_PAGE
ENDSELECT

CALL メッセージ欄表示用関数()
CALL 口上変数初期化

CALL セクハラ反応タイプ判定(対象キャラ番号)
TRYCALLFORM KOJO_ハプニング_あなた乳タッチ_{NO:対象キャラ番号}(対象キャラ番号)
IF 口上有無確認(0, 1) == 0
	SELECTCASE RFLAG:コマンド結果受渡し変数２
		CASE 1
			;無知
			KSTR:(K++) = %CALLNAME:対象キャラ番号%道谢后若无其事地继续探索。
		CASE 2
			;無反応
			KSTR:(K++) = %CALLNAME:対象キャラ番号%若无其事地继续探索。
		CASE 6
			;羞恥
			IF TALENT:対象キャラ番号:性別 == TALENT:PLAYER:性別 || TALENT:PLAYER:性別 == 0
				;同性、あるいは無性なら大した反応はしない
				KSTR:(K++) = %CALLNAME:対象キャラ番号%道谢并致歉后继续探索。
			ELSE
				SELECTCASE RFLAG:コマンド結果受渡し変数
					CASE 1
						;拒絶
						KSTR:(K++) = %CALLNAME:対象キャラ番号%触电般弹开，反复鞠躬道歉…
					CASE 2
						;受入れ
						KSTR:(K++) = %CALLNAME:PLAYER%关切询问%CALLNAME:対象キャラ番号%是否受伤， 
						KSTR:(K++) = %CALLNAME:対象キャラ番号%面红耳赤地九十度鞠躬致歉。
					CASE 3
						;喜び
						KSTR:(K++) = %CALLNAME:PLAYER%关切询问%CALLNAME:対象キャラ番号%是否受伤， 
						KSTR:(K++) = %CALLNAME:対象キャラ番号%红着脸将感谢与歉意糅合成含糊的嘟囔。
				ENDSELECT
			ENDIF
		CASE 8
			;汎用
			IF TALENT:対象キャラ番号:性別 == TALENT:PLAYER:性別 || TALENT:PLAYER:性別 == 0
				;同性、あるいは無性なら大した反応はしない
				KSTR:(K++) = %CALLNAME:対象キャラ番号%道谢后若无其事地继续探索。
			ELSE
				SELECTCASE RFLAG:コマンド結果受渡し変数
					CASE 1
						;拒絶
						KSTR:(K++) = %CALLNAME:対象キャラ番号%后退两步整理仪容，生硬地道谢。
					CASE 2
						;受入れ
						KSTR:(K++) = %CALLNAME:PLAYER%关切询问%CALLNAME:対象キャラ番号%是否受伤， 
						KSTR:(K++) = %CALLNAME:対象キャラ番号%摸着发烫的耳尖表示无碍。
					CASE 3
						;喜び
						KSTR:(K++) = %CALLNAME:PLAYER%关切询问%CALLNAME:対象キャラ番号%是否受伤， 
						KSTR:(K++) = %CALLNAME:対象キャラ番号%借着整理装备掩饰嘴角笑意。
				ENDSELECT
			ENDIF
	ENDSELECT
	CALL メッセージ欄表示用関数()
ENDIF

;ソース増減部分
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 1
		;拒絶
		CALL SOURCE_CALC_反感(対象キャラ番号, PLAYER, 50)
		CALL SOURCE_CALC_接触(対象キャラ番号, PLAYER, 100)
		CALL SOURCE_CALC_露出(PLAYER, 対象キャラ番号, 100)
	CASE 2
		;受け入れ
		CALL SOURCE_CALC_歓楽(対象キャラ番号, PLAYER, 20)
		CALL SOURCE_CALC_接触(対象キャラ番号, PLAYER, 100)
		CALL SOURCE_CALC_露出(PLAYER, 対象キャラ番号, 100)
		CALL SOURCE_CALC_好感度要素_性愛度UP(対象キャラ番号, PLAYER, 50)
		CALL SOURCE_CALC_快Ｂ(PLAYER, 対象キャラ番号, 10)
	CASE 3
		;喜び
		CALL SOURCE_CALC_歓楽(対象キャラ番号, PLAYER, 50)
		CALL SOURCE_CALC_接触(対象キャラ番号, PLAYER, 100)
		CALL SOURCE_CALC_露出(PLAYER, 対象キャラ番号, 100)
		CALL SOURCE_CALC_好感度要素_性愛度UP(対象キャラ番号, PLAYER, 100)
		CALL SOURCE_CALC_快Ｂ(PLAYER, 対象キャラ番号, 10)
ENDSELECT



@ダンジョンハプニング_ABLE_あなた乳タッチ(判定キャラ番号)
#DIM 判定キャラ番号

SIF TALENT:PLAYER:性別 == 2
	RETURN 0

RETURN 1
