;-------------------------------------------------
;定住の誘い
;-------------------------------------------------
@COMNAME605
#FUNCTIONS
TSTR:コマンド名受渡 = 同居邀请

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_605
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_605
LOCALS = <br>■同居邀请<br>
LOCALS += "邀请对方在自宅同居的指令。<br>"
LOCALS += @"成功时将在%CALLNAME:MASTER%的自宅定居。<br>"
LOCALS += "<br>指令类型：日常互动<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM605
#DIM 部屋上限
DRAWLINE
PRINTFORML 要邀请%CALLNAME:TARGET%与%CALLNAME:MASTER%同居吗？



PRINTBUTTON "[0]确定", 0
PRINTL
PRINTBUTTON "[1]取消", 1
PRINTL
$INPUT_LOOP
BINPUT

SELECTCASE RESULT
	CASE 0
		;恋慕度80or好感度100必要
		LOCAL = 1
		IF TALENT:PLAYER:性別 == 2 && TALENT:TARGET:性別 != 2 && TALENT:TARGET:性別 != 0
			SIF CFLAG:TARGET:恋慕レベル < 関係閾値:4 && CFLAG:TARGET:好感度レベル < 関係閾値:5
				LOCAL = 0
		ELSEIF TALENT:PLAYER:性別 != 2 && TALENT:TARGET:性別 == 2
			SIF CFLAG:TARGET:恋慕レベル < 関係閾値:4 && CFLAG:TARGET:好感度レベル < 関係閾値:5
				LOCAL = 0
		ELSE
			;同一性別なら好感度でOK
			SIF CFLAG:TARGET:好感度レベル < 関係閾値:4
				LOCAL = 0
		ENDIF
		IF LOCAL == 0
			RFLAG:コマンド結果受渡し変数 = 0
		ELSE
			RFLAG:コマンド結果受渡し変数 = 1
			CFLAG:TARGET:滞在期間 = 999
			CFLAG:TARGET:長期雇用 = 0
			;部屋から削除
			IF TALENT:TARGET:定住者
				CALL 滞在者部屋割り配列削除処理(TARGET)
			ELSEIF TALENT:TARGET:従業員
				FOR LOCAL, 0, 従業員部屋数
					IF 従業員部屋割り配列:LOCAL == TARGET
						従業員部屋割り配列:LOCAL = 0
						BREAK
					ENDIF
				NEXT
			ELSE
				CALL 滞在者部屋割り配列削除処理(TARGET)
			ENDIF
			;個人部屋に登録
			IF 施設改造度:20:0 == 1
				部屋上限 = 2
			ELSEIF 施設改造度:20:0 == 2
				部屋上限 = 6
			ENDIF
			FOR LOCAL, 0, 部屋上限
				IF 同棲部屋割り配列:LOCAL == 0
					同棲部屋割り配列:LOCAL = TARGET
					BREAK
				ENDIF
			NEXT
			TALENT:TARGET:定住者 = 2
		ENDIF
	CASE 1
		RETURN -1
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE605
#DIM ループ用
#DIM 部屋上限

;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(605)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;自宅がまだないと出ない
SIF 施設改造度:20:0 < 1
	RETURN 0
;既に同棲だと無し
SIF TALENT:TARGET:定住者 == 2
	RETURN 0
;成功するのは80以上だが、話題に出すのに陥落条件
IF TALENT:PLAYER:性別 == 2 && TALENT:TARGET:性別 != 2 && TALENT:TARGET:性別 != 0
	SIF 陥落チェック(TARGET) == 0
		RETURN 0
ELSEIF TALENT:PLAYER:性別 != 2 && TALENT:TARGET:性別 == 2
	SIF 陥落チェック(TARGET) == 0
		RETURN 0
ELSE
	;同一性別なら好感度でOK
	SIF CFLAG:TARGET:好感度レベル < 関係閾値:3
		RETURN 0
ENDIF
;部屋が一杯
IF 施設改造度:20:0 == 1
	部屋上限 = 2
ELSEIF 施設改造度:20:0 == 2
	部屋上限 = 6
ENDIF
RESULT = 0
FOR ループ用, 0, 部屋上限
	IF 同棲部屋割り配列:ループ用 == 0
		RESULT = 1
		BREAK
	ENDIF
NEXT
SIF RESULT == 0
	RETURN 0

RETURN 1

;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM605

IF RFLAG:コマンド結果受渡し変数
	;成功
	PRINTFORML %CALLNAME:TARGET%接受了%CALLNAME:PLAYER%的邀请，开始在%CALLNAME:MASTER%的自宅同居！
ELSE
	;失敗
	PRINTFORML %CALLNAME:TARGET%似乎没有与%CALLNAME:MASTER%同居的意愿……
ENDIF



