
@敵BATTLE_STATE_Lv20_格拉辛(対象エネミー)
#DIM 対象エネミー
敵BATTLE_STATE:対象エネミー:Lv = 20
敵BATTLE_STATE:対象エネミー:最大ＨＰ = 27000
敵BATTLE_STATE:対象エネミー:ＨＰ = 27000
敵BATTLE_STATE:対象エネミー:攻撃力 = 750
敵BATTLE_STATE:対象エネミー:防御力 = 780
敵BATTLE_STATE:対象エネミー:回復力 = 0
敵BATTLE_STATE:対象エネミー:行動速度 = 675
敵BATTLE_STATE:対象エネミー:DA率 = 50
敵BATTLE_STATE:対象エネミー:TA率 = 0
敵BATTLE_STATE:対象エネミー:命中率 = 110
敵BATTLE_STATE:対象エネミー:回避率 = 5
敵BATTLE_STATE:対象エネミー:属性 = 5
敵BATTLE_STATE:対象エネミー:所持EXP = 2200
敵BATTLE_STATE:対象エネミー:所持ルピ = 2850
敵BATTLE_STATE:対象エネミー:最大チャージターン = 3

敵BATTLE_STATE:対象エネミー:ボスフラグ = 1

敵BATTLE_STATE_STR:対象エネミー:種族 = アンデッド

@敵討伐後処理_Lv20_格拉辛(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")

ドロップ確率 = 30 + トレハンLv * 2
ドロップアイテム = 白银古纹
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 + トレハンLv * 2
ドロップアイテム = 七星剑碎片
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

@敵トリガー行動_Lv20_格拉辛

IF ダンジョン名 == "深海の古戦場跡" && GETBIT(ダンジョン構造配列_深海の古戦場跡:55:7, 4) == 0 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	IF バフ番号取得_枠("大技待機", 戦闘行動キャラ) < 0 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
		;イベント戦闘時、初回ターンに問答無用で大技待機
		アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的周围聚集着怨念……！！
		アビ汎用文字列:実行時メッセージ２ = _COLOR_%カラーパレット_HTML("黄")%_这是极其危险技能的征兆_！　若不防御，可能会被一击全灭！
		アビテンプレート用_アビ名 = 大技待機

		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:特殊表示オプション = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 大技待機
		アビ汎用文字列:バフ・デバフ対象能力 = 大技待機
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット

		;HPトリガー使い終わったのでフラグを立てる
		SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
		RETURN 1
	ELSEIF バフ番号取得_枠("大技待機", 戦闘行動キャラ) > -1 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
		;大技発動
		IF GETBIT(ダンジョン構造配列_深海の古戦場跡:34:6, 4)
			;剣を回収している場合、不発
			WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%向四周放射必中的怨念！
			WSTR:(K++) = 刹那间，%CALLNAME:PLAYER%的行李中飞出一柄利剑！
			WSTR:(K++) = 那柄插在海底的剑笔直飞来──将怨念波动尽数抵消！
			WSTR:(K++) = 剑身虽化为齑粉，但袭向你们的攻击也随之消散！
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
			WSTR:(K++) = 「那、那把剑……应该是我的……！！」
			WSTR:(K++) = 「为何背叛我！！区区武器竟敢！！」
			CALL メッセージ欄表示用関数(,"巨大スケルトン",,0)
			CALL 口上変数初期化
			WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%因暴怒而露出破绽……
			WSTR:(K++) = 现在正是攻击的绝佳时机！
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化

			CALL 大技待機解除(戦闘行動キャラ)
			SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
			RETURN 1
		ELSE
			;剣を回収してないならクソバカ攻撃が発動
			アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%向四周放射必中的怨念！
			アビテンプレート用_アビ名 = 怨念放射

			CALL アビ対象選択テンプレート_味方全体_生者のみ()
			アビ汎用変数:効果割合 = 700
			アビ汎用変数:命中率補正値 = 999
			CALL アビテンプレート_ダメージ処理_自属性
			敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
			
			行動種別フラグ:戦闘行動キャラ:大技発動 = 1
			CALL 大技待機解除(戦闘行動キャラ)
			SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
			RETURN 1
		ENDIF
	ENDIF
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2) == 0
	;HPが1/2以下になったら発狂
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%缠绕暗黑气息进入狂暴状态！
	アビテンプレート用_アビ名 = 暗黑气息

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 暗黑气息
	アビ汎用文字列:バフ・デバフ対象能力 = TA率
	アビ汎用変数:効果量 = 100
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 5 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 3) == 0
	;HPが1/5以下になったらオーラ掛け直した上で攻撃
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%缠绕暗黑气息进入狂暴状态！
	アビテンプレート用_アビ名 = 暗黑气息

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 暗黑气息
	アビ汎用文字列:バフ・デバフ対象能力 = TA率
	アビ汎用変数:効果量 = 100
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 3
	;0を返すとそのまま通常攻撃に入る
	RETURN 0
ENDIF


@敵チャージ行動_Lv20_格拉辛
アビテンプレート用_アビ名 = 暗黑咆哮
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%将暗黑魔力融入声波释放！
アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的暗黑咆哮！

CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用変数:効果割合 = 150
CALL アビテンプレート_ダメージ処理_闇属性




@バフ・デバフ特殊表示_暗黑气息(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML TA率增益：暗黑气息
PRINTFORML 效果量：100
PRINTFORML 持续回合：永久
PRINTFORML ※使用净化盐可消除
