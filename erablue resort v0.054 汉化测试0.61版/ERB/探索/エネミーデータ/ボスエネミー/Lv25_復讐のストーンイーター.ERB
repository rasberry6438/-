@敵BATTLE_STATE_Lv25_复仇的噬石兽(対象エネミー)
#DIM 対象エネミー
敵BATTLE_STATE:対象エネミー:Lv = 6
敵BATTLE_STATE:対象エネミー:最大ＨＰ = 22000
敵BATTLE_STATE:対象エネミー:ＨＰ = 22000
敵BATTLE_STATE:対象エネミー:攻撃力 = 1300
敵BATTLE_STATE:対象エネミー:防御力 = 760
敵BATTLE_STATE:対象エネミー:回復力 = 0
敵BATTLE_STATE:対象エネミー:行動速度 = 560
敵BATTLE_STATE:対象エネミー:DA率 = 0
敵BATTLE_STATE:対象エネミー:TA率 = 50
敵BATTLE_STATE:対象エネミー:命中率 = 105
敵BATTLE_STATE:対象エネミー:回避率 = 5
敵BATTLE_STATE:対象エネミー:属性 = 3
敵BATTLE_STATE:対象エネミー:所持EXP = 2455
敵BATTLE_STATE:対象エネミー:所持ルピ = 3115
敵BATTLE_STATE:対象エネミー:最大チャージターン = 3

敵BATTLE_STATE:対象エネミー:ボスフラグ = 1
敵BATTLE_STATE:対象エネミー:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:対象エネミー:種族 = 動物_爬虫


@敵討伐後処理_Lv25_复仇的噬石兽(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")

ドロップ確率 = 30 + トレハンLv * 2
ドロップアイテム = 涡琥珀
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)


@敵トリガー行動_Lv25_复仇的噬石兽
IF GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
	;初手石弹连射
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的复仇之火熊熊燃烧！
	アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的石弹连射！

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	;HPが半分以下になったら呪いを撒いてくる
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%因重伤陷入狂暴状态！
	アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的特殊行动已变化！
	アビ汎用文字列:実行時メッセージ３ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的诅咒宝石！

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:基礎成功確率 = 100
	アビ汎用文字列:バフ・デバフ枠 = 呪い
	アビ汎用文字列:バフ・デバフ対象能力 = 呪い
	アビ汎用変数:持続ターン = 7
	CALL アビテンプレート_不利効果_デバフ効果セット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	敵BATTLE_STATE:(戦闘行動キャラ - 10):最大チャージターン = 1
	RETURN 1
ENDIF


@敵チャージ行動_Lv25_复仇的噬石兽
IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大チャージターン == 3
	アビテンプレート用_アビ名 = 石弹连射

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
ELSE
	CALL アビ対象選択テンプレート_味方ランダムＸ体(7, 0, "呪い")
	IF RESULT == -1
		;呪いが一人もいない場合
		アビテンプレート用_アビ名 = 诅咒宝石

		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:基礎成功確率 = 100
		アビ汎用文字列:バフ・デバフ枠 = 呪い
		アビ汎用文字列:バフ・デバフ対象能力 = 呪い
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_不利効果_デバフ効果セット
	ELSE
		アビテンプレート用_アビ名 = 石弹弹幕

		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_ダメージ処理_自属性
	ENDIF
ENDIF
