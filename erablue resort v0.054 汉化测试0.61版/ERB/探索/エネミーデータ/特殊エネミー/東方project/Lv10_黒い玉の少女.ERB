
@敵BATTLE_STATE_Lv10_漆黑球体少女(ARG)
敵BATTLE_STATE:ARG:Lv = 10
敵BATTLE_STATE:ARG:最大ＨＰ = 12000
敵BATTLE_STATE:ARG:ＨＰ = 12000
敵BATTLE_STATE:ARG:攻撃力 = 480
敵BATTLE_STATE:ARG:防御力 = 450
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 400
敵BATTLE_STATE:ARG:DA率 = 0
敵BATTLE_STATE:ARG:TA率 = 0
敵BATTLE_STATE:ARG:命中率 = 70
敵BATTLE_STATE:ARG:回避率 = 0
敵BATTLE_STATE:ARG:属性 = 5
敵BATTLE_STATE:ARG:所持EXP = 650
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_妖怪

SIF EXIST画像FILE(@"%CSTR:(キャラ検索("[宵暗的妖怪]露米娅")):画像フォルダ%/顔_デフォルト")
	敵BATTLE_STATE_STR:ARG:ボス画像 = %CSTR:(キャラ検索("[宵暗的妖怪]露米娅")):画像フォルダ%/顔_デフォルト

@敵討伐後処理_Lv10_漆黑球体少女(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")


ドロップ確率 = 10 + トレハンLv * 5
IF RAND:100 < ドロップ確率
	リザルト後特殊処理_処理キー = ルーミア招待フラグ解放
ENDIF

@リザルト後特殊処理_ルーミア招待フラグ解放()
#DIMS 立ち絵_ルーミア
#DIM 対象キャラ

対象キャラ = キャラ検索("[宵暗的妖怪]露米娅")
;既に解放済みなら戻す
SIF CFLAG:対象キャラ:招待不可フラグ == 0
	RETURN 0

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(対象キャラ,100,,,1,1)
立ち絵_ルーミア = %立ち絵リソース記録%

;会話部分
CALL 口上変数初期化
WSTR:(K++) = ……难以置信！漆黑球体少女爬起身，用毫无敌意的眼神望过来！
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「痛痛痛……看来你不是能抓来吃的人类。
WSTR:(K++) = 　刚才真是倒霉，果然得先确认产地才行」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 产地啊，虽然确实不是这个世界的人类。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「从异世界来的？好像很有趣，我也想去看看。
WSTR:(K++) = 　希望那边的世界不会一片漆黑」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = %CALLNAME:PLAYER%提出若对方承诺不吃人类并支付报酬，可以介绍度假村。
WSTR:(K++) = 同时指出漆黑环境是她自己释放的黑暗所致。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「不用担心啦，异世界产的人类已经吃腻了。
WSTR:(K++) = 　至于报酬就是钱吧？人类偶尔带着的发光物我倒是收集了不少」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「话说异世界人类既然不能吃，那要用来做什么？当宠物养吗？」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 大概是用来友好玩耍的吧。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「嘿——这样啊——」
CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
CALL 口上変数初期化
WSTR:(K++) = [宵暗的妖怪]露米娅开始造访度假村了！
CALL メッセージ欄表示用関数(,,,2)
CALL 口上変数初期化
CFLAG:対象キャラ:招待不可フラグ = 0


@敵トリガー行動_Lv10_漆黑球体少女
#DIM 攻撃回数
#DIMS 立ち絵_ルーミア

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[宵暗的妖怪]露米娅"),100,,,1,1)
立ち絵_ルーミア = %立ち絵リソース記録%


IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("你是可以吃掉的人类吗？/符卡要来了哦/被黑暗吞噬……后面怎么说来着？")%」
	CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
	アビ汎用文字列:実行時メッセージ２ = ──暗符「境界线」──

	CALL アビ対象選択テンプレート_味方全体_生者のみ()
	アビ汎用変数:効果割合 = 150
	CALL アビテンプレート_ダメージ処理_自属性

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 暗闇
	アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
	アビ汎用変数:効果割合 = 30
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:基礎成功確率 = 60
	CALL アビテンプレート_不利効果_デバフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("你是可以吃掉的人类吗？/符卡要来了哦/被黑暗吞噬……后面怎么说来着？")%」
	CALL メッセージ欄表示用関数(立ち絵_ルーミア,"黒い球の少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
	アビ汎用文字列:実行時メッセージ２ = ──夜符「夜雀」──

	FOR 攻撃回数, 0, 3
		CALL アビ対象選択テンプレート_味方ランダムＸ体(2, 1)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 40
		CALL アビテンプレート_不利効果_デバフ効果セット
	NEXT

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	;通常攻撃は複数回攻撃
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の通常弾幕！

	FOR 攻撃回数, 0, RAND:2 + 2
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 80
		アビ汎用文字列:ダメージ種類 = 通常攻撃ダメージ
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT
	IF デバフ番号取得("恐怖", 戦闘行動キャラ) < 0
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン += 1
	ELSE
		;回数消費
		CALL デバフ回数経過処理(戦闘行動キャラ, "恐怖")
	ENDIF
ENDIF

RETURN 1

@敵チャージ行動_Lv10_漆黑球体少女
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
アビ汎用文字列:実行時メッセージ２ = ──暗符「境界线」──

CALL アビ対象選択テンプレート_味方全体_生者のみ()
アビ汎用変数:効果割合 = 150
CALL アビテンプレート_ダメージ処理_自属性

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 暗闇
アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = 3
アビ汎用変数:基礎成功確率 = 60
CALL アビテンプレート_不利効果_デバフ効果セット

