;なんか特殊な行動の時のテンプレ


@アビテンプレート_何もしない
CALL アビテンプレート_効果共通処理(1)
SIF RESULT == -1
	RETURN -1


@アビテンプレート_処理分配(関数名)
#DIMS 関数名
#DIM 一時保存用_対象保存, アビ対象最大数
#DIM 一時保存用_奥義使用先保存, アビ対象最大数
#DIMS 一時保存用_表示メッセージ格納, (アビ対象最大数 + 1), メッセージ格納最大行数
#DIM 一時保存用_A_M_NUM, (アビ対象最大数 + 1)
#DIM 一時保存用_アビ汎用変数, VARSIZE("アビ汎用変数")
#DIMS 一時保存用_アビ汎用文字列, VARSIZE("アビ汎用文字列")
#DIM 一時保存用_全体フラグ
#DIM 一時保存用_回避フラグ
#DIM 対象番号
#DIM メッセージ番号

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

;一時保存に退避
ARRAYCOPY "アビテンプレート用_対象保存", "一時保存用_対象保存"
ARRAYCOPY "アビテンプレート用_奥義使用先保存", "一時保存用_奥義使用先保存"
ARRAYCOPY "アビテンプレート用_表示メッセージ格納", "一時保存用_表示メッセージ格納"
ARRAYCOPY "A_M_NUM", "一時保存用_A_M_NUM"
ARRAYCOPY "アビ汎用変数", "一時保存用_アビ汎用変数"
ARRAYCOPY "アビ汎用文字列", "一時保存用_アビ汎用文字列"
一時保存用_全体フラグ = アビテンプレート用_全体フラグ
一時保存用_回避フラグ = アビテンプレート用_回避フラグ

FOR 対象番号, 0, アビ対象最大数
	SIF 一時保存用_対象保存:対象番号 < 0
		CONTINUE

	;対象関連・メッセージ関連・回避フラグは初期化
	VARSET アビテンプレート用_対象保存:0, -1
	VARSET アビテンプレート用_奥義使用先保存, -1
	VARSET アビテンプレート用_表示メッセージ格納:0:0
	VARSET A_M_NUM:0
	アビテンプレート用_回避フラグ = 0

	;アビ汎用変数文字列と全体フラグは呼び出し時点の状態に
	ARRAYCOPY "一時保存用_アビ汎用変数", "アビ汎用変数"
	ARRAYCOPY "一時保存用_アビ汎用文字列", "アビ汎用文字列"
	アビテンプレート用_全体フラグ = 一時保存用_全体フラグ

	;一時保存の対象を読み出してセット
	アビテンプレート用_対象保存:0 = 一時保存用_対象保存:対象番号

	;関数を実行
	CALLFORM %関数名%(対象番号)

	;メッセージを一時保存に移動
	FOR メッセージ番号, 0, 50
		SIF アビテンプレート用_表示メッセージ格納:システムメッセージ番号:メッセージ番号 == ""
			BREAK
		一時保存用_表示メッセージ格納:システムメッセージ番号:(一時保存用_A_M_NUM:システムメッセージ番号++) '= アビテンプレート用_表示メッセージ格納:システムメッセージ番号:メッセージ番号
	NEXT
	FOR メッセージ番号, 0, 50
		SIF アビテンプレート用_表示メッセージ格納:0:メッセージ番号 == ""
			BREAK
		一時保存用_表示メッセージ格納:対象番号:(一時保存用_A_M_NUM:対象番号++) '= アビテンプレート用_表示メッセージ格納:0:メッセージ番号
	NEXT
NEXT

;一時保存から戻す
ARRAYCOPY "一時保存用_対象保存", "アビテンプレート用_対象保存"
ARRAYCOPY "一時保存用_奥義使用先保存", "アビテンプレート用_奥義使用先保存"
ARRAYCOPY "一時保存用_表示メッセージ格納", "アビテンプレート用_表示メッセージ格納"
ARRAYCOPY "一時保存用_A_M_NUM", "A_M_NUM"
ARRAYCOPY "一時保存用_アビ汎用変数", "アビ汎用変数"
ARRAYCOPY "一時保存用_アビ汎用文字列", "アビ汎用文字列"
アビテンプレート用_全体フラグ = 一時保存用_全体フラグ
アビテンプレート用_回避フラグ = 一時保存用_回避フラグ
