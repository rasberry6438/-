@バフ・デバフ汎用表示(隊列番号, バフ・デバフ行数, デバフフラグ)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM デバフフラグ
#DIM 効果タイプ
#DIM 要改行フラグ
;以下、一時保存用
#DIMS バフ・デバフ対象能力
#DIMS バフ・デバフ名
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
#DIM バフ・デバフ持続ターン
#DIM バフ・デバフ持続回数
#DIM バフ・デバフ持続行動回数
#DIM バフ・デバフ不可驱散

バフ・デバフ対象能力 '= DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "対象能力")
バフ・デバフ名 '= DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")
バフ・デバフ効果枠 '= DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果枠")
バフ・デバフ効果量 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_固定値")
バフ・デバフ効果割合 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "効果量_割合")
バフ・デバフ持続ターン = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")
バフ・デバフ持続回数 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続回数")
バフ・デバフ持続行動回数 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続行動回数")
バフ・デバフ不可驱散 = DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "不可驱散フラグ")

PRINTFORML %バフ・デバフ表示名(バフ・デバフ対象能力)%\@ デバフフラグ ? de # \@buff：%バフ・デバフ名%

バフ・デバフ説明文 = 
TRYCCALLFORM \@ デバフフラグ ? デ # \@バフ固有説明文_%バフ・デバフ効果枠%(バフ・デバフ効果量, バフ・デバフ効果割合)
	;固有説明文があればそれを表示する
	効果タイプ = RESULT
CATCH
	TRYCCALLFORM \@ デバフフラグ ? デ # \@バフ説明文_%バフ・デバフ対象能力%(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
		;暗闇などの特殊なバフは説明文を用意してあげよう
		HTML_PRINT バフ・デバフ説明文
		効果タイプ = RESULT
	CATCH
		;それ以外はステータスかそれと同じように数値計算に絡むタイプのものなので、説明文はなし
		効果タイプ = バフ・デバフ効果タイプ(バフ・デバフ対象能力)
	ENDCATCH
ENDCATCH

要改行フラグ = 0
IF !GETBIT(効果タイプ, 2)
	PRINTFORM 效果栏位：%バフ・デバフ効果枠, 20, LEFT%
	要改行フラグ = 1
ENDIF
IF !GETBIT(効果タイプ, 0) && (バフ・デバフ効果量 != 0 || バフ・デバフ効果割合 == 0 || GETBIT(効果タイプ, 1))
	PRINTFORM 效果数值：{バフ・デバフ効果量, 4}　　
	要改行フラグ = 1
ENDIF
IF !GETBIT(効果タイプ, 1) && (バフ・デバフ効果割合 != 0 || GETBIT(効果タイプ, 0))
	PRINTFORM 效果比率：{バフ・デバフ効果割合, 4}％　　
	要改行フラグ = 1
ENDIF
SIF 要改行フラグ
	PRINTL 

要改行フラグ = 0
IF バフ・デバフ持続ターン < 0
	PRINT 持续回合：永久　　
	要改行フラグ = 1
ELSEIF バフ・デバフ持続ターン > 0
	PRINTFORM 持续回合：{バフ・デバフ持続ターン}　　
	要改行フラグ = 1
ENDIF
IF バフ・デバフ持続回数 > 0
	PRINTFORM 持续次数：{バフ・デバフ持続回数}　　
	要改行フラグ = 1
ENDIF
IF バフ・デバフ持続行動回数 > 0
	PRINTFORM 持续行动次数：{バフ・デバフ持続行動回数}　　
	要改行フラグ = 1
ENDIF
SIF 要改行フラグ
	PRINTL 

要改行フラグ = 0
IF バフ・デバフ不可驱散
	PRINT 不可驱散
	要改行フラグ = 1
ENDIF
SIF 要改行フラグ
	PRINTL 


@バフ汎用表示(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
JUMP バフ・デバフ汎用表示(隊列番号, バフ・デバフ行数, 0)


@デバフ汎用表示(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
JUMP バフ・デバフ汎用表示(隊列番号, バフ・デバフ行数, 1)


@バフ・デバフ表示名(バフ・デバフ種類)
#FUNCTIONS
#DIMS バフ・デバフ種類
#DIMS 効果対象
;状態異常耐性やダメージ補正のように、名前が表示向きじゃないものは表示用に補正する

SIF バフ・デバフ種類 == "ゲージ上昇効率_被ダメ時"
	RETURNF "ゲージ上昇効率（被ダメージ時）"
SIF バフ・デバフ種類 == "特殊反応_被ダメ時"
	RETURNF "特殊反応（被ダメージ時）"
SIF バフ・デバフ種類 == "特殊反応_回避時"
	RETURNF "特殊反応（回避時）"
SIF バフ・デバフ種類 == "特殊反応_被タゲ時"
	RETURNF "特殊反応（被ターゲット時）"
SIF バフ・デバフ種類 == "特殊反応_戦闘不能時"
	RETURNF "特殊反応（戦闘不能時）"
IF REGEXPMATCH(バフ・デバフ種類, "^ダメージ補正_.*$") > 0
	効果対象 '= SUBSTRINGU(バフ・デバフ種類, STRLENSU("ダメージ補正_"), -1)
	RETURNF 効果対象 + "補正"
ENDIF
IF REGEXPMATCH(バフ・デバフ種類, "^状態異常耐性_.*$") > 0
	効果対象 '= SUBSTRINGU(バフ・デバフ種類, STRLENSU("状態異常耐性_"), -1)
	RETURNF "状態異常耐性（" + 効果対象 + "）"
ENDIF
RETURNF バフ・デバフ種類

@戦闘時ツールチップ埋め込み_起動時処理
#DIM 登録番号
#DIM 候補数
;毎回関数検索すると重たくなるんで、起動時にリストに突っ込む。

VARSET 登録済バフ種類
VARSET 登録済デバフ種類
VARSET RESULTS
登録番号 = 0

候補数 = ENUMFUNCBEGINSWITH("バフ説明文_")
FOR LOCAL, 0, 候補数
	登録済バフ種類:登録番号 = %SUBSTRING(RESULTS:LOCAL, 11)%
	登録番号 ++
NEXT

VARSET RESULTS
登録番号 = 0
候補数 = ENUMFUNCBEGINSWITH("デバフ説明文_")
FOR LOCAL, 0, 候補数
	登録済デバフ種類:登録番号 = %SUBSTRING(RESULTS:LOCAL, 13)%
	登録番号 ++
NEXT


@戦闘時ツールチップ埋め込み(説明文文字列)
#DIMS REF 説明文文字列

;文字列を検索してヒットしたらツールチップ付きに入れ替える。
FOR LOCAL, 0, VARSIZE("登録済バフ種類")
	SIF 登録済バフ種類:LOCAL == ""
		BREAK
	IF STRFIND(説明文文字列, 登録済バフ種類:LOCAL) >= 0
		バフ・デバフ説明文 = 
		CALLFORM バフ説明文_%登録済バフ種類:LOCAL%("", 0, 0)
		説明文文字列 '= REPLACE(説明文文字列, 登録済バフ種類:LOCAL, @"<font color='#%カラーパレット_HTML("黄")%'><nonbutton title='<br>%バフ・デバフ説明文%'>%登録済バフ種類:LOCAL%</nonbutton></font>")
	ENDIF
NEXT
FOR LOCAL, 0, VARSIZE("登録済デバフ種類")
	SIF 登録済デバフ種類:LOCAL == ""
		BREAK
	IF STRFIND(説明文文字列, @"%登録済デバフ種類:LOCAL%効果") >= 0
		バフ・デバフ説明文 = 
		CALLFORM デバフ説明文_%登録済デバフ種類:LOCAL%("", 0, 0)
		説明文文字列 '= REPLACE(説明文文字列, @"%登録済デバフ種類:LOCAL%効果", @"<font color='#%カラーパレット_HTML("紫")%'><nonbutton title='<br>%バフ・デバフ説明文%'>%登録済デバフ種類:LOCAL%</nonbutton></font>効果")
	ENDIF
NEXT


@バフ説明文_追撃(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "普通攻击时，根据造成的伤害比例追加额外伤害。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "普通攻击时，追加额外伤害。<br>"
	RETURN 1p1
ENDIF


@バフ説明文_カウンター(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "受到伤害时，会发动反击。<br>"
RETURN 0


@バフ説明文_回避反撃(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "当闪避攻击时，会发动反击。<br>"
RETURN 0


@バフ説明文_ガッツ(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	PRINTL 即使承受濒死的巨额伤害，仍会以ＨＰ1状态存活。
	RETURN 1p1
ENDIF

@バフ説明文_自動復活(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "战斗不能时，将根据最大ＨＰ比例自动复活。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "战斗不能时，将自动复活。<br>"
	RETURN 1p1
ENDIF


@バフ説明文_再生(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＨＰ比例恢复ＨＰ。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，恢复ＨＰ。<br>"
	RETURN 1p1
ENDIF


@バフ説明文_高揚(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据当前奥义槽存量比例增加奥义槽。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，增加奥义槽。<br>"
	RETURN 1p1
ENDIF


@バフ説明文_ＨＰ転換(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "将目标所受伤害转化为ＨＰ恢复。"
SIF バフ・デバフ効果量 > 0
	バフ・デバフ説明文 += @"（上限：{バフ・デバフ効果量}）"
バフ・デバフ説明文 += "<br>"
RETURN 1p0


@バフ説明文_ＭＰ転換(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "将目标所受伤害转化为ＭＰ恢复。"
SIF バフ・デバフ効果量 > 0
	バフ・デバフ説明文 += @"（上限：{バフ・デバフ効果量}）"
バフ・デバフ説明文 += "<br>"
RETURN 1p0


@バフ説明文_バリア(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "由屏障代为承受目标所受伤害。<br>"
RETURN 1p1


@バフ説明文_単体かばう(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "从非全体攻击中掩护队友。<br>"
RETURN 1p0 | 1p1


@バフ説明文_全体かばう(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "掩护队友免受攻击。<br>"
RETURN 1p0 | 1p1


@バフ説明文_反射(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "将针对自身的非普通攻击和奥义攻击反射给施放者。<br>"
RETURN 1p0 | 1p1


@バフ説明文_再行動(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "追加一回合内的可行动次数。<br>"
RETURN 1p1


@バフ説明文_奥義再発動(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "使用奥义时，该效果会额外发动一次。<br>"
RETURN 1p0 | 1p1


@バフ説明文_弱体無効(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "反弹减益施加效果。<br>"
RETURN 1p0 | 1p1


@バフ説明文_ディスペルガード(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "反弹增益驱散效果。<br>"
RETURN 1p0 | 1p1


@バフ説明文_交代(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "行动结束后与替补成员交换位置。<br>"
RETURN 1p0 | 1p1


@バフ説明文_慧眼(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "攻击必定命中敌人弱点（无视属性抗性）。<br>"
RETURN 1p0 | 1p1


@バフ説明文_テンション(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
#DIM 対象キャラ隊列
#DIM バフ・デバフ行数

バフ・デバフ説明文 += "攻击力上升。<br>"
バフ・デバフ説明文 += "等级越高（最高Lv4），提升比例越大。<br>"
IF バフ・デバフ効果量 > 0
	バフ・デバフ説明文 += @"Lv：{バフ・デバフ効果量}　"
	SELECTCASE バフ・デバフ効果量
		CASE 1
			バフ・デバフ説明文 += "（+10％）<br>"
		CASE 2
			バフ・デバフ説明文 += "（+30％）<br>"
		CASE 3
			バフ・デバフ説明文 += "（+60％）<br>"
		CASE 4
			バフ・デバフ説明文 += "（+100％）<br>"
	ENDSELECT
ENDIF
RETURN 1p1


@バフ説明文_ストレングス(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "攻击力上升。<br>"
バフ・デバフ説明文 += "自身ＨＰ越多，提升比例越大。<br>"
IF バフ・デバフ効果割合 > 0
	バフ・デバフ説明文 += @"（{バフ・デバフ効果割合/3}％ - {バフ・デバフ効果割合}％）<br>"
ENDIF
RETURN 1p0


@バフ説明文_逆境(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "攻击力上升。<br>"
バフ・デバフ説明文 += "自身ＨＰ越少，提升比例越大。<br>"
IF バフ・デバフ効果割合 > 0
	バフ・デバフ説明文 += @"（{バフ・デバフ効果割合/3}％ - {バフ・デバフ効果割合}％）<br>"
ENDIF
RETURN 1p0


@デバフ説明文_毒(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＨＰ比例承受伤害。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，承受伤害。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_火傷(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＨＰ比例承受伤害。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，承受伤害。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_氷結(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＨＰ比例承受伤害。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，承受伤害。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_感電(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "连续攻击率下降。<br>"
IF バフ・デバフ効果割合 != 0
	RETURN 0
ELSE
	RETURN 1p1
ENDIF


@デバフ説明文_自傷(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＨＰ比例承受伤害。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，承受伤害。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_消耗(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＭＰ比例减少ＭＰ。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，ＭＰ减少。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_ＭＰ自傷(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
IF バフ・デバフ効果割合 != 0
	バフ・デバフ説明文 += "行动结束时，根据最大ＭＰ比例减少ＭＰ。<br>"
	RETURN 0
ELSE
	バフ・デバフ説明文 += "行动结束时，ＭＰ减少。<br>"
	RETURN 1p1
ENDIF


@デバフ説明文_暗闇(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
SIF バフ・デバフ効果割合 < 100
	バフ・デバフ説明文 += "有一定概率"
バフ・デバフ説明文 += "普通攻击失败。<br>"
RETURN 1p0


@デバフ説明文_魅了(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
SIF バフ・デバフ効果割合 < 100
	バフ・デバフ説明文 += "有一定概率"
バフ・デバフ説明文 += "无法进行行动。<br>"
RETURN 1p0


@デバフ説明文_麻痺(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
SIF バフ・デバフ効果割合 < 100
	バフ・デバフ説明文 += "有一定概率"
バフ・デバフ説明文 += "无法进行行动。<br>"
RETURN 1p0


@デバフ説明文_睡眠(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
SIF バフ・デバフ効果割合 < 100
	バフ・デバフ説明文 += "有一定概率"
バフ・デバフ説明文 += "无法进行行动。<br>"
RETURN 1p0


@デバフ説明文_封印(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
SIF バフ・デバフ効果割合 < 100
	バフ・デバフ説明文 += "有一定概率"
バフ・デバフ説明文 += "无法进行行动。<br>"
RETURN 1p0


@デバフ説明文_混乱(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "行动将变为随机对一名友方或敌人发动无属性攻击。<br>"
RETURN 1p0 | 1p1


@デバフ説明文_呪い(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "将目标者的ＨＰ恢复转化为伤害。"
SIF バフ・デバフ効果量 > 0
	バフ・デバフ説明文 += @"（上限：{バフ・デバフ効果量}）"
バフ・デバフ説明文 += "<br>"
RETURN 1p0


@デバフ説明文_強圧(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "无法恢复ＨＰ。<br>"
RETURN 1p0 | 1p1


@デバフ説明文_恐怖(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "普通攻击及行动结束时的奥义槽・充能回合增加效果失效。<br>"
RETURN 1p0 | 1p1


@デバフ説明文_奥義封印(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "无法使用奥义。<br>"
RETURN 1p0 | 1p1


@デバフ説明文_アビリティ封印(バフ・デバフ効果枠, バフ・デバフ効果量, バフ・デバフ効果割合)
#DIMS バフ・デバフ効果枠
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
バフ・デバフ説明文 += "无法使用技能。<br>"
RETURN 1p0 | 1p1
