
@乗機取得処理(乗機名一時保存)
#DIMS 乗機名一時保存

;アンロック式なのでフラグ立てるだけ
;ERDいじったりしなくていいようにデータベース管理
ENUMFUNCBEGINSWITH @"乗機関数_%乗機名一時保存%"
IF RESULT
	FOR LOCAL, 0, DT_ROW_LENGTH("所持乗機データベース")
		SELECTCASE DT_CELL_GETS("所持乗機データベース", LOCAL, "乗機名")
			CASE 乗機名一時保存
				PRINTW 已持有该机体或设计图。
				;すでに追加済みの場合は-2を返す
				RETURN -2
		ENDSELECT
	NEXT
	;抜けてきたら登録
	CALLFORM 乗機関数_%乗機名一時保存%("乗機登録")
	RETURN 1
ELSE
	;そもそも関数そのものが無い時は-1を返す
	PRINTW 未找到指定机体。
	RETURN -1
ENDIF

@乗機取得処理_設計図(乗機名一時保存)
#DIMS 乗機名一時保存

CALL 乗機取得処理(乗機名一時保存)

IF RESULT == 1
	;設計図状態なので設計図フラグを立てる
	DT_CELL_SET "所持乗機データベース", DT_ROW_LENGTH("所持乗機データベース") - 1, "設計状態フラグ", 1
	RETURN 1
ELSE
	RETURN -1
ENDIF


@乗機所持チェック(乗機名, 設計状態 = 2)
#FUNCTION
#DIMS 乗機名
;0 = 実物のみチェック, 1 = 設計図のみチェック, それ以外 = どちらかあればOK
#DIM 設計状態
#DIM ループ用

;指定した名前の乗機を持っているか確認する
;関数が存在しないケースは想定していないため注意
FOR ループ用, 0, DT_ROW_LENGTH("所持乗機データベース")
	IF DT_CELL_GETS("所持乗機データベース", ループ用, "乗機名") == 乗機名
		IF DT_CELL_GET("所持乗機データベース", ループ用, "設計状態フラグ")
			SIF 設計状態 != 0
				RETURNF 1
		ELSE
			SIF 設計状態 != 1
				RETURNF 1
		ENDIF
	ENDIF
NEXT
RETURNF 0


@所持乗機リスト表示(ページ番号, ソート基準, 設計フラグ = 0, y軸位置)
#DIM ページ番号
#DIM 装備中乗機番号
#DIM 武装番号
#DIM 表示数
#DIM y軸位置
#DIM 計算値
#DIM 兵器id
#DIMS ソート基準
#DIM ソート後配列, 500
#DIM 設計フラグ
VARSET ソート後配列

IF ソート基準 != ""
	ソート基準 = %ソート基準% DESC
ENDIF
DT_SELECT "所持乗機データベース", , ソート基準, ソート後配列

;まず装備している乗機番号取得
装備中乗機番号 = 装備乗機ID

VARSET LOCAL
VARSET LOCALS
表示数 = 0
LOCALS = <div rect='262,{y軸位置},5537,3625'><nobr>
LOCALS += "　　　 机体名　　　　　　　　　　<button value='乗機攻撃力_強化後'>攻击力</button>　<button value='乗機命中率_強化後'>命中率</button>　<button value='武装0'>槽０</button>　<button value='武装1'>槽１</button>　<button value='武装2'>槽２</button>　<button value='武装3'>槽３</button>　<button value='武装4'>槽４</button>　<button value='武装5'>槽５</button>　<button value='サブ搭乗数_強化後'>副驾数</button><br>"
LOCALS += "--------------------------------------------------------------------------------------------------------------<br>"
FOR LOCAL, 0, DT_ROW_LENGTH("所持乗機データベース")
	IF 設計フラグ
		SIF DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "設計状態フラグ", 1) == 0
			CONTINUE
	ELSE
		SIF DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "設計状態フラグ", 1)
			CONTINUE
	ENDIF
	表示用ステータス格納配列:表示数:0 = ソート後配列:LOCAL
	DT_CELL_GETS "所持乗機データベース", ソート後配列:LOCAL, "乗機名", 1
	;乗機名を一旦保存
	LOCALS:10 = %RESULTS%
	IF 装備中乗機番号 == ソート後配列:LOCAL
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>"
	ENDIF
	LOCALS += @"　<button value='{表示数}'>"
	LOCALS += @"%LOCALS:10, 24, LEFT%</button>"
	IF 装備中乗機番号 == ソート後配列:LOCAL
		LOCALS += "</font>"
	ENDIF
	LOCALS += @"<button value='{表示数 + 500}'>[详情]</button>"

	計算値 = 0
	FOR 武装番号, 0, 6
		IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1))
			兵器id = TOINT(DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1))
			計算値 += DT_CELL_GET("所持兵器データベース", 兵器id, "攻撃力変動値_強化後", 1)
		ENDIF
	NEXT
	SELECTCASE 計算値
		CASE IS > 0
			LOCALS += @"<font color='#%カラーパレット_HTML("緑")%'>{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機攻撃力", 1) + 計算値, 7}</font>"
		CASE IS < 0
			LOCALS += @"<font color='#%カラーパレット_HTML("赤ピンク")%'>{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機攻撃力", 1) + 計算値, 7}</font>"
		CASEELSE
			LOCALS += @"{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機攻撃力", 1), 7}"
	ENDSELECT

	LOCALS += " "

	計算値 = 0
	FOR 武装番号, 0, 6
		IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1))
			兵器id = TOINT(DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1))
			計算値 += DT_CELL_GET("所持兵器データベース", 兵器id, "命中率変動値_強化後", 1)
		ENDIF
	NEXT
	SELECTCASE 計算値
		CASE IS > 0
			LOCALS += @"<font color='#%カラーパレット_HTML("緑")%'>{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機命中率", 1) + 計算値, 7}</font>"
		CASE IS < 0
			LOCALS += @"<font color='#%カラーパレット_HTML("赤ピンク")%'>{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機命中率", 1) + 計算値, 7}</font>"
		CASEELSE
			LOCALS += @"{DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "乗機命中率", 1), 7}"
	ENDSELECT

	FOR 武装番号, 0, 6
		SELECTCASE DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1)
			CASE "不可"
				LOCALS += @"　　<font color='#%カラーパレット_HTML("グレーアウト")%'>无</font>"
			CASE "固有"
				LOCALS += @"　<font color='#%カラーパレット_HTML("薄ピンク")%'>固有</font>"
			CASEELSE
				IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", ソート後配列:LOCAL, @"武装{武装番号}", 1))
					LOCALS += @"　<font color='#%カラーパレット_HTML("クリーム色")%'>装备</font>"
				ELSE
					LOCALS += @"　　空"
				ENDIF
		ENDSELECT
	NEXT

	LOCALS += @"　%TOFULL(TOSTR(DT_CELL_GET("所持乗機データベース", ソート後配列:LOCAL, "サブ搭乗数_強化後", 1))), 6%"

	IF ソート後配列:LOCAL == 装備乗機ID
		LOCALS += @"　　<font color='#%カラーパレット_HTML("黄")%'>装备中</font>"
	ENDIF

	LOCALS += "<br>"
	表示数 += 1
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS, 1

@所持乗機リスト表示_USERCOM(y軸位置)
#DIM ページ番号
#DIM 表示アビ番号
#DIMS ソート基準
#DIM y軸位置
#DIM 選択乗機番号
#DIM 表示id
#DIM 兵器id
#DIM サブメンバー数保存

LOCALS = 
LOCALS += @"<div rect='200,{y軸位置},5662,3750' border='31' bcolor='#C0C0C0'></div>"
HTML_PRINT LOCALS, 1
CALL 所持乗機リスト表示(ページ番号, ソート基準, , y軸位置 + 75)
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += @"<div rect='262,{y軸位置 + 3600},5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]上一页</button>　　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:(29 * ページ番号 + 30):0 > 0
	LOCALS += "<button value='1001'>[1001]下一页</button>　　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 32 + (y軸位置 / 100)
	PRINTL
NEXT
BINPUTS
IF STRFIND(RESULTS, "探索集約_") > -1
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	ページ番号 = 0
	RETURN 0
ENDIF
IF ISNUMERIC(RESULTS) == 0
	IF DT_COLUMN_EXIST("所持乗機データベース", RESULTS)
		;ソート条件
		ソート基準 = %RESULTS%
	ELSE
		;ソート条件でない場合は切り替えのはず
		表示切り替え文字列 = %RESULTS%
	ENDIF
	RETURN 0
ENDIF
選択乗機番号 = TOINT(RESULTS)
SELECTCASE 選択乗機番号
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN -1
	CASE IS < 500
		IF 表示用ステータス格納配列:選択乗機番号:0 == 装備乗機ID
			PRINTFORMW %DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%从机体配置中移除。
			装備乗機ID = 0
			FOR LOCAL, 10, 14
				BATTLE_STATE:LOCAL:キャラ登録番号 = 0
			NEXT
		ELSE
			サブメンバー数保存 = DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1)
			装備乗機ID = 表示用ステータス格納配列:選択乗機番号:0
			PRINTFORMW %DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%设为配置机体。
			IF サブメンバー数保存 > DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1)
				FOR LOCAL, (10 + DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1)), 14
					BATTLE_STATE:LOCAL:キャラ登録番号 = 0
				NEXT
			ENDIF
		ENDIF
	CASE IS < 1000
		表示id = 表示用ステータス格納配列:(選択乗機番号 - 500):0
		$乗機詳細_LOOP
		LOCALS = <br>■%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%<br><br>
		TRYCALLFORM 乗機関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%("フレーバー")
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"

		LOCALS += @"・可用武装<br>"
		FOR 表示アビ番号, 0, 6
			SIF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "不可"
				CONTINUE
			IF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "固有"
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット")
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名")
				LOCALS += @"■%TSTR:コマンド名受渡%（专属武装）<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSEIF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				兵器id = TOINT(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット", 兵器id)
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名", 兵器id)
				LOCALS += @"■%TSTR:コマンド名受渡%　　<button value='{表示アビ番号}'>[更换搭载武器]</button>　<button value='{表示アビ番号 + 100}'>[卸除]</button><br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文", 兵器id)
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("装備条件説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSE
				LOCALS += @"■可更换装备槽位　　<button value='{表示アビ番号}'>[更换搭载武器]</button>　<button value='{表示アビ番号 + 100}'>[卸除]</button><br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("装備条件説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ENDIF
		NEXT
		LOCALS += "<br><button value='999'>[999] 返回</button><br>"
		HTML_PRINT LOCALS
		BINPUT
		LOCAL = RESULT
		SELECTCASE LOCAL
			CASE 999
				;リスト表示に戻るだけなのでRETURNしない
			CASE 0 TO 5
				IF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL}", 1) != "固有" && DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL}", 1) != "不可"
					;固有でも不可でもない場合に搭載兵器装備画面へ行く
					CALL 搭載兵器装備処理(表示id, LOCAL)
				ENDIF
				GOTO 乗機詳細_LOOP
			CASE 100 TO 105
				IF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL}", 1) != "固有" && DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL}", 1) != "不可"
					;装備を外す
					PRINTFORMW 已卸除%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%的武装%TOFULL(TOSTR(LOCAL - 100))%上装备的%DT_CELL_GETS("所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL - 100}", 1)), "兵器名", 1)%。
					DT_CELL_SET "所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{LOCAL - 100}", 1)), "装備乗機id", 0, 1
					DT_CELL_SET "所持乗機データベース", 表示id, @"武装{LOCAL - 100}", "", 1
				ENDIF
				GOTO 乗機詳細_LOOP
		ENDSELECT
ENDSELECT


@所持乗機リスト表示_作成時
#DIM ページ番号
#DIM 表示アビ番号
#DIMS ソート基準
#DIM 選択乗機番号
#DIM 表示id
#DIM 兵器id
#DIM 素材id, 50
#DIM 素材不足フラグ
#DIM 必要数
#DIMS 分割文字列, 2

DO
LOCALS = 
LOCALS += "<div rect='200,12,5662,200' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='250,60,5662,200'>请选择要制造的机体。</div>"
LOCALS += "<div rect='200,262,5662,3750' border='31' bcolor='#C0C0C0'></div>"
HTML_PRINT LOCALS, 1
;設計状態のみ表示
CALL 所持乗機リスト表示(ページ番号, ソート基準, 1, 350)
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,3825,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]上一页</button>　　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:(29 * ページ番号 + 30):0 > 0
	LOCALS += "<button value='1001'>[1001]下一页</button>　　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='9999'>[9999]返回</button></div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 36
	PRINTL
NEXT
BINPUTS
IF ISNUMERIC(RESULTS) == 0
	;文字列が入る場合、ソート条件が入るはず
	ソート基準 = %RESULTS%
	CONTINUE
ENDIF
選択乗機番号 = TOINT(RESULTS)
SELECTCASE 選択乗機番号
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 1
	CASE IS < 500
		表示id = 表示用ステータス格納配列:(選択乗機番号):0
		LOCALS = <br>■%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%<br><br>
		TRYCALLFORM 乗機関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%("フレーバー")
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"

		LOCALS += @"・可用武装<br>"
		FOR 表示アビ番号, 0, 6
			SIF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "不可"
				CONTINUE
			IF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "固有"
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット")
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名")
				LOCALS += @"■%TSTR:コマンド名受渡%（专属武装）<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSEIF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				兵器id = TOINT(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット", 兵器id)
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名", 兵器id)
				LOCALS += @"■%TSTR:コマンド名受渡%<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文", 兵器id)
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSE
				LOCALS += @"■可更换装备<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("装備条件説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ENDIF
		NEXT
		HTML_PRINT LOCALS
		VARSET 作成素材登録
		TRYCALLFORM 乗機関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%("作成素材")
		DRAWLINE
		PRINTL ■必要素材
		素材不足フラグ = 0
		VARSET 素材id
		FOR LOCAL, 0, 50
			SIF 作成素材登録:LOCAL == ""
				BREAK
			SPLIT 作成素材登録:LOCAL, "_", 分割文字列

			FOR LOCAL:1, 0, DT_ROW_LENGTH("所持素材データベース")
				;該当素材のidを引っ張ってくる
				IF DT_CELL_GETS("所持素材データベース", LOCAL:1, "素材アイテム名") == 分割文字列:0
					素材id:LOCAL = DT_CELL_GET("所持素材データベース", LOCAL:1, "id")
					BREAK
				ENDIF
			NEXT
			IF 素材id:LOCAL == 0
				PRINTW 素材名称无效。
				RETURN -1
			ENDIF
			SETCOLOR 属性数値文字色変換(DT_CELL_GET("所持素材データベース", 素材id:LOCAL, "素材属性", 1))
			必要数 = TOINT(分割文字列:1)
			IF 必要数 >= 10 && 高速船記録:派遣先登録 == "巴鲁慈公国" && 高速船記録:派遣状態 == "派遣中"
				必要数 -= 3
			ENDIF
			PRINTFORML 　%分割文字列:0%：{必要数, 3}个 / 所持数{DT_CELL_GET("所持素材データベース", 素材id:LOCAL, "所持素材数", 1)}个
			RESETCOLOR
			IF DT_CELL_GET("所持素材データベース", 素材id:LOCAL, "所持素材数", 1) < 必要数
				素材不足フラグ = 1
			ENDIF
		NEXT
		PRINTL
		IF 素材不足フラグ
			PRINTL 素材不足。
		ELSE
			PRINTBUTTON "[0] 制造", 0
		ENDIF
		PRINTL
		PRINTBUTTON "[999] 返回", 999
		PRINTL
		BINPUT
		LOCAL = RESULT
		SELECTCASE LOCAL
			CASE 999
				;リスト表示に戻るだけなのでRETURNしない
			CASE 0
				FOR LOCAL, 0, 50
					SIF 作成素材登録:LOCAL == ""
						BREAK
					SPLIT 作成素材登録:LOCAL, "_", 分割文字列
					;素材消費
					必要数 = TOINT(分割文字列:1)
					IF 必要数 >= 10 && 高速船記録:派遣先登録 == "巴鲁慈公国" && 高速船記録:派遣状態 == "派遣中"
						必要数 -= 3
					ENDIF
					DT_CELL_SET "所持素材データベース", 素材id:LOCAL, "所持素材数", DT_CELL_GET("所持素材データベース", 素材id:LOCAL, "所持素材数", 1) - 必要数, 1
				NEXT
				;設計フラグを0にする
				DT_CELL_SET "所持乗機データベース", 表示id, "設計状態フラグ", 0, 1
				PRINTFORMW 已制造%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%。
		ENDSELECT
	CASE IS < 1000
		表示id = 表示用ステータス格納配列:(選択乗機番号 - 500):0
		LOCALS = <br>■%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%<br><br>
		TRYCALLFORM 乗機関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%("フレーバー")
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"

		LOCALS += @"・可用武装<br>"
		FOR 表示アビ番号, 0, 6
			SIF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "不可"
				CONTINUE
			IF DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1) == "固有"
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット")
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名")
				LOCALS += @"■%TSTR:コマンド名受渡%（专属武装）<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSEIF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				兵器id = TOINT(DT_CELL_GETS("所持乗機データベース", 表示id, @"武装{表示アビ番号}", 1))
				詳細文文字列受け渡し変数 = 
				IF ダンジョン表示モード == ""
					TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("残弾セット", 兵器id)
				ENDIF
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ名", 兵器id)
				LOCALS += @"■%TSTR:コマンド名受渡%<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("アビ説明文", 兵器id)
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ELSE
				LOCALS += @"■可更换装备<br>"
				TRYCALLFORM 乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 表示id, "乗機名", 1)%_{表示アビ番号}("装備条件説明文")
				LOCALS += @"%詳細文文字列受け渡し変数%<br>"
			ENDIF
		NEXT
		LOCALS += "<br><button value='999'>[999] 返回</button><br>"
		HTML_PRINT LOCALS
		BINPUT
		LOCAL = RESULT
		SELECTCASE LOCAL
			CASE 999
				;リスト表示に戻るだけなのでRETURNしない
		ENDSELECT
ENDSELECT
LOOP 1


@乗機兵装使用実処理
#DIM 表示兵装数
#DIM 兵装番号
#DIM 攻撃力_一時保存
#DIM 命中率_一時保存
#DIM INPUT保存
#DIMS 兵器名保存
;使用兵装表示から兵装実行処理まで全部ここで完結させる

;まずはメッセージ欄に使用可能兵装を並べる

CALL 口上変数初期化
VARSET 兵装関数名保存
VARSET RESULTS
LOCAL:1 = 0
表示兵装数 = ENUMFUNCBEGINSWITH(@"乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%_")
FOR LOCAL, 0, 表示兵装数
	TSTR:コマンド名受渡 = 
	アビテンプレート用_アビ封印用フラグ = 0
	兵装番号 = TOINT(REPLACE(RESULTS:LOCAL, @"乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%_", ""))
	IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
		CALLFORM %RESULTS:LOCAL%("アビ名", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
		CALLFORM %RESULTS:LOCAL%("アビ説明文", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
	ELSEIF DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1) == "固有"
		CALLFORM %RESULTS:LOCAL%("アビ名")
		CALLFORM %RESULTS:LOCAL%("アビ説明文")
	ELSE
		CONTINUE
	ENDIF
	IF TSTR:コマンド名受渡 != ""
		IF アビテンプレート用_アビ封印用フラグ || (乗機残弾種別:兵装番号 == 2 && 乗機残弾数:兵装番号 > 0) || (乗機残弾種別:兵装番号 != 2 && 乗機残弾数:兵装番号 < 1)
			IF アビ使用確認スキップフラグ == 0
				NSTR:(K++) = <font color='#%カラーパレット_HTML("グレーアウト")%'><button value = '{LOCAL}'>[{LOCAL}] %TSTR:コマンド名受渡, 26, LEFT%</button>　　%残弾種別名:(乗機残弾種別:兵装番号)%剩余：{乗機残弾数:兵装番号, 2}</font>　　
			ELSE
				NSTR:(K++) = <font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL}] %TSTR:コマンド名受渡, 26, LEFT%　　%残弾種別名:(乗機残弾種別:兵装番号)%剩余：{乗機残弾数:兵装番号, 2}</font>　　
			ENDIF
		ELSE
			NSTR:(K++) = <button value = '{LOCAL}'>[{LOCAL}] %TSTR:コマンド名受渡, 26, LEFT%　　%残弾種別名:(乗機残弾種別:兵装番号)%剩余：{乗機残弾数:兵装番号, 2}</button>　　
		ENDIF
		兵装関数名保存:LOCAL = %RESULTS:LOCAL%
		LOCAL:1 += 1
		SIF LOCAL:1 % 2 == 0
			NSTR:(K++) = <br>
	ENDIF
NEXT
NSTR:(K++) = <br>
SIF LOCAL:1 % 2
	NSTR:(K++) = <br>
NSTR:(K++) = <button value = '999'>[999] 返回</button>


CALL メッセージ欄表示用関数(, @"%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%",,0,1)

BINPUT
INPUT保存 = RESULT
SELECTCASE INPUT保存
	CASE 999
		RETURN 0
	CASE 0 TO 6
		CALL アビテンプレ変数リセット
		CALL 口上変数初期化
		VARSET RESULTS
		兵装番号 = TOINT(REPLACE(兵装関数名保存:INPUT保存, @"乗機兵装関数_%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%_", ""))

		IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
			CALLFORM %兵装関数名保存:INPUT保存%("アビ名", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
			兵器名保存 = %DT_CELL_GETS("所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)), "兵器名", 1)%
		ELSE
			CALLFORM %兵装関数名保存:INPUT保存%("アビ名")
			兵器名保存 = %TSTR:コマンド名受渡%
		ENDIF
		アビテンプレート用_アビ名 = %TSTR:コマンド名受渡%
		;ここに来てる時点で存在しないアビが選ばれてことは無いはずなので例外規定は作らない
		IF アビ使用確認スキップフラグ == 0
			KSTR:(K++) = ■%TSTR:コマンド名受渡%
			IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
				CALLFORM %兵装関数名保存:INPUT保存%("アビ説明文", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
			ELSE
				CALLFORM %兵装関数名保存:INPUT保存%("アビ説明文")
			ENDIF
			KSTR:(K++) = %詳細文文字列受け渡し変数%
			;改行の数をカウントし、必要数を改行して戻るボタンを表示行の最後に入れ込む
			STRCOUNT 詳細文文字列受け渡し変数, "<br>"
			IF RESULT < 4
				FOR LOCAL, 0, 4 - RESULT
					KSTR:(K++) = 　
				NEXT
			ENDIF
			IF アビテンプレート用_アビ封印用フラグ
				KSTR:(K++) = <nonbutton>无法使用</nonbutton> 　　　<button value='999'>[999]返回</button>
			ELSEIF (乗機残弾種別:兵装番号 == 2 && 乗機残弾数:兵装番号 > 0) || (乗機残弾種別:兵装番号 != 2 && 乗機残弾数:兵装番号 < 1)
				KSTR:(K++) = <nonbutton>未满足使用条件。</nonbutton> 　　<button value='999'>[999]返回</button>
			ELSE
				KSTR:(K++) = <button value='0'>[0]使用</button>　　　　　<button value='999'>[999]返回</button>
			ENDIF
				CALL メッセージ欄表示用関数(,,,0,1)
			$INPUT_LOOP3
			BINPUTS
			IF STRCOUNT(RESULTS, "サイド領域") > 0
				CALL サイド領域_実行処理_戦闘中
				CALL 画面再描画
				GOTO INPUT_LOOP3
			ENDIF
			RESULT = TOINT(RESULTS)
			SELECTCASE RESULT
				CASE 0
					IF (乗機残弾種別:兵装番号 == 2 && 乗機残弾数:兵装番号 > 0) || (乗機残弾種別:兵装番号 != 2 && 乗機残弾数:兵装番号 < 1)
						REUSELASTLINE 
						GOTO INPUT_LOOP3
					ENDIF
				CASE 999
					RESTART
				CASEELSE
					REUSELASTLINE 
					GOTO INPUT_LOOP3
			ENDSELECT
		ELSE
			IF (乗機残弾種別:兵装番号 == 2 && 乗機残弾数:兵装番号 > 0) || (乗機残弾種別:兵装番号 != 2 && 乗機残弾数:兵装番号 < 1)
				WSTR:(K++) = 未满足使用条件。
				CALL メッセージ欄表示用関数(, @"%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%",,0,1)
				RESTART
			ENDIF
		ENDIF
		CALL 口上変数初期化
		IF アビテンプレート用_アビ名 == 兵器名保存
			アビ汎用文字列:実行時メッセージ１ = %CALLNAME:PLAYER%は%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%の%アビテンプレート用_アビ名%を使った！
		ELSE
			アビ汎用文字列:実行時メッセージ１ = %CALLNAME:PLAYER%は%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%の%DT_CELL_GETS("所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)), "兵器名", 1)%を使った！
			アビ汎用文字列:実行時メッセージ２ = %アビテンプレート用_アビ名%！
		ENDIF
		;搭載兵器の補正を載せる
		IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
			;数字が入るのは搭載兵器装備時だけ
			攻撃力_一時保存 = BATTLE_STATE:9:攻撃力
			命中率_一時保存 = BATTLE_STATE:9:命中率
			BATTLE_STATE:9:攻撃力 += DT_CELL_GET("所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)), "攻撃力補正_強化後", 1)
			BATTLE_STATE:9:命中率 += DT_CELL_GET("所持兵器データベース", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)), "命中率補正_強化後", 1)
			CALLFORM %兵装関数名保存:INPUT保存%("アビ効果", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
			BATTLE_STATE:9:攻撃力 = 攻撃力_一時保存
			BATTLE_STATE:9:命中率 = 命中率_一時保存
		ELSE
			CALLFORM %兵装関数名保存:INPUT保存%("アビ効果")
		ENDIF
		IF アビテンプレート用_キャンセルフラグ
			アビテンプレート用_キャンセルフラグ = 0
			RESTART
		ELSE
			;残弾減少
			IF 乗機残弾種別:兵装番号 == 2
				;CT制の場合は残弾セット
				IF ISNUMERIC(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1))
					CALLFORM %兵装関数名保存:INPUT保存%("残弾セット", TOINT(DT_CELL_GETS("所持乗機データベース", 装備乗機ID, @"武装{兵装番号}", 1)))
				ELSE
					CALLFORM %兵装関数名保存:INPUT保存%("残弾セット")
				ENDIF
			ELSE
				;残弾制の場合は１減らす
				乗機残弾数:兵装番号 -= 1
			ENDIF
			;ターン中一回まで
			兵装使用済みフラグ = 1
			CALL 口上変数初期化
			CALL アビテンプレート用_表示メッセージ変換処理
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
ENDSELECT


;装備不可だと0、可能だと1を返す
@乗機装備条件判定(兵器id, 装備可能カテゴリ, 装備可能距離)
#FUNCTION
#DIM 兵器id
#DIMS 兵器文字列
#DIMS 装備可能カテゴリ
#DIMS 装備可能距離

兵器文字列 '= DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器カテゴリ", 1)
SIF 兵器文字列 != "汎用" && !搭載兵器タイプチェック(兵器文字列, 装備可能カテゴリ)
	RETURNF 0
兵器文字列 '= DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器距離適性", 1)
SIF !搭載兵器タイプチェック(兵器文字列, 装備可能距離)
	RETURNF 0
RETURNF 1


@搭載兵器タイプチェック(兵器文字列, 検索文字列)
#FUNCTION
#DIMS 兵器文字列
#DIMS 検索文字列
#DIM 終了フラグ
#DIM 文字位置, 2
#DIMS 検索部分文字列

終了フラグ = 1
文字位置:0 = 0
DO
	文字位置:1 = STRFINDU(検索文字列, "_", 文字位置:0)
	IF 文字位置:1 == -1
		終了フラグ = 0
		文字位置:1 = STRLENSU(検索文字列)
	ENDIF
	検索部分文字列 '= SUBSTRINGU(検索文字列, 文字位置:0, 文字位置:1 - 文字位置:0)
	SIF 検索部分文字列 != "" && STRFINDU(兵器文字列, 検索部分文字列) >= 0
		RETURNF 1
	文字位置:0 = 文字位置:1 + 1
LOOP 終了フラグ
RETURNF 0
