;-------------------------------------------------
;定居邀请
;-------------------------------------------------
@COMNAME604
#FUNCTIONS
TSTR:コマンド名受渡 = 定居邀请

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_604
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_604
LOCALS = <br>■定居邀请<br>
LOCALS += "邀请对方在居住区域定居的指令。<br>"
LOCALS += "成功时将在居住区域定居。<br>"
LOCALS += "<br>指令类型：日常互动<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM604

;定住エリアが一杯
IF 定住エリア満杯()
	PRINTW 居住区目前似乎已满员……
	RETURN -1
ENDIF

DRAWLINE
PRINTFORML 要邀请%CALLNAME:TARGET%在居住区域定居吗？
PRINTFORML 剩余房间数：{RFLAG:定住枠残りカウント, 3}间
DRAWLINE


PRINTBUTTON "[0]确定", 0
PRINTL
PRINTBUTTON "[1]取消", 1
PRINTL
$INPUT_LOOP
BINPUT

SELECTCASE RESULT
	CASE 0
		;好感度レベル80以上必要
		IF CFLAG:TARGET:好感度レベル < 関係閾値:4
			RFLAG:コマンド結果受渡し変数 = 0
		ELSE
			IF TALENT:TARGET:従業員
				FOR LOCAL, 0, 従業員部屋数
					IF 従業員部屋割り配列:LOCAL == TARGET
						従業員部屋割り配列:LOCAL = 0
						BREAK
					ENDIF
				NEXT
			ELSE
				CALL 滞在者部屋割り配列削除処理(TARGET)
			ENDIF
			RFLAG:コマンド結果受渡し変数 = 1
			TALENT:TARGET:定住者 = 1
			CFLAG:TARGET:長期雇用 = 0
			CFLAG:TARGET:滞在期間 = 999
			FOR LOCAL:1, 0, 住宅街広さ()
				IF 定住者部屋割り配列:(LOCAL:1) == ""
					定住者部屋割り配列:(LOCAL:1) = {TARGET}_
					BREAK
				ENDIF
			NEXT
		ENDIF
	CASE 1
		RETURN -1
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE604
#DIM ループ用
;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(604)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;居住区エリアがまだないと出ない
SIF MATCH(開拓エリア開発番号, 2) < 1
	RETURN 0
;既に定住者だと無し
SIF TALENT:TARGET:定住者
	RETURN 0
;成功するのは80以上だが、話題に出すのに好感度レベル50以上
SIF CFLAG:TARGET:好感度レベル < 関係閾値:3
	RETURN 0
RETURN 1

;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM604

IF RFLAG:コマンド結果受渡し変数
	;成功
	PRINTFORML %CALLNAME:TARGET%接受了%CALLNAME:PLAYER%的邀请，决定在居住区域定居！
ELSE
	;失敗
	PRINTFORML %CALLNAME:TARGET%似乎还没有在此定居的意愿……
ENDIF




@定住エリア満杯()
#FUNCTION

RFLAG:定住枠残りカウント = 0
FOR LOCAL, 0, 住宅街広さ()
	SIF 定住者部屋割り配列:LOCAL == ""
		RFLAG:定住枠残りカウント ++
NEXT

SIF RFLAG:定住枠残りカウント > 0
	RETURNF 0

RETURNF 1
