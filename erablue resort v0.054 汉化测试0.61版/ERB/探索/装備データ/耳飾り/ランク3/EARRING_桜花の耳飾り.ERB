
@耳飾り個別文章表示_樱花耳饰
LOCALS = 
LOCALS += @"■樱花耳饰<br><br>"
LOCALS += "装饰着樱花的精致耳饰。<br>"
LOCALS += "春日芬芳可激活土属性之力，也深受讲究穿搭的骑空士喜爱。<br>"
LOCALS += "作为通用装备在全空域广泛使用，也常见于地城掉落物中。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 27000

@耳飾りショップ簡易文章表示_樱花耳饰
詳細文文字列受け渡し変数 = 奥义威力+40\% + １个随机效果

@耳飾り基礎効果文章表示_樱花耳饰(耳飾り番号 = 0, 効果番号 = 0)
#DIM 耳飾り番号
#DIM 効果番号

LOCALS = 
LOCALS += "【属性变更】<br>"
LOCALS += "土<br><br>"
LOCALS += "【奥义修正】<br>"
LOCALS += "奥义威力+40%"
IF 耳飾り番号
	IF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自我回复")
		効果番号 = 1
	ELSEIF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自我攻击力强化")
		効果番号 = 2
	ELSEIF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自我奥义槽增加")
		効果番号 = 3
	ENDIF
ENDIF
IF 効果番号
	SELECTCASE 効果番号
		CASE 1
			LOCALS += "　自我回复 回复力x80%<br>"
			詳細文文字列受け渡し変数２ = 奥义威力+40\%　自我回复 回复力x80\%
		CASE 2
			LOCALS += "　自我攻击力强化 A槽+30% 3回合<br>"
			詳細文文字列受け渡し変数２ = 奥义威力+40\%　自我攻击力强化 Ａ槽+30\% ３回合
		CASE 3
			LOCALS += "　自我奥义槽增加 +20%<br>"
			詳細文文字列受け渡し変数２ = 奥义威力+40\%　自我奥义槽增加 +20\%
	ENDSELECT
ELSE
	LOCALS += "<br>以下效果随机获得一种，获取时决定<br>"
	LOCALS += "・自我回复 回复力x80%<br>"
	LOCALS += "・自我攻击力强化 A槽+30% 3回合<br>"
	LOCALS += "・自我奥义槽增加 +20%<br>"
	詳細文文字列受け渡し変数２ = 奥义威力+40\%　１个随机效果
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%
;ランダム効果数を返す
RETURN 3

@耳飾り出現率_樱花耳饰(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@耳飾りステータス決定_樱花耳饰(ARG = 0)

LOCALS = 奥义威力_
IF ARG
	LOCAL = ARG
ELSE
	LOCAL = RAND:3 + 1
ENDIF
SELECTCASE LOCAL
	CASE 1
		LOCALS += "自我回复"
	CASE 2
		LOCALS += "自我攻击力强化"
	CASE 3
		LOCALS += "自我奥义槽增加"
ENDSELECT

LOCAL = DT_ROW_ADD("所持耳飾りデータベース", "耳飾り名", "樱花耳饰", "変更属性", "土", "奥義補正", LOCALS)

@奥義威力_効果量_樱花耳饰
RETURN 40

;@奥義補正処理関数_(奥義補正名)_(耳飾り名)
;ここに効果処理を記述する

@奥義補正処理関数_自我回复_樱花耳饰
CALL アビ対象選択テンプレート_自己のみ
;回复力x80%
アビ汎用変数:効果割合 = 80
CALL アビテンプレート_回復処理_MAXでも回復

@奥義補正処理関数_自我攻击力强化_樱花耳饰
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果割合 = 30
アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = 3
CALL アビテンプレート_有利効果_バフ効果セット

@奥義補正処理関数_自我奥义槽增加_樱花耳饰
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 20
CALL アビテンプレート_有利効果_奥義ゲージUP
