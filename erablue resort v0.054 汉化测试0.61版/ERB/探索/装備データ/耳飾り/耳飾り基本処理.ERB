;NEWGAMEに突っ込む用


@汎用耳飾り取得処理(耳飾り名, ランダム性能固定フラグ = 0)
#DIMS 耳飾り名
#DIM ランダム性能固定フラグ

;最大個数制限
IF DT_ROW_LENGTH("所持指輪データベース") >= 耳飾り所持最大数
	PRINTFORMW 耳饰持有数量已超过上限（%TOFULL(TOSTR(耳飾り所持最大数))%个）。
	RETURN -1
ENDIF

;個別の耳飾りERBに飛んでステータス補正を入れ込む
TRYCCALLFORM 耳飾りステータス決定_%耳飾り名%(ランダム性能固定フラグ)
CATCH
	PRINTFORMW 未找到指定耳饰（%耳飾り名%, {ランダム性能固定フラグ}）。
	RETURN -1
ENDCATCH

@汎用耳飾り削除処理(耳飾りid番号)
#DIM 耳飾りid番号

IF DT_CELL_GETS("所持耳飾りデータベース", 耳飾りid番号, "耳飾り名", 1) != ""
	;装備してるキャラがいたらキャラんとこから番号削除
	IF DT_CELL_GET("所持耳飾りデータベース", 耳飾りid番号, "装備キャラ番号", 1)
		装備ステータス補正保存:(DT_CELL_GET("所持耳飾りデータベース", 耳飾りid番号, "装備キャラ番号", 1)):装備耳飾り番号 = 0
	ENDIF
	DT_ROW_REMOVE "所持耳飾りデータベース", 耳飾りid番号
ELSE
	PRINTW 未找到指定耳饰。
	RETURN -1
ENDIF

@耳飾り付け替え処理画面(キャラ番号)
#DIM キャラ番号
#DIM ページ番号
#DIMS ソート条件

;表示部分
;装備中耳飾り表示
DRAWLINE
詳細文文字列受け渡し変数 = 
TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:キャラ番号:装備耳飾り番号, "耳飾り名", 1)%(装備ステータス補正保存:キャラ番号:装備耳飾り番号)
LOCALS = <div rect='200,0,5662,1250' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='262, 62, 1125, 1125'>%顔グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += @"<div rect='1387,62,5537,1125'>■%CALLNAME:キャラ番号%当前装备的耳饰<br>"
IF 詳細文文字列受け渡し変数 == ""
	LOCALS += "无</div>"
ELSE
	LOCALS += @"%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:キャラ番号:装備耳飾り番号, "耳飾り名", 1)%<br>%詳細文文字列受け渡し変数%</div>"
ENDIF
;所持耳飾りリスト表示
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
CALL 所持耳飾りリスト表示(装備ステータス補正保存:キャラ番号:装備耳飾り番号, ページ番号, , ソート条件)

;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]上一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += @"- PAGE {ページ番号 + 1, 2, LEFT} -　　"
IF DT_ROW_LENGTH("所持耳飾りデータベース") > 30 + (ページ番号 * 29)
	LOCALS += "<button value='1001'>[1001]下一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 装備ステータス補正保存:キャラ番号:装備耳飾り番号 > 0
	LOCALS += "　　　　　　<button value='1002'>[1002]卸下装备</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='9999'>[9999]返回</button></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 45
	PRINTL
NEXT

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE -1
		IF ソート条件 == "耳飾り名 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 耳飾り名 ASC
		ENDIF
		RESTART
	CASE -2
		IF ソート条件 == "変更属性 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 変更属性 ASC
		ENDIF
		RESTART
	CASE -3
		IF ソート条件 == "奥義補正 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 奥義補正 ASC
		ENDIF
		RESTART
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE 1000
		ページ番号 -= 1
		RESTART
	CASE 1001
		ページ番号 += 1
		RESTART
	CASE 1002
		DT_CELL_SET "所持耳飾りデータベース", 装備ステータス補正保存:キャラ番号:装備耳飾り番号, "装備キャラ番号", 0, 1
		装備ステータス補正保存:キャラ番号:装備耳飾り番号 = 0
		CALL 装備再計算処理(キャラ番号)
		PRINTFORMW %CALLNAME:キャラ番号%卸下了耳饰。
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE IS < 500
		IF 装備ステータス補正保存:キャラ番号:装備耳飾り番号 > 0
			DT_CELL_SET "所持耳飾りデータベース", 装備ステータス補正保存:キャラ番号:装備耳飾り番号, "装備キャラ番号", 0, 1
		ENDIF
		LOCAL:1 = DT_CELL_GET("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", 1)
		IF LOCAL:1 > 0
			装備ステータス補正保存:(LOCAL:1):装備耳飾り番号 = 0
			CALL 装備再計算処理(LOCAL:1)
		ENDIF
		装備ステータス補正保存:キャラ番号:装備耳飾り番号 = 表示用ステータス格納配列:LOCAL:0
		DT_CELL_SET "所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", キャラ番号, 1
		CALL 装備再計算処理(キャラ番号)
		PRINTFORMW %CALLNAME:キャラ番号%装备了%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1)%。
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE IS < 1000
		TRYCALLFORM 耳飾り個別文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:(LOCAL - 500):0, "耳飾り名", 1)%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:(LOCAL - 500):0, "耳飾り名", 1)%(表示用ステータス格納配列:(LOCAL - 500):0)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
		RESTART
ENDSELECT
LOOP 1

@所持耳飾りリスト表示(選択中耳飾りid, ページ番号, y軸位置 = 1375, ソート条件 = "", テンプレフラグ = -1)
#DIM 選択中耳飾りid
#DIM ページ番号
#DIM y軸位置
#DIM テンプレフラグ
#DIMS ソート条件
#DIMS ソート配列, キャラクタ数上限
#DIM 編成枠番号

;ページめくり用にステータス格納
IF ソート条件 != ""
	IF ソート条件 == "奥義補正 ASC"
		VARSET ソート配列
		ソート配列:0 = 0
		FOR LOCAL, 1, DT_ROW_LENGTH("所持耳飾りデータベース")
			表示用ステータス格納配列:LOCAL:0 = DT_CELL_GET("所持耳飾りデータベース", LOCAL, "id")
			TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", LOCAL, "耳飾り名")%(DT_CELL_GET("所持耳飾りデータベース", LOCAL, "id"))
			ソート配列:LOCAL = %詳細文文字列受け渡し変数２%
		NEXT
		ARRAYMSORT ソート配列, 表示用ステータス格納配列
	ELSE
		FOR LOCAL, 0, DT_SELECT("所持耳飾りデータベース", , ソート条件)
			表示用ステータス格納配列:LOCAL:0 = RESULT:(LOCAL + 1)
		NEXT
	ENDIF
ELSE
	FOR LOCAL, 0, DT_ROW_LENGTH("所持耳飾りデータベース")
		表示用ステータス格納配列:LOCAL:0 = DT_CELL_GET("所持耳飾りデータベース", LOCAL, "id")
	NEXT
ENDIF

LOCALS = <div rect='262,{y軸位置},5537,3625'><nobr>
LOCALS += "　　　<button value='-1'>[耳饰名称]</button> 　　　　　　　 <button value='-2'>[属性]</button><button value='-3'>[奥义修正]</button> 　　　　　　　　　　　　　　　　　　　　　　　装备者<br>"
LOCALS += "--------------------------------------------------------------------------------------------------------------<br>"
IF 選択中耳飾りid > 0
	LOCAL:1 = 選択中耳飾りid
	FOR LOCAL, 1 + (ページ番号 * 29), 30 + (ページ番号 * 29)
		IF DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1) == ""
			BREAK
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>[详情]</button> "
		IF LOCAL:1 == 表示用ステータス格納配列:LOCAL:0
			LOCALS += "<font color='#CCCC00'>"
		ENDIF
		LOCALS += @"<button value='{LOCAL}'>"
		LOCALS += @"%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1), 26, LEFT%"
		LOCALS += @"%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "変更属性", 1), 6, LEFT%"
		TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1)%(表示用ステータス格納配列:LOCAL:0)
		LOCALS += @"%詳細文文字列受け渡し変数２, 56, LEFT%"
		IF テンプレフラグ >= 0
			FOR 編成枠番号, 0, 14
				SIF 編成枠番号 > 3 && 編成枠番号 < 10
					CONTINUE
				IF 装備テンプレ記録配列:テンプレフラグ:(編成枠番号 * 10 + 1) == 表示用ステータス格納配列:LOCAL:0
					LOCALS += @"編成{編成枠番号}"
				ENDIF
			NEXT
		ELSE
			IF DT_CELL_GET("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", 1)
				LOCALS += @"%CALLNAME:(DT_CELL_GET("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", 1))%"
			ENDIF
		ENDIF
		LOCALS += "</button>"
		IF LOCAL:1 == 表示用ステータス格納配列:LOCAL:0
			LOCALS += "</font>"
		ENDIF
		LOCALS += "<br>"
	NEXT
ELSE
	FOR LOCAL, 1 + (ページ番号 * 29), 30 + (ページ番号 * 29)
		IF DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1) == ""
			BREAK
		ENDIF
		IF MATCH(選択配列, 表示用ステータス格納配列:LOCAL:0)
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>"
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>[详情]</button> "
		LOCALS += @"<button value='{LOCAL}'>"
		LOCALS += @"%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1), 26, LEFT%"
		LOCALS += @"%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "変更属性", 1), 6, LEFT%"
		TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1)%(表示用ステータス格納配列:LOCAL:0)
		LOCALS += @"%詳細文文字列受け渡し変数２, 56, LEFT%"
		IF テンプレフラグ >= 0
			FOR 編成枠番号, 0, 14
				SIF 編成枠番号 > 3 && 編成枠番号 < 10
					CONTINUE
				IF 装備テンプレ記録配列:テンプレフラグ:(編成枠番号 * 10 + 1) == 表示用ステータス格納配列:LOCAL:0
					LOCALS += @"編成{編成枠番号}"
				ENDIF
			NEXT
		ELSE
			IF DT_CELL_GET("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", 1)
				LOCALS += @"%CALLNAME:(DT_CELL_GET("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "装備キャラ番号", 1))%"
			ENDIF
		ENDIF
		IF MATCH(選択配列, 表示用ステータス格納配列:LOCAL:0)
			LOCALS += "</font>"
		ENDIF
		LOCALS += "</button><br>"
	NEXT
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS, 1

@所持耳飾りリスト表示_USERCOM(戻るボタンフラグ = 0)
#DIM 戻るボタンフラグ
#DIM ページ番号
#DIMS ソート条件

LOCALS = 
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
HTML_PRINT LOCALS, 1
CALL 所持耳飾りリスト表示(0, ページ番号, , ソート条件)
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]上一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += @"- PAGE {ページ番号 + 1, 2, LEFT} -　　"
IF DT_ROW_LENGTH("所持耳飾りデータベース") > 30 + (ページ番号 * 29)
	LOCALS += "<button value='1001'>[1001]下一页</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 戻るボタンフラグ
	LOCALS += "<button value='9999'>[9999] 返回</button>"
ENDIF
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 45
	PRINTL
NEXT
BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	ページ番号 = 0
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE -1
		IF ソート条件 == "耳飾り名 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 耳飾り名 ASC
		ENDIF
	CASE -2
		IF ソート条件 == "変更属性 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 変更属性 ASC
		ENDIF
	CASE -3
		IF ソート条件 == "奥義補正 ASC"
			ソート条件 = 
		ELSE
			ソート条件 = 奥義補正 ASC
		ENDIF
	CASE 1000
		ページ番号 -= 1
	CASE 1001
		ページ番号 += 1
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN -1
	CASE IS < 500
		TRYCALLFORM 耳飾り個別文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1)%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:LOCAL:0, "耳飾り名", 1)%(表示用ステータス格納配列:LOCAL:0)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
	CASE IS < 1000
		TRYCALLFORM 耳飾り個別文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:(LOCAL - 500):0, "耳飾り名", 1)%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 表示用ステータス格納配列:(LOCAL - 500):0, "耳飾り名", 1)%(表示用ステータス格納配列:(LOCAL - 500):0)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
ENDSELECT
