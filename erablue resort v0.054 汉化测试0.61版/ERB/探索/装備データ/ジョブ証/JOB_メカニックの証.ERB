@証関数_机工士之证(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = 机工士之证
	CASE "クラス名"
		; SIF 対象キャラ <= 0
		; 	RETURN 0
		; SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
		; 	CASEELSE
		; 		RESULTS = メカニック
		; 		RESULTS:1 = Extra.I
		; 		RETURN 1
		; ENDSELECT
		RETURN 0
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "专精于机械操控的职业证。<br>"
		詳細文文字列受け渡し変数 += "通过牺牲ＨＰ换取能量，从而能够执行各种行动。<br>"
		詳細文文字列受け渡し変数 += "装备时在迷宫探索中可获得与「机械知识Lv1」相同的效果。<br>"
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「バルツ地下廃工場」のイベントで入手）
;@証出現率_机工士之证(入手タイプ, 装備ランク)

@証アビ関数_机工士之证_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフ番号
#DIM CONST 消費ＭＰ = 100

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 能量获取
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■能量获取<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：将自身当前ＨＰ减半，获得［１０］点能量。（最大１０）<br>"
		詳細文文字列受け渡し変数 += "　　　　使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%将自身ＨＰ转化为能量！
		BATTLE_STATE:戦闘行動キャラ:ＨＰ = MAX(1, BATTLE_STATE:戦闘行動キャラ:ＨＰ / 2)
		アビテンプレート用_行動消費無しフラグ = 1
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:累積上限_固定値 = 10
		アビ汎用文字列:バフ・デバフ対象能力 = エネルギー_メカニック
		CALL アビテンプレート_刻印追加(10)
ENDSELECT

@証アビ関数_机工士之证_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 进攻指令
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■进攻指令<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：消耗［２］点能量。<br>"
		詳細文文字列受け渡し変数 += "　　　　对敌方单体造成［攻击力✕200％］的自身属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(2)
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 200
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_机工士之证_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = 防守指令
		ELSEIF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = 防守指令
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("防御")
			TSTR:コマンド名受渡 = 防守指令
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■防守指令（装备者持有机械知识Lv2以上或为防御类型时）<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：消耗［４］点能量。<br>"
		詳細文文字列受け渡し変数 += "　　　　赋予我方全体1次伤害减免［50％］效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(4)
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_机工士之证_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = 辐光指令
		ELSEIF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = 辐光指令
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("回復")
			TSTR:コマンド名受渡 = 辐光指令
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■辐光指令（装备者持有机械知识Lv2以上或为恢复类型时）<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：消耗［３］点能量。<br>"
		詳細文文字列受け渡し変数 += "　　　　赋予我方全体3回合内再生［恢复力✕10％（上限500）/B槽］效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(3)
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = MIN(RESULT * 10 / 100, 500)
		アビ汎用文字列:バフ・デバフ枠 = 再生B
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_机工士之证_5(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフ番号
#DIM 消費エネルギー
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = 充能指令
		ELSEIF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = 充能指令
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("奥義")
			TSTR:コマンド名受渡 = 充能指令
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■充能指令（装备者持有机械知识Lv2以上或为奥义类型时）<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：消耗全部能量。<br>"
		詳細文文字列受け渡し変数 += "　　　　赋予自身1次奥义威力提升［消耗能量✕10％/A槽］效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		消費エネルギー = 刻印現在数確認("エネルギー_メカニック", 戦闘行動キャラ)
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = エネルギー_メカニック
		CALL アビテンプレート_刻印減少(消費エネルギー)
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 消費エネルギー * 10
		アビ汎用文字列:バフ・デバフ枠 = ダメージ補正_奥義ダメージA
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_机工士之证_6(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		IF 対象キャラ <= 0
			TSTR:コマンド名受渡 = 爆破指令
		ELSEIF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = 爆破指令
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
			TSTR:コマンド名受渡 = 爆破指令
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■爆破指令（装备者持有机械知识Lv2以上或为攻击类型时）<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：消耗［３］点能量。<br>"
		詳細文文字列受け渡し変数 += "　　　　对敌方单体造成［自身等级✕50］的自属性固定伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(3)
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果量 = BATTLE_STATE:戦闘行動キャラ:Lv * 50
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@エネルギー_メカニック消費(消費エネルギー)
#DIM 消費エネルギー
#DIM バフ番号
#DIM エネルギー不足

IF 刻印現在数確認("エネルギー_メカニック", 戦闘行動キャラ) < 消費エネルギー
	CALL 口上変数初期化
	WSTR:(K++) = 能量不足。
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔",,,0,1)
	アビテンプレート用_キャンセルフラグ = 1
ELSE
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = エネルギー_メカニック
	CALL アビテンプレート_刻印減少(消費エネルギー)
ENDIF

CALL アビ汎用変数文字列リセット

