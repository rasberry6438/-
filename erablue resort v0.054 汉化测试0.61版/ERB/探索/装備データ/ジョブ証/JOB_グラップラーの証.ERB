@証関数_格斗家之证(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = 格斗家之证
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = 战鬼
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = 功夫大师
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = 格斗家
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "舍弃武器、仅锤炼自身肉体的格斗家之证。<br>"
		詳細文文字列受け渡し変数 += "这条道路漫长而艰险，因此立志者极为稀少。<br>"
		詳細文文字列受け渡し変数 += "装备时，仿佛能触及武道之心的真谛。<br>"
ENDSELECT

@証出現率_格斗家之证(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_格斗家之证_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "舍身之型"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 65
	CASE 2
		効果数値:0 = 60
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：5回合内自身攻击力增加［{効果数値:0}％/A槽］。<br>"
		詳細文文字列受け渡し変数 += "　　　　5回合内自身防御力下降［20％/A槽］。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_格斗家之证_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "反击"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "格闘")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("回避")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 4
		効果数値:2 = 100
	CASE 2
		効果数値:0 = 150
		効果数値:1 = 3
		効果数値:2 = 0
	CASEELSE
		効果数値:0 = 100
		効果数値:1 = 3
		効果数値:2 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者为格斗专精或回避类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：使自身回避率在承受{効果数値:1}次攻击前提升［+100％/A槽］。<br>"
		詳細文文字列受け渡し変数 += @"　　　　赋予自身{効果数値:1}次回避时反击［攻击力✕{効果数値:0}％］效果。"
		IF 効果数値:2 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　使自身受到的敌意值至下次行动前提升［+{効果数値:2}/C槽］。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:持続回数 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回避反撃
		アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:持続回数 = 効果数値:1
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 効果数値:2 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 敵対心C
			アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
			アビ汎用変数:効果割合 = 効果数値:2
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_格斗家之证_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "练气"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "格闘")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 17
		効果数値:1 = 50
	CASE 2
		効果数値:0 = 13
		効果数値:1 = 50
	CASEELSE
		効果数値:0 = 10
		効果数値:1 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者为格斗专精或连击类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：5回合内自身攻击力增加［{効果数値:0}％/独立槽］。<br>"
		詳細文文字列受け渡し変数 += @"　　　　该攻击力提升可累积至［{効果数値:1}％］。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 練気_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:累積上限_割合 = 効果数値:1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_累積バフ効果セット
ENDSELECT

@証アビ関数_格斗家之证_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "斗气"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 200
		効果数値:1 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：对敌方单体造成［攻击力✕{効果数値:0}％］的自身属性伤害。<br>"
		詳細文文字列受け渡し変数 += "　　　　该技能无视对方防御力。<br>"
		詳細文文字列受け渡し変数 += "　　　　使自身奥义槽累积［+{効果数値:1}％］。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 効果数値:1
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT
