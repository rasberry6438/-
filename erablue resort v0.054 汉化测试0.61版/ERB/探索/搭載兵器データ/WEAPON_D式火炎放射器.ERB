@搭載兵器関数_D式火焰喷射器(処理モード, 兵器id)
#DIMS 処理モード
#DIM 兵器id
#DIM 兵器_攻撃力補正
#DIM 兵器_命中率補正
#DIM 兵器_攻撃力変動
#DIM 兵器_命中率変動
#DIM 兵器_残弾種別
#DIM 兵器_残弾数
#DIMS 兵器_カテゴリ
#DIMS 兵器_距離適性
#DIMS 兵器名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "兵器登録"
		兵器名一時保存 = D式火焰喷射器
		;攻撃力補正、命中率補正は「その兵器を使用した時に補正を掛ける数値」
		;攻撃力変動、命中率変動は「その兵器を装備しているだけで乗機のステータスを変える数値」
		兵器_攻撃力補正 = 0
		兵器_命中率補正 = -10
		兵器_攻撃力変動 = 0
		兵器_命中率変動 = 0
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		兵器_残弾種別 = 1
		兵器_残弾数 = 5
		;カテゴリ 基本は「機械系」と「魔法系」の二択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;距離適性 基本は「近距離」と「遠距離」と「味方支援」の三択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;複数カテゴリをもたせる場合は「近距離_遠距離」のように記載
		兵器_カテゴリ = 機械系
		兵器_距離適性 = 遠距離

		{
		DT_ROW_ADD "所持兵器データベース", "兵器名", 兵器名一時保存, 
			"攻撃力補正", 兵器_攻撃力補正, "攻撃力補正_強化後", 兵器_攻撃力補正, 
			"命中率補正", 兵器_命中率補正, "命中率補正_強化後", 兵器_命中率補正, 
			"攻撃力変動値", 兵器_攻撃力変動, "攻撃力変動値_強化後", 兵器_攻撃力変動, 
			"命中率変動値", 兵器_命中率変動, "命中率変動値_強化後", 兵器_命中率変動, 
			"残弾種別", 兵器_残弾種別, "残弾数", 兵器_残弾数, "残弾数_強化後", 兵器_残弾数, 
			"兵器カテゴリ", 兵器_カテゴリ, "兵器距離適性", 兵器_距離適性,
		}
	CASE "フレーバー"
		LOCALS += "向广域范围散布火焰的喷射器。<br>"
		LOCALS += "因燃料体积较大，探索中无法重新装填。<br>"
		LOCALS += "但相应地，攻击力较高。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 铁矿石_2
		作成素材登録:1 = 赤热矿_5
		作成素材登録:2 = 陈旧齿轮_5
	CASE "アビ名"
		TSTR:コマンド名受渡 = D式火焰喷射器
	CASE "アビ説明文"
		;残弾などの説明文字列は機体側で記述
		LOCALS += "　效果：随机对敌人造成4次[攻击力✕110%]的火属性伤害。<br>"
		LOCALS += "　　　　搭载替补成员时，根据替补成员的最大等级÷4增加伤害系数。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		FOR LOCAL, 0, 4
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 110
			アビ汎用変数:効果割合 += サブメンバーステータス取得("Lv") / 4
			CALL アビテンプレート_ダメージ処理_火属性
		NEXT
ENDSELECT

@兵器開発対象_D式火焰喷射器(処理モード)
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "兵装名"
		RESULTS = D式火焰喷射器
		RETURN
	CASE "優先度"
		SIF TARGET <= 0
			RETURN 0
		FOR LOCAL, 1, VARSIZE("TARGET")
			SIF TARGET:LOCAL <= 0
				BREAK
			SIF !COMABLEキャラ個別_382_1_11(TARGET:LOCAL)
				CONTINUE
			SIF !キャラ一致チェック(TARGET:LOCAL, "[火焰女仆]多萝西")
				CONTINUE

			RETURN 100
		NEXT
		RETURN 0
	CASE "複雑さ"
		RETURN 5
	CASE "要求知識"
		RETURN GETNUM(知識素質, "機械知識")
ENDSELECT
