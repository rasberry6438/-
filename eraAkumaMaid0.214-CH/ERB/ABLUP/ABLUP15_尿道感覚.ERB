;-------------------------------------------------
;ABL:尿道感覚 のLVUP処理
;-------------------------------------------------
@ABLUP15
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP15
;何もできない場合
IF RESULT == 0 && ABL:尿道感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE

VARSET NUM_JUEL
NUM_CHOICES = 0
IF ABL:尿道感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:尿道経験
	CASE IS >= EXP_LV(ABL:尿道感覚 + 3)
		PRINTFORML ※尿道經驗非常的多、要求的寶珠減少80％※
	CASE IS >= EXP_LV(ABL:尿道感覚 + 2)
		PRINTFORML ※尿道經驗很多、要求的寶珠減少50％※
	CASE IS >= EXP_LV(ABL:尿道感覚 + 1)
		PRINTFORML ※尿道經驗較多、要求的寶珠減少20％※
	;著しく足りていない
	CASE IS < MAX(EXP_LV(ABL:尿道感覚), 1)
		PRINTFORML ※尿道經驗太少、要求的寶珠增加3倍※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;尿道狂を得る
IF TALENT:尿道狂 == 0
	IF COND("尿道狂取得可能")
		PRINTFORML  [10] 獲得[尿道狂]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[尿道狂](要:快尿の珠{NUM("宝珠：尿道狂取得")}、尿道感覚4LV、今回尿道絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快尿 >= NUM("宝珠：尿道狂取得") && TALENT:尿道狂 == 0
		JUEL:快尿 -= NUM("宝珠：尿道狂取得")
		TALENT:尿道狂 = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		IF TALENT:恋慕 || TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%在激烈的性愛結束後、用柔軟嫵媚的語気懇求%CALLNAME:MASTER%継続蹂躪她的尿道……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%一邊用手指愛撫著被擴張的尿道口、一邊將渴求而朦朧的視線集中在%CALLNAME:MASTER%身上……
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[尿道狂]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:尿道感覚 += 1
		PRINTFORMW 尿道感覚等級提升到{ABL:尿道感覚}LV了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP15
SIF RESULT
	RESTART



;ABL:尿道感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP15, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:尿道感覚
CASE 0
	JUEL:0:快尿 = 500
CASE 1
	JUEL:0:快尿 = 1000
CASE 2
	JUEL:0:快尿 = 3000
CASE 3 TO 4
	JUEL:0:快尿 = (ABL:尿道感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快尿 = 10000 + (ABL:尿道感覚 - 4) * 10000
CASEELSE
	JUEL:0:快尿 = 70000 + (ABL:尿道感覚 - 9) * 30000
ENDSELECT

;尿道経験による軽減(増えづらい経験なので回数はＶやＡより格段に少ない)
SELECTCASE EXP:尿道経験
CASE IS >= EXP_LV(ABL:尿道感覚 + 3)
	TIMES JUEL:0:快尿, 0.20
CASE IS >= EXP_LV(ABL:尿道感覚 + 2)
	TIMES JUEL:0:快尿, 0.50
CASE IS >= EXP_LV(ABL:尿道感覚 + 1)
	TIMES JUEL:0:快尿, 0.80
;著しく足りていない
CASE IS < MAX(EXP_LV(ABL:尿道感覚), 1)
	TIMES JUEL:0:快尿, 3.00
ENDSELECT


;尿道狂が獲得できる？
SIF COND("尿道狂取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:尿道感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快尿 && JUEL:快尿 >= JUEL:0:快尿
	RETURN 1
RETURN 0
