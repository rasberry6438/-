;-------------------------------------------------
;ABL:Ｍ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP14
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP14
;何もできない場合
IF RESULT == 0 && ABL:Ｍ感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｍ感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:接吻経験
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 3)
		PRINTFORML ※接吻經驗非常的多、要求的寶珠減少80％※
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 2)
		PRINTFORML ※接吻經驗很多、要求的寶珠減少50％※
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 1)
		PRINTFORML ※接吻經驗較多、要求的寶珠減少20％※
	;著しく足りていない
	CASE IS < MAX(EXP_LV(ABL:Ｍ感覚), 1)
		PRINTFORML ※接吻經驗太少、要求的寶珠增加3倍※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;蕩唇を得る
IF TALENT:蕩唇 == 0
	IF COND("蕩唇取得可能")
		PRINTFORML  [10] 獲得[蕩唇]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[蕩唇](要:快Ｍの珠{NUM("宝珠：蕩唇取得")}、Ｍ感覚4LV、今回Ｍ絶頂数10)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｍ >= NUM("宝珠：蕩唇取得") && TALENT:蕩唇 == 0
		JUEL:快Ｍ -= NUM("宝珠：蕩唇取得")
		TALENT:蕩唇 = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		PRINTFORMW %CALLNAME:TARGET%在性行為結束後、將拖著筋疲力竭的身體整理殘局的%CALLNAME:MASTER%抱在懷裡、強硬的索取著舌吻……
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[蕩唇]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｍ感覚 += 1
		PRINTFORMW Ａ感覚等級提升到{ABL:Ｍ感覚}LV了。
	ENDIF
ENDSELECT

;喉性感の獲得
IF TALENT:喉性感 == 0 && ABL:Ｍ感覚 + TALENT:Ｍ性向 >= 5
	PRINTFORM %CALLNAME:TARGET%
	CALL PRINT_COLORTEXT, "[喉性感]", DEF_COLOR("黄色")
	PRINTFORML 獲得了
	TALENT:喉性感 = 1
	FORCEWAIT
ENDIF

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP14
SIF RESULT
	RESTART



;ABL:Ｍ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP14, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｍ感覚
CASE 0
	JUEL:0:快Ｍ = 500
CASE 1
	JUEL:0:快Ｍ = 1000
CASE 2
	JUEL:0:快Ｍ = 3000
CASE 3 TO 4
	JUEL:0:快Ｍ = (ABL:Ｍ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｍ = 10000 + (ABL:Ｍ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｍ = 70000 + (ABL:Ｍ感覚 - 9) * 30000
ENDSELECT

;接吻経験による軽減
SELECTCASE EXP:接吻経験
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 3)
	TIMES JUEL:0:快Ｍ, 0.20
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 2)
	TIMES JUEL:0:快Ｍ, 0.50
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 1)
	TIMES JUEL:0:快Ｍ, 0.80
;著しく足りていない
CASE IS < MAX(EXP_LV(ABL:Ｍ感覚), 1)
	TIMES JUEL:0:快Ｍ, 3.00
ENDSELECT


;蕩唇が獲得できる？
SIF COND("蕩唇取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｍ感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快Ｍ && JUEL:快Ｍ >= JUEL:0:快Ｍ
	RETURN 1
RETURN 0
