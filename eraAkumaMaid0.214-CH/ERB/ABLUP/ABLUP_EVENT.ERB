;TARGETの素質変動など
@EVENTCHECK_T
#DIM ADDCOND_RESULT
;-------------------------------------------------------------------------------------------------
;技能の成長
;-------------------------------------------------------------------------------------------------
CALL EX_ABLUP, TARGET

;-------------------------------------------------------------------------------------------------
;ネガティブな素質の克服
;-------------------------------------------------------------------------------------------------
CALL SETFLAG, "叛逆消去", TARGET

CALL EVENT_NEXTTIME

;恋慕と淫乱の自動取得
IF COND("恋慕と淫乱の自動獲得可能")
	IF TALENT:恋慕 == 0
		STR:実行条件 = 
		RESULT = 0
		TRYCALLFORM ADDCOND_K{NO:TARGET}("恋慕")
		ADDCOND_RESULT = RESULT
		SIF STR:実行条件 == "" && (COND("恋慕取得可能") || ADDCOND_RESULT)
			CALL SETFLAG, "恋慕"
	ENDIF
	IF TALENT:淫乱 == 0 && COND("恋慕と淫乱の自動獲得可能")
		STR:実行条件 = 
		RESULT = 0
		TRYCALLFORM ADDCOND_K{NO:TARGET}("淫乱")
		ADDCOND_RESULT = RESULT
		SIF STR:実行条件 == "" && (COND("淫乱取得可能") || ADDCOND_RESULT)
			CALL SETFLAG, "淫乱"
	ENDIF
ENDIF

;同居できるよう变成了か格子
CALL CHECK_GOUI, "同居", TARGET

;其他合意格子
CALL CHECK_GOUI, "汎用", TARGET

;助手化が可能变成了か
IF COND("助手可能") == 0 && ABL:信頼 >= 5 && ABL:技巧 >= 3
	CALL PRINT_STR, @"%CALLNAME:TARGET%_黄色_成为了%TEXTS("助手の名称")%_ _W"
	CFLAG:助手可能 = 1
ENDIF



;MASTERの素質変動など
@EVENTCHECK_M
;堕とした人数が1人以上で、謎之魅力が付く
IF FLAG:この周での陥落人数 && TALENT:MASTER:謎之魅力 == 0
	PRINTFORMW 在経験的积累下%CALLNAME:MASTER%獲得了【謎之魅力】
	TALENT:MASTER:謎之魅力 = 1
ENDIF
;Ａ経験５００以上、Ａ拡張経験３５以上で尻穴狂い化
IF EXP:MASTER:Ａ経験 >= 500 && EXP:MASTER:Ａ拡張経験 >= 35 && TALENT:MASTER:尻穴狂い == 0
	PRINTFORMW 身体的情况有点奇怪…
	PRINTFORML 屁股又热又舒服、疼得受不了。
	PRINTFORMW 看起来在累积的刺激下%CALLNAME:MASTER%觉醒了自身新的快楽…
	PRINTFORMW %CALLNAME:MASTER%獲得了【尻穴狂い】
	TALENT:MASTER:尻穴狂い = 1
ENDIF
;Ｖ経験５００以上で淫壷化
IF EXP:MASTER:Ｖ経験 >= 500 && TALENT:MASTER:淫壷 == 0
	PRINTFORMW 身体的情况有点奇怪…
	PRINTFORML 子宮附近又热又舒服、疼得受不了。
	PRINTFORMW 看起来在累积的刺激下%CALLNAME:MASTER%觉醒了自身新的快楽…
	PRINTFORMW %CALLNAME:MASTER%獲得了【淫壷】
	TALENT:MASTER:淫壷 = 1
ENDIF
;経験値によって復活がレベルアップ
IF EXP:MASTER:経験値 >= 300 && TALENT:MASTER:復活 < 200
	TALENT:MASTER:復活 = 200
	PRINTFORMW 感觉从身体深处涌出了活力……
	CALL PRINT_STR, @"在経験的积累下%CALLNAME:MASTER%獲得了_黄色_[%TALENT_NAME(GETNUM(TALENT, "復活"), TALENT:MASTER:復活)%]_"
	FORCEWAIT
ENDIF

CALL ABLUP_MASTER, "通常"


;MASTERの技能獲得
@ABLUP_MASTER, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE
#DIM MAXABL_TECH
#DIM MEMO_MASTERABL
#DIM AVERAGE_TECH
#DIM MEMO_FLAG

MEMO_LINECOUNT = LINECOUNT

SELECTCASE ARGS
CASE "通常"
	IF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0
		ABL:MASTER:Ｖ拡張 = CALCF("Ｖ拡張の上限", MASTER)
		ABL:MASTER:Ａ拡張 = CALCF("Ａ拡張の上限", MASTER)
	ENDIF
	CALL EX_ABLUP, MASTER
CASE "週末", "能力変更"
	REDRAW 0

	;技巧の現在値記録
	MEMO_MASTERABL = ABL:MASTER:技巧

	$START
	VARSET LOCAL, 0, 0, 100
	SELECTCASE ARGS
	CASE "週末"
		PRINTFORML 为了下周、进行%CALLNAME:MASTER%的能力的LV変更
		PRINTFORML 请在可能的範囲内自由変更
		PRINTFORML (在能力の表示上昇画面里点进来、什么時候都可以进行)
	CASEELSE
		PRINTFORML 进行%CALLNAME:MASTER%的能力的LV変更
		PRINTFORML 请在可能的範囲内自由変更
	ENDSELECT
	CALL PRINT_STR, "イエロー_※技巧LV的上昇是指靈活等、～使い的LV的合計满足必要値的条件※_L_"

	MEMO_FLAG = FLAG:項目OFF
	CLEARBIT FLAG:項目OFF, 12
	CALL NEW_PRINT_ABL, MASTER
	FLAG:項目OFF = MEMO_FLAG

	DRAWLINE

	;技巧ＬＶが上げられるかどうか
	SELECTCASE ABL:MASTER:舌靈活+ABL:MASTER:指靈活+ABL:MASTER:腰靈活+ABL:MASTER:魔乳
	CASE IS <= 20
		MAXABL_TECH = LIMIT( (ABL:MASTER:舌靈活+ABL:MASTER:指靈活+ABL:MASTER:腰靈活+ABL:MASTER:魔乳)/2, COND("初期技巧", MASTER), 99)
	CASEELSE
		MAXABL_TECH = LIMIT(ABL:MASTER:舌靈活+ABL:MASTER:指靈活+ABL:MASTER:腰靈活+ABL:MASTER:魔乳-10, COND("初期技巧", MASTER), 99)
	ENDSELECT

	SIF ABL:MASTER:技巧 < MAX(MAXABL_TECH, MEMO_MASTERABL)
		LOCAL:0 = 1
	PRINTFORM  [\@ LOCAL:0 ?  {0, 2} # × \@] 技巧＋1LV
	SIF LOCAL:0 == 0 && ABL:MASTER:技巧 < 99
		PRINTFORM 　升到次LV需要～各个LV的总和：还需要{(ABL:MASTER:技巧 + 1)*2 - (ABL:MASTER:舌靈活+ABL:MASTER:指靈活+ABL:MASTER:腰靈活+ABL:MASTER:魔乳)}
	PRINTL 

	SIF ABL:MASTER:技巧 > 0
		LOCAL:1 = 1
	PRINTFORML  [\@ LOCAL:1 ?  {1, 2} # × \@] 技巧－1LV
	SIF ABL:MASTER:受虐属性 < 5
		LOCAL:2 = 1
	PRINTFORML  [\@ LOCAL:2 ?  {2, 2} # × \@] 受虐属性＋1LV
	SIF ABL:MASTER:受虐属性 > 0
		LOCAL:3 = 1
	PRINTFORML  [\@ LOCAL:3 ?  {3, 2} # × \@] 受虐属性－1LV
	SIF ABL:MASTER:施虐属性 < 5
		LOCAL:4 = 1
	PRINTFORML  [\@ LOCAL:4 ?  {4, 2} # × \@] 施虐属性＋1LV
	SIF ABL:MASTER:施虐属性 > 0
		LOCAL:5 = 1
	PRINTFORML  [\@ LOCAL:5 ?  {5, 2} # × \@] 施虐属性－1LV
	IF CONFIG("ＶＡ拡張の手動ＬＶＵＰ")
		IF TALENT:MASTER:男性 == 0
			SIF CALCF("Ｖ拡張の上限", MASTER) > ABL:MASTER:Ｖ拡張
				LOCAL:10 = 1
			PRINTFORML  [\@ LOCAL:10 ?  {10, 2} # × \@] Ｖ拡張＋1LV
		ENDIF
		SIF CALCF("Ａ拡張の上限", MASTER) > ABL:MASTER:Ａ拡張
			LOCAL:11 = 1
		PRINTFORML  [\@ LOCAL:11 ?  {11, 2} # × \@] Ａ拡張＋1LV
	ENDIF

	PRINTFORML  [98] 使技巧符合全体部員的平均値
	PRINTFORML  [99] 尽可能提升技巧
	PRINTFORML [100] 終了

	CALL INPUT_SELECT, 12, 98, 99, 100

	CHOICE = RESULT

	IF CHOICE == 100
		REDRAW 1
		RETURN 0
	ELSEIF CHOICE == 98
		AVERAGE_TECH = 0
		FOR LCOUNT, 2, CHARANUM
			AVERAGE_TECH += ABL:LCOUNT:技巧*10
		NEXT
		AVERAGE_TECH /= (CHARANUM - 2)
		;小数点第一位を四捨五入する
		IF AVERAGE_TECH%10 >= 5
			ABL:MASTER:技巧 = MIN(MAXABL_TECH, (AVERAGE_TECH/10) + 1)
		ELSE
			ABL:MASTER:技巧 = MIN(MAXABL_TECH, AVERAGE_TECH/10)
		ENDIF
	ELSEIF CHOICE == 99
		ABL:MASTER:技巧 = MAX(MAXABL_TECH, MEMO_MASTERABL)
	ELSEIF LOCAL:CHOICE
		SELECTCASE CHOICE
		CASE 0
			ABL:MASTER:技巧 += 1
		CASE 1
			ABL:MASTER:技巧 -= 1
		CASE 2
			ABL:MASTER:受虐属性 += 1
		CASE 3
			ABL:MASTER:受虐属性 -= 1
		CASE 4
			ABL:MASTER:施虐属性 += 1
		CASE 5
			ABL:MASTER:施虐属性 -= 1
		CASE 10
			ABL:MASTER:Ｖ拡張 += 1
		CASE 11
			ABL:MASTER:Ａ拡張 += 1
		ENDSELECT
	ENDIF
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO START
ENDSELECT


@EX_ABLUP, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

VARSET LOCAL
;ABL:舌靈活, ABL:指靈活, ABL:腰靈活, ABL:魔乳
FOR LCOUNT, 60, 64
	LOCAL:LCOUNT = ABL:ARG:LCOUNT
NEXT
CALL GET_EXABL, ARG

MEMO_LINECOUNT = LINECOUNT
DRAWLINE
FOR LCOUNT, 60, 64
	IF ABL:ARG:LCOUNT > LOCAL:LCOUNT
		PRINTFORML %NAME:ARG%的%ABLNAME:LCOUNT%的LV提升了！
		PRINTFORMW %ABLNAME:LCOUNT%的LV变成了{ABL:ARG:LCOUNT}！
		PRINTFORML 
	ENDIF
NEXT

LOCAL = TALENT:ARG:吸精体質

CALL GET_EXTALENT, ARG

IF TALENT:ARG:吸精体質 > LOCAL
	PRINTFORMW %CALLNAME:ARG%的吸精体質上升到了LV{TALENT:ARG:吸精体質}！
	PRINTFORML 
ENDIF

CLEARLINE 1
SIF LINECOUNT > MEMO_LINECOUNT
	DRAWLINE

;経験値の増加処理。ARGには主にCOND("絶頂", PLAYER)orNOWEX:絶頂数合計、ARG:1は経験値の受け取り手が入る
@GET_EXPERIENCE, ARG, ARG:1
#DIM ACTOR
IF ARG:1 == TARGET
	ACTOR = PLAYER
ELSE
	ACTOR = TARGET
ENDIF
;技巧差による補正をかける
LOCAL = MULTIPLY(ARG*5, 100 + LIMIT(ABL:ACTOR:技巧 - ABL:(ARG:1):技巧, -100, 100)*20)
LOCAL = MAX(LOCAL - 3, 1)
EXP:(ARG:1):経験値 += LOCAL
