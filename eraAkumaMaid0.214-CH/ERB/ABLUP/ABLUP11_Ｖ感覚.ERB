;-------------------------------------------------
;ABL:Ｖ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP11
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

SIF TALENT:男性
	RETURN 0

;必要な珠などの値を取得
CALL IS_ABLUP11
;何もできない場合
IF RESULT == 0 && ABL:Ｖ感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｖ感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:Ｖ経験
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 4)
		PRINTFORML ※Ｖ経験非常的多、要求的寶珠減少80％※
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 3)
		PRINTFORML ※Ｖ經驗很多、要求的寶珠減少50％※
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 2)
		PRINTFORML ※Ｖ經驗較多、要求的寶珠減少20％※
	;著しく足りていない
	CASE IS < EXP_LV(ABL:Ｖ感覚 + 1)
		PRINTFORML ※Ｖ經驗太少、要求的寶珠增加3倍※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;淫壷を得る
IF TALENT:淫壷 == 0
	IF COND("淫壷取得可能")
		PRINTFORML  [10] 獲得[淫壷]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[淫壷](要:快Ｖの珠{NUM("宝珠：淫壷取得")}、Ｖ感覚4LV、今回Ｖ絶頂数10)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｖ >= NUM("宝珠：淫壷取得") && TALENT:淫壷 == 0
		JUEL:快Ｖ -= NUM("宝珠：淫壷取得")
		TALENT:淫壷 = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		IF TALENT:恋慕 || PENIS(MASTER) == 0
			PRINTFORMW %CALLNAME:TARGET%在性行為結束之後、擁抱住%CALLNAME:MASTER%懇求著能夠撫平子宮中飢渴的灼痛……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%的蜜壺被徹底地開發了、已經完全地雌伏在%CALLNAME:MASTER%的面前……
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[淫壷]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｖ感覚 += 1
		PRINTFORMW Ｖ感覚等級提升到LV{ABL:Ｖ感覚}了。
	ENDIF
ENDSELECT

;子宮性感の獲得
IF TALENT:子宮性感 == 0 && ABL:Ｖ感覚 >= 5 && (ABL:Ｖ感覚 >= 9 || EXP:子宮口経験 >= 100)
	CALL PRINT_STRL, @"%CALLNAME:TARGET%獲得_黄色_[子宮性感]_"
	TALENT:子宮性感 = 1
	FORCEWAIT
ENDIF

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP11
SIF RESULT
	RESTART



;ABL:Ｖ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP11, ARGS
SIF TALENT:男性
	RETURN 0

CALL VARSET_ABLUP
SELECTCASE ABL:Ｖ感覚
CASE 0
	JUEL:0:快Ｖ = 500
CASE 1
	JUEL:0:快Ｖ = 1000
CASE 2
	JUEL:0:快Ｖ = 3000
CASE 3 TO 4
	JUEL:0:快Ｖ = (ABL:Ｖ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｖ = 10000 + (ABL:Ｖ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｖ = 70000 + (ABL:Ｖ感覚 - 9) * 30000
ENDSELECT

;Ｖ経験による軽減
SELECTCASE EXP:Ｖ経験
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 4)
	TIMES JUEL:0:快Ｖ, 0.20
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 3)
	TIMES JUEL:0:快Ｖ, 0.50
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 2)
	TIMES JUEL:0:快Ｖ, 0.80
;著しく足りていない
CASE IS < EXP_LV(ABL:Ｖ感覚 + 1)
	TIMES JUEL:0:快Ｖ, 3.00
ENDSELECT


;淫壷が獲得できる？
SIF COND("淫壷取得可能") && ARGS != "ＬＶ限定"
	RETURN 2
;最大LV未満か？
SIF ABL:Ｖ感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快Ｖ && JUEL:快Ｖ >= JUEL:0:快Ｖ
	RETURN 1
RETURN 0

