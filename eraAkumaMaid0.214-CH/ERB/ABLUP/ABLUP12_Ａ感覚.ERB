;-------------------------------------------------
;ABL:Ａ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP12
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP12
;何もできない場合
IF RESULT == 0 && ABL:Ａ感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ａ感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:Ａ経験
	CASE IS >= EXP_LV(ABL:Ａ感覚 + 4)
		PRINTFORML ※Ａ経験非常的多、要求的寶珠減少80％※
	CASE IS >= EXP_LV(ABL:Ａ感覚 + 3)
		PRINTFORML ※Ａ經驗很多、要求的寶珠減少50％※
	CASE IS >= EXP_LV(ABL:Ａ感覚 + 2)
		PRINTFORML ※Ａ經驗較多、要求的寶珠減少20％※
	;著しく足りていない
	CASE IS < EXP_LV(ABL:Ａ感覚 + 1)
		PRINTFORML ※Ａ經驗太少、要求的寶珠增加3倍※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;尻穴狂いを得る
IF TALENT:尻穴狂い == 0
	IF COND("尻穴狂い取得可能")
		PRINTFORML  [10] 獲得[尻穴狂]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[尻穴狂](要:快Ａの珠{NUM("宝珠：尻穴狂い取得")}、Ａ感覚4LV、今回Ａ絶頂数10)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ａ >= NUM("宝珠：尻穴狂い取得") && TALENT:尻穴狂い == 0
		JUEL:快Ａ -= NUM("宝珠：尻穴狂い取得")
		TALENT:尻穴狂い = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		IF TALENT:恋慕 || TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%在性行為結束之後、也嬌喘著懇求%CALLNAME:MASTER%繼續狠狠地蹂躪她的尻穴……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%一邊用手指摳挖著自己的尻穴、一邊用像是在渴求著什麼的眼神凝視著%CALLNAME:MASTER%……
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[尻穴狂い]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ａ感覚 += 1
		PRINTFORMW Ａ感覚等級提升到{ABL:Ａ感覚}LV了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP12
SIF RESULT
	RESTART



;ABL:Ａ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP12, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ａ感覚
CASE 0
	JUEL:0:快Ａ = 500
CASE 1
	JUEL:0:快Ａ = 1000
CASE 2
	JUEL:0:快Ａ = 3000
CASE 3 TO 4
	JUEL:0:快Ａ = (ABL:Ａ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ａ = 10000 + (ABL:Ａ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ａ = 70000 + (ABL:Ａ感覚 - 9) * 30000
ENDSELECT

;Ａ経験による軽減
SELECTCASE EXP:Ａ経験
CASE IS >= EXP_LV(ABL:Ａ感覚 + 4)
	TIMES JUEL:0:快Ａ, 0.20
CASE IS >= EXP_LV(ABL:Ａ感覚 + 3)
	TIMES JUEL:0:快Ａ, 0.50
CASE IS >= EXP_LV(ABL:Ａ感覚 + 2)
	TIMES JUEL:0:快Ａ, 0.80
;著しく足りていない
CASE IS < EXP_LV(ABL:Ａ感覚 + 1)
	TIMES JUEL:0:快Ａ, 3.00
ENDSELECT


;尻穴狂いが獲得できる？
SIF COND("尻穴狂い取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ａ感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快Ａ && JUEL:快Ａ >= JUEL:0:快Ａ
	RETURN 1
RETURN 0

