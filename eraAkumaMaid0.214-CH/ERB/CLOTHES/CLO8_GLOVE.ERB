;-------------------------------------------------
;角色ARGに手套ARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_GLOVE, ARGS, ARG
#DIMS MEMO_CLO
SIF ARG == 0 && TARGET > 0
	ARG = TARGET
MEMO_CLO = %CSTR:ARG:手套%

;汚れ＆移開たりしたフラグ等を初期化して新品にする
STAIN:ARG:手套 = 0

CALL PALAM_GLOVE, ARGS, ARG

EQUIP:ARG:手套 = STRLENS(CSTR:ARG:手套)
SIF MEMO_CLO != CSTR:ARG:手套
	TFLAG:着替えた = 1

;-------------------------------------------------
;ARGの手套の名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@GLOVE(ARG, ARGS)
#FUNCTIONS

VARSET LOCALS
SELECTCASE ARGS
CASE "略"
	IF CHECK_GLOVE("手露出", ARG)
		LOCALS = 手
	ELSEIF CHECK_GLOVE("指露出", ARG)
		LOCALS = 指
	ELSEIF CHECK_GLOVE("鎧", ARG)
		LOCALS = 篭手
	ELSEIF CHECK_GLOVE("肉球", ARG)
		LOCALS = 肉球
	ELSE
		LOCALS = 手套
	ENDIF
CASE "種類"
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:手套, "/", LOCALS
CASE ""
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:手套, "/", LOCALS
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番の手套の名称。換装などで表を作る際に使う
;-------------------------------------------------
@NAME_GLOVE(ARG)
#FUNCTIONS

VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = 絲綢手套
CASE 1
	LOCALS = 礼服手套
CASE 2
	LOCALS = 皮革袖套
CASE 3
	LOCALS = 礼服袖套
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;角色ARGに手套ARGSを着せた場合のCSTR処理
;-------------------------------------------------
@PALAM_GLOVE, ARGS, ARG
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

SELECTCASE ARGS
CASE "", "没有"
	CSTR:ARG:手套 = 
CASE "手套"
	CSTR:ARG:手套 = 手套/
CASE "絹手套"
	CSTR:ARG:手套 = 絹手套/
CASE "革手套"
	CSTR:ARG:手套 = 革手套/黒/
CASE "絲綢手套"
	CSTR:ARG:手套 = 絲綢手套/
CASE "礼服手套"
	CSTR:ARG:手套 = 礼服手套/長/蕾丝/
CASE "皮革袖套"
	CSTR:ARG:手套 = 皮革袖套/直到手肘/手露出/黒/
CASE "皮革手套"
	CSTR:ARG:手套 = 皮革手套/長/革/黒/
CASE "礼服袖套"
	CSTR:ARG:手套 = 礼服袖套/長/蕾丝/手露出/
CASE "手臂束缚器"
	CSTR:ARG:手套 = 手臂束缚器/長/革/指の使用禁止/黒/
CASEELSE
	CSTR:ARG:手套 = %ARGS%/
ENDSELECT

CSTR:ARG:手套 = %MERGE_STR(CSTR:ARG:手套, STR_ADD)%

;-------------------------------------------------
;ARGの手套が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_GLOVE(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:手套 == 0
	RETURNF 0

SELECTCASE ARGS
CASE "前腕露出"
	SIF STRCOUNTS(CSTR:ARG:手套, "直到手肘", "長") == 0
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:手套, ARGS)
		RETURNF 1
ENDSELECT

RETURNF 0

