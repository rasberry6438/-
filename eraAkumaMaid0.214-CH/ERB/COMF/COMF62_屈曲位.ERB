@COM62
;屈曲位
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM62, ARG
CALL FLAG_INSERTION, "Ｖ挿入", ARG, 120

@SOURCE_COM62, ARG
CALL SOURCE_INSERTION, "Ｖ挿入", ARG, 120
CALL VARSET_COMF

;露出、屈従のソース
SOURCE:0:露出 = 3000
SOURCE:0:屈従 = 3000

;根元まで受け入れられる場合には阴蒂を擦られてしまう
IF ABL:Ｖ拡張 > SIZE("阴茎", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 20)
	;調教者の腰靈活をみる
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰靈活", PLAYER)
ENDIF
SOURCE:0:情愛 = 300

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM62
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;前回、挿入前に暴発してしまった
IF PREVCOM == TNUM("挿入前暴発") && COND("前Ｔ調教者") == PLAYER
	PRINTFORM 重新振作起來的%CALLNAME:PLAYER%、
ELSE
	PRINTFORM %CALLNAME:PLAYER%将
ENDIF
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
	PRINTFORM 將%CALLNAME:TARGET%的身體翻轉過來、
;穴開きでない胖次を是ていて、ずらされていないなら、必ずずらす
ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
	PRINTFORM %CALLNAME:TARGET%的
	CALL SETFLAG, "下身衣装を露出：テ接吻ト有些", TARGET
	PRINTFORM 、做出將羞澀的淫穴向上展示一樣的姿勢
ELSE
	PRINTFORM 将%CALLNAME:TARGET%的脚抓住抱在一起压倒之后、
ENDIF

IF TEQUIP:接吻
	PRINTFORM 在熱情地親吻著的同時、
	CALL SETFLAG, "追加接吻フラグ", TARGET
ELSEIF NUM("回合数") == 0 && TALENT:妊娠 == 0
	PRINTFORM 将%TEXTR("完全沒有前戲的/沒有任何準備的")%
ELSE
	PRINTFORM 在清楚的觀賞著結合處的同時將
ENDIF
IF TALENT:娇小
	PRINT 幼嫩秘縫
ELSE
	PRINT 秘裂
ENDIF
IF TALENT:妊娠
	PRINTFORMW 比平時更加溫柔地淺淺地侵犯著……
;巨根分岐
ELSEIF ABL:PLAYER:施虐属性 && SIZE("阴茎", PLAYER) >= 2 && CHOSECOMS("Ｖ挿入") == 0
	PRINTFORMW 的最深處用巨根沉入其中。
ELSE
	PRINTFORMW 的深處猛力地戳刺著……
ENDIF
