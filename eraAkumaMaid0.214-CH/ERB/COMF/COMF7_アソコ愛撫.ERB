@COM7
;阴部愛撫。指を入れないで、阴蒂や陰唇、膣口までを弄る（挿入中でも実行可能）
;TEQUIP:阴部愛撫で行為を記録する
;0bit阴部表層 1bit恥丘 2bit阴蒂を弄る 3bit穿環をいじめる
;基本的には阴部or恥丘愛撫→陰核愛撫 となる

CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

RETURN 1

@FLAG_COM7, ARG
IF PREVCOM == 5
	CALL FLAG_COM5, 30
ELSEIF PREVCOM == 6
	CALL FLAG_COM6, 30
ENDIF
IF CHECK_COM("擦拭")
	CALL SETFLAG, "擦拭フラグ", TARGET
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
	STAIN:Ａ = SET_STAIN(0, "Ａ")
	STAIN:PLAYER:手 = 0
ELSE
	;対象のＶ⇔調教者の指的污垢が移動
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
ENDIF

@SOURCE_COM7, ARG
IF PREVCOM == 5
	CALL SOURCE_COM5, 30
ELSEIF PREVCOM == 6
	CALL SOURCE_COM6, 30
ENDIF
SIF CHECK_COM("擦拭")
	CALL SOURCE_COM13, 30

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 7

SOURCE:0:露出 = 300
SOURCE:0:逸脱 = 100

;ABL:Ｃ感覚をみる
SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 1)
;調教者の指靈活をみる
SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "指靈活", PLAYER)

SOURCE:0:情愛 = 200 + (ABL:Ｃ感覚 + ABL:Ｖ感覚)*100

;阴部が露出していないと快Ｃ減
IF !CHECK_CLO("Ｖ露出") && COND("調教中")
	;休闲褲を是ていると快Ｃ激減
	IF CHECK_BOTTOM("休闲褲") || CHECK_PANTIES("皮革")
		TIMES SOURCE:0:Ｃ快楽, 0.20
		TIMES SOURCE:0:露出, 0.20
	ELSE
		TIMES SOURCE:0:Ｃ快楽, 0.60
		TIMES SOURCE:0:露出, 0.50
	ENDIF
ENDIF

;贞操重视
IF TALENT:贞操重视
	IF TALENT:処女 == 1
		TIMES SOURCE:0:情愛, 0.60
		TIMES SOURCE:0:屈従, 2.00
		SOURCE:0:逸脱 += 1000
	ENDIF
;不在乎貞操
ELSEIF TALENT:不在乎貞操
	IF TALENT:処女 == 1
		TIMES SOURCE:0:情愛, 0.60
		SOURCE:0:逸脱 += 300
	ENDIF
ELSE
	IF TALENT:処女 == 1
		SOURCE:0:逸脱 += 500
	ENDIF
ENDIF

IF CHECK_COM("擦拭")
	TIMES SOURCE:0:Ｃ快楽, 0.10
ELSEIF CHECK_COM("陰核愛撫")
	TIMES SOURCE:0:Ｃ快楽, 1.50
ENDIF

SIF TALENT:Ｃ性向
	CALL CALC, "好みのPLAY", SOURCE:0:Ｃ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM7
;TEQUIP:阴部愛撫で行為を記録する
;0bit阴部表層 1bit恥丘 2bit阴蒂を弄る 3bit穿環をいじめる
;基本的には阴部or恥丘愛撫→陰核愛撫 となる

PRINTFORM %CALLNAME:PLAYER%将

IF INSERT("合体中")
	PRINTFORM %TEXTS("合体中", PLAYER)%
;連続実行
ELSEIF PREVCOM == SELECTCOM
	;陰核愛撫連続の際には、穴開きでない胖次を是ていて、ずらされていないならずらす事もある
	IF RAND:2 && CHECK_COM("陰核愛撫") && CHECK_CLO("Ｖ露出") == 0
		PRINTFORM %CALLNAME:TARGET%的
		CALL SETFLAG, "下身衣装を露出：テ接吻ト有些", TARGET
		PRINTFORM 、
	ELSEIF TEQUIP:接吻
		PRINTFORM %TEXTS("接吻継続")%、
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ENDIF
ELSEIF TEQUIP:六九式 || PREVCOM == 1 || PREVCOM == 9
	;においは確率高め
	IF RAND:3
		IF AGE(TARGET) == 1 && RAND:3 == 0
			PRINTFORM 將少女特有的性器的%TEXTS("气味")%
		ELSEIF AGE(TARGET) == 4 && RAND:3 == 0
			PRINTFORM 將成熟雌性的醇厚的%TEXTS("气味")%
		ELSEIF TALENT:好色 && CALCF("Ｖ発情") >= 2 && RAND:3 == 0
			PRINTFORM 將漸漸濃烈起來的
			IF RAND:2
				PRINTFORM 陰部的%TEXTS("气味")%
			ELSE
				PRINTFORM 濃郁的雌性的%TEXTS("气味")%
			ENDIF
		ELSEIF RAND:3 == 0 && (AGE(TARGET) == 1 || TALENT:絶頂失禁体質)
			PRINTFORM 將微弱的尿臭
		ELSEIF CALCF("Ｖ発情")
			IF RAND:2 && CHECK_CLO("Ｖ露出") == 0
				PRINTFORM 將股間飄蕩起來的蒸騰的愛液的媚香
			ELSE
				PRINTFORM 將混雜著濕氣與滲透著的汗液的%TEXTS("气味")%
			ENDIF
		ELSE
			PRINTFORM 將散發著微妙的香味的汗液的%TEXTS("气味")%
		ENDIF
		IF RAND:2
			PRINTFORM 仔細嗅聞著的同時、
		ELSE
			PRINTFORM 盡情品味著的同時、
		ENDIF
	;眺めは少し似てるので確率低め
	ELSEIF RAND:2 && CHECK_CLO("Ｖ露出")
		IF AGE(TARGET) == 2 && RAND:2
			PRINTFORM 將少女特有的性器的形狀
		ELSEIF AGE(TARGET) == 4 && RAND:2
			PRINTFORM 將成熟雌性的艷麗的陰部
		ELSE
			PRINTFORM 將及近距離被清楚展示著的性器
		ENDIF
		IF RAND:2
			PRINTFORM 目不轉睛的眺望著的同時、
		ELSE
			PRINTFORM 仔細觀察著的同時、
		ENDIF
	ELSE
		PRINTFORM 將眼前的美麗風景
		IF RAND:2
			PRINTFORM 盡情享受著的同時、
		ELSE
			PRINTFORM 滿足的觀賞著的同時、
		ENDIF
	ENDIF
ELSE
	IF TEQUIP:接吻
		PRINTFORM %TEXTS("接吻継続")%、
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ELSEIF PREVCOM == 5
		PRINTFORM 輕柔的愛撫著%CALLNAME:TARGET%的胸部的同時、
	ELSEIF RAND:3 == 0
		IF TALENT:害羞 && MARK:屈服刻印 < 2 && EX:絶頂数合計 == 0
			PRINTFORM 在%CALLNAME:TARGET%反射性的閉合起來的雙腿間的空隙將手伸入、
		ELSEIF (ABL:信頼 >= 3 && RAND:3 == 0) || CHECK_COM("擦拭")
			PRINTFORM 在%CALLNAME:TARGET%順從地張開雙腿之後、
		ELSEIF RAND:2
			PRINTFORM %CALLNAME:TARGET%的
			IF EX:絶頂数合計
				PRINTFORM 因絕頂的快感而泛起紅潮的
			ELSEIF PALAM:欲情 >= 3000
				SIF PALAM:欲情 < 10000
					PRINTFORM 微微地
				PRINTFORM 附著晶瑩汗滴的
			ENDIF
			PRINTFORM 大腿內側用手撫摸起來、
		ELSE
			SIF TALENT:好色
				PRINTFORM 無意識的張開雙腿的
			PRINTFORM %CALLNAME:TARGET%的股間用手慢慢地撫摸著、
		ENDIF
	ELSEIF RAND:2
		PRINTFORM 在將%CALLNAME:TARGET%抱入懷中之後、
	ELSE
		PRINTFORM %CALLNAME:TARGET%的
		IF EX:絶頂数合計 >= 5 && RAND:3
			PRINTFORM 因劇烈而源源不斷的快感而完全溶化的臉
		ELSE
			SIF EX:絶頂数合計 >= 1
				PRINTFORM 因快感而溶化的
			IF RAND:2
				PRINTFORM 脸上
			ELSE
				PRINTFORM 雙瞳
			ENDIF
		ENDIF
		PRINTFORM 目不轉睛的凝視著的同時、
	ENDIF
ENDIF


;主なルートは三つで、TEQUIP:阴部愛撫 で行動を記録
;0bit阴部表層 1bit恥丘 2bit阴蒂を弄る 3bit穿環をいじめる
;まずは不要なフラグを消去する…まだ陰蒂環愛撫無かったわ
;CLEARBIT TEQUIP:阴部愛撫, 3

VARSET LOCAL
VARSET LOCALS

;阴蒂を重点的に弄る
IF CHECK_COM("陰核愛撫に変化")
	;連続実行でルートも一緒
	IF CHECK_COM("陰核愛撫")
		;0通常 1皮の下で勃起 2顔を出す 3ギンギン
		SELECTCASE CALCF("Ｃ発情")
		CASE 0
			PRINTFORM 將被包皮保護著的
		CASE 1
			PRINTFORM 將即使被包皮包裹著也能感受到硬度的
		CASE 2
			PRINTFORM 將勃起到從包皮中露出的
		CASEELSE
			PRINTFORM 將興奮地充血勃起的
		ENDSELECT
		PRINTFORM %TEXTR("陰蒂/淫核/突起")%
		PRINTFORM %TEXTR("執拗地/徹底地/毫不留情地")%
		IF CALCF("Ｃ発情") >= 2
			PRINTFORMW %TEXTR("輕輕擼動著/輕柔地捻動著/持續玩弄著")%……
		ELSE
			PRINTFORMW %TEXTR("輕輕地摁倒/持續玩弄著/不斷刺激著")%……
		ENDIF
	ELSE
		IF CHECK_CLO("Ｖ露出") == 0
			;内衣越し
			IF CHECK_CLO("胖次露出")
				PRINTFORM 連同%CLO("股間")%一併將
			;生地が分厚い
			ELSE
				PRINTFORM 隔著%CLO("股間")%
			ENDIF
		ELSE
			PRINTFORM 將%TEXTS("阴蒂")%
		ENDIF
		IF RAND:4 == 0
			PRINTFORM 阴蒂
		ELSEIF RAND:3 == 0
			PRINTFORM 淫核
		ELSEIF RAND:2
			PRINTFORM 肉豆
		ELSE
			PRINTFORM 突起
		ENDIF
		;包茎状態 + 胖次无 なら皮の描写
		IF CALCF("Ｃ発情") < 2 && CHECK_CLO("Ｖ露出")
			IF RAND:2
				LOCALS = 從包皮中剝出、
			ELSE
				LOCALS = 間隔著包皮
			ENDIF
		ENDIF
		IF ABL:PLAYER:技巧 + ABL:PLAYER:指靈活 >= 4 && RAND:3
			IF RAND:5 == 0 && CALCF("Ｃ発情") >= 2 && CHECK_CLO("Ｖ露出")
				PRINTFORMW %LOCALS%捏住並不斷地搖晃著……
			ELSEIF RAND:4 == 0 && CALCF("Ｃ発情") >= 2
				PRINTFORMW %LOCALS%用指尖不斷地捻動……
			ELSEIF RAND:3 == 0
				PRINTFORMW %LOCALS%持續地玩弄著……
			ELSEIF RAND:2
				PRINTFORMW %LOCALS%不斷變換節奏的擼動著……
			ELSE
				PRINTFORMW %LOCALS%用手指不時的輕輕彈動……
			ENDIF
		ELSEIF RAND:4 == 0 && CALCF("Ｃ発情") >= 2
			PRINTFORMW %LOCALS%用指尖輾轉著……
		ELSEIF RAND:3 == 0
			PRINTFORMW %LOCALS%用指腹輕輕摩挲著……
		ELSEIF RAND:2
			PRINTFORMW %LOCALS%來回地撫摸著……
		ELSE
			PRINTFORMW %LOCALS%來回地愛撫著……
		ENDIF
	ENDIF
	TEQUIP:阴部愛撫 = 1p2
;阴部愛撫
ELSEIF RAND:2 || TEQUIP:六九式 || PREVCOM == 1 || PREVCOM == 9 || CHECK_COM("擦拭")
	;内衣越し
	IF CHECK_CLO("Ｖ露出") == 0
		IF CHECK_CLO("湿的") && RAND:2
			PRINTFORM 將被染上淫靡色斑的%CLO("股間")%之內的
		ELSEIF CHECK_CLO("闷热的") && RAND:2
			PRINTFORM 將%CLO("股間")%內部
			SIF PALAM:欲情 >= 10000
				PRINTFORM 濃烈地
			PRINTFORM 蒸騰著熱汽的
		ELSEIF CHECK_CLO("勒紧的") && RAND:2
			PRINTFORM 將被%CLO("股間")%緊緊勒住的
		ELSE
			IF (TEQUIP:六九式 || PREVCOM == 1 || PREVCOM == 9) && RAND:2
				PRINTFORM 將在%CLO("股間")%的遮掩下若隱若現的
			ELSE
				PRINTFORM 將被%CLO("股間")%遮掩著的
			ENDIF
		ENDIF
	ELSE
		;処女 LOCAL == 1 で管理
		IF TALENT:処女 == 1 && RAND:4 == 0
			PRINTFORM 不识男人滋味的
			LOCAL = 1
		;前回CV絶頂or中出し
		ELSEIF PREVEX("Ｃ絶頂") + PREVEX("Ｖ絶頂") + PREVEX("Ｖ中出し") && RAND:3 == 0
			IF PREVEX("Ｖ中出し") && RAND:3
				IF EXP_GAIN("膣内精液経験") >= 4
					PRINTFORM 將被充分中出之后的
				ELSE
					PRINTFORM 將緩緩流出精液的
				ENDIF
			ELSEIF PREVEX("Ｃ絶頂") + PREVEX("Ｖ絶頂") && RAND:3
				IF RAND:2
					PRINTFORM 將在快感之下完全溶化的
				ELSEIF TEQUIP:推倒
					PRINTFORM 剛剛迎來高潮的
				ELSE
					PRINTFORM 刚刚被欺负的
				ENDIF
			ELSE
				PRINTFORM 將因快感而不斷顫抖著的
			ENDIF
		ELSEIF TALENT:陰唇環 && PREVEX("Ｃ絶頂") + PREVEX("Ｖ絶頂") + PREVEX("Ｖ中出し") == 0 && RAND:5 == 0
			IF RAND:2
				PRINTFORM 將被反射著光芒的穿環穿刺著的
			ELSE
				PRINTFORM 被奇妙的金屬環装飾著的
			ENDIF
		ELSEIF EX:Ｖ絶頂 > 3 && RAND:5 == 0
			PRINTFORM 將剛剛不斷迎來高潮的
		ELSEIF CHECK_STAIN(STAIN:Ｖ, "精液") && RAND:4 == 0
			PRINTFORM 將被精液弄髒的
		ELSEIF CALCF("Ｖ発情") >= 4 && RAND:3
			PRINTFORM 將不斷溢出白濁淫汁的
		ELSEIF CALCF("Ｖ発情") >= 3
			IF RAND:2
				PRINTFORM 將不斷流下愛液的
			ELSE
				PRINTFORM 將渴望的不斷張合著的
			ENDIF
		ELSEIF CALCF("Ｖ発情") == 2
			IF RAND:2
				PRINTFORM 將早已被愛液浸透的
			ELSE
				PRINTFORM 將被愛液濡濕到發光的
			ENDIF
		ELSEIF CALCF("Ｖ発情")
			PRINTFORM 发情了
		ENDIF
	ENDIF
	IF LOCAL == 1 && TALENT:処女 == 1 && CHECK_CLO("Ｖ露出")
		PRINTFORM 処女膜
	ELSEIF !CHECK_CLO("Ｖ露出") && RAND:3 == 0
		PRINTFORM 重要部分
	ELSEIF TALENT:害羞 && RAND:2
		PRINTFORM 令人害羞的秘處
	ELSEIF TALENT:好色 && RAND:2
		PRINTFORM 淫亂裂口
	ELSE
		;2020/2/1　加筆
		IF AGE(TARGET) == 4
			PRINTFORM %TEXTR("肉厚的/成熟大人样的/发黑的")%
		ELSEIF AGE(TARGET) == 1
			PRINTFORM 年幼
		ELSEIF TALENT:娇小
			PRINTFORM 小小的
		ENDIF
		PRINTFORM 阴部
	ENDIF
	IF CHECK_COM("擦拭")
		PRINTFORMW 用潔淨的濕布輕柔地擦拭著……
	ELSEIF TEQUIP:六九式 && CHECK_CLO("Ｖ露出") && RAND:3 == 0
		IF TALENT:処女 == 0
			PRINTFORMW 用指尖輕巧的玩弄著……
		ELSE
			PRINTFORMW 推開、用指尖輕巧的玩弄著……
		ENDIF
	ELSEIF RAND:3 == 0
		PRINTFORMW 用手指描繪著、將凹凸不平的地方輕輕摩擦著……
	ELSEIF RAND:2
		PRINTFORMW 的邊緣用手指描繪著、用時有時無的力道慢慢摩擦著……
	ELSE
		PRINTFORMW 用指腹非常輕柔地摩挲著……
	ENDIF
	TEQUIP:阴部愛撫 = 1p0
;恥丘愛撫
ELSE
	;内衣越し
	IF CHECK_CLO("Ｖ露出") == 0
		IF CHECK_CLO("勒紧的") && RAND:2
			IF CHECK_CLO("贴身制服露出") && RAND:2
				PRINTFORM 將被緊身衣緊貼著勾出輪廓的、柔軟的隆起的
			ELSE
				PRINTFORM 將被高衩設計的布料緊緊勒住的柔軟的
			ENDIF
		ELSE
			IF RAND:4 == 0
				PRINTFORM 將被%CLO("股間")%包裹著的
			ELSEIF RAND:3 == 0
				PRINTFORM 將以美妙的曲線隆起的
			ELSEIF RAND:2
				PRINTFORM 將略微突出的
			ELSE
				PRINTFORM 將柔軟而突出的
			ENDIF
		ENDIF
	ELSE
		IF COND("陰毛") && RAND:3 == 0
			SELECTCASE COND("陰毛")
			CASE 1
				PRINTFORM 將剝殼雞蛋一樣光潔的
			CASE 2
				PRINTFORM 將被完全剔除陰毛的
			CASE 3
				IF PREVCOM == 145
					PRINTFORM 將剛剛被完全剃除毛髮的
				ELSEIF RAND:2
					PRINTFORM 將毛髮被剃除乾淨的
				ELSE
					PRINTFORM 陰毛被剃除、猶如剝殼雞蛋的
				ENDIF
			CASE 4
				;剃过留痕
				IF CONDS("陰毛") == "剃过留痕"
					IF RAND:2
						PRINTFORM 有明显的修剪痕迹的
					ELSE
						PRINTFORM 將生長著微小毛茬的
					ENDIF
				;还没長出来
				ELSEIF AGE(TARGET) == 1
					IF RAND:2
						PRINTFORM 生長稀疏胎毛的
					ELSE
						PRINTFORM 还有些稚嫩的孩子气的
					ENDIF
				;还没長出来（大人）
				ELSE
					IF RAND:2 && BASE:陰毛%100 >= 5
						PRINTFORM 將陰毛被整齊修剪過的
					ELSE
						PRINTFORM 有著薄薄陰毛的
					ENDIF
				ENDIF
			CASE 5, 6
				IF AGE(TARGET) == 1
					IF RAND:2
						PRINTFORM 將陰毛稍微生長了的
					ELSE
						PRINTFORM 可愛的
					ENDIF
				ELSE
					PRINTFORM 將被柔軟的毛髮覆蓋著的
				ENDIF
			CASE 7
				IF RAND:3 == 0 && AGE(TARGET) >= 2
					PRINTFORM 被濃密黑林所覆蓋的
				ELSEIF RAND:2
					PRINTFORM 將生長著茂密毛髮的
				ELSE
					PRINTFORM 將被厚重毛髮覆蓋著的
				ENDIF
			ENDSELECT
		ELSEIF RAND:2
			IF (TALENT:好色 || CALCF("Ｖ発情") >= 4) && AGE(TARGET) >= 2
				PRINTFORM 將暴露出內部抽搐著的肉襞的
			ELSE
				PRINTFORM 將羞澀地閉合著的
			ENDIF
		ELSE
			IF RAND:2
				PRINTFORM 將緩慢地隆起的
			ELSE
				PRINTFORM 將緩慢地膨脹起來的
			ENDIF
		ENDIF
	ENDIF
	IF RAND:2
		PRINTFORM 胯股之间
	ELSE
		SIF TALENT:好色
			PRINTFORM 、恬不知恥的淫亂
		PRINTFORM 肉丘
	ENDIF
	IF RAND:3 == 0
		PRINTFORM 整個用手掌
		IF RAND:2 && ABL:PLAYER:指靈活 >= 3
			PRINTFORMW 包裹住、將濕潤的裂口用指尖輕輕地來回撫摸著……
		ELSE
			PRINTFORMW 包裹著揉搓起來……
		ENDIF
	ELSEIF RAND:2
		PRINTFORMW 那柔軟的觸感盡情地品味著……
	ELSE
		PRINTFORMW 毫無間歇地撫摸著……
	ENDIF
	TEQUIP:阴部愛撫 = 1p1
ENDIF
