@COM171
;野外PLAY
;TEUIP:54のビットが 0人里, 1森林, 2花園, 3水辺, 4海
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

IF TEQUIP:野外PLAY == 0
	PREVCOM = 171
	RETURN 0
ELSEIF GETBIT(TFLAG:地の文分岐, 0)
	STR:前回指令 = 野外PLAY(人里)
ELSEIF GETBIT(TFLAG:地の文分岐, 1)
	STR:前回指令 = 野外PLAY(森林)
ELSEIF GETBIT(TFLAG:地の文分岐, 2)
	STR:前回指令 = 野外PLAY(花園)
ELSEIF GETBIT(TFLAG:地の文分岐, 3)
	STR:前回指令 = 野外PLAY(水辺)
ELSEIF GETBIT(TFLAG:地の文分岐, 4)
	STR:前回指令 = 野外PLAY(海)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM171
#DIM MEMO_KISEKAE
#DIM MEMO_LINECOUNT

IF TEQUIP:野外PLAY == 0
	PRINTFORML 那麼、要和%CALLNAME:TARGET%一起到哪裡去呢？
	PRINTFORML 
	PRINTL  [ 0] 一起到聚居地去吧
	PRINTL  [ 1] 一起到小樹林去吧
	PRINTL  [ 2] 一起到花園去吧
	PRINTL  [ 3] 一起到水辺去吧
	PRINTL  [ 4] 一起到海辺去吧
	PRINTL [100] 不、再考慮一下吧。
	CALL INPUT_SELECT, 5, 100
ELSEIF TEQUIP:野外PLAY
	;野外PLAY行き先フラグ
	TFLAG:地の文分岐 = TEQUIP:野外PLAY
	PRINTFORMW 和%CALLNAME:TARGET%回到部屋了……
	TEQUIP:野外PLAY = 0
	SIF ASSI
		TEQUIP:ASSI:野外PLAY = 0
	CALL KOJO_MESSAGE_COM
	RETURN 1
ENDIF

IF RESULT == 100
	TFLAG:指令中止 = 1
	RETURN 0
ELSEIF RESULT == 0
	LOCALS = 人里
ELSEIF RESULT == 1
	LOCALS = 森林
ELSEIF RESULT == 2
	LOCALS = 花園
ELSEIF RESULT == 3
	LOCALS = 水辺
ELSEIF RESULT == 4
	LOCALS = 海
ENDIF

SETBIT TEQUIP:野外PLAY, RESULT
SIF ASSI
	TEQUIP:ASSI:野外PLAY = TEQUIP:野外PLAY

;野外PLAY行き先フラグ
TFLAG:地の文分岐 = TEQUIP:野外PLAY

;換装させた状態を格子
MEMO_KISEKAE = 0

;選択肢の存在判定に用いる
VARSET LOCAL
;選択肢の二重出現を防ぐ
MEMO_LINECOUNT = LINECOUNT

;其他の場合
IF MEMO_LINECOUNT == LINECOUNT && EQUIP:胖次
	PRINTFORML ……在外出之前、要先脫下%CALLNAME:TARGET%的內褲嗎？
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM 現在的服装：
	CALL PRINT_DRESSL, TARGET
	RESETCOLOR
	PRINTFORML 
	PRINTFORML  [ 0] 是
	PRINTFORML  [ 1] 否

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		CALL SET_PANTIES, ""
		MEMO_KISEKAE = 3
	ENDSELECT
	PRINTL 
ENDIF

IF MEMO_KISEKAE == 1
	PRINT 順從地更衣的
ELSEIF CHECK_CLO("裸体")
	PRINT 預先脫光的
ENDIF
PRINTFORM %CALLNAME:TARGET%
IF MEMO_KISEKAE == 2
	PRINTFORM 換上了%SHOES(TARGET, "略")%、
	IF CHECK_CLO("裸体")
		PRINT 現在只穿著這個、
	ELSE
		PRINT 現在穿著這個、
	ENDIF
ELSEIF MEMO_KISEKAE == 3 && CHECK_CLO("裸体") == 0
	PRINT 內褲被預先收走了、
ELSEIF MARK:屈服刻印 == 3 || TALENT:恋慕
	PRINT 相互牽著手、
ENDIF
PRINTFORMW 一起到%LOCALS%那裡去了……
PRINTFORM %CALLNAME:TARGET%
IF (ABL:露出癖 >= 3 || TALENT:害羞) && GETBIT(TEQUIP:野外PLAY, 0)
	PRINTFORMW 感覺到有什麼人在注視著、臉頰被紅潮席捲、身體微微的顫抖著……
ELSEIF ABL:露出癖 >= 3
	PRINTFORMW 想像著盯著自己的視線、臉頰被紅潮席捲、身體微微的顫抖著……
ELSEIF TALENT:害羞
	PRINTFORMW 對細微的聲響也會敏感的做出反應的身體在微弱的顫抖著……
ELSEIF TALENT:小悪魔 || TALENT:坏心眼
	PRINTFORM 在行走的同時、抱住%CALLNAME:MASTER%的手臂、一邊凝視著臉頰、一邊用
	IF TALENT:娇小
		PRINT 發育不全的
	ELSEIF TALENT:巨乳
		PRINT 豊満的
	ELSE
		PRINT 十分柔軟的
	ENDIF
	PRINTW 身體輕輕摩擦著……
ELSEIF TALENT:好奇心
	PRINTFORMW 對四周的風景都是一副十分好奇的樣子……
ELSE
	PRINTFORMW 似乎在想像著可能在前方發生的事情……
ENDIF

RETURN 1

@FLAG_COM171, ARG

@SOURCE_COM171, ARG
CALL VARSET_COMF

IF GETBIT(TEQUIP:野外PLAY, 0)
	SOURCE:0:露出 = 2000
	SOURCE:0:逸脱 = 400
ELSEIF GETBIT(TEQUIP:野外PLAY, 1)
	SOURCE:0:露出 = 400
ELSEIF GETBIT(TEQUIP:野外PLAY, 2)
	SOURCE:0:露出 = 400
ELSEIF GETBIT(TEQUIP:野外PLAY, 3) || GETBIT(TEQUIP:野外PLAY, 4)
	SOURCE:0:露出 = 600
ENDIF
SOURCE:0:逸脱 += 500

SOURCE:0:充足 += ABL:露出癖 * 300

;服の有無で羞恥、逸脱度が上昇。人里だと物凄い事に。
IF CHECK_CLO("裸体")
	;種族衣装が裸体でない
	IF CLO_ORI(TARGET, "服", "贴身制服", "下身衣装", "胖次")
		IF GETBIT(TEQUIP:野外PLAY, 0)
			SOURCE:0:露出 += 6000
			SOURCE:0:逸脱 += 1000
		ELSE
			SOURCE:0:露出 += 2000
			SOURCE:0:逸脱 += 500
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("内衣姿")
	;種族衣装が服or贴身制服or下身衣装を含む
	IF CLO_ORI(TARGET, "服", "贴身制服", "下身衣装")
		IF GETBIT(TEQUIP:野外PLAY, 0)
			SOURCE:0:露出 += 4000
			SOURCE:0:逸脱 += 500
		ELSE
			SOURCE:0:露出 += 1000
			SOURCE:0:逸脱 += 200
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("没穿内褲")
	;種族衣装に内衣有
	IF CLO_ORI(TARGET, "胖次")
		IF GETBIT(TEQUIP:野外PLAY, 0)
			SOURCE:0:露出 += 2000
			SOURCE:0:逸脱 += 300
		ELSE
			SOURCE:0:露出 += 600
			SOURCE:0:逸脱 += 100
		ENDIF
	ENDIF
ENDIF

;昼だと恥ずかしい
IF COND("夜") == 0
	TIMES SOURCE:0:露出, 2.00
	TIMES SOURCE:0:逸脱, 2.00
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:露出, 0.80
CASE IS < PALAMLV:2
	TIMES SOURCE:0:露出, 0.90
CASE IS < PALAMLV:3
	TIMES SOURCE:0:露出, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:露出, 1.10
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:露出, 1.20
ENDSELECT
;ABL:受虐属性をみる
SELECTCASE ABL:受虐属性
CASE 0
	TIMES SOURCE:0:露出, 0.80
CASE 1
	TIMES SOURCE:0:露出, 1.00
CASE 2
	TIMES SOURCE:0:露出, 1.30
CASE 3
	TIMES SOURCE:0:露出, 1.60
CASE 4
	TIMES SOURCE:0:露出, 2.00
CASEELSE
	TIMES SOURCE:0:露出, 3.00
ENDSELECT

SIF TALENT:喜歡引人注目
	TIMES SOURCE:0:露出, 1.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 2.00
SIF TALENT:害羞
	TIMES SOURCE:0:逸脱, 2.00

SOURCE:0:恐惧 += SOURCE:0:逸脱 / 2

CALL CALC, "調教ソース", ARG


@EQUIP_COM171
SIF TEQUIP:野外PLAY == 0
	RETURN 0

CALL SOURCE_COM171, 100

@EQUIP_COM171_2
SIF TEQUIP:野外PLAY == 0
	RETURN 0
;露出経験の値を管理
LOCAL = 1
IF GETBIT(TEQUIP:野外PLAY, 0)
	PRINTL ＜野外PLAY中(人里)＞
	IF COND("夜") == 0
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
ELSEIF GETBIT(TEQUIP:野外PLAY, 1)
	PRINTL ＜野外PLAY中(森林)＞

	;獣耳ならBOUNS
	SIF PLAYER == MASTER && TALENT:動物耳
		TFLAG:好感度BOUNS += 1
	;一部種族にBOUNS
	SIF BASE:種族 == 3
		TFLAG:好感度BOUNS += 1
ELSEIF GETBIT(TEQUIP:野外PLAY, 2)
	PRINTL ＜野外PLAY中(花園)＞

	;花園補正。好感度にBOUNS。
	SIF PLAYER == MASTER
		TFLAG:好感度BOUNS += 1
ELSEIF GETBIT(TEQUIP:野外PLAY, 3) || GETBIT(TEQUIP:野外PLAY, 4)
	PRINTL ＜野外PLAY中(水辺)＞

	;夏ならBOUNS
	SIF PLAYER == MASTER && FLAG:季節 == 1
		TFLAG:好感度BOUNS += 2

	;水辺補正。汚れを洗い流せる。
	;汚れを初期化
	CALL RESET_STAIN_EX, "全部", MASTER
	CALL RESET_STAIN_EX, "全部", TARGET
	;助手がいる場合は、助手もきれいに
	SIF ASSI
		CALL RESET_STAIN_EX, "全部", ASSI
ENDIF

EXP:露出経験 += LOCAL

SIF PLAYER == MASTER && ABL:露出癖 >= 3
	TFLAG:好感度BOUNS += 1
