@COM158
;写真撮影調教版
;特定の条件でのみ撮影可能
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

CALL SOURCE_CHECK_SHORT

RETURN 0

;今回のエロ中に既に撮った物と似たシチュなら保存はしない
@TRAIN_MESSAGE_COM158
#DIM LCOUNT
#DIMS EVENT_TABLE
;誰の精液か
#DIMS SPERMS

STR:写真 = 
STR:補足 = 

SAVESTR:写真 = 
EVENT_TABLE = 
SPERMS = %TEXTS("最新中出し相手の")%

PRINTFORM %CALLNAME:PLAYER%将
;射精直後は抜いて撮る　中出し後の撮影機会を増やすため
IF PREVEX("射精", PLAYER) && INSERT("挿入")
	SELECTCASE PREVCOM
	CASE TNUM("扒開阴道"), TNUM("扒開菊穴")
	CASEELSE
		PRINTFORM 把陰莖拔出、
		CALL RESET_INSERT, TARGET, PLAYER
	ENDSELECT
ENDIF
IF PREVCOM == TNUM("扒開阴道") && STR:青写真 != ""
	PRINTFORM 押開了自己的阴部的%CALLNAME:TARGET%朝向这边
	STR:写真 = %STR:青写真%
	STR:補足 += "_阴部"
ELSEIF PREVCOM == TNUM("扒開菊穴") && STR:青写真 != ""
	IF PREVPLAY("排泄")
		PRINTFORM 将出来的东西排尽直後的%CALLNAME:TARGET%朝向这边
		STR:写真 = 押開%TEXTS("お尻：感触")%、排泄直後的
		IF TALENT:縦深菊穴
			STR:写真 += "縦深菊穴被"
		ELSE
			STR:写真 += "肛門被"
		ENDIF
		STR:写真 += @"%PHOTO_SHOWING("見せつけ")%"
	ELSE
		PRINTFORM 広開了自己尻部的%CALLNAME:TARGET%朝向这边
		STR:写真 = %STR:青写真%
	ENDIF
	STR:補足 += "_肛門"
;口に出した
ELSEIF PREVEX("口内射精") && !PREVPLAY("清洁口交")
	IF PREVPLAY("自我口内射精")
		PRINTFORM 朝向刚刚在自己的口中射出精液的%CALLNAME:TARGET%
		SPERMS = 自己的
	ELSE
		PRINTFORM 对着刚刚吐出精液的%CALLNAME:TARGET%
	ENDIF
	IF ABL:精液中毒 == 0 && ABL:奉仕精神 < 5
		STR:写真 += @"因为%SPERMS%精液的味道而紧缩眉頭的"
	ELSE
		IF TEQUIP:口枷
			STR:写真 += "被道具强迫打開的口对着相机、"
		ELSE
			STR:写真 += "大张着嘴、"
		ENDIF
		IF ABL:精液中毒 || ABL:奉仕精神 >= 5 || TALENT:精液嗜好
			STR:写真 += @"证明自己将所有的%SPERMS%精液吞下去的"
		ELSE
			STR:写真 += "展示着积存在口中的%SPERMS%精液の"
		ENDIF
	ENDIF
;ハメ撮り
ELSEIF INSERT("挿入")
	PRINTFORM 保持着和%CALLNAME:TARGET%相连的体勢
	IF INSERT("正常位") || INSERT("屈曲位") || INSERT("対面座位")
		IF PREVEX("前列腺高潮射精")
			STR:写真 += @"从%TEXTR("软软的/歪歪扭扭的")%阴茎中下流地%TEXTR("漏出/流出")%精液"
		ELSEIF (INSERT("正常位") || INSERT("屈曲位")) && TEQUIP:拘束 == 0 && RAND:2
			IF (TALENT:開朗 || TALENT:喜歡引人注目 || TALENT:笨蛋) && CFLAG:心情好 + TEQUIP:甜言蜜语
				STR:写真 += "笑着"
			ELSEIF ABL:PLAYER:施虐属性 >= 3 && RAND:3 == 0
				STR:写真 += "被单手压住"
			ELSEIF (TALENT:害羞 || TALENT:内気) && RAND:2
				STR:写真 += "両手捂住脸"
			ELSE
				STR:写真 += "在背后抓住床单"
			ENDIF
		ELSEIF ABL:欲望 >= 5 && PALAM:欲情 >= 10000 && COND("池面") == 0
			STR:写真 += @"%TEXTR("浮现出雌性的表情/嘴角流着口水")%"
		ELSE
			STR:写真 += "脸颊发红"
		ENDIF
		STR:写真 += @"ながら%CALLNAME:PLAYER%"
		IF ABL:欲望 >= 5 && RAND:3 == 0
			IF INSERT("Ａ挿入")
				STR:写真 += "进行着肛門性交在享受着の"
			ELSEIF TEQUIP:PLAYER:避孕套
				STR:写真 += "进行着以快楽为目的的性交在享受着の"
			ELSE
				STR:写真 += "进行着生性交在享受着の"
			ENDIF
		ELSEIF SIZE("阴茎", PLAYER) >= 2 && RAND:2
			IF INSERT("Ａ挿入")
				STR:写真 += @"的巨根%TEXTR("在肛門侵犯着/塞在尻穴里")%の"
			ELSE
				STR:写真 += @"的巨根%TEXTR("侵犯着/塞得慢慢的")%の"
			ENDIF
		ELSEIF INSERT("Ａ挿入")
			STR:写真 += @"%TEXTR("侵犯着肛門/在尻穴里交合着")%の"
		ELSE
			STR:写真 += @"%TEXTR("侵犯着/交合着")%の"
		ENDIF
	ELSEIF INSERT("後背位")
		SIF PREVEX("前列腺高潮射精")
			STR:写真 += @"从%TEXTR("软软的/歪歪扭扭的")%阴茎中下流地%TEXTR("漏出/流出")%精液的同时、"
		IF TEQUIP:肛用振動棒
			STR:写真 += "被振動棒挿入着的"
		ELSEIF COND("肛門栓")
			STR:写真 += "被塞子栓上的"
		ENDIF
		IF TALENT:巨尻
			STR:写真 += "大号的"
		ELSEIF TALENT:小尻
			STR:写真 += "小"
		ENDIF
		STR:写真 += @"尻%TEXTR("伸出/交出")%来供给%CALLNAME:PLAYER%侵犯の"
	ELSEIF INSERT("背面座位")
		SIF PREVEX("前列腺高潮射精")
			STR:写真 += @"从%TEXTR("软软的/歪歪扭扭的")%阴茎中下流地%TEXTR("漏出/流出")%精液的同时、"
		STR:写真 += @"坐在%CALLNAME:PLAYER%腿上被阴茎貫穿の"
	ELSEIF INSERT("騎乗位")
		IF TALENT:淫乱 + TALENT:好色 + TALENT:小悪魔 + TALENT:不知羞恥 && TEQUIP:拘束 == 0 && RANDIF(2, !TALENT:貧乳)
			IF CHECK_BODYS("兔") && RAND:3 == 0
				STR:写真 += @"双手放在頭顶上做出像是兔耳一样的手势、在%CALLNAME:MASTER%的身上%TEXTR("跳跃着/跳着下流的舞")%"
			ELSEIF EQUIP:裙 && RAND:2
				STR:写真 += "将裙子"
				IF CHECK_SKIRT("迷你") == 0 && COND("口の使用") && RAND:2
					STR:写真 += "用嘴唇叼住、"
				ELSE
					STR:写真 += "卷起来、"
				ENDIF
				IF PALAM:Ｖ潤 >= 5000 && RANDIF(1, EQUIP:胖次)
					STR:写真 += "咕啾咕啾的"
				ELSEIF EQUIP:胖次
					STR:写真 += "被挤到一边的内裤"
				ENDIF
				STR:写真 += "結合部露出来进行展示"
			ELSE
				STR:写真 += @"骑在%CALLNAME:PLAYER%上、"
				SIF PREVEX("前列腺高潮射精")
					STR:写真 += @"从%TEXTR("软软的/歪歪扭扭的")%阴茎中下流地%TEXTR("漏出/流出")%精液的同时、"
				IF ABL:露出癖 >= 3 && RAND:2
					STR:写真 += "大腿张開露出结合部の"
				ELSE
					STR:写真 += "十分享受地摇着腰の"
				ENDIF
			ENDIF
		ELSE
			STR:写真 += @"%CALLNAME:PLAYER%に突き上げられ、"
			SIF PREVEX("前列腺高潮射精")
				STR:写真 += @"%TEXTR("柔らかくなった/ふにゃふにゃの")%阴茎からだらしなく精液を%TEXTR("漏ら/垂ら")%しつつ"
			IF !TALENT:貧乳 && !TALENT:男性 && ABL:Ｂ感覚 >= 3 && TEQUIP:拘束 == 0 && RAND:2
				STR:写真 += "一边揉捏自己的胸一边喘息の"
			ELSEIF ABL:欲望 >= 3 && PALAM:欲情 >= 10000
				STR:写真 += @"带着%TEXTR("官能的/难以忍耐的/舒服的")%表情苦恼着の"
			ELSE
				STR:写真 += "脸上写满了欲望の"
			ENDIF
		ENDIF
	ENDIF
;拘束中
ELSEIF TEQUIP:拘束
	PRINTFORM 对着身體無法自由活動的%CALLNAME:TARGET%
	IF BIND("縄", TEQUIP:拘束)
		IF BIND("縄：亀甲縛り", TEQUIP:拘束)
			STR:写真 += "以龜甲縛的形式被拘束著"
		;ELSEIF BIND("縄：足吊", TEQUIP:拘束)
		;	STR:写真 += "片足を縄で吊られ"
		ELSE
			STR:写真 += "以被繩索牢牢捆住身體的形式被拘束著"
		ENDIF
	ELSE
		IF BIND("吊り", TEQUIP:拘束)
			STR:写真 += "以手腕被與天花板連接著的鎖鏈高高吊起的形式被拘束著"
		ELSE
			IF BIND("手枷", TEQUIP:拘束)
				STR:写真 += "以雙手被手枷固定的形式"
			ELSEIF BIND("床", TEQUIP:拘束)
				STR:写真 += "以身體鎖死在床鋪上的形式"
			ELSEIF BIND("四つん這い", TEQUIP:拘束)
				STR:写真 += "以身體鎖死在方桌上的形式"
			ELSE
				STR:写真 += "以身體鎖死在專用的高台上的形式"
			ENDIF
			STR:写真 += "被拘束著"
		ENDIF
	ENDIF
	IF PREVPLAY("清洁口交")
		STR:写真 += @"たまま%CALLNAME:PLAYER%の"
		IF SIZE("阴茎", PLAYER) >= 3
			STR:写真 += "巨大阴茎"
		ELSEIF SIZE("阴茎", PLAYER) >= 2
			STR:写真 += "巨根"
		ELSE
			STR:写真 += "阴茎"
		ENDIF
		STR:写真 += "陰莖用口腔仔細清掃著の"
	ELSE
		;足開かれてる
		IF GETBIT(TEQUIP:拘束, 21)
			IF PREVPLAY("挿入")
				IF PREVEX("Ｖ中出し") || PREVEX("Ａ中出し") || PREVEX("尿道内射精")
					STR:写真 += "不斷流淌出%SPERMS%精液的"
				ELSE
					STR:写真 += "剛剛被充分侵犯過的"
				ENDIF
				IF PREVPLAY("Ｖ挿入")
					STR:写真 += "阴部"
				ELSEIF PREVPLAY("Ａ挿入")
					STR:写真 += "尻"
				ELSE
					STR:写真 += "尿道"
				ENDIF
			;多分磨鏡と指挿れの場合　女你用
			ELSE
				STR:写真 += @"%TEXTR("不斷激烈地痙攣著的/被愛液變得黏黏糊糊的")%阴部"
			ENDIF
			STR:写真 += "清楚地暴露著の"
		ELSE
			IF ABL:受虐属性 >= 3 && RAND:2
				STR:写真 += "、因興奮而使臉頰佈滿紅潮の"
			ELSEIF PREVEX("絶頂数合計") && RAND:2
				STR:写真 += "、以完全溶化的表情沉浸在絕頂的餘韻中の"
			ELSE
				STR:写真 += "、因心中的羞恥而染紅了臉頰の"
			ENDIF
		ENDIF
	ENDIF
;お掃除
ELSEIF PREVPLAY("清洁口交")
	PRINTFORM 在%CALLNAME:TARGET%在持續著侍奉的同時
	IF (TALENT:喜歡引人注目 || TALENT:開朗) && RAND:2
		STR:写真 += @"%TEXTR("对着相机比出剪刀手/用一隻手向著鏡片比出了攝像的手勢/一边对着镜頭")%、"
	ELSEIF (TALENT:好奇心 || TALENT:献身 || TALENT:小悪魔) && RAND:2
		STR:写真 += "用上翻的眼珠靜靜地凝視著鏡頭、"
	ELSEIF (TALENT:冷静 || TALENT:感情缺乏) && RAND:2
 		IF PREVEX("絶頂数合計") || COND("弛緩")
			STR:写真 += "带着放松的表情、"
		ELSE
			STR:写真 += "臉上的表情毫不動搖、"
		ENDIF
	ELSEIF (TALENT:害羞 || TALENT:内気) && RAND:2
		STR:写真 += "羞澀地將視線與照相機偏移開來、"
	ELSEIF ABL:精液中毒 <= 2 && ABL:奉仕精神 <= 2
		STR:写真 += "眉頭因不適而微微皺起、"
	ELSEIF ABL:精液中毒 >= 3 && RANDIF(1, ABL:奉仕精神 >= 3)
		STR:写真 += "因侍奉的快樂而神志恍惚、"
	ELSEIF ABL:奉仕精神 >= 3
		STR:写真 += "緊閉著雙眼一心一意的持續侍奉著、"
	ENDIF
	STR:写真 += @"%CALLNAME:PLAYER%の"
	IF PREVPLAY("恥垢清洁口交")
		STR:写真 += @"沾满恥垢的\@ SIZE("阴茎", PLAYER) >= 2 ? 巨根 # 阴茎 \@"
	ELSEIF SIZE("阴茎", PLAYER) >= 3
		STR:写真 += "巨大阴茎"
	ELSEIF SIZE("阴茎", PLAYER) >= 2
		STR:写真 += "巨根"
	ELSE
		STR:写真 += "阴茎"
	ENDIF
	STR:写真 += "在口腔内仔細清掃著の"
;避孕套で玩遊戲
ELSEIF PREVPLAY("套套内射精", PLAYER) && RAND:2
	PRINTFORM 取下避孕套
	STR:写真 += "将装滿了精液而膨脹起來的避孕套"
	IF (TALENT:小悪魔 || TALENT:好色 || TALENT:淫乱) && RAND:3 == 0
		PRINTFORM 交給%CALLNAME:TARGET%之後、
		IF COND("精液入り避孕套数") >= 2
			STR:写真 += "提在双手中展示の"
		ELSE
			IF (TALENT:喜歡引人注目|| TALENT:開朗 || ABL:露出癖 >= 3) && RAND:2
				STR:写真 += @"用嘴叼住%TEXTR("Ｗ/＿")%比出剪刀手"
			ELSE
				STR:写真 += "叼在嘴里の"
			ENDIF
		ENDIF
	ELSEIF (TALENT:好奇心 || MARK:屈服刻印 >= 2) && RAND:2
		PRINTFORM 要求%CALLNAME:TARGET%用手拿住之后、
		IF (TALENT:喜歡引人注目|| TALENT:開朗 || ABL:露出癖 >= 3) && RAND:2
			STR:写真 += "一只手捏着一只手比出剪刀手"
		ELSE
			STR:写真 += "高高舉起の"
		ENDIF
	ELSE
		PRINTFORM 放好后
		IF ABL:PLAYER:施虐属性 >= 3 && RAND:4 == 0
			STR:写真 += "在脸上"
		ELSE
			IF PREVPLAY("後背位")
				STR:写真 += "在挺直的脊背上"
			ELSE
				STR:写真 += "在緊縮的小腹上"
			ENDIF
			SIF COND("精液入り避孕套数") >= 3
				STR:写真 += "复数的"
			STR:写真 += "放置着の"
		ENDIF
	ENDIF
;０７２１
ELSEIF PREVCOM == TNUM("自慰")
	IF TALENT:淫乱 && ABL:自慰中毒 >= 5
		STR:写真 += "一边吐出舌頭露出下流的表情一边"
	ELSEIF ABL:自慰中毒 >= 3
		STR:写真 += @"%TEXTR("完全沉浸于快感中/闭着眼睛看起来很舒服地样子")%的同时"
	ELSEIF TALENT:喜歡引人注目 || TALENT:開朗 || TALENT:不知羞恥
		STR:写真 += @"一边%TEXTR("向着这边比出剪刀手/向这边展示着")%一边"
	ELSEIF TALENT:叛逆
		STR:写真 += "斜着眼看着这边的同时"
	ELSE
		STR:写真 += "害羞地闭上眼睛的同时"
	ENDIF
	IF TEQUIP:振動棒 || TEQUIP:肛用振動棒 || TEQUIP:尿道振動棒
		IF TEQUIP:振動棒
			STR:写真 += "阴部"
		ELSEIF TEQUIP:肛用振動棒
			STR:写真 += "肛門"
		ELSEIF TEQUIP:尿道振動棒
			STR:写真 += "尿道"
		ENDIF
		STR:写真 += "中的玩具用手抓住前后缓慢地抽插着"
	ELSE
		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｃ"
			STR:写真 += "用手指责弄着秘所"
		CASE "Ｖ"
			STR:写真 += "将手指插入秘裂中"
		CASE "Ａ"
		 	STR:写真 += "将手指玩弄着菊門"
		CASE "Ｂ"
			STR:写真 += "揉弄着胸部"
		CASE "尿"
			STR:写真 += "用手指玩弄着尿道口"
		ENDSELECT
	ENDIF
;尿道
ELSEIF PREVCOM == 151
	STR:写真 += "将因为玩具而扩张開的尿道口"
	IF ABL:露出癖 >= 5
		STR:写真 += "展示给你看の"
	ELSE
		STR:写真 += "裸露出来"
	ENDIF
;展示肉棒
ELSEIF PREVPLAY("展示肉棒")
	PRINTFORM 你转向%CALLNAME:TARGET%挺出阴茎的同时
	STR:写真 += ""
	IF TALENT:淫乱 && BASE:欲求不満 >= 50 && RAND:3 == 0
		IF RAND:2
			STR:写真 += @"%TEXTR("用眼中浮现出爱心符号的发情的眼神/快要流出口水的恍惚表情")%持续不断地盯着你的阴茎的"
		ELSE
			STR:写真 += "温柔地握住你的阴茎的"
		ENDIF
	ELSEIF TALENT:好奇心 && RAND:2
		STR:写真 += "用想要吃进去试试看的眼神盯着你的阴茎的"
	ELSEIF (TALENT:開朗 || TALENT:喜歡引人注目) && RAND:2
		STR:写真 += "一边用一只手抚摸着你的阴茎一边做出姿势的"
	ELSEIF TALENT:害羞 || TALENT:清楚  && RAND:2
		STR:写真 += "在你的阴茎前露出了害羞的表情的"
	ELSEIF TALENT:叛逆
		STR:写真 += @"将你的阴茎推到一边%TEXTR("露出了不满的表情的/蔑视着这边的")%"
	ELSEIF TALENT:感情缺乏
		STR:写真 += "面无表情地看着你的阴茎的"
	ELSE
		STR:写真 += @"将你的阴茎推開%TEXTR("脸上泛起红潮的/微微飘逸着视线的")%"
	ENDIF
;其他
ELSE
	PRINTFORM 对准神情恍惚的吐出絮亂的氣息的%CALLNAME:TARGET%
	;ダブル剪刀手とかくぱぁとか
	IF MARK:屈服刻印 >= 2 && COND("指の使用") && TALENT:好色 + TALENT:淫乱 + COND("陶酔") + COND("満足") && RAND:2
		IF PREVPLAY("Ａ挿入")
			IF ABL:Ａ拡張 >= 3 && PREVPLAY("Ａ挿入中", PLAYER) && SIZE("阴茎", PLAYER) >= 2
				IF PREVEX("Ａ中出し")
					STR:写真 += "不檢點的精液垂流着的"
				ELSE
					STR:写真 += "肛門性交後中规中矩地张開的"
				ENDIF
			ELSEIF PREVEX("Ａ中出し")
				STR:写真 += "不斷流淌出%SPERMS%精液的"
			ELSE
				STR:写真 += "剛剛被充分侵犯過的"
			ENDIF
			STR:写真 += "尻穴"
		ELSEIF PREVPLAY("挿入")
			IF PREVEX("尿道内射精")
				STR:写真 += "不斷流淌出%SPERMS%精液的"
			ELSEIF PREVEX("Ｖ中出し")
				IF PREVEX("絶頂漏尿")
					STR:写真 += @"%TEXTR(@"漏尿之后从膣口逆流出精液的/刚刚絶頂放尿垂落出%SPERMS%精液的/因为自己尿液的残液濡湿而微微发光刚被中出的")%"
				ELSE
					STR:写真 += "不斷流淌出%SPERMS%精液的"
				ENDIF
			ELSEIF PREVEX("絶頂漏尿")
				STR:写真 += @"%TEXTR("刚刚漏出来还散发着气味的/刚刚激烈地喷溅出尿液的/被自己尿液的残液濡湿而微微发光")%"
			ELSE
				STR:写真 += "剛剛被充分侵犯過的"
			ENDIF
			IF PREVPLAY("Ｖ挿入")
				SIF COND("陰毛") && COND("陰毛") <= 3 && PREVEX("絶頂漏尿") == 0
					STR:写真 += "光滑的"
				STR:写真 += "阴部"
			ELSE
				STR:写真 += "尿道"
			ENDIF
		;女你用
		ELSE
			STR:写真 += @"%TEXTR("不斷激烈地痙攣著的/被愛液變得黏黏糊糊的")%阴部"
		ENDIF
		;Ｗ剪刀手乗り気
		IF TALENT:開朗 + TALENT:好色 + TALENT:淫乱 && RAND:3
			STR:写真 += "以Ｗ字開腿的姿勢主動地暴露着の"
		;Ｗ剪刀手陶酔
		ELSEIF COND("陶酔") && COND("大満足") && RAND:3
			STR:写真 += "露出的同时、带着荡漾着快楽的表情不检点地保持着Ｗ字開腿的姿勢の"
		ELSE
			STR:写真 += "用手指扩開、暴露在摄像机前の"
		ENDIF
	ELSE
		IF PREVPLAY("騎乗位")
			IF (TALENT:小悪魔 || TALENT:好色 || TALENT:淫乱) && RAND:2
				STR:写真 += @"%CALLNAME:PLAYER%を跨ぐように立ち、"
				IF PREVEX("Ｖ中出し") && PREVEX("Ａ中出し")
					STR:写真 += "精液垂流的前穴和後穴"
				ELSEIF PREVEX("Ｖ中出し")
					IF PREVEX("絶頂漏尿")
						STR:写真 += "尿和精液一并垂流的股間"
					ELSE
						STR:写真 += "%SPERMS%精液垂流的膣穴"
					ENDIF
				ELSEIF PREVEX("Ａ中出し")
					STR:写真 += "%SPERMS%精液垂流的肛門"
				ELSE
					STR:写真 += "剛剛被充分侵犯過的"
					IF PREVPLAY("Ｖ挿入")
						STR:写真 += "膣穴"
					ELSEIF PREVPLAY("Ａ挿入")
						STR:写真 += "肛門"
					ELSE
						STR:写真 += "尿道"
					ENDIF
				ENDIF
				STR:写真 += "对准镜頭展示の"
			ELSE
				STR:写真 += @"%CALLNAME:PLAYER%に跨ったまま"
				IF (TALENT:小悪魔 || TALENT:好色 || TALENT:淫乱) && RAND:2
					STR:写真 += "帶著妖艷的表情"
				ELSE
					STR:写真 += "带着佈滿紅潮的臉頰"
				ENDIF
				STR:写真 += "眼神恍惚地俯視著の"
			ENDIF
		;うつ伏せor屈曲位
		ELSEIF PREVPLAY("後背位") || PREVPLAY("屈曲位")
			IF PREVPLAY("後背位")
				IF RAND:2 && NAMES("床") == "床"
					STR:写真 += @"將床單緊緊抓握著、因短暫的脫力而伏在%NAMES("床")%上の"
				ELSE
					STR:写真 += @"渾身癱軟無力的趴伏在%NAMES("床")%上、短暫地休息著の"
				ENDIF
			ELSEIF PREVPLAY("屈曲位")
				STR:写真 += "保持着屈曲体勢の"
			ENDIF
			IF PREVEX("Ｖ中出し") && PREVEX("Ａ中出し")
				STR:写真 += "前後穴都有精液不斷流淌の"
			ELSEIF PREVEX("Ｖ中出し")
				IF PREVEX("絶頂漏尿")
					STR:写真 += "股間有尿和精液不斷流淌の"
				ELSE
					STR:写真 += @"从膣穴不断漏出%SPERMS%精液的"
				ENDIF
			ELSEIF PREVEX("Ａ中出し")
				IF PREVEX("前列腺高潮射精")
					STR:写真 += "肛門和阴茎中不断漏出精液的"
				ELSE
					STR:写真 += @"从肛門不断漏出%SPERMS%精液的"
				ENDIF
			ELSE
				STR:写真 += "精疲力尽の"
			ENDIF
		ELSEIF PREVPLAY("背面座位")
			IF PREVEX("前列腺高潮射精")
				STR:写真 += @"靠在%CALLNAME:PLAYER%的身上、从阴茎中咕噜咕噜地不断流出精液的"
			ELSE
				STR:写真 += @"因疲憊而倚靠在%CALLNAME:PLAYER%的懷中休息著の"
			ENDIF
		ELSE
			IF RAND:2 && NAMES("床") == "床"
				STR:写真 += "在被各種汁液浸透的床單上半倚著直起腰"
			ELSE
				STR:写真 += @"在%NAMES("床")%上仰面躺下"
			ENDIF
			IF PREVEX("Ｖ中出し") && PREVEX("Ａ中出し")
				STR:写真 += "、前後穴都有精液不斷流淌、"
			ELSEIF PREVEX("Ｖ中出し")
				IF PREVEX("絶頂漏尿")
					STR:写真 += "、阴部有尿和精液不斷流淌、"
				ELSE
					STR:写真 += "、膣穴有%SPERMS%精液不斷流淌、"
				ENDIF
			ELSEIF PREVEX("Ａ中出し")
				IF PREVEX("前列腺高潮射精")
					STR:写真 += "、尻穴和阴茎中不断流出精液、"
				ELSE
					STR:写真 += "、尻穴有%SPERMS%精液不斷流淌、"
				ENDIF
			ENDIF
			IF PREVEX("絶頂数合計") && RAND:4 == 0
				SIF TALENT:感情缺乏
					STR:写真 += "隐隐"
				STR:写真 += "用湿润的眼瞳看向这边的"
			ELSEIF TALENT:相思相愛 && RAND:2
				STR:写真 += "露出放荡的笑颜的"
			ELSEIF TALENT:不知羞恥 || TALENT:感情缺乏
				STR:写真 += "以舒緩的表情靜靜地發呆の"
			ELSE
				STR:写真 += @"用%TEXTR("両手/单手/手腕")%遮住眼睛休息著の"
			ENDIF
		ENDIF
	ENDIF
ENDIF

STR:写真 += @"%PHOTO_COSTUME(TARGET)%%CALLNAME:TARGET%"

STR:補足 += @"_%CALLNAME:TARGET%"
STR:補足 += "_事後"

SIF CHECK_CLO("泳装姿")
	STR:補足 += "_泳装"
SIF CHECK_CLO("体操服姿")
	STR:補足 += "_体操服"
SIF CHECK_CLO("学生服姿")
	STR:補足 += "_学生服"

;日期の記録
STR:補足 += @"_日期%TOSTR(DAY*10 + TIME)%"

SAVESTR:写真 = %STR:写真%

CALL SAVE_PHOTO, @"%STR:写真%%STR:補足%"

PRINTFORMW 摆好手机
PRINTFORML 咔嚓（攝像聲）
PRINTFORMW %CALLNAME:PLAYER%得到了『%SAVESTR:写真%』的写真。

;履歴を残す
VARSET LOCALS
SPLIT SAVESTR:Ｈ中の撮影履歴, "/", LOCALS

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK
	;すでに似た写真があるかを格子
	SIF CHECK_HIGHMATCH(SAVESTR:写真, LOCALS:LCOUNT) == 0
		CONTINUE
	SAVESTR:写真 = 
NEXT
SIF SAVESTR:写真 != ""
	SAVESTR:Ｈ中の撮影履歴 += @"%SAVESTR:写真%\/"

STR:青写真 = 


@FLAG_COM158, ARG

@SOURCE_COM158, ARG
;とりあえず攝像機を少し弱めた感じ
CALL VARSET_COMF

SOURCE:0:露出 = 2000
SOURCE:0:逸脱 = 1000

;裸体
IF CHECK_CLO("裸体")
	;種族衣装が裸体でない
	IF CLO_ORI(TARGET, "服", "贴身制服", "下身衣装", "胖次")
		SOURCE:0:露出 += 3000
		SOURCE:0:逸脱 += 1000
	ENDIF
;胖次のみ
ELSEIF CHECK_CLO("内衣姿")
	;種族衣装が服or贴身制服or下身衣装を含む
	IF CLO_ORI(TARGET, "服", "贴身制服", "下身衣装")
		SOURCE:0:露出 += 2000
		SOURCE:0:逸脱 += 500
	ENDIF
;没穿内褲
ELSEIF CHECK_CLO("没穿内褲")
	;種族衣装に内衣有
	IF CLO_ORI(TARGET, "胖次")
		SOURCE:0:露出 += 1000
		SOURCE:0:逸脱 += 300
	ENDIF
ENDIF

;拘束の状態により露出を追加
IF GETBIT(TEQUIP:拘束, 5)
	SOURCE:0:露出 += 1000
ELSEIF GETBIT(TEQUIP:拘束, 4)
	SOURCE:0:露出 += 500
ELSEIF GETBIT(TEQUIP:拘束, 3)
	SOURCE:0:露出 += 200
ENDIF

;露出はTRACHECKで補正がかかるので、逸脱に害羞などで補正
SIF TALENT:害羞
	TIMES SOURCE:0:逸脱, 2.00
SIF TALENT:清楚
	TIMES SOURCE:0:逸脱, 1.50
SIF TALENT:喜歡引人注目
	TIMES SOURCE:0:逸脱, 0.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 1.70
SIF TALENT:聰慧
	TIMES SOURCE:0:露出, 1.50
SIF TALENT:笨蛋
	TIMES SOURCE:0:露出, 0.70

SOURCE:0:恐惧 += SOURCE:0:逸脱 / 10

CALL CALC, "調教ソース", ARG


