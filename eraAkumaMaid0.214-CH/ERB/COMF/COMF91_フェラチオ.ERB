@COM91
;口交
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 91

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
IF CHECK_COM("手交口交")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 90
	CALL SAMEN_CHECK, 50
	SELECTCOM = 91
ELSEIF CHECK_COM("乳夾口交")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 92
	CALL SAMEN_CHECK, 50
	SELECTCOM = 91
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM91, ARG
CALL SETFLAG, "口交フラグ", TARGET

SIF CHECK_COM("阴茎接吻", 91)
	CALL SETFLAG, "阴茎接吻フラグ", TARGET

IF CHECK_COM("手交口交") == 91
	CALL FLAG_COM90, 50
ELSEIF CHECK_COM("乳夾口交") == 91
	CALL FLAG_COM92, 50
ENDIF

;対象の口⇔調教者のＰ的污垢が移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ｐ"
IF CHECK_COM("恥垢清洁口交", 91)
	CALL SETFLAG, "恥垢清洁口交", TARGET, PLAYER
ELSEIF CHECK_COM("清洁口交", 91)
	CALL SETFLAG, "清洁口交", TARGET, PLAYER
ENDIF

SIF ASSIPLAY == 0 && EXP:口交経験 >= EXPLV:3
	TFLAG:好感度BOUNS += 1

@SOURCE_COM91, ARG
IF CHECK_COM("手交口交") == 91
	CALL SOURCE_COM90, 50
ELSEIF CHECK_COM("乳夾口交") == 91
	CALL SOURCE_COM92, 50
ENDIF

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "口交"

CALL SOURCE_DOWNBASE_HP, 91

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｍ")*100 + 100
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 1500
SOURCE:0:逸脱 = 500

IF CHECK_COM("清洁口交", 91)
	SOURCE:0:性行動 += 1000
	SOURCE:0:達成感 += 1000
	TIMES SOURCE:0:Ｍ快楽, 0.20
	SOURCE:0:屈従 += 1000
	SIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ａ")
		SOURCE:0:屈従 += 3000
ENDIF

;快Mのソース・M感覚を見る
SOURCE:0:Ｍ快楽 = CALCF("Ｍ刺激", 2)

;喉性感がないならば激減
IF TALENT:喉性感 == 0
	TIMES SOURCE:0:Ｍ快楽, 0.05
	SIF SOURCE:0:Ｍ快楽 < 100
		SOURCE:0:Ｍ快楽 = 0
ENDIF

;深喉中？
IF TEQUIP:深喉
	TIMES SOURCE:0:性行動, 1.50
	TIMES SOURCE:0:達成感, 1.50
	TIMES SOURCE:0:疼痛, 1.50
	TIMES SOURCE:0:不潔, 1.50
	TIMES SOURCE:0:恐惧, 1.50
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
	TIMES SOURCE:0:Ｍ快楽, 1.50
ENDIF

SIF TALENT:Ｍ性向
	CALL CALC, "好みのPLAY", SOURCE:0:Ｍ快楽

IF CHECK_COM("阴茎接吻", 91)
	TIMES SOURCE:0:性行動, 0.10
	TIMES SOURCE:0:達成感, 0.10
	TIMES SOURCE:0:疼痛, 0.10
	TIMES SOURCE:0:不潔, 0.50
	TIMES SOURCE:0:恐惧, 0.50
	TIMES SOURCE:0:屈従, 0.50
	TIMES SOURCE:0:逸脱, 0.50
	TIMES SOURCE:0:Ｍ快楽, 0.10
	SOURCE:0:情愛 += 700
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM91
IF CHECK_COM("Ｗ奉仕")
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 兩人
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT 神情恍惚、眼神濕潤的對視著的同時
		ELSEIF RAND:2 == 0
			PRINT 如同完全沉浸於快感一樣無意識的漏出涎水的同時
		ENDIF
		PRINTFORMW 將%CALLNAME:MASTER%的陰莖用舌頭爬上、旋轉的舔舐起來……
	ELSE
		PRINTFORM %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 在用充滿慾望與依戀的雙瞳凝視著%CALLNAME:MASTER%赤裸身體的同時、
		IF RAND:3
			LOCAL = RAND:2
			SIF LOCAL == 0
				PRINTFORM 像是在競爭一樣
			IF (PREVCOM >= 65 && PREVCOM <= 67) || PREVCOM == 75
				PRINTFORM %CALLNAME:TARGET%的
			ELSEIF (PREVCOM:1 >= 65 && PREVCOM:1 <= 67) || PREVCOM:1 == 75
				PRINTFORM %CALLNAME:(TARGET:1)%將
			ENDIF
			IF PREVCOM == 75 || PREVCOM:1 == 75
				PRINTFORM 沾滿了腸道污液的
			ELSEIF (PREVCOM >= 65 && PREVCOM <= 67) || (PREVCOM:1 >= 65 && PREVCOM:1 <= 67)
				PRINTFORM 被膣液所完全濡濕的
			ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢")
				PRINTFORM 殘留著充滿淫臭的包皮垢的
			ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ａ")
				PRINTFORM 沾滿了腸道污液的
			ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ")
				PRINTFORM 被膣液所完全濡濕的
			ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液")
				PRINTFORM 被新鮮灼熱的精液包裹著的
			ENDIF
			IF CHECK_COM("清洁口交")
				PRINTFORMW 陰莖用口腔包裹住並吮吸著清潔起來……
			ELSEIF LOCAL == 0
				IF TALENT:(TARGET:1):恋慕 && TALENT:恋慕 && RAND:2
					PRINTFORMW 陰莖不斷地啄吻著、在充滿依戀的親吻與舔舐過後開始吮吸起來……
				ELSE
					PRINTFORMW 陰莖投入的吮吸著……
				ENDIF
			ELSE
				PRINTFORMW 陰莖交替地吮吸著……
			ENDIF
		ELSE
			IF CHECK_COM("清洁口交")
				PRINTFORMW 相互協助的將陰莖用兩人的口腔舔舐吮吸著清潔乾淨了……
			ELSE
				PRINTFORMW 相互協助的將陰莖用舌頭舔舐全身後交替地吮吸起來……
			ENDIF
		ENDIF
	ENDIF
;==91で双龙入穴口交、== 90なら口交＆手交、それ以外は犯されながら口交
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;同時推倒
ELSEIF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM 、%TEXTS("Ｗ推倒中")%、
	ELSEIF PREVCOM:1 >= 65 && PREVCOM:1 <= 67
		PRINTFORM 將之前被%CALLNAME:(TARGET:1)%的膣道深深地吞沒、沾染了白色的泡沫的
	ELSEIF PREVCOM:1 == 75
		PRINTFORM 將被%CALLNAME:(TARGET:1)%的腸液完全地污染的
	ELSEIF PREVCOM >= 65 && PREVCOM <= 67
		PRINTFORM 刚刚从自己的膣穴中解放还散发着强烈气味的
	ELSEIF PREVCOM == 75
		PRINTFORM 刚刚从自己的尻穴中解放染上了污物的
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW 將%CALLNAME:MASTER%的陰莖用舌頭爬上、旋轉的舔舐起來……
	ELSEIF CHECK_COM("清洁口交")
		IF PREVCOM:1 >= 60 && PREVCOM <= 79
			PRINTFORMW %CALLNAME:MASTER%的陰莖熱心地用口腔漂亮的清潔了……
		ELSE
			PRINTFORMW %CALLNAME:MASTER%的髒污的陰莖用口腔打掃了……
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:MASTER%的陰莖含住後吮吸起來……
	ENDIF
ELSEIF CHECK_COM("恥垢清洁口交")
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:電話中
		PRINTFORM 和%TEXTR(@"感觉和平时的电话没有什么不同的/什么都不知道地给这边打电话的/完全没有任何怀疑持续地通电话的/完全不知情/完全不知道发生了这样的事情的/不知道对方已经出轨了的/相信着%CALLNAME:TARGET%的")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("说这话的同时/持续通话的同时/进行着日常会话的同时/谈论着最近发生的事情的同时/讲着情话的同时")%、
	ENDIF
	;气味弱点分岐
	IF PLAYER == MASTER && TALENT:气味弱点 && TALENT:恋慕 && RAND:5
		IF RAND:5 == 0
			PRINTFORM 戀戀不捨的將陰莖的淫靡%TEXTS("气味")%深深嗅聞之後、
		ELSEIF RAND:4 == 0
			PRINTFORM 將陰莖的%TEXTS("气味")%仔細嗅聞之後、
		ELSEIF RAND:3 == 0
			PRINTFORM 因對著衝入鼻腔的%TEXTS("气味")%深呼吸之後興奮起來的同時、
		ELSEIF RAND:2
			PRINTFORM 用龜頭緊緊抵住鼻尖將散發的%TEXTS("气味")%烙印在腦海中之後、
		ELSE
			PRINTFORM 對%CALLNAME:PLAYER%的陰莖所散發的淫靡%TEXTS("气味")%深深入迷的同時、
		ENDIF
	ELSE
		IF COND("假性包茎", PLAYER) && RAND:2
			PRINTFORM 將薄薄的包皮拉下、讓龜頭完全露出、
		ELSEIF RAND:2
			PRINTFORM 不讓舌頭停歇片刻、
		ELSE
			PRINTFORM 像是對普通的清潔口交產生厭倦一樣、
		ENDIF
	ENDIF
	PRINTFORM 将%TEXTR("恥垢/散發著醉人淫臭的包皮垢/緊緊粘附的污垢/像是臭味濃郁的奶酪般熟成的污垢")%
	PRINTFORMW %TEXTR("也用舌頭刮干净、将阴茎漂亮地打磨了一遍/也毫无保留地用嘴掃除了/和阴茎一起掃除得干干净净/也熱心地開始舔了起来")%…

;手交口交or乳夾口交
ELSEIF CHECK_COM("手交口交") || CHECK_COM("乳夾口交")
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:電話中
		PRINTFORM 和%TEXTR(@"感觉和平时的电话没有什么不同的/什么都不知道地给这边打电话的/完全没有任何怀疑持续地通电话的/完全不知情/完全不知道发生了这样的事情的/不知道对方已经出轨了的/相信着%CALLNAME:TARGET%的")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("说这话的同时/持续通话的同时/进行着日常会话的同时/谈论着最近发生的事情的同时/讲着情话的同时")%、
	ENDIF
	IF CHECK_COM("手交口交")
		PRINTFORM 用手指將陰莖輕輕擼動的同時、向因快感而不斷溢出先走汁的龜頭鈴口
	ELSEIF CHECK_COM("乳夾口交")
		IF TALENT:貧乳 || TALENT:男性
			PRINT 用貧乏單薄的胸部拼命地將陰莖
		ELSEIF TALENT:巨乳
			PRINT 用丰腴柔軟的乳肉將陰莖包裹住、
		ELSE
			PRINT 用胸部夾住陰莖的同時
		ENDIF
		PRINTFORM 不斷地上下摩擦著、向穿過胸前峽谷清晰露出的龜頭
	ENDIF
	;恋慕or献身的or奉仕精神3以上or汚臭鈍感
	IF (TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3 || TALENT:汚臭鈍感) && RAND:2 == 0
		PRINT 就好像是在品嚐美味一樣的
	;害羞
	ELSEIF TALENT:害羞 && RAND:3 == 0
		IF RAND:2 == 0
			PRINT 害羞地
		ELSE
			PRINT 臉頰染上慾望與羞澀的紅暈、
		ENDIF
	;欲情4以上
	ELSEIF PALAM:欲情 >= PALAMLV:4 && RAND:3 == 0
		SIF TALENT:倒錯的 && RAND:3 == 0
			PRINTFORM 因慾望的%TEXTS("气味")%而
		PRINT 臉頰發燙、情緒興奮的
	;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
	ELSEIF ( (TALENT:恋慕 && ABL:奉仕精神 == 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
		PRINT 滿臉幸福快樂的
	;恋慕or献身的or奉仕精神3以上
	ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
		PRINT 就好像是在品嚐美味一樣的
	ENDIF
	;奉仕精神3以上か欲情4以上
	IF (ABL:奉仕精神 >= 3 || PALAM:欲情 >= PALAMLV:4) && RAND:3 >= 1
		PRINTFORM 深深含住然後開始吮吸
	ELSE
		PRINTFORM 旋轉著舔舐起來
	ENDIF
	PRINTW ……
;前回は強制口交
ELSEIF PREVCOM == 140 && COND("前Ｔ調教者") == PLAYER && TEQUIP:推倒 == 0
	PRINTFORM 在%CALLNAME:PLAYER%短暫的停歇腰肢的擺動並輕輕地撫摸著%CALLNAME:TARGET%頭頂的時候、她不禁
	;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
	IF ( (TALENT:恋慕 && ABL:奉仕精神 == 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
		IF COND("視力") && RAND:2
			PRINT 用不住上翻的眼珠仰視著神情的同時、
		ELSE
			PRINT 滿臉幸福快樂的
		ENDIF
	;恋慕or献身的or奉仕精神3以上
	ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
		PRINT 就好像是在品嚐美味一樣的
	;強気
	ELSEIF TALENT:強気 && COND("視力") && RAND:3
		PRINTFORM 用與平日裡強硬眼神截然不同的濕潤瞳孔凝視著%CALLNAME:PLAYER%的同時、
	ELSEIF RAND:2 == 0 && ABL:技巧 < 2
		PRINT 開始生疏的吞吐侍奉著
	ENDIF
	;技巧４かつ奉仕精神３
	IF ABL:技巧 >= 4 && ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORMW 將陰莖用咽喉含住、讓啾嚕啾嚕的淫亂水聲與壓抑著的呻吟隨著頭頸的動作不斷迴響著……
	;奉仕精神3以上か欲情4以上
	ELSEIF (ABL:奉仕精神 >= 3 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINTFORMW 將陰莖深深含住然後開始吮吸……
	ELSE
		PRINTFORMW 將陰莖旋轉著舔舐起來……
	ENDIF
;前回と同じor深喉中
ELSEIF (PREVCOM == SELECTCOM || TEQUIP:深喉) && COND("前Ｔ調教者") == PLAYER && PREVPLAY("阴茎接吻") == 0
	PRINTFORM %CALLNAME:TARGET%、
	IF TEQUIP:電話中
		PRINTFORM 和%TEXTR(@"感觉和平时的电话没有什么不同的/什么都不知道地给这边打电话的/完全没有任何怀疑持续地通电话的/完全不知情/完全不知道发生了这样的事情的/不知道对方已经出轨了的/相信着%CALLNAME:TARGET%的")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("说这话的同时/持续通话的同时/进行着日常会话的同时/谈论着最近发生的事情的同时/讲着情话的同时")%、
	ENDIF
	IF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM 神情恍惚的緊緊凝視著%CALLNAME:PLAYER%
	ELSEIF ABL:奉仕精神 >= 3 && COND("視力") && RAND:3 == 0
		PRINTFORM 凝视着%CALLNAME:PLAYER%的脸
	ELSEIF ABL:技巧 >= 3 && RAND:2
		PRINTFORM 用嫻熟的手法調節節奏將%CALLNAME:PLAYER%
	ELSEIF ABL:精液中毒 >= 3 && RAND:2
		PRINTFORM 在口中積攢出充分地唾液來與腥臭的先走汁混合後、將口感複雜的淫靡果汁一飲而盡。隨後在因烙印在腦內的奇妙快感而恍惚的同時投入地將%CALLNAME:PLAYER%
	ELSE
		PRINTFORM 將粘稠的唾液塗抹在唇角之後、將%CALLNAME:PLAYER%
	ENDIF
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢")
		IF RAND:3 == 0
			PRINTFORM 殘留著充滿淫臭的包皮垢的
		ELSEIF RAND:2
			PRINTFORM 黏著了苦澀恥垢的
		ELSE
			PRINTFORM 蒸騰著醉人%TEXTS("气味")%的
		ENDIF
	ENDIF
	PRINTFORM 阴茎
	;前回or今回清洁口交
	IF CHECK_COM("清洁口交")
		PRINTFORMW \@ ABL:奉仕精神 + TALENT:恋慕 >= 5 ? 全心灌注地 # \@徹底的打掃乾淨了……
	ELSEIF PREVPLAY("清洁口交")
		PRINTFORMW 用舌尖持續的摩擦著……
	ELSE
		PRINTFORMW 用口腔含住並持續吮吸……
	ENDIF
ELSE
	SIF TEQUIP:眼罩 && TEQUIP:推倒 + TEQUIP:六九式 == 0
		PRINTFORM 被蒙上眼睛的
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:電話中
		PRINTFORM 和%TEXTR(@"感觉和平时的电话没有什么不同的/什么都不知道地给这边打电话的/完全没有任何怀疑持续地通电话的/完全不知情/完全不知道发生了这样的事情的/不知道对方已经出轨了的/相信着%CALLNAME:TARGET%的")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("说这话的同时/持续通话的同时/进行着日常会话的同时/谈论着最近发生的事情的同时/讲着情话的同时")%、
	ENDIF
	;前回阴茎接吻
	IF COND("前Ｔ調教者") == PLAYER && PREVPLAY("阴茎接吻")
		;あんまり慣れてない
		IF TALENT:献身 + TALENT:好色 + TALENT:小悪魔 == 0 && (MARK:屈服刻印 <= 1 || EXP:口交経験 < 20)
			PRINTFORM 按照%CALLNAME:PLAYER%的要求、
		ELSE
			PRINTFORM %TEXTR("伸长了舌頭将其润滑后/探出脸来/在深呼吸一次之后")%
		ENDIF
		PRINTFORM %TEXTR("打完招呼的/亲吻完毕的/刚刚结束接吻的")%
	;气味弱点分岐
	ELSEIF PLAYER == MASTER && TALENT:气味弱点 && (TALENT:恋慕 || TALENT:淫乱) && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
		IF CHECK_COM("清洁口交")
			IF PREVPLAY("挿入中", PLAYER)
				CALL ADD_TEXT, "性器から口へ"
				PRINTFORM %CALLNAME:PLAYER%的陰莖的美味用舌頭品尝着、
			ELSEIF PREVPLAY("放尿", PLAYER)
				PRINTFORM 舌頭朝着放尿直後的%CALLNAME:PLAYER%的阴茎面露興奮地伸出、
			ELSE
				SIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液")
					PRINTFORM 被白濁精液所
				PRINTFORM 弄脏的%CALLNAME:PLAYER%的阴茎的%TEXTS("气味")%嗅着的同时、
			ENDIF
			PRINTFORMW 口腔\@ ABL:奉仕精神 + TALENT:恋慕 >= 5 ? 全心灌注地 # \@徹底的打掃乾淨了……
		ELSEIF PREVPLAY("挿入中", PLAYER)
			CALL ADD_TEXT, "性器から口へ"
			PRINTFORMW %CALLNAME:PLAYER%的阴茎開心地将脸贴近、伸出了舌頭将其光滑地迎入口中…
		ELSEIF RAND:3 == 0 && TALENT:男性
			PRINTFORMW %CALLNAME:PLAYER%緊縮玉袋的淫靡%TEXTS("气味")%深深嗅聞的同時、帶著恍惚的神情將陰莖\@ CHECK_COM("阴茎接吻") ? 反復的啄吻著 # 旋轉著舔舐起來 \@……
		ELSEIF RAND:2
			PRINTFORM 用龜頭緊緊抵住鼻尖、將
			SIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液")
				PRINTFORM 摻雜著濃郁精臭的
			PRINTFORMW 散發的%TEXTS("气味")%深深地嗅聞著、同時\@ CHECK_COM("阴茎接吻") ? 與鈴口熱情地深吻 # 就好像是在品嚐美味一樣吮吸 \@起来……
		ELSE
			PRINTFORMW 陰莖\@ CHECK_COM("阴茎接吻") ? 不斷地啄吻著 # 深深含入口中 \@的同時、任由心神陶醉在%CALLNAME:PLAYER%股間那淫靡的%TEXTS("气味")%所醞釀的淫猥氣氛之中……
		ENDIF
		RETURN 1
	;被逆推ているとき
	ELSEIF TEQUIP:推倒
		;直前の調教と汚れで分岐
		IF COND("Ｐ汚れ", PLAYER)
			SIF (PREVCOM >= 70 && PREVCOM <= 79) && COND("前Ｔ調教者") == PLAYER && TEQUIP:眼罩 == 0
				PRINT 從自己的肛門剛剛拔出、
			;容易濕で追加
			SIF TALENT:容易濕 && RAND:3 && TEQUIP:眼罩 == 0
				PRINT べっとりと
			SIF (PREVCOM >= 60 && PREVCOM <= 69) && COND("前Ｔ調教者") == PLAYER && TEQUIP:眼罩 == 0
				PRINT 被自己的甜膩愛液弄髒的、
			PRINT 染上淫靡污漬的、
		;淫乱or施虐狂or施虐属性Lv.5以上で「害羞」を持たない
		;ELSEIF (TALENT:淫乱 || TALENT:小悪魔 || ABL:施虐属性 >= 5) && TALENT:害羞 == 0
		;	PRINT 滴落著透明唾液的
		ELSEIF RAND:3 == 0
			PRINT 不斷散發出淫靡氣味
		ELSE
			PRINTFORM %TEXTS("阴茎：視覚", PLAYER)%
		ENDIF
	ELSE
		;六九式
		IF TEQUIP:六九式
			PRINTFORM %TEXTS("６９")%
		;眼罩で分岐。ところで、Ｍ字開脚座りってもっといい名称無いですかね？
		ELSEIF TEQUIP:眼罩 || TEQUIP:拘束
			PRINT 、被嘴唇慢慢摸索到位置的
		;奉仕精神3以上で拉米亚
		ELSEIF ABL:奉仕精神 >= 3 && COND("種族：拉米亚")
			PRINTFORM %CALLNAME:PLAYER%的股間貼近並摩擦著、將
		;淫乱かつ奉仕精神3以上かつ欲情4以上
		ELSEIF TALENT:好色 + TALENT:淫乱 && ABL:奉仕精神 >= 3 && PALAM:欲情 >= PALAMLV:4 && RAND:3
			IF PREVEX("Ｖ中出し") + PREVEX("Ａ中出し") && CHECK_CLO("Ｖ露出") + CHECK_CLO("Ａ露出")
				PRINTFORM Ｍ字開腿的坐姿保持著、
				IF RAND:2
					LOCALS = 因羞耻而剧烈颤抖着、
				ELSE
					LOCALS = 散發著劇烈的淫猥%TEXTS("气味")%、
				ENDIF
				IF PREVEX("Ｖ中出し") && CHECK_CLO("Ｖ露出")
					PRINTFORM 从那里
					IF RAND:2
						PRINTFORM 零星地灑落混雜著精液的愛液之雨的同時、將
					ELSE
						PRINTFORM %LOCALS%任由精液混雜著流淌而出的液體灑下的同時、將
					ENDIF
				ELSEIF PREVEX("Ａ中出し") && ABL:Ａ拡張 >= 3 && CHECK_CLO("Ａ露出")
					PRINTFORM 在肛門
					IF BASE:排泄物 == 0
						PRINTFORM 中剛剛灌注的新鮮精液慢慢的%TEXTR("零星地/垂流著")%灑落、將
					ELSEIF RAND:2
						PRINTFORM 中與%TEXTR("腸液/汚液")%混合而變色的精液正%TEXTR("零星地/垂流著")%灑落的同時、將
					ELSE
						PRINTFORM %LOCALS%任由精液混雜著流淌而出的液體灑下的同時、將
					ENDIF
				ELSE
					PRINTFORM 胯股之间%LOCALS%讓漸漸溢出的白濁精液混雜著流淌而出的淫液混合而成的淫猥果汁正淅淅瀝瀝的灑落下來、將
				ENDIF
			ELSE
				PRINTFORM 向%CALLNAME:PLAYER%突出股間、以Ｍ字開腿的姿勢保持並端坐著、將
			ENDIF
		;献身的or恋慕or奉仕精神3以上
		ELSEIF CHECK_COM("阴茎接吻") || ( (TALENT:献身 || TALENT:恋慕 || ABL:奉仕精神 >= 3) && RAND:3)
			IF TALENT:高贵 && RAND:2
				PRINTFORM 垂落的髮梢優雅的向一側撩上、將
			ELSE
				PRINTFORM 跪坐在%CALLNAME:PLAYER%股間之前的姿態保持著、將
			ENDIF
		ENDIF

		;直前の調教と汚れで分岐
		IF PREVEX("放尿", PLAYER)
			IF RAND:2
				PRINTFORM 散發著撲鼻而來的強烈臭味的
			ELSE
				PRINTFORM 殘留著濁黃尿液的
			ENDIF
		ELSEIF PREVPLAY("挿入中", PLAYER)
			CALL ADD_TEXT, "性器から口へ"
		ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") && CHOSECOM(SELECTCOM) == 0 && RAND:3 == 0
			PRINTFORM 殘留著充滿淫臭的包皮垢的
		;汚れていない時
		ELSEIF RAND:3 == 0 && CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ", "Ｐ恥垢") == 0
			PRINTFORM 散發著異臭的
		ELSE
			PRINTFORM %TEXTS("阴茎：視覚", PLAYER)%
			SIF PREVEX("射精", PLAYER) && CONDS("間柄", PLAYER) == "教え子"
				PRINTFORM 學生的
		ENDIF
	ENDIF
	IF CHECK_COM("阴茎接吻")
		;あんまり慣れてない
		IF TALENT:献身 + TALENT:好色 + TALENT:小悪魔 == 0 && (MARK:屈服刻印 <= 1 || EXP:口交経験 < 20)
			PRINTFORMW 陰莖%TEXTR("滿面羞紅的輕輕接吻了/膽怯而小心的用嘴唇輕輕貼住了/猶豫地親吻了")%…
		ELSEIF COND("種族：悪魔") + COND("種族：天使") + COND("種族：淫魔") && MARK:屈服刻印 && RAND:2
			PRINTFORMW 陰莖%TEXTR("帶著恍惚的神情持續深吻著/恭敬地與之深吻著/就像是迫不及待一樣與之熱吻著")%…
		ELSE
			PRINTFORMW 陰莖%TEXTR("不斷地啄吻著全身上下/恭敬地與之深吻著/熱情地持續深吻著")%……
		ENDIF
	ELSE
		PRINTFORM 陰莖

		;汚れてますか？汚臭鈍感で無いならば、実行値の10分の一の値とRAND:5を比べて判定
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ", "Ｐ恥垢") && TALENT:汚臭鈍感 == 0 && RAND:6 > TCVAR:干劲/10
			PRINTFORM 因精液的淫臭而皺著眉頭
		;恋慕or献身的or奉仕精神3以上or汚臭鈍感
		ELSEIF (TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3 || TALENT:汚臭鈍感) && RAND:2 == 0
			PRINTFORM 看上去很美味一般
		;害羞
		ELSEIF TALENT:害羞 && RAND:3 == 0
			PRINT 害羞地
		;欲情4以上
		ELSEIF PALAM:欲情 >= PALAMLV:4 && RAND:3 == 0
			SIF TALENT:倒錯的 + TALENT:气味弱点 && RAND:3 == 0
				PRINTFORM 因慾望的%TEXTS("气味")%而
			PRINT 臉頰發燙、情緒興奮的
		;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
		ELSEIF ( (TALENT:恋慕 && ABL:奉仕精神 == 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
			PRINT 滿臉幸福快樂的
		;恋慕or献身的or奉仕精神3以上
		ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
			PRINT 就好像是在品嚐美味一樣的
		ELSEIF RAND:2 == 0 && TEQUIP:推倒 == 0 && ABL:技巧 < 2
			PRINT 開始生疏的吞吐侍奉著
		ENDIF

		IF CHECK_COM("清洁口交")
			PRINTFORMW 口腔\@ ABL:奉仕精神 + TALENT:恋慕 >= 5 ? 全心灌注地 # \@徹底的打掃乾淨了……
		;信頼4以上or奉仕精神3以上or受虐属性3以上、かつ技巧3以上だとディープをやってきたり。信頼2以下or奉仕精神1以下は舐めるのみ
		ELSEIF ABL:信頼 <= 2 || ABL:奉仕精神 <= 1
			;娇小で追加
			SIF TALENT:娇小
				PRINT 小小的
			PRINTFORMW 將陰莖旋轉著舔舐起來……
		ELSE
			;深喉判定
			;信頼４or奉仕精神３or受虐属性３で、技巧が３以上
			IF (ABL:信頼 >= 4 || ABL:奉仕精神 >= 3 || ABL:受虐属性 >= 3) && ABL:技巧 >= 3 && CHECK("喉姦") && RAND:3 == 0
				TEQUIP:深喉 = PLAYER
				;フラグとTEQUIPが分かれているのは、射精後などにTEQUIP:深喉が０となっている時でも参照するため
				CALL SETFLAG, "深喉フラグ", TARGET
				
				IF CHECK("喉姦") >= 2 && RAND:3
					PRINTFORM 整個口腔\@ SIZE("阴茎") >= 2 ? 甚至連連著食道也被貫穿的 # \@直到根部
				ELSE
					PRINTFORM \@ TALENT:娇小 ? 狹窄的 # \@咽喉最深處
				ENDIF
				;喉LVで追加
				IF CHECK("喉姦") >= 3 && RAND:2
					PRINT 帶著就像是進行平常的侍奉一樣冷靜的表情
				ELSEIF CHECK("喉姦") >= 2
					IF RAND:2
						PRINT 一口気
					ELSE
						PRINT 輕而易舉地
					ENDIF
				ELSE
					PRINT 磕磕碰碰地
				ENDIF
				IF CHECK("喉姦") >= 2 && RAND:2
					PRINT 吞入口中、
				ELSEIF RAND:2
					PRINT 接納至體內、
				ELSE
					PRINT 用口腔包裹住、
				ENDIF
			ELSE
				PRINT 用口腔包裹住、
			ENDIF
			;眼罩没有で、奉仕精神3以上
			IF ABL:奉仕精神 >= 3 && TEQUIP:眼罩 == 0 && TEQUIP:六九式 == 0 && RAND:3 == 0
				IF TEQUIP:鼻钩
					PRINTFORM 露出%TEXTR("抬起的猪鼻子/被広開的鼻穴/笨拙的鼻钩脸")%、用不住上翻的眼珠仰視著神情的同時
					IF TEQUIP:深喉
						PRINTFORMW 用咽喉痙攣的軟肉侍奉著…
					ELSE
						PRINTFORMW 將陰莖深深含住然後開始吮吸……
					ENDIF
				ELSE
					;害羞
					SIF (TALENT:害羞 || TALENT:恋慕) && RAND:4
						PRINT 臉頰染上慾望與羞澀的紅暈、
					IF TEQUIP:深喉
						PRINTFORMW 用%TEXTR("不住上翻的眼珠/濕潤的瞳孔")%仰視著神情的同時、用咽喉痙攣的軟肉侍奉著……
					ELSE
						PRINTFORMW 用不住上翻的眼珠仰視著神情的同時、用整個口腔吮吸侍奉著……
					ENDIF
				ENDIF
			;技巧４かつ奉仕精神３
			ELSEIF ABL:技巧 >= 4 && ABL:奉仕精神 >= 3 && RAND:3 == 0
				IF TEQUIP:深喉 && ABL:舌靈活 >= 3
					IF CHECK("喉姦") >= 2 && ABL:舌靈活 >= 5 && RAND:2
						PRINTFORMW 用伸出的舌頭將\@ TALENT:PLAYER:男性 ? 玉袋 # 陰部 \@挑弄著愛撫的同時、將\@ EXP:口交経験 >= 200 ? 龜頭用食道痙攣的肌肉摩挲的同時、將 # \@整根陰莖用口腔與咽喉侍奉著……
					ELSEIF RAND:2
						PRINTFORMW 將整個咽喉驅使著刺激深入口中的陰莖……
					ELSE
						PRINTFORMW 用抽搐的咽喉侍奉著、讓啾嚕啾嚕的淫亂水聲與壓抑著的呻吟隨著頭頸的動作不斷迴響著……
					ENDIF
				ELSE
					PRINTFORMW 用緊縮的雙唇緊緊捆住的同時搖晃著腦袋給予刺激……
				ENDIF
			;奉仕精神3以上か欲情4以上
			ELSEIF (ABL:奉仕精神 >= 3 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
				IF ABL:技巧 >= 4 && RAND:2
					IF TEQUIP:深喉
						PRINT 用緊縮的雙唇緊緊捆住的同時
					ELSE
						PRINT 啜飲著溢出唾液與先走汁的混合濃汁的同時
					ENDIF
				ENDIF
				PRINTFORMW 開始猛烈地打喷嚏了……
			ELSEIF (RAND:2 == 0 && ABL:技巧 >= 2) || TEQUIP:深喉
				IF TEQUIP:深喉
					PRINTFORMW 整根吸入之後用不住抽搐的咽喉開始吮吸……
				ELSE
					PRINTFORMW 吸了一口气開始说话……
				ENDIF
			ELSE
				PRINTFORMW 舔过来了……
			ENDIF
		ENDIF
	ENDIF
ENDIF


@COM_ORDER_COM91, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 5
	LOCALS = %LOCALS% - 害羞(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;Ｍ性向
IF TALENT:Ｍ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｍ性向(10)
ENDIF
;男性
IF TALENT:男性 && TALENT:兩面通吃 == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - 男性(15)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｐ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｍ")
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｍ")})
ENDIF

;合計を表示(34以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 34

RETURN RESULT
