@COM147
;喝套内精液
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 147

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

RETURN 1

@FLAG_COM147, ARG
;口に精液汚れ追加
CALL ADD_STAIN, TARGET, "Ｍ", "精液"
;喝套内精液フラグ
SETBIT TCVAR:イベント, 51


@SOURCE_COM147, ARG
CALL SOURCE_SMPLAY, "喝套内精液", ARG

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 147

SOURCE:0:不潔 = (ITEM:套套内精液 + 4)/5 * 500
SOURCE:0:欲情追加 = (ITEM:套套内精液 + 4)/5 * 500 + CALCF("実行値による欲情追加")
SOURCE:0:屈従 = (ITEM:套套内精液 + 4)/5 * 1000
SOURCE:0:逸脱 = (ITEM:套套内精液 + 4)/5 * 500
SOURCE:0:充足 = (ITEM:套套内精液 + 4)/5 * 1000

;精液経験をみる
SELECTCASE EXP:精液経験
CASE IS < 5
	TIMES SOURCE:0:逸脱, 4.00
CASE IS < 20
	TIMES SOURCE:0:逸脱, 2.00
CASE IS < 50
	TIMES SOURCE:0:逸脱, 1.50
CASE IS < 100
	TIMES SOURCE:0:逸脱, 1.20
CASE IS < 300
	TIMES SOURCE:0:逸脱, 1.00
CASEELSE
	TIMES SOURCE:0:逸脱, 0.50
ENDSELECT

;ABL:精液中毒をみる
SELECTCASE ABL:精液中毒
CASE 0
	TIMES SOURCE:0:欲情追加, 0.10
	TIMES SOURCE:0:屈従, 0.50
CASE 1
	TIMES SOURCE:0:欲情追加, 1.00
	TIMES SOURCE:0:屈従, 1.00
CASE 2
	TIMES SOURCE:0:欲情追加, 1.20
	TIMES SOURCE:0:屈従, 1.30
CASE 3
	TIMES SOURCE:0:欲情追加, 1.40
	TIMES SOURCE:0:屈従, 1.60
CASE 4
	TIMES SOURCE:0:欲情追加, 1.80
	TIMES SOURCE:0:屈従, 2.00
CASEELSE
	TIMES SOURCE:0:欲情追加, 2.00
	TIMES SOURCE:0:屈従, 3.00
ENDSELECT

CALL CALC, "調教ソース", ARG

@TRAIN_MESSAGE_COM147

;前回ターンに出した直後の避孕套有些
IF PREVPLAY("套套内射精", PLAYER)
	PRINTFORM %CALLNAME:PLAYER%從陰莖上取下了装滿精液的避孕套、
ELSE
	PRINTFORM %CALLNAME:PLAYER%不知從哪裡取出了一個装滿了精液的使用過的避孕套、
ENDIF
PRINTFORM %CALLNAME:TARGET%、
IF ABL:精液中毒 >= 3 && RAND:2
	PRINTFORMW 將倒過來的避孕套上流下的精液用%TEXTR("伸出的舌頭/大張的嘴")%接住了。
ELSEIF ABL:精液中毒 && (RAND:2 || ABL:精液中毒 >= 3)
	PRINTFORMW 緊緊含住避孕套的開口後、%TEXTR("氣勢洶湧地/咕嚕咕嚕地")%吮吸著。
ELSE
	PRINTFORMW 躊躇的張開嘴、將漸漸滴落的精液用舌尖接住了。
ENDIF

PRINTFORM %CALLNAME:TARGET%
SELECTCASE ABL:精液中毒
CASE IS < 3
	PRINTFORM 似乎對於%TEXTR("摻雜著橡膠臭味的精液/這種行為/吮吸使用過的避孕套")%還是%TEXTR("毫無辦法的樣子/不能抑制的皺起眉頭/還不習慣的樣子")%、
	IF ABL:精液中毒 && RAND:2
		PRINTFORMW %TEXTR("順從身體的飢渴將腥臭的精液/毫不費力的將粘稠的白果醬/一邊因為異常的行為而變得更加興奮、一邊將灼熱的液體")%灌入咽喉……
	ELSE
		PRINTFORMW 在口中積累了足夠的唾液來稀釋後、帶著苦澀的表情將精液灌入喉中……
	ENDIF
CASEELSE
	IF PREVPLAY("套套内射精", PLAYER) && RAND:2
		IF RAND:2
			PRINTFORM 將從避孕套中滴落的%TEXTR("新鮮的/餘溫尚在的/剛射出來的")%精液、
		ELSE
			PRINTFORM 將%CALLNAME:PLAYER%所射出的精液、
		ENDIF
	ELSE
		PRINTFORM %TEXTR("毫不介意帶著橡膠臭味的精液/反而對清理用過的避孕套很起勁一樣/痴迷於精液中的橡膠臭味一樣")%、
	ENDIF
	IF ABL:精液中毒 >= 5 && RAND:2
		PRINTFORMW %TEXTR("好像很美味一样/一边品尝着/一边吐着舌頭/一边舔着舌頭")%一口氣喝光了…
	ELSE
		PRINTFORMW %TEXTR("一副若無其事的樣子/喉嚨咕嚕咕嚕地響著/眼神濕潤、臉颊也染上紅暈")%、将精液一口氣喝光了……
	ENDIF
ENDSELECT


@COM_ORDER_COM147, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 5
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 5})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 3
	LOCALS = %LOCALS% - 害羞(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

;合計を表示(50以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
