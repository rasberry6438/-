@COM96
;肛門奉仕
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 96

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
IF CHECK_COM("玩弄Ａ手交")
	CALL SAMEN_CHECK, 70
	SELECTCOM = PREVCOM
	CALL SAMEN_CHECK, 50
	SELECTCOM = 96
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM96, ARG
SIF TEQUIP:推倒 == 0
	CALL SETFLAG, "Ａ奉仕FLAG", TARGET
SIF CHECK_COM("玩弄Ａ手交") == 96
	CALL FLAG_COM90, 50
;対象の口⇔調教者のA的污垢が移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ａ"

@SOURCE_COM96, ARG
SIF CHECK_COM("玩弄Ａ手交") == 96
	CALL SOURCE_COM90, 50

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "肛門奉仕"

CALL SOURCE_DOWNBASE_HP, 96

SOURCE:0:不潔 = COM_ORDER_STAIN("Ａ", "Ｍ")*100 + 100
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:逸脱 = 3000

IF TEQUIP:推倒
	SOURCE:0:性行動 += 1000
ELSE
	SOURCE:0:屈従 = 5000
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM96
;同時推倒
IF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ推倒中")%、
	IF SELECTCOM == PREVCOM
		PRINTFORMW 將%CALLNAME:MASTER%的肛門持續用舌頭舔舐著…
	ELSE
		IF ABL:奉仕精神 >= 3 && ABL:施虐属性 >= 3 && RAND:2 == 0
			PRINTFORMW 將%CALLNAME:MASTER%的尻穴用舌尖深入的舔舐、摳挖著內側……
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW 將%CALLNAME:MASTER%的肛門用舌頭細緻地舔舐清理著……
			STAIN:MASTER:Ａ = SET_STAIN(0, "Ａ")
		ELSE
			PRINTFORMW 將%CALLNAME:MASTER%的尻穴用舌頭咕嚕咕嚕的玩弄著……
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
ELSE
	PRINTFORM %CALLNAME:TARGET%
	;前回逆肛交
	IF PREVPLAY("Ａ挿入", PLAYER)
		PRINTFORM 將失去填充物而不斷張合著的%CALLNAME:PLAYER%的肛門
		IF ABL:奉仕精神 >= 3 && RAND:2 == 0
			PRINTW 用舌頭撫慰著……
		ELSE
			PRINTW 用舌頭舔舐著……
		ENDIF
	ELSE
		IF CHECK_COM("玩弄Ａ手交")
			IF PREVCOM == 97
				LOCALS = %CLO("内裤冲")%
				LOCALS:1 = %LOCALS%
			ELSE
				LOCALS = 手
				LOCALS:1 = 掌
			ENDIF
			IF TALENT:恋慕 && RAND:4 == 0
				PRINTFORM 將最愛的人的陰莖用%LOCALS%溫柔細緻地侍奉著的同時、
			ELSEIF ABL:技巧 >= 3 && ABL:奉仕精神 >= 3 && RAND:3 == 0
				PRINTFORM 將敏感的龜頭用%LOCALS:1%來回地揉搓旋轉著的同時、
			ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
				PRINTFORM 將陰莖用絕妙的力道刺激著的同時、
			ELSEIF RAND:2 == 0
				PRINTFORM 將陰莖用%LOCALS%慢慢地擼動著的同時、
			ELSE
				PRINTFORM 將陰莖用%LOCALS%擼動著的同時、
			ENDIF
		ENDIF
		IF ABL:奉仕精神 >= 3 && ABL:施虐属性 >= 3 && RAND:2 == 0
			PRINTFORMW 將%CALLNAME:MASTER%的尻穴用舌尖深入的舔舐、摳挖著內側……
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW 細緻地用舌頭舔舐着肛門清理著……
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSEIF ABL:施虐属性 >= 3 || TEQUIP:推倒
			PRINTFORMW 將%CALLNAME:MASTER%的尻穴用舌頭咕嚕咕嚕的玩弄著……
		ELSE
			PRINTFORMW 將%CALLNAME:MASTER%的肛門戰戰兢兢地舔舐著……
		ENDIF
	ENDIF
ENDIF


@COM_ORDER_COM96, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF

;害羞
IF TALENT:害羞
	LOCAL -= 5
	LOCALS = %LOCALS% - 害羞(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ａ", "Ｍ")})
ENDIF

;合計を表示(40以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
