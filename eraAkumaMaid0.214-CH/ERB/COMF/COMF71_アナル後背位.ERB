@COM71
;肛門性交
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM71, ARG
CALL FLAG_INSERTION, "Ａ挿入", ARG, 110

@SOURCE_COM71, ARG
CALL SOURCE_INSERTION, "Ａ挿入", ARG, 110

CALL VARSET_COMF

SOURCE:0:露出 = 2000
;動物耳
SIF TALENT:動物耳
	SOURCE:0:露出 = 1500
SOURCE:0:情愛 = 350

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM71
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;肛門が露出していないで、胖次をずらしていないなら自動的にずらす
ELSEIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
	SIF TEQUIP:拘束
		PRINTFORM 被拘束具牢牢禁錮著的
	PRINTFORM %CALLNAME:TARGET%的
	CALL SETFLAG, "下身衣装を露出：テ接吻ト有些", TARGET
;肛門性交に慣れている、かつ欲情LV4以上で縛られてないと誘ってきます
ELSEIF CHECK("Ａ挿入") && PALAM:欲情 >= PALAMLV:4 && TEQUIP:拘束 == 0 && RAND:2 == 0
	IF PREVCOM == 13
		PRINT 用手指展開陰部的同時、將
	ELSEIF PREVCOM == 14
		PRINT 用手指推開臀瓣的同時、將
	;淫乱
	ELSEIF (TALENT:淫乱 || ABL:Ａ感覚 >= 3) && RAND:2 == 0
		IF RAND:2 || TALENT:男性
			IF TALENT:小尻
				PRINT 小小的
			ELSEIF TALENT:巨尻
				PRINT 粗大的
			ENDIF
			PRINT 尻部推開展示出來的同時、將
		ELSE
			PRINT 將重要的地方展示出來的同時、將
		ENDIF
	ELSE
		PRINTFORM %TEXTR("將羞恥的地方暴露到一覽無遺的同時、將/將顯示出雌性氣息的股間清楚地暴露著的同時、將")%
	ENDIF
	;小悪魔
	IF TALENT:小悪魔 && RAND:3
		PRINT 無聲的輕笑著的
	;(叛逆でなくて淫乱) or(叛逆かつ恋慕)
	ELSEIF  ( (TALENT:淫乱 && TALENT:叛逆 == 0) || (TALENT:叛逆 && TALENT:恋慕) ) && RAND:3
		SIF TALENT:小悪魔 && RAND:2 == 0
			PRINT 、大膽地
		PRINT 輕聲發起邀請的
	;叛逆かつ淫乱
	ELSEIF TALENT:叛逆 && TALENT:淫乱
		PRINT 偏過頭、臉頰被羞恥染紅的
	;害羞
	ELSEIF TALENT:害羞
		PRINT 臉頰微紅、不自覺緊張的微笑的
	ELSE
		PRINTFORM %TEXTR("微笑んでいる/横たわっている")%
	ENDIF
	PRINTFORM %CALLNAME:TARGET%的
ELSE
	;縛ってますか？
	IF TEQUIP:拘束
		PRINTFORM 被拘束具牢牢禁錮著的%CALLNAME:TARGET%那
	;拉米亚ですか？
	ELSEIF COND("種族：拉米亚")
		SIF CHECK("Ａ挿入") && RAND:2 == 0
			PRINT 用尾巴摩擦並纏繞著的
		PRINTFORM %CALLNAME:TARGET%的
	;肛門性交に慣れているなら誘ってきます
	ELSEIF CHECK("Ａ挿入") && RAND:2 == 0
		SIF TALENT:害羞
			PRINT 脸颊因羞涩而染红的
		IF CHECK_COM("立位")
			PRINT 变成站立后背位体勢
		ELSE
			PRINT 四肢着地趴跪着
		ENDIF
		;淫乱かつ欲情4以上
		IF TALENT:淫乱 && PALAM:欲情 >= PALAMLV:4 && RAND:2 == 0
			PRINT 、并且轻轻摇晃着尻部诱惑着的
		ELSE
			PRINT 的
		ENDIF
		PRINTFORM %CALLNAME:TARGET%的
	ELSE
		IF TALENT:叛逆 && ABL:Ａ感覚 <= 2 && TALENT:恋慕 == 0
			PRINT 讓滿臉嫌惡的
		ELSEIF TALENT:害羞
			PRINT 臉頰因害羞而染上紅暈的
		ENDIF
		IF CHECK_COM("立位")
			PRINTFORM %CALLNAME:TARGET%的尻部转了过来
		ELSE
			PRINTFORM %CALLNAME:TARGET%四肢著地趴跪下來、將
		ENDIF
		SIF TALENT:害羞 || (TALENT:叛逆 && ABL:Ａ感覚 <= 2 && TALENT:恋慕 == 0)
			PRINT 、
	ENDIF
	PRINTFORM %TEXTS("肛門")%
ENDIF
PRINTFORM %TEXTR("お菊門/肛門")%に
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
ELSE
	PRINTFORM %NAMES("阴茎", PLAYER)%を
ENDIF
IF NUM("回合数") == 0
	PRINTFORM %TEXTR("完全沒有前戲的/沒有任何準備的")%
;PLAYERが施虐狂かつA経験20以下orＡ潤1LV未満
ELSEIF (ABL:PLAYER:施虐属性 >= 3 && EXP:Ａ経験 <= 20) || PALAM:Ａ潤 < PALAMLV:1
	PRINT 用力地
;PLAYERが施虐狂or肛門性交に慣れている
ELSEIF (CHECK("Ａ挿入") || ABL:PLAYER:施虐属性 >= 3) && RAND:2 == 0
	PRINT 一口気
ELSEIF RAND:2 == 0 || EXP:Ａ経験 <= EXPLV:3
	PRINT 慎重地
ENDIF
;PLAYERが施虐狂orA経験20以上or恋慕かつA感覚Lv3以上
SIF ABL:PLAYER:施虐属性 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3)
	PRINT 直達根部地
;PLAYERが施虐狂orＡ潤1LV未満
IF ABL:PLAYER:施虐属性 >= 3 || PALAM:Ａ潤 < PALAMLV:1
	PRINT 壓入、
ELSE
	PRINT 刺入、
ENDIF
IF TALENT:妊娠 && ABL:PLAYER:施虐属性 >= 3
	PRINT 像是對妊娠中的她腹中的孩子毫不在意一樣、
ELSEIF TALENT:妊娠 && RAND:2 == 0
	PRINT 在輕輕撫摸膨脹的腹部的同時、
ELSEIF TALENT:妊娠
	PRINT 細緻地關注並小心的不對腹腔中的小生命造成負擔的同時、
ELSEIF RAND:2
	PRINTFORM %TEXTR("抚摸着/揉捏着/抓捏着")%屁股的同时
;PLAYERが施虐狂orA経験20以上or恋慕かつA感覚Lv3以上
ELSEIF TALENT:娇小 && (ABL:PLAYER:施虐属性 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3) )
	PRINT 將整個嬌小的身體緊緊抓住、
ELSEIF ABL:PLAYER:施虐属性 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3)
	PRINT 將纖細的腰肢緊緊抓住、
ENDIF
;PLAYERが施虐狂or妊娠していない、かつA経験3以上or恋慕かつA感覚Lv4以上
IF (ABL:PLAYER:施虐属性 >= 3 || TALENT:妊娠 == 0) && (MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3) ) && RAND:3 == 0
	PRINT 像是在享受痛苦的反應一樣粗暴地
ELSEIF (ABL:PLAYER:施虐属性 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || ABL:Ａ感覚 >= 3 || ABL:PLAYER:腰靈活) && SIZE("阴茎", PLAYER) && RAND:2 == 0
	SIF ABL:PLAYER:腰靈活 && RAND:2
		PRINT 從入口一直到最深處都一併玩弄的
	PRINT 给予沉重一击似地
ELSEIF MAX(EXP:Ａ経験, EXP:Ａ性交経験) < 50
	PRINTFORM %TEXTR("浅浅地/慢慢地")%
ELSE
	IF RAND:2 == 0
		IF RAND:2 == 0 && TALENT:男性 == 0
			PRINT 將敏感腸壁對著子宮的方向不斷壓入著同时
		ELSE
			PRINT 將敏感腸壁咯哧咯哧的摩擦著地的同时
		ENDIF
	ELSE
		PRINT 激烈地
	ENDIF
ENDIF
;PLAYERが施虐狂orA経験20以上orA感覚Lv3以上
IF (ABL:PLAYER:施虐属性 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || ABL:Ａ感覚 >= 3) && RAND:3 == 0
	PRINTFORMW 用腰肢擊打著……
ELSE
	PRINTFORMW %TEXTR("反復進出著/腰肢前後衝擊著/晃動著腰肢")%。
ENDIF
