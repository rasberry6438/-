@COM177
;被两人逆推
;一人はTARGET、もう一人はTARGET:1で管理する。もしもTARGET:1が未設定の場合には助手がTARGET:1となる

CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 177

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

CALL SETFLAG, "Ｗ推倒"

RETURN 1

@FLAG_COM177, ARG

@SOURCE_COM177, ARG
CALL VARSET_COMF

SOURCE:0:性行動 = 600
SOURCE:0:達成感 = 1500

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM177
IF TEQUIP:推倒
	PRINTFORMW 像是為了響應%CALLNAME:TARGET%的邀請、%CALLNAME:(TARGET:1)%慢慢的靠近了%CALLNAME:PLAYER%……
	IF TARGET:1 == ASSI && TEQUIP:攝像機
		PRINTFORMW 顯而易見的、%CALLNAME:(TARGET:1)%決定將攝影中止一起享受快樂……
	ELSE
		PRINTFORMW 顯而易見的、%CALLNAME:(TARGET:1)%決定一起享受快樂……
	ENDIF
ELSE
	PRINTFORMW 露出妖艷笑容的%CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%慢慢靠近了%CALLNAME:PLAYER%……
	PRINTFORMW %CALLNAME:PLAYER%被兩個人推到了……
ENDIF
TEQUIP:攝像機 = 0


;@EVENTCOMでSELECTCOMに応じて決定される。
;挣扎、顺其自然の時にはそれぞれのCOMF中で決定するため、@EVENTCOMでは呼ばない。
@SET_SELECTCOM_W
#DIM LCOUNT
;それぞれ、TARGET:0orTARGET:1が面部騎乗、挿入、奉仕中かを見る
#DIM IS_FENDAMU, 2
#DIM IS_INSERT, 2
#DIM IS_SERVICE, 2
;二穴挿入に派生しやすい条件を満たしていたらＯＮ
#DIM IS_WPENE
;TARGET:1が指令実行可能かどうか
#DIM CANCOM, 160


CALL SET_FORBIDCOM, TARGET:1, SELECTCOM

SWAP TARGET, TARGET:1

VARSET CANCOM
FOR LCOUNT, 0, 110
	;格子必要な指令は実は少ない
	SELECTCASE LCOUNT
	CASE 1, 9, 65, 75, 83, 90 TO 95, 97, 104
		TRYCALLFORM COM_ABLE{LCOUNT}
		CANCOM:LCOUNT = RESULT
	ENDSELECT
NEXT

SWAP TARGET, TARGET:1

VARSET IS_FENDAMU
VARSET IS_INSERT
VARSET IS_SERVICE
VARSET IS_WPENE

SELECTCASE SELECTCOM
CASE 1, 9
	IS_FENDAMU = 1
CASE 60 TO 79, 83
	IS_INSERT = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE = 1
CASE 120
	SIF PREVCOM:1 == 126
		IS_WPENE = 126
CASE 126
	SIF PREVCOM:1 == 120
		IS_WPENE = 120
ENDSELECT

SELECTCASE PREVCOM:1
CASE 1, 9
	IS_FENDAMU:1 = 1
CASE 60 TO 79, 83
	IS_INSERT:1 = 1
CASE 90 TO 95, 97, 104
	IS_SERVICE:1 = 1
ENDSELECT

;挿入中なら、続行
IF IS_INSERT:1 && FORBIDCOM(TARGET:1, "性交", "Ｐ下半身") == 0
	SELECTCOM:1 = PREVCOM:1
;泡泡浴はＷ泡泡浴
ELSEIF SELECTCOM == 94 && CANCOM:94
	SELECTCOM:1 = 94
;Ｗ騎乗中なら高確率で続行
ELSEIF RAND:3 && IS_INSERT && IS_FENDAMU:1
	SELECTCOM:1 = PREVCOM:1
ELSEIF RAND:3 && IS_FENDAMU && IS_INSERT:1
	SELECTCOM:1 = PREVCOM:1
;Ｗ騎乗を起き易く
ELSEIF IS_INSERT && CANCOM:1 + CANCOM:9 && RAND:3 == 0
	IF CANCOM:1 && RANDIF(1, CANCOM:9)
		SELECTCOM:1 = 1
	ELSE
		SELECTCOM:1 = 9
	ENDIF
ELSEIF IS_FENDAMU && CANCOM:65 + CANCOM:75 + CANCOM:83 && RAND:3 == 0
	IF CANCOM:83 && RANDIF(1, CANCOM:75, CANCOM:65)
		SELECTCOM:1 = 83
	ELSEIF CANCOM:65 && RANDIF(1, CANCOM:75)
		SELECTCOM:1 = 65
	ELSE
		SELECTCOM:1 = 75
	ENDIF
;同時奉仕中なら続行
ELSEIF IS_SERVICE && SELECTCOM == PREVCOM && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
;同時奉仕を起き易く
ELSEIF IS_SERVICE && PREVCOM:1 == SELECTCOM
	SELECTCOM:1 = PREVCOM:1
ELSEIF IS_SERVICE && CANCOM:SELECTCOM && RAND:3 == 0
	SELECTCOM:1 = SELECTCOM
;二穴挿入中なら、続行
ELSEIF INSERT("二穴挿入", PLAYER)
	SELECTCOM:1 = PREVCOM:1
;二穴挿入になりやすくしてみる
ELSEIF IS_WPENE && RAND:2 == 0
	SELECTCOM:1 = IS_WPENE
ELSEIF FORBIDCOM(TARGET:1)
	SELECTCOM:1 = -1
ELSE
	;補佐役のABL:手～ABL:强奸を決定
	CALL CALC_COUNTER_ABL, TARGET:1

	;强奸の後は連続强奸
	IF (PREVCOM:1 == 120 || PREVCOM:1 == 126) && ABL:(TARGET:1):强奸 && RAND:4
		ABL:(TARGET:1):手 = 0
		ABL:(TARGET:1):口 = 0
		ABL:(TARGET:1):胸 = 0
		ABL:(TARGET:1):足 = 0
		ABL:(TARGET:1):面部騎乗 = 0
		ABL:(TARGET:1):騎乗位 = 0
		ABL:(TARGET:1):接吻 = 0
		ABL:(TARGET:1):玩弄臀部 = 0
		ABL:(TARGET:1):素股 = 0
	ENDIF

	SELECTCOM:1 = SET_COUNTERCOM(TARGET:1)
ENDIF

;禁止指令消去
CALL SET_FORBIDCOM, TARGET:1, -1

@EQUIP_COM177_2
;3P中
SIF TEQUIP:Ｗ推倒 || SELECTCOM:1 >= 0
	CALL EQUIP_COM_3P


@COM_ORDER_COM177, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:施虐属性
IF ABL:施虐属性
	LOCAL += MIN(ABL:施虐属性, 5) * 3
	LOCALS = %LOCALS% + 施虐属性LV{ABL:施虐属性}({MIN(ABL:施虐属性, 5) * 3})
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 10
	LOCALS = %LOCALS% - 害羞(10)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 5
	LOCALS = %LOCALS% - 否定快感(5)
ENDIF

;処女
IF TALENT:処女
	LOCAL -= 20
	LOCALS = %LOCALS% - 処女(20)
ELSEIF EXP:Ｖ経験 < EXPLV:2
	LOCAL -= 5
	LOCALS = %LOCALS% - Ｖ経験不足(5)
ENDIF

;受け身
IF TALENT:受け身
	LOCAL -= 10
	LOCALS = %LOCALS% - 受け身(10)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;合計を表示(70以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 70

RETURN RESULT
