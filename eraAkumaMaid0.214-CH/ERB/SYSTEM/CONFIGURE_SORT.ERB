;-------------------------------------------------
;角色クターの並び替えを行う
;合わせて@SORT_CHARA, ARGSも参照すること
;-------------------------------------------------
@CHARA_SORT
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE, 3
#DIMS CHOICES, 2

MEMO_LINECOUNT = LINECOUNT

REDRAW 0
PRINTL 
PRINTFORML 进行角色的重新排列。
PRINTFORML 如果想切换某２人的顺序的话就直接选择该二人就行了
PRINTFORML 以及、想根据好感度或者角色番号之类的一口气完成排列的话就选择[200]就行了

FOR LCOUNT, 2, CHARANUM
	CALL PRINT_NUM_NAME, "能力表示", LCOUNT
NEXT
PRINTL  [100]返回　[200]整体排序

$INPUT_LOOP
CALL INPUT_SELECT, CHARANUM, 100, 200

SELECTCASE RESULT
CASE 0, 1
	GOTO INPUT_LOOP
CASE 100
	REDRAW 1
	RETURN 0
CASE 200
	PRINTL 
	PRINTFORML 以哪个为要素进行排列？関于[ 6]～[10]、可以选择任意的元素进行排序
	PRINTFORML  [ 0] 加入時的顺序　　　　（这个会以昇序来从初期開始排列）
	PRINTFORML  [ 1] 角色番号　　　　　（勧誘時表示的番号順序）
	PRINTFORML  [ 2] 好感度
	PRINTFORML  [ 3] 最後一起H的日子远近　（時间距离越近的角色该值越大）
	PRINTFORML  [ 4] 身高
	PRINTFORML  [ 5] 胸部大小
	PRINTFORML  [ 6] CFLAG
	PRINTFORML  [ 7] ABL
	PRINTFORML  [ 8] EXP
	PRINTFORML  [ 9] TALENT
	PRINTFORML  [10] BASE
	PRINTFORML [100] 返回

	CALL INPUT_SELECT, 11, 100

	CHOICE = RESULT

	SELECTCASE CHOICE
	CASE 100
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CASEELSE
		SELECTCASE CHOICE
		CASE 0
			CHOICES = 加入順,BASE,識別番号
		CASE 1
			CHOICES = 角色番号,NO,
		CASE 2
			CHOICES = 好感度,CFLAG,好感度
		CASE 3
			CHOICES = 最後に相手した日期,CFLAG,前回Ｈ日
		CASE 4
			CHOICES = 身高,BASE,身高
		CASE 5
			CHOICES = 胸の尺寸,BASE,胸圍
		CASE 6
			CALL CHARA_SORT_SELECT_OTHER("CFLAG")
			CHOICES = %RESULTS%
		CASE 7
			CALL CHARA_SORT_SELECT_OTHER("ABL")
			CHOICES = %RESULTS%
		CASE 8
			CALL CHARA_SORT_SELECT_OTHER("EXP")
			CHOICES = %RESULTS%
		CASE 9
			CALL CHARA_SORT_SELECT_OTHER("TALENT")
			CHOICES = %RESULTS%
		CASE 10
			CALL CHARA_SORT_SELECT_OTHER("BASE")
			CHOICES = %RESULTS%
		ENDSELECT

		IF CHOICES == ""
			CLEARLINE LINECOUNT - MEMO_LINECOUNT
			RESTART
		ENDIF

		PRINTL 根据選択的要素、从小的来还是从大的来？
		PRINTL [ 0] 从小的来（昇順）
		PRINTL [ 1] 从大的来（降順）

		CALL INPUT_SELECT, 2

		SELECTCASE RESULT
		CASE 0
			CHOICES:1 = 昇順
		CASE 1
			CHOICES:1 = 降順
		ENDSELECT

		CALL SORT_CHARA, CHOICES, CHOICES:1
		IF RESULTS == ""
			CLEARLINE LINECOUNT - MEMO_LINECOUNT
			RESTART
		ENDIF

		PRINTFORMW 角色全員以[%CHOICES%]の[%CHOICES:1%]进行重新排列了
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		;履歴を残してみる
		PRINTFORML 角色全員以[%CHOICES%]の[%CHOICES:1%]进行重新排列了
	ENDSELECT

	RESTART
ENDSELECT

CHOICE:1 = RESULT
CLEARLINE LINECOUNT - MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

PRINTL 
PRINTFORML 替换顺序的第一人选择为[{CHOICE:1, 2}]%CALLNAME:(CHOICE:1)%
PRINTL 
PRINTFORML 请再选择一个

FOR LCOUNT, 2, CHARANUM
	SIF LCOUNT == CHOICE:1
		SETCOLOR DEF_COLOR("黄色")

	CALL PRINT_NUM_NAME, "能力表示", LCOUNT

	RESETCOLOR
NEXT
PRINTL  [100]返回

$INPUT_LOOP_2
CALL INPUT_SELECT, CHARANUM, 100

SELECTCASE RESULT
CASE 0, 1, CHOICE:1
	GOTO INPUT_LOOP_2
CASE 100
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDSELECT

CHOICE:2 = RESULT

PRINTFORMW [{CHOICE:1, 2}]%CALLNAME:(CHOICE:1)%和[{CHOICE:2, 2}]%CALLNAME:(CHOICE:2)%的顺序交换了

CLEARLINE LINECOUNT - MEMO_LINECOUNT
;履歴を残してみる
PRINTFORML [{CHOICE:1, 2}]%CALLNAME:(CHOICE:1)%和[{CHOICE:2, 2}]%CALLNAME:(CHOICE:2)%的顺序交换了

CALL SWAP_CHARA, CHOICE:1, CHOICE:2

RESTART





;-------------------------------------------------
;ユーザーに項目を選んでもらって、@SORT_CHARAに渡すための文字列を作る
;作った文字列はRESULTSに入れて返す 中止の場合はRESULTSを空文字列にして返す
;-------------------------------------------------
@CHARA_SORT_SELECT_OTHER, 大項目
#DIMS 大項目
#DIM LCOUNT
#DIM LOOPSIZE
#DIMS 小項目
#DIM MAXLEN
#DIM 桁数
#DIM NEXTLINE
#DIM 並べる数
#DIM MAXLOOP

LOOPSIZE = VARSIZE(大項目)
SIF 大項目 == "CFLAG"
	LOOPSIZE = MIN(LOOPSIZE, 1100) ; 角色ごとの項目はソートに不要じゃろ

MAXLEN = 0
MAXLOOP = 0
FOR LCOUNT, 0, LOOPSIZE
	SELECTCASE 大項目
	CASE "CFLAG"
		小項目 = %CFLAGNAME:LCOUNT%
	CASE "ABL"
		小項目 = %ABLNAME:LCOUNT%
	CASE "EXP"
		小項目 = %EXPNAME:LCOUNT%
	CASE "TALENT"
		小項目 = %TALENTNAME:LCOUNT%
	CASE "BASE"
		小項目 = %BASENAME:LCOUNT%
	CASEELSE
		; THROW %大項目%で並び替えできません
		PRINTFORMW 不能以%大項目%进行排列
		RESULTS = 
		RETURN
	ENDSELECT
	SIF 小項目 == ""
		CONTINUE
	SIF MAXLEN < STRLENS(小項目)
		MAXLEN = STRLENS(小項目)
	MAXLOOP = LCOUNT
NEXT

桁数 = LOG10(MAX(MAXLOOP,1)) + 1
; 一行に表示できる文字数 https://jbbs.shitaraba.net/bbs/read.cgi/otaku/16783/1538925902/75 の参考値から2ぐらい引いておけば大丈夫かな？
並べる数 = (STRLENSU(GETLINESTR("あ"))*2 - 2) / (桁数+MAXLEN+1+3)
NEXTLINE = 0
FOR LCOUNT, 0, MAXLOOP + 1
	SELECTCASE 大項目
	CASE "CFLAG"
		小項目 = %CFLAGNAME:LCOUNT%
	CASE "ABL"
		小項目 = %ABLNAME:LCOUNT%
	CASE "EXP"
		小項目 = %EXPNAME:LCOUNT%
	CASE "TALENT"
		小項目 = %TALENTNAME:LCOUNT%
	CASE "BASE"
		小項目 = %BASENAME:LCOUNT%
	CASEELSE
	ENDSELECT
	SIF 小項目 == ""
		CONTINUE
	PRINTFORM  [{LCOUNT, 桁数}] %小項目, MAXLEN, LEFT%
	NEXTLINE ++
	IF NEXTLINE % 並べる数 == 0
		PRINTL 
	ELSE
		PRINTFORM %" "%
	ENDIF
NEXT
SIF NEXTLINE % 並べる数
	PRINTL 
PRINTFORML [999] 返回

$INPUTLOOP1
INPUT

IF RESULT == 999
	RESULTS = 
	RETURN
ELSEIF (0 <= RESULT) && (RESULT <= MAXLOOP)
	SELECTCASE 大項目
	CASE "CFLAG"
		小項目 = %CFLAGNAME:RESULT%
	CASE "ABL"
		小項目 = %ABLNAME:RESULT%
	CASE "EXP"
		小項目 = %EXPNAME:RESULT%
	CASE "TALENT"
		小項目 = %TALENTNAME:RESULT%
	CASE "BASE"
		小項目 = %BASENAME:RESULT%
	CASEELSE
	ENDSELECT
	IF 小項目 != ""
		RESULTS = %大項目%:%小項目%,%大項目%,%小項目%
		RETURN
	ENDIF
ENDIF

GOTO INPUTLOOP1



;-------------------------------------------------
;Emueraの命令SORTCHARAのＳＱＮ対応版の改変
;ARGSにはソート条件の表記と内容（例 "加入順,BASE,識別番号"）、ARGS:1には昇順か降順のどちらかが入る
;ソート条件が正常の場合は、ソート条件の表記をRESULTSに入れて返す 正常ではない場合はRESULTSを空文字列にして返す
;Emueraの命令SORTCHARAはTARGETとASSIは保つがASSI:1についてはノータッチなので、ASSI:1についても保つようにする
;-------------------------------------------------
@SORT_CHARA, ARGS, ARGS:1
#DIM LCOUNT
#DIMS ソート条件, 3
#DIMS 大項目
#DIMS 小項目
#DIM 小番号
;並び変えで齟齬が生じるものを記録しておく
#DIM MEMO_ASSI, 2

VARSET MEMO_ASSI
SIF ASSI:1 > 0
	MEMO_ASSI:1 = BASE:(ASSI:1):識別番号

;ドッペル你を一時的に消す
CALL DELETE_CHARA, 0
;MASTERを主人に設定
MASTER = 0

VARSET ソート条件
SPLIT ARGS, ",", ソート条件
大項目 = %ソート条件:1%
小項目 = %ソート条件:2%

小番号 = 0

SELECTCASE ARGS:1
CASE "昇順"
	SELECTCASE 大項目
	CASE "CFLAG"
		小番号 = GETNUM(CFLAG, 小項目)
		SIF 小番号 >= 0
			SORTCHARA CFLAG:小番号
	CASE "ABL"
		小番号 = GETNUM(ABL, 小項目)
		SIF 小番号 >= 0
			SORTCHARA ABL:小番号
	CASE "EXP"
		小番号 = GETNUM(EXP, 小項目)
		SIF 小番号 >= 0
			SORTCHARA EXP:小番号
	CASE "TALENT"
		小番号 = GETNUM(TALENT, 小項目)
		SIF 小番号 >= 0
			SORTCHARA TALENT:小番号
	CASE "BASE"
		小番号 = GETNUM(BASE, 小項目)
		SIF 小番号 >= 0
			SORTCHARA BASE:小番号
	CASE "NO"
		SORTCHARA
	CASEELSE
		; THROW %大項目%で並び替えできません
		PRINTFORMW 不能以%大項目%进行排列
		ソート条件:0 = 
	ENDSELECT
CASEELSE
	SELECTCASE 大項目
	CASE "CFLAG"
		小番号 = GETNUM(CFLAG, 小項目)
		SIF 小番号 >= 0
			SORTCHARA CFLAG:小番号, BACK
	CASE "ABL"
		小番号 = GETNUM(ABL, 小項目)
		SIF 小番号 >= 0
			SORTCHARA ABL:小番号, BACK
	CASE "EXP"
		小番号 = GETNUM(EXP, 小項目)
		SIF 小番号 >= 0
			SORTCHARA EXP:小番号, BACK
	CASE "TALENT"
		小番号 = GETNUM(TALENT, 小項目)
		SIF 小番号 >= 0
			SORTCHARA TALENT:小番号, BACK
	CASE "BASE"
		小番号 = GETNUM(BASE, 小項目)
		SIF 小番号 >= 0
			SORTCHARA BASE:小番号, BACK
	CASE "NO"
		SORTCHARA BACK
	CASEELSE
		; THROW %大項目%で並び替えできません
		PRINTFORMW 不能以%大項目%进行排列
		ソート条件:0 = 
	ENDSELECT
ENDSELECT

IF 小番号 < 0
	; THROW %大項目%の中に%小項目%が見つかりません
	PRINTFORMW 没有在%大項目%中找到%小項目%
	ソート条件:0 = 
ENDIF

;ドッペル你を復帰
ADDCHARA 99
;番号をドッペル你が[0]番になるように揃える
FOR LCOUNT, 1, CHARANUM
	LOCAL = CHARANUM - LCOUNT
	LOCAL:1 = LOCAL - 1
	CALL SWAP_CHARA, LOCAL, LOCAL:1
NEXT

;MASTERを主人に設定
MASTER = 1
;ASSI:1を再設定
SIF MEMO_ASSI:1 > 0
	ASSI:1 = TO_NUMBER(MEMO_ASSI:1)

RESULTS = %ソート条件:0%
