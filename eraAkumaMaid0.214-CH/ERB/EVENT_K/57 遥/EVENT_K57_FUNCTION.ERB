;遥君が你に愛憎を抱いているか判定
;ぶっちゃけYOKUBOと同じく長判定が連続すると判断したので入れた
@AIZOU, ARG
#FUNCTION

SIF ARG == 0
	ARG = TARGET

SIF CEVENT("『のぞき』見", ARG) && TALENT:ARG:恋慕 && TALENT:ARG:相思相愛 == 0 && CEVENT("前輩の意見", ARG) == 0
	RETURNF 1

RETURNF 0

;遥君がお休みしているときの汎用セリフ
;まず使うことはそうそうないと思うものの、他イベが思いついた時に流用できる&シチュ的にあまり口上に変化が出ないのでここで作っておく
@REST_K57, ARG, ARGS
#FUNCTIONS
#DIMS NAR

SIF ARG == 0
	ARG = TARGET

SELECTCASE RAND:3
CASE 0
	NAR = 誰也不在……
CASE 1
	NAR = どうやら%CALLNAME:ARG%は休みのようだ……
CASE 2
	NAR = 静かだ……
ENDSELECT

RETURNF @"%NAR%"

;遥君の吸血鬼化判定が複雑になってしまったため、関数化して可読性UPを行う
@VAMP_K57, ARG
#FUNCTION

SIF ARG == 0
	ARG = TARGET

;COND("同居可能")では自動的に0になるようになってしまっていたため、同居可能の構文を就这様引用する
SIF TALENT:ARG:恋慕 == 0 && MARK:ARG:屈服刻印 < 3 && ABL:ARG:信頼 < 5
	RETURNF 0

SIF CEVENT("昼と夜", ARG) == 0
	RETURNF 0

IF CEVENT("ＴＳ吸血鬼フラグ", ARG)
	SIF CEVENT("ＴＳ完了済", ARG)
		RETURNF 0
ELSEIF DAY - CFLAG:ARG:昼と夜発生日 < 3
	RETURNF 0
ENDIF

RETURNF 1

;-------------------------------------------------
;以降は喘ぎ声を生成するための関数
;六華口上や哈娜口上の汎用喘ぎ声を参考、また一部流用、改変させていただいて製作しています
;-------------------------------------------------

;喘ぎ声を随机で発声させる関数。挿入などの条件があればいくつかの喘ぎ声を混ぜ合わせる
;回数上限、ループ、爱心要不要は共通になる
;現状加える予定の喘ぎ声は、1.通常(製作済)、2.Ｍ異物（製作済）、3Ｖ異物（製作前、伪娘に膣がつけられるようにならない限り作らない予定）4Ａ異物（製作済）5挿入(製作前)
@MPANT_K57, 回数上限, ARG, ARGS
#FUNCTIONS
#DIMS DYNAMIC 喘ぎ
#DIMS 爱心;ARGSを直接いじってしまうと爱心が不要になって以後出なくなるので、そうしないための一時的な変数
#DIM 回数上限
#DIM ループ
#DIMS 連続过滤器;あまり繰り返させたくない言葉に过滤器を掛ける

連続过滤器 = 無
爱心 = %ARGS%
FOR ループ, 0, 回数上限
	
	;肛門に挿入されていた場合
	IF (TEQUIP:ARG:肛用振動棒 || TEQUIP:ARG:浣腸 || INSERT("Ａ挿入", ARG) || GROUPMATCH(SELECTCOM, 2, 9, 14, 33, 70, 71, 72, 73, 74, 75, 136, 142, 143)) && RAND:7 == 0 && 連続过滤器 != "Ａ異物"
		喘ぎ += @"%PANT_K57(ARG, 爱心, "Ａ異物")%"
		;肛門を連続して話さないために过滤器をかける
		連続过滤器 = Ａ異物
	;尿道に挿入されていた場合
	ELSEIF (INSERT("尿道挿入", ARG) || GROUPMATCH(SELECTCOM, 15, 83, 84, 85, 86, 87, 88, 149, 151)) && RAND:6 == 0 && 連続过滤器 != "尿道異物"
		喘ぎ += @"%PANT_K57(ARG, 爱心, "尿道異物")%"
		;尿道を連続して話さないために过滤器をかける
		連続过滤器 = 尿道異物
	;口に何か入っていた場合
	ELSEIF (TEQUIP:ARG:接吻 || TEQUIP:ARG:深喉 || TEQUIP:ARG:六九式 || GROUPMATCH(SELECTCOM, 6, 91, 96, 98, 99, 135, 148)) && RAND:5 == 0
		喘ぎ += @"%PANT_K57(ARG, 爱心, "Ｍ異物")%"
	;Ｍ異物と条件が被るものの、接吻の時限定
	ELSEIF (TEQUIP:ARG:接吻 || SELECTCOM == 6) && RAND:4 == 0
		喘ぎ += @"%PANT_K57(ARG, 爱心, "接吻")%"
	;膣や肛門に挿入していた場合
	ELSEIF (INSERT("挿入", PLAYER, ARG) || (ASSI && INSERT("挿入", ASSI, ARG)) || GROUPMATCH(SELECTCOM, 120, 121, 122, 125, 126, 127)) && RAND:3 == 0 && 連続过滤器 != "挿入"
		喘ぎ += @"%PANT_K57(ARG, 爱心, "挿入")%"
		;挿入を連続して話さないために过滤器をかける
		連続过滤器 = 挿入
	;飞机杯に挿入していた場合
	ELSEIF SELECTCOM == 37 && RAND:2 && 連続过滤器 != "飞机杯挿入"
		喘ぎ += @"%PANT_K57(ARG, 爱心, "飞机杯挿入")%"
		;飞机杯挿入を連続して話さないために过滤器をかける
		連続过滤器 = 飞机杯挿入
	;条件に引っかからなければ汎用を叫出来
	ELSE
		喘ぎ += @"%PANT_K57(ARG, 爱心, "通常")%"
	ENDIF
	
	;連続して指定されなかった場合、过滤器を解除する
	IF 連続过滤器 != "無"
		IF LOCAL == 3
			連続过滤器 = 無
			LOCAL = 0
		ELSE
			LOCAL++
		ENDIF
	ENDIF
	
	;爱心が必要なときは随机でどちらにするのか指定する
	IF ARGS == "要" && (ループ == 0 || RAND:2)
		爱心 = %ARGS%
	;爱心が不要な場合
	;回数を繰り返したときは随机でこちらの方を選択する
	ELSEIF ARGS == "不要" || ループ >= 1
		爱心 = 不要
	;今のところは完全に不要だが後の拡張のため＆構文ミスに気付くために其他にはエラーをだす
	ELSE
		喘ぎ += "指定の範囲外です"
	ENDIF
NEXT

RETURNF 喘ぎ

;喘ぎ声を生成する関数。爱心を加えるか否かをARGSで指定する。
;喘ぎは使う度に空白にしていくので動的変数として指定
;それぞれのパターンを変数パターンに指定し、そのCASEにあわせて喘ぎ方を変えて行く(予定)
@PANT_K57, ARG, ARGS, パターン, 回数
#FUNCTIONS
#DIM ループ
#DIMS パターン
#DIMS DYNAMIC 喘ぎ
#DIM 回数

FOR ループ, 0, MAX(回数, 1)
	SELECTCASE パターン
	;汎用パターン
	CASE "通常"
		IF PALAM:ARG:欲情 >= PALAMLV:5 && RAND:3
			喘ぎ += @"%TEXTR("啊゛/喔゛/啊゛啊/喔゛ほ/啊゛呼/哈、啊゛、/啊啊/啊゛啊、")%"
		ELSEIF PALAM:ARG:欲情 >= PALAMLV:3 && RAND:2
			喘ぎ += @"%TEXTR("啊哈、/嗯哈、/哈嗯、/啊嗯、/啊、/哈ぅ、/嗯啊、/噫、/啊啊、")%"
		ELSE
			喘ぎ += @"%TEXTR("啊、……/咿嗯/嗯啊、……/啊嗯、/噫ゃぅ……/呼ぁ……/哈ぅ嗯……/嗯、……/噫ゃ、……/呼嗯、……/、……")%"
		ENDIF
	;口に何か入っているとき
	CASE "Ｍ異物"
		;長くなるのでいくつかに分割
		IF RAND:2
			喘ぎ += @"%TEXTR("呼姆ぅ……/嗯姆、……/哈姆ぁ……/啾啪……/舔……/啾……/嗯……/嗯、、你这…/你这…ぁ姆…嗯、/嗯、…、你这……/啾、、你这、…啾哟、/嗯嗯、……你这…/你这、哈姆……嗯、")%"
		ELSE
			喘ぎ += @"%TEXTR("嗯、、嗯咕、…/嗯嗯…嗯ぅ……、/嗯、…、嗯嗯……/嗯咕、、嗯、…嗯、/嗯姆、……嗯、/嗯姆、、嗯嗯…、")%"
		ENDIF
	;接吻しているとき
	CASE "接吻"
		喘ぎ += @"%TEXTR("啾……/啾哟……/嗯姆、……/嗯……/啾、、啾、…/啾…啾……、/啾、…、啾……/啾、、啾、…啾、/啾ぅ……啾、/嗯啾、、啾……、")%"
	;肛門に何か入っているとき
	CASE "Ａ異物"
		喘ぎ += @"%TEXTR("屁股/啊嗯/啊啊~")%"
	;尿道に何か入っているとき
	CASE "尿道異物"
		喘ぎ += "小便的地方吗"
	;何かに挿入しているとき
	CASE "挿入"
		IF RAND:2
			喘ぎ += @"%CNAMET(ARG, "二人称")%的"
			IF RAND:2
				IF GROUPMATCH(SELECTCOM, 120, 121, 122)
					喘ぎ += @"%TEXTR("小穴/蜜裂")%"
				ELSEIF GROUPMATCH(SELECTCOM, 125, 126, 127)
					喘ぎ += @"%TEXTR("屁股/肛門")%"
				ENDIF
			ELSE
				喘ぎ += "里面"
			ENDIF
			喘ぎ += "啊、_H_好深、"
		ELSE
			喘ぎ += @"%TEXTR("嗯、/咕、/呼、/呜、")%"
		ENDIF
	;飞机杯に挿入しているとき
	CASE "飞机杯挿入"
		喘ぎ += @"%TEXTR("飞机杯吗……/啊……不要乱动……")%"
	ENDSELECT
	;爱心が必要な場合
	IF ARGS == "要"
		喘ぎ += "_H_"
	;爱心が不要な場合
	ELSEIF ARGS == "不要"
	;今のところは完全に不要だが後の拡張のため＆構文ミスに気付くために其他にはエラーをだす
	ELSE
		喘ぎ = "PANT_K57に問題が有ます"
		RETURNF 喘ぎ
	ENDIF
	;読みやすいように半角スペースをくわえる
	喘ぎ += " "
NEXT

RETURNF 喘ぎ