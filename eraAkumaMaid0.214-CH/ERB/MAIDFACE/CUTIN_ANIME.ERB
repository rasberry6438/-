;テスト
@ANIME_LEVELUP_INMON, ARGS, ARG
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC NUM_CUTIN = -1
#DIM DYNAMIC GRAPHICS_WIDTH = 300
#DIM DYNAMIC GRAPHICS_HEIGHT = 300

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;表示した画像に該当する角色の存在確認
FOR LCOUNT, 0, 6
	SIF LATESTCUTINCHARA:LCOUNT <= 0
		BREAK
	IF ARG == LATESTCUTINCHARA:LCOUNT
		NUM_CUTIN = LCOUNT
		BREAK
	ENDIF
NEXT

SIF NUM_CUTIN < 0
	RETURN 0

;ARGSのスプライトから尺寸を測る
GRAPHICS_WIDTH = GWIDTH(LATESTCUTINID:NUM_CUTIN)
GRAPHICS_HEIGHT = GHEIGHT(LATESTCUTINID:NUM_CUTIN)

CALL OVERLAY_GCREATE, 0, SAVEINMON_BOTTOM:NUM_CUTIN, GRAPHICS_WIDTH, GRAPHICS_HEIGHT
CALL OVERLAY_GCREATE, 1, SAVEINMON_TOP:NUM_CUTIN, GRAPHICS_WIDTH, GRAPHICS_HEIGHT

;COLOR_MATRIXを入力しておく
COLOR_MATRIX_ELEMENTS:0:0 = 255, 0, 0, 0, 0
COLOR_MATRIX_ELEMENTS:1:0 = 0, 255, 0, 0, 0
COLOR_MATRIX_ELEMENTS:2:0 = 0, 0, 255, 0, 0
COLOR_MATRIX_ELEMENTS:3:0 = 0, 0, 0, 255, 0
COLOR_MATRIX_ELEMENTS:4:0 = 0, 0, 0, 0, 255

FOR LCOUNT, 1, 6
	;まずは下地を敷く
	GDRAWG LATESTCUTINID:NUM_CUTIN, 0, 0, 0, GRAPHICS_WIDTH, GRAPHICS_HEIGHT, 0, 0, GRAPHICS_WIDTH, GRAPHICS_HEIGHT

	;透明度の調整
	COLOR_MATRIX_ELEMENTS:3:3 = LCOUNT*51

	;ARGSのスプライトを乗せる
	GDRAWSPRITE LATESTCUTINID:NUM_CUTIN, ARGS, 0, 0, GRAPHICS_WIDTH, GRAPHICS_HEIGHT, COLOR_MATRIX_ELEMENTS:0:0

	;ARGSに重ねる画像を書き込む
	GDRAWG LATESTCUTINID:NUM_CUTIN, 1, 0, 0, GRAPHICS_WIDTH, GRAPHICS_HEIGHT, 0, 0, GRAPHICS_WIDTH, GRAPHICS_HEIGHT

	TWAIT 300, 1
NEXT

GDISPOSE 0
GDISPOSE 1

RETURN 1
