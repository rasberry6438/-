;-------------------------------------------------
;ARGの颜绘の横にテ接吻トSTR:1～STR:19を出す
;颜绘変化をさせたい場合にはARGSに入れる
;颜绘や淫紋が表示されない場合にはテ接吻トが出るだけとなる
;-------------------------------------------------
@DIALOGUE_FRAME, ARG, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_REDRAW
#DIM MEMO_LINECOUNT
#DIM FSIZE
;テ接吻トのインデント
#DIMS INDENT_TEXT
;1行内のテ接吻トの色
#DIMS TEXT_COLOR
#DIMS HTMLF

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0

SIF TFLAG:颜绘表示中 == LINECOUNT
	CALL CLEAR_MAIDFACE

;颜绘変化の指定がある？
CALL SET_CHANGE_FACE, ARG, ARGS

STR:颜绘再現文字列 =
HTMLF = 
INDENT_TEXT = 
TEXT_COLOR = 

MEMO_LINECOUNT = LINECOUNT
;颜绘と淫紋を表示する
CALL PRINT_DIALOGUE, ARG
;表示された場合
IF LINECOUNT > MEMO_LINECOUNT
	;一度描画した物を解体してHPRINT_STRに適用可能な形状にする
	HTMLF = %HTML_GETPRINTEDSTR(0)%
	;ここでいったん表示されている画像を消し、HTMLFを整理したうえで取得しなおす
	CLEARLINE 1
	HTML_TAGSPLIT HTMLF
	HTMLF = 
	FOR LCOUNT, 2, RESULT-2
		HTMLF += RESULTS:LCOUNT
	NEXT
ENDIF

;画像が出てるならインデントをつける
IF STR:颜绘再現文字列 != ""
	FSIZE = MAIDFACESIZE()
	INDENT_TEXT = %BL(FSIZE/50 + 1)%
ENDIF

MEMO_LINECOUNT = LINECOUNT
FOR LCOUNT, 0, 20
	SELECTCASE LCOUNT
	;0は颜绘と淫紋を表示する場合は空文字でもCONTINUEしないで続ける
	CASE 0
		SIF COND("颜绘表示中", ARG) == 0 && STR:0 == ""
			CONTINUE
	CASE 19
		SIF STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテ接吻ト表示終了
	CASEELSE
		SIF LCOUNT >= 7 && STR:LCOUNT == "" && STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	;その行の默认文字色を設定
	IF SUBSTRING(STR:LCOUNT, 0, 7) == "#TEXTC="
		TEXT_COLOR = %EXTSTR(STR:LCOUNT, "", ":")%_
		STR:LCOUNT = %EXTSTR(STR:LCOUNT, ":")%
	ENDIF
	;STR:0に颜绘と淫紋の表示を追加
	IF LCOUNT == 0 && COND("颜绘表示中", ARG)
		STR:0 = %TEXT_COLOR%%HTMLF%_ %STR:LCOUNT%
		CALL HPRINT_STRL, STR:LCOUNT
		TEXT_COLOR =
		CONTINUE
	ENDIF
	;HTMLへの分岐
	IF SUBSTRING(STR:LCOUNT, 0, 5) == "HTML:"
		STR:LCOUNT = %TEXT_COLOR%%INDENT_TEXT%_%SUBSTRING(STR:LCOUNT, 5, -1)%
		CALL HPRINT_STRL, STR:LCOUNT
		TEXT_COLOR =
		CONTINUE
	ENDIF

	STR:LCOUNT = %INDENT_TEXT%%STR:LCOUNT%

	;画面内に収圆圈かの格子
	SELECTCASE STRLENS(STR:LCOUNT)
	CASE IS <= NUM("折り返し文字数")
		CALL PRINT_STRL, @"%TEXT_COLOR%%STR:LCOUNT%"
		STR:LCOUNT = 
		TEXT_COLOR = 
	CASEELSE
		;、を優遇
		IF STRCOUNT(STR:LCOUNT, "、") && STRCOUNTS(STR:LCOUNT, "喜欢的东西:", "讨厌的东西:") == 0 && STRLENS(EXTSTR(STR:LCOUNT, "", "、") ) >= NUM("折り返し文字数")*2/3 && STRLENS(EXTSTR(STR:LCOUNT, "", "、") ) <= NUM("折り返し文字数") - 2
			CALL PRINT_STRL, @"%TEXT_COLOR%%EXTSTR(STR:LCOUNT, "", "、")%、"
			STR:LCOUNT = %EXTSTR(STR:LCOUNT, "、")%
		ELSE
			CALL PRINT_STRL, @"%TEXT_COLOR%%SUBSTRING(STR:LCOUNT, 0, NUM("折り返し文字数")-1)%"
			STR:LCOUNT = %SUBSTRING(STR:LCOUNT, NUM("折り返し文字数")-1, -1)%
		ENDIF
		SELECTCASE STR:LCOUNT
		CASE "", "_"
			TEXT_COLOR = 
		CASEELSE
			LCOUNT -= 1
		ENDSELECT
	ENDSELECT
NEXT

;改行のつじつま合わせ
SIF STR:颜绘再現文字列 != ""
	CALL MULTILINE, TFLAG:MAX_height - (LINECOUNT - MEMO_LINECOUNT) + 1
SIF COND("颜绘表示中", ARG)
	TFLAG:颜绘表示中 = LINECOUNT
CSTR:ARG:颜绘変化 =

CALL BLANKLINE

VARSET STR, "", 0, 20
REDRAW MEMO_REDRAW


;-------------------------------------------------
;@PRINT_CUTINのDIALOGUE_FRAME版のようなもの
;颜绘、その下段に淫紋が表示される
;流石に淫紋の隣に角色解説が出るのはシュールなので颜绘オフの時には淫紋もオフ
;-------------------------------------------------
@PRINT_DIALOGUE, ARG
#DIM MEMO_REDRAW
#DIM ypos

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

SIF COND("颜绘表示中", ARG) == 0
	RETURN 0

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0

VARSET LATESTFACECHARA
VARSET LATESTFACEID
STR:参照用MAIDFACE文字列 = 

;この中で STR:参照用MAIDFACE文字列 を用いて計算する
CALL PRINT_MAIDFACE_SUB, ARG, 0

TFLAG:MAX_height = 0
STR:颜绘再現文字列 = 
IF STR:参照用MAIDFACE文字列 != ""
	STR:颜绘再現文字列 = %STR:参照用MAIDFACE文字列%

	HTML_PRINT CONVERTSTR(STR:颜绘再現文字列)
ENDIF
REDRAW MEMO_REDRAW

;-------------------------------------------------
;STR毎にWAITするDIALOGUE_FRAME
;STRが空だった場合はその行は止まらないので間を作りたいなら空白文字でも入れといてください
;ARGはTARGETとかARGSに初期の颜绘変化
;STR内で文章の後に"#F"で区切って颜绘を変化させることができる
;例）STR:1 = こんにちは#F笑颜
;-------------------------------------------------
@DIALOGUE_FRAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC MEMO_LINECOUNT
#DIM DYNAMIC MEMO_REDRAW
#DIMS DYNAMIC MEMO_EMOTION
#DIM DYNAMIC MEMO_MAIDEFACEID
#DIMS DYNAMIC MEMO_STR, 20

;颜绘がない場合は単純に台詞を出す
IF COND("颜绘表示中", ARG) == 0
	FOR LCOUNT, 0, 20
		VARSET LOCALS
		SPLIT STR:LCOUNT, "#F", LOCALS
		SELECTCASE LOCALS
		CASE ""
		CASEELSE
			SELECTCASE LCOUNT
			CASE 0
				CALL PRINT_STRL, LOCALS
			CASEELSE
				CALL PRINT_STRW, LOCALS
			ENDSELECT
		ENDSELECT
	NEXT
	RETURN 1
ENDIF

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0

SIF TFLAG:颜绘表示中 == LINECOUNT
	CALL CLEAR_MAIDFACE

MEMO_LINECOUNT = LINECOUNT
;無駄にMAIDEFACEIDを消費しないために現在的値を記録しておく
MEMO_MAIDEFACEID = MAIDEFACEID

;STRの上書き
FOR LCOUNT, 1, 10
	SIF ARGS:LCOUNT != ""
		STR:LCOUNT = %ARGS:LCOUNT%
NEXT

;表情のコピー
SIF ARGS:0 != ""
	MEMO_EMOTION = %ARGS:0%

;STRの内容をコピー
FOR LCOUNT, 0, 20
	MEMO_STR:LCOUNT = %STR:LCOUNT%
NEXT

;メモを取ったSTRは空にする
VARSET STR, "", 0, 20

FOR LCOUNT, 1, 20
	SELECTCASE LCOUNT
	CASE 19
		SIF MEMO_STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテ接吻ト表示終了
	CASEELSE
		SIF LCOUNT >= 7 && MEMO_STR:LCOUNT == "" && MEMO_STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	;颜绘変化用の処理
	IF STRCOUNT(MEMO_STR:LCOUNT, "#F")
		VARSET LOCALS
		SPLIT MEMO_STR:LCOUNT, "#F", LOCALS
		MEMO_STR:LCOUNT = %LOCALS:0%
		MEMO_EMOTION = %LOCALS:1%
	ENDIF

	;MEMOからSTRの内容をもどしていく
	FOR LCOUNT2, 0, LCOUNT + 1
		STR:LCOUNT2 = %MEMO_STR:LCOUNT2%
	NEXT

	;以前表示させたものは消す
	SIF MEMO_LINECOUNT != LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT

	MAIDEFACEID = MEMO_MAIDEFACEID

	;表示させる
	CALL DIALOGUE_FRAME, ARG, MEMO_EMOTION
	SIF MEMO_STR:LCOUNT != ""
		WAIT
NEXT
CALL BLANKLINE

REDRAW MEMO_REDRAW


;-------------------------------------------------
;上の@DIALOGUE_FRAMEの名前表示追加版
;-------------------------------------------------
@DIALOGUE_FRAME_NAME, ARG, ARGS
STR:0 = - _%DISPLAY_NAME(ARG)%_ -
CALL DIALOGUE_FRAME, ARG, ARGS
;-------------------------------------------------
;@DIALOGUE_FRAMEWの名前表示追加版
;-------------------------------------------------
@DIALOGUE_FRAME_NAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7
STR:0 = - _%DISPLAY_NAME(ARG)%_ -
CALL DIALOGUE_FRAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7


;-------------------------------------------------
;道具解説用。グラの横にテ接吻トSTR:1～STR:19を出す
;-------------------------------------------------
@DIALOGUE_FRAME_ITEM, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_REDRAW
#DIM MEMO_LINECOUNT
#DIM FSIZE

SIF ARGS == ""
	RETURN 0

SIF TFLAG:颜绘表示中 == LINECOUNT
	CALL CLEAR_MAIDFACE

FSIZE = MAIDFACESIZE()

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0

HTML_PRINT @"<nobr><img src='0背景黄色枠有' height = '{FSIZE}' width = '{FSIZE}' ypos = '0'><nonbutton pos='0'><img src='%ARGS%' height = '{FSIZE}' width = '{FSIZE}' ypos = '0'></nonbutton>"

MEMO_LINECOUNT = LINECOUNT
FOR LCOUNT, 0, 20
	SELECTCASE LCOUNT
	CASE 0, 19
		SIF STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテ接吻ト表示終了
	CASEELSE
		SIF STR:LCOUNT == "" && STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	IF SUBSTRING(STR:LCOUNT, 0, 5) == "HTML:"
		STR:LCOUNT = %BL(FSIZE/50 + 1)%%SUBSTRING(STR:LCOUNT, 5, -1)%
		CALL HPRINT_STRL, STR:LCOUNT
		CONTINUE
	ENDIF

	STR:LCOUNT = %BL(FSIZE/50 + 1)%%STR:LCOUNT%

	;画面内に収圆圈かの格子
	SELECTCASE STRLENS(STR:LCOUNT)
	CASE IS <= NUM("折り返し文字数")
		CALL PRINT_STRL, STR:LCOUNT
		STR:LCOUNT = 
	CASEELSE
		;、を優遇
		IF STRCOUNT(STR:LCOUNT, "、") && STRLENS(EXTSTR(STR:LCOUNT, "", "、") ) >= NUM("折り返し文字数")*2/3 && STRLENS(EXTSTR(STR:LCOUNT, "", "、") ) <= NUM("折り返し文字数") - 2
			CALL PRINT_STRL, @"%EXTSTR(STR:LCOUNT, "", "、")%、"
			STR:LCOUNT = %EXTSTR(STR:LCOUNT, "、")%
		ELSE
			CALL PRINT_STRL, SUBSTRING(STR:LCOUNT, 0, NUM("折り返し文字数")-1)
			STR:LCOUNT = %SUBSTRING(STR:LCOUNT, NUM("折り返し文字数")-1, -1)%
		ENDIF
		SELECTCASE STR:LCOUNT
		CASE "", "_"
		CASEELSE
			LCOUNT -= 1
		ENDSELECT
	ENDSELECT
NEXT
;改行のつじつま合わせ
FOR LCOUNT, 1, (FSIZE/100) - (LINECOUNT - MEMO_LINECOUNT)
	PRINTL 
NEXT

CALL BLANKLINE

VARSET STR, "", 0, 20
REDRAW MEMO_REDRAW

;-------------------------------------------------
;STR毎にWAITするITEM版
;STR内で文章の後に"#G"で区切って表示画像を変更できる
;例）STR:1 = こんにちは#G22まぐろう1
;-------------------------------------------------
@DIALOGUE_FRAME_ITEMW, ARGS
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC MEMO_LINECOUNT
#DIM DYNAMIC MEMO_REDRAW
#DIMS DYNAMIC MEMO_IMG
#DIMS DYNAMIC MEMO_STR, 20

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0

SIF TFLAG:颜绘表示中 == LINECOUNT
	CALL CLEAR_MAIDFACE

MEMO_LINECOUNT = LINECOUNT

;表示画像のコピー
SIF ARGS:0 != ""
	MEMO_IMG = %ARGS:0%

;STRの内容をコピー
FOR LCOUNT, 0, 20
	MEMO_STR:LCOUNT = %STR:LCOUNT%
NEXT

;メモを取ったSTRは空にする
VARSET STR, "", 0, 20

FOR LCOUNT, 1, 20

	SELECTCASE LCOUNT
	CASE 19
		SIF MEMO_STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテ接吻ト表示終了
	CASEELSE
		SIF MEMO_STR:LCOUNT == "" && MEMO_STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	;表示画像変更用の処理
	IF STRCOUNT(MEMO_STR:LCOUNT, "#G")
		VARSET LOCALS
		SPLIT MEMO_STR:LCOUNT, "#G", LOCALS
		MEMO_STR:LCOUNT = %LOCALS:0%
		MEMO_IMG = %LOCALS:1%
	ENDIF

	;MEMOからSTRの内容をもどしていく
	FOR LCOUNT2, 0, LCOUNT + 1
		STR:LCOUNT2 = %MEMO_STR:LCOUNT2%
	NEXT

	;以前表示させたものは消す
	SIF MEMO_LINECOUNT != LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT

	;表示させる
	CALL DIALOGUE_FRAME_ITEM, MEMO_IMG
	SIF MEMO_STR:LCOUNT != ""
		WAIT
NEXT
CALL BLANKLINE

REDRAW MEMO_REDRAW
