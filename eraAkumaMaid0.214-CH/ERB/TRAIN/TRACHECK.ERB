;SOURCEとUPの違いを轻轻地確認。
;例えば、SOURCE:Ｃ快楽はPALAM:快ＣのみならずPALAM:恭順などが発生する等、”快感を得て望ましいか？”を含んだ要素。
;CUP:快Ｃは純粋に快Ｃのみに影響する。
;-------------------------------------------------
;SOURCEの計算
;あらかじめターンの開始と終了時に計算されている補正値TCVAR:600～659を用いて計算する
;１：ここの計算前に獲得していたCUPをUPに退避してCUPを初期化
;２：SOURCEからCUPを算出してSOURCEを初期化
;３：UPに退避させていたCUPを２で計算したCUPに追加
;-------------------------------------------------
@CALC_SOURCE, ARGS
#DIM LCOUNT
;ARGSには試算と調教指令、其他がある。調教指令だとその調教独自の補正が入ったりする
;-------------------------------------------------
;SOURCEとCUPにかかる補正を再計算
;-------------------------------------------------
CALL CALC, "調教補正値", TARGET

IF ARGS != "試算"
	;補正を二度かけないように、CUPをUPに移す
	FOR LCOUNT, 0, 30
		UP:LCOUNT = CUP:LCOUNT
	NEXT
ENDIF
VARSET CUP

;-------------------------------------------------
;SOURCEに補正を加える
;-------------------------------------------------
FOR LCOUNT, 0, 30
	SIF SOURCE:LCOUNT <= 0
		CONTINUE
	SELECTCASE SOURCENAME:LCOUNT
	;快楽のSOURCEには、補正値に応じたBOUNSを加える
	CASE "Ｃ快楽", "Ｖ快楽", "Ａ快楽", "Ｂ快楽", "Ｍ快楽", "尿道快楽"
		SELECTCASE TCVAR:(600 + LCOUNT)
		CASE IS > 0
			SOURCE:LCOUNT += TCVAR:(600 + LCOUNT)*10
		ENDSELECT
	ENDSELECT
	SOURCE:LCOUNT = MULTIPLY(SOURCE:LCOUNT, 100 + TCVAR:(600 + LCOUNT) )

	;素質による補正の最終値がプラスならば、最低上昇値となる。
	SIF SOURCE:LCOUNT < TCVAR:(600 + LCOUNT)
		SOURCE:LCOUNT = TCVAR:(600 + LCOUNT)
NEXT
;-------------------------------------------------
;感度OFF指定がある場合は、対応した快楽のSOURCEを0にする
;-------------------------------------------------
IF TCVAR:Ｃ感度OFF
	SOURCE:Ｃ快楽 = 0
	TCVAR:Ｃ快楽記録 = 0
ENDIF
IF TCVAR:Ｖ感度OFF
	SOURCE:Ｖ快楽 = 0
	TCVAR:Ｖ快楽記録 = 0
ENDIF
IF TCVAR:Ａ感度OFF
	SOURCE:Ａ快楽 = 0
	TCVAR:Ａ快楽記録 = 0
ENDIF
IF TCVAR:Ｂ感度OFF
	SOURCE:Ｂ快楽 = 0
	TCVAR:Ｂ快楽記録 = 0
ENDIF
IF TCVAR:Ｍ感度OFF
	SOURCE:Ｍ快楽 = 0
	TCVAR:Ｍ快楽記録 = 0
ENDIF
IF TCVAR:尿感度OFF
	SOURCE:尿道快楽 = 0
	TCVAR:尿道快楽記録 = 0
ENDIF
;-------------------------------------------------
;同じ指令の連続実行による上下の処理（快楽と消費体力は1.5倍）
;-------------------------------------------------
IF COND("連続実行")
	TIMES SOURCE:Ｃ快楽, 1.50
	TIMES SOURCE:Ｖ快楽, 1.50
	TIMES SOURCE:Ａ快楽, 1.50
	TIMES SOURCE:Ｂ快楽, 1.50
	TIMES SOURCE:Ｍ快楽, 1.50
	TIMES SOURCE:尿道快楽, 1.50
ENDIF
;手下留情
IF COND("手下留情中") || TFLAG:进行性処理
	TIMES SOURCE:Ｃ快楽, 0.50
	TIMES SOURCE:Ｖ快楽, 0.50
	TIMES SOURCE:Ａ快楽, 0.50
	TIMES SOURCE:Ｂ快楽, 0.50
	TIMES SOURCE:Ｍ快楽, 0.50
	TIMES SOURCE:尿道快楽, 0.50
ENDIF

;-------------------------------------------------
;拘束による身体の感覚の補正
;-------------------------------------------------
IF     BIND("縄：手足縛", TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.20
	TIMES SOURCE:Ｖ快楽  , 1.20
	TIMES SOURCE:Ａ快楽  , 1.20
	TIMES SOURCE:Ｂ快楽  , 0.80
	TIMES SOURCE:尿道快楽, 1.20
	TIMES SOURCE:疼痛    , 1.20
ELSEIF BIND("縄：亀甲縛り"    , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 0.80
	TIMES SOURCE:Ｖ快楽  , 0.80
	TIMES SOURCE:Ａ快楽  , 0.80
	TIMES SOURCE:Ｂ快楽  , 1.20
	TIMES SOURCE:尿道快楽, 0.80
	TIMES SOURCE:疼痛    , 1.20
ELSEIF BIND("縄：逆海老"      , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.20
	TIMES SOURCE:Ｖ快楽  , 1.20
	TIMES SOURCE:Ａ快楽  , 1.20
	TIMES SOURCE:Ｂ快楽  , 1.20
	TIMES SOURCE:尿道快楽, 1.20
	TIMES SOURCE:疼痛    , 1.50
ELSEIF BIND("縄：足吊"      , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.20
	TIMES SOURCE:Ｖ快楽  , 1.20
	TIMES SOURCE:Ａ快楽  , 1.20
	TIMES SOURCE:Ｂ快楽  , 1.20
	TIMES SOURCE:尿道快楽, 1.20
	TIMES SOURCE:疼痛    , 1.50
ELSEIF BIND("手枷"            , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.00
	TIMES SOURCE:Ｖ快楽  , 1.00
	TIMES SOURCE:Ａ快楽  , 1.00
	TIMES SOURCE:Ｂ快楽  , 1.00
	TIMES SOURCE:尿道快楽, 1.00
	TIMES SOURCE:疼痛    , 1.00
ELSEIF BIND("吊り"            , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.20
	TIMES SOURCE:Ｖ快楽  , 1.20
	TIMES SOURCE:Ａ快楽  , 1.20
	TIMES SOURCE:Ｂ快楽  , 1.20
	TIMES SOURCE:尿道快楽, 1.20
	TIMES SOURCE:疼痛    , 1.20
ELSEIF BIND("Ｍ字開脚"        , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 2.00
	TIMES SOURCE:Ｖ快楽  , 2.00
	TIMES SOURCE:Ａ快楽  , 1.50
	TIMES SOURCE:Ｂ快楽  , 0.80
	TIMES SOURCE:尿道快楽, 1.50
	TIMES SOURCE:疼痛    , 1.00
ELSEIF BIND("床"          , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.50
	TIMES SOURCE:Ｖ快楽  , 1.50
	TIMES SOURCE:Ａ快楽  , 1.50
	TIMES SOURCE:Ｂ快楽  , 2.00
	TIMES SOURCE:尿道快楽, 1.50
	TIMES SOURCE:疼痛    , 1.00
ELSEIF BIND("四つん這い"      , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.50
	TIMES SOURCE:Ｖ快楽  , 1.50
	TIMES SOURCE:Ａ快楽  , 2.00
	TIMES SOURCE:Ｂ快楽  , 1.00
	TIMES SOURCE:尿道快楽, 1.50
	TIMES SOURCE:疼痛    , 1.00
ELSEIF BIND("縄"              , TEQUIP:拘束)
	TIMES SOURCE:Ｃ快楽  , 1.20
	TIMES SOURCE:Ｖ快楽  , 1.20
	TIMES SOURCE:Ａ快楽  , 1.20
	TIMES SOURCE:Ｂ快楽  , 1.20
	TIMES SOURCE:尿道快楽, 1.00
	TIMES SOURCE:疼痛    , 1.20
ENDIF

;-------------------------------------------------
;SOURCEからCUPを算出
;-------------------------------------------------
CALL SOURCE_CVABM
CALL SOURCE_NOTCVABM

;-------------------------------------------------
;素質などによるCUPの上下の処理
;-------------------------------------------------
FOR LCOUNT, 0, 30
	SIF CUP:LCOUNT <= 0
		CONTINUE

	CUP:LCOUNT = MULTIPLY(CUP:LCOUNT, 100 + TCVAR:(630 + LCOUNT) )

	;减少要素はある程度より低いなら０になる
	SELECTCASE PALAMNAME:LCOUNT
	CASE "恐怖", "反感"
		SIF CUP:LCOUNT < 10
			CUP:LCOUNT = 0
	ENDSELECT

	;素質による補正の最終値がプラスならば、最低上昇値となる。
	SIF CUP:LCOUNT > 0 && CUP:LCOUNT < TCVAR:(630 + LCOUNT)
		CUP:LCOUNT = TCVAR:(630 + LCOUNT)
NEXT

;-------------------------------------------------
;感度OFF指定がある場合は、対応した快楽のCUPを0にする
;-------------------------------------------------
IF TCVAR:Ｃ感度OFF
	CUP:快Ｃ = 0
	UP:快Ｃ = 0
ENDIF
IF TCVAR:Ｖ感度OFF
	CUP:快Ｖ = 0
	UP:快Ｖ = 0
ENDIF
IF TCVAR:Ａ感度OFF
	CUP:快Ａ = 0
	UP:快Ａ = 0
ENDIF
IF TCVAR:Ｂ感度OFF
	CUP:快Ｂ = 0
	UP:快Ｂ = 0
ENDIF
IF TCVAR:Ｍ感度OFF
	CUP:快Ｍ = 0
	UP:快Ｍ = 0
ENDIF
IF TCVAR:尿感度OFF
	CUP:快尿 = 0
	UP:快尿 = 0
ENDIF

;調教知識による補正
SELECTCASE CONDS("調教知識", PLAYER)
CASE "愛撫・奉仕知識"
	SELECTCASE ARGS
	CASE "調教指令", "試算"
		SELECTCASE SELECTCOM
		;PLAYヤーが愛撫
		CASE 0 TO 2, 4 TO 12, 15
			TIMES CUP:快Ｃ, 1.50
			TIMES CUP:快Ｖ, 1.50
			TIMES CUP:快Ａ, 1.50
			TIMES CUP:快Ｂ, 1.50
			TIMES CUP:快Ｍ, 1.50
			TIMES CUP:快尿, 1.50
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:Ａ潤, 1.50
			TIMES CUP:恭順, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:恥情, 0.50
			TIMES CUP:苦痛, 0.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		ENDSELECT
	ENDSELECT
CASE "教育擅長"
	SELECTCASE ARGS
	CASE "調教指令", "試算"
		IF TEQUIP:推倒 == 0
			SELECTCASE SELECTCOM
			;相手がPLAYヤーに奉仕
			CASE 65, 75, 90 TO 119, 120, 121, 125, 126
				TIMES CUP:習得, 1.50
				TIMES CUP:恐怖, 0.50
				TIMES CUP:反感, 0.50
			ENDSELECT
		ENDIF
	ENDSELECT
CASE "ＳＭ知識"
	SELECTCASE ARGS
	CASE "調教指令", "試算"
		SELECTCASE SELECTCOM
		;ＳＭPLAY
		CASE 130 TO 159
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:恭順, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		ENDSELECT
	ENDSELECT
CASE "Ａ調教知識"
	SELECTCASE ARGS
	CASE "調教指令", "試算"
		SELECTCASE SELECTCOM
		;PLAYヤーが相手の肛門を弄る
		CASE 2, 9, 33, 70 TO 79, 136, 142
			TIMES CUP:快Ａ, 1.50
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:Ａ潤, 1.50
			TIMES CUP:恭順, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:苦痛, 0.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		ENDSELECT
	ENDSELECT
;これについては@SOURCE_INSERTION等で増減
CASE "性器拡張知識"

CASE "道具使"
	SELECTCASE ARGS
	CASE "調教指令", "試算"
		SELECTCASE SELECTCOM
		CASE 2, 33
			IF TEQUIP:肛用振動棒
				TIMES CUP:快Ａ, 1.50
				TIMES CUP:Ｖ潤, 1.50
				TIMES CUP:Ａ潤, 1.50
				TIMES CUP:恭順, 1.50
				TIMES CUP:欲情, 1.50
				TIMES CUP:屈服, 1.50
				TIMES CUP:苦痛, 0.50
				TIMES CUP:恐怖, 0.50
				TIMES CUP:反感, 0.50
			ENDIF
		CASE 8, 31
			IF TEQUIP:振動棒
				TIMES CUP:快Ｖ, 1.50
				TIMES CUP:Ｖ潤, 1.50
				TIMES CUP:恭順, 1.50
				TIMES CUP:欲情, 1.50
				TIMES CUP:屈服, 1.50
				TIMES CUP:苦痛, 0.50
				TIMES CUP:恐怖, 0.50
				TIMES CUP:反感, 0.50
			ENDIF
		CASE 30 TO 59
			TIMES CUP:快Ｃ, 1.50
			TIMES CUP:快Ｖ, 1.50
			TIMES CUP:快Ａ, 1.50
			TIMES CUP:快Ｂ, 1.50
			TIMES CUP:快Ｍ, 1.50
			TIMES CUP:快尿, 1.50
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:Ａ潤, 1.50
			TIMES CUP:恭順, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:苦痛, 0.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		;尿道
		CASE 149, 151
			TIMES CUP:快尿, 1.50
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:Ａ潤, 1.50
			TIMES CUP:恭順, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:苦痛, 0.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		CASE 152
			IF TEQUIP:尿道振動棒
				TIMES CUP:快尿, 1.50
				TIMES CUP:Ｖ潤, 1.50
				TIMES CUP:Ａ潤, 1.50
				TIMES CUP:恭順, 1.50
				TIMES CUP:欲情, 1.50
				TIMES CUP:屈服, 1.50
				TIMES CUP:苦痛, 0.50
				TIMES CUP:恐怖, 0.50
				TIMES CUP:反感, 0.50
			ENDIF
		;ＳＭ関連の道具
		CASE 131 TO 136, 148
			TIMES CUP:Ｖ潤, 1.20
			TIMES CUP:恭順, 1.20
			TIMES CUP:欲情, 1.20
			TIMES CUP:屈服, 1.20
			TIMES CUP:苦痛, 0.80
			TIMES CUP:恐怖, 0.80
			TIMES CUP:反感, 0.80
		ENDSELECT
	ENDSELECT
;これについては常時効果を発揮
CASE "露出知識"
	TIMES CUP:恥情, 1.50
	IF CUP:恥情 >= 3000
		TIMES CUP:Ｖ潤, 1.50
		TIMES CUP:欲情, 1.50
		TIMES CUP:屈服, 1.50
		TIMES CUP:恐怖, 0.50
		TIMES CUP:反感, 0.50
	ENDIF
ENDSELECT

;助手の調教知識による補正
IF ASSI && PLAYER == MASTER
	SELECTCASE CONDS("調教知識", ASSI)
	;これについては常時効果を発揮
	CASE "露出知識"
		TIMES CUP:恥情, 1.50
		IF CUP:恥情 >= 3000
			TIMES CUP:Ｖ潤, 1.50
			TIMES CUP:欲情, 1.50
			TIMES CUP:屈服, 1.50
			TIMES CUP:恐怖, 0.50
			TIMES CUP:反感, 0.50
		ENDIF
	ENDSELECT
ENDIF

;-------------------------------------------------
;指令によるLuxurious判定の計算
;-------------------------------------------------
SIF STATE("恍惚", PLAYER) == 0
	CALL CHECK_LUXURIOUS_HIT, "指令", PLAYER
;PLAYERが恍惚になってしまったらTARGETは恍惚にならない
SIF STATE("恍惚", PLAYER) == 0 && STATE("恍惚", TARGET) == 0
	CALL CHECK_LUXURIOUS_HIT, "指令", TARGET

;Luxurious補正
IF NOWEX:Luxurious
	SIF TALENT:Ｃ性向 || TALENT:淫核
		TIMES CUP:快Ｃ, 2.00
	SIF TALENT:Ｖ性向 || TALENT:淫壷
		TIMES CUP:快Ｖ, 2.00
	SIF TALENT:Ａ性向 || TALENT:尻穴狂い
		TIMES CUP:快Ａ, 2.00
	SIF TALENT:Ｂ性向 || TALENT:淫乳
		TIMES CUP:快Ｂ, 2.00
	SIF TALENT:Ｍ性向 || TALENT:蕩唇
		TIMES CUP:快Ｍ, 2.00
ENDIF

;感度ＬＶによる快感上昇の上限措置
IF CUP:快Ｃ
	;PRINTFORM 本来のＣ快感 = {CUP:快Ｃ}　→　
	CUP:快Ｃ = CALCF("快感上昇の上限措置", CUP:快Ｃ, ABL:Ｃ感覚 + PENIS(TARGET) + TALENT:淫核 + TALENT:Ｃ性向)
	;PRINTFORML 修正後のＣ快感 = {CUP:快Ｃ}
ENDIF
IF CUP:快Ｖ
	;PRINTFORM 本来のＶ快感 = {CUP:快Ｖ}　→　
	CUP:快Ｖ = CALCF("快感上昇の上限措置", CUP:快Ｖ, ABL:Ｖ感覚 + TALENT:淫壷 + TALENT:Ｖ性向)
	;PRINTFORML 修正後のＶ快感 = {CUP:快Ｖ}
ENDIF
IF CUP:快Ａ
	;PRINTFORM 本来のＡ快感 = {CUP:快Ａ}　→　
	CUP:快Ａ = CALCF("快感上昇の上限措置", CUP:快Ａ, ABL:Ａ感覚 + TALENT:尻穴狂い + TALENT:Ａ性向)
	;PRINTFORML 修正後のＡ快感 = {CUP:快Ａ}
ENDIF
IF CUP:快Ｂ
	;PRINTFORM 本来のＢ快感 = {CUP:快Ｂ}　→　
	CUP:快Ｂ = CALCF("快感上昇の上限措置", CUP:快Ｂ, ABL:Ｂ感覚 + TALENT:淫乳 + TALENT:Ｂ性向)
	;PRINTFORML 修正後のＢ快感 = {CUP:快Ｂ}
ENDIF
IF CUP:快Ｍ
	;PRINTFORM 本来のＭ快感 = {CUP:快Ｍ}　→　
	CUP:快Ｍ = CALCF("快感上昇の上限措置", CUP:快Ｍ, ABL:Ｍ感覚 + TALENT:蕩唇 + TALENT:Ｍ性向)
	;PRINTFORML 修正後のＭ快感 = {CUP:快Ｍ}
ENDIF
IF CUP:快尿
	;PRINTFORM 本来の尿道快感 = {CUP:快尿}　→　
	CUP:快尿 = CALCF("快感上昇の上限措置", CUP:快尿, ABL:尿道感覚 + TALENT:尿道狂 + TALENT:尿道性感)
	;PRINTFORML 修正後の尿道快感 = {CUP:快尿}
ENDIF

;痛覚による快感補正（ペナルティだが、受虐狂な人にはちょっとだけご褒美）
IF CUP:苦痛 >= 100
	LOCAL = LIMIT(100 - GET_PALAMLV(CUP:苦痛)*20 + ABL:受虐属性*20, 10, 110)
	CUP:快Ｃ = MULTIPLY(CUP:快Ｃ, LOCAL)
	CUP:快Ｖ = MULTIPLY(CUP:快Ｖ, LOCAL)
	CUP:快Ａ = MULTIPLY(CUP:快Ａ, LOCAL)
	CUP:快Ｂ = MULTIPLY(CUP:快Ｂ, LOCAL)
	CUP:快Ｍ = MULTIPLY(CUP:快Ｍ, LOCAL)
	CUP:快尿 = MULTIPLY(CUP:快尿, LOCAL)
	;体力の消費に影響がでる
	;PRINTFORM 苦痛による体力消費 {BASE:体力}, {DOWNBASE:体力}→
	DOWNBASE:体力 = MULTIPLY(DOWNBASE:体力, 100 + (GET_PALAMLV(CUP:苦痛) * (GET_PALAMLV(CUP:苦痛) + 1) / 2)*10)
	;PRINTFORML {DOWNBASE:体力}
ENDIF


;-------------------------------------------------
;愛液処理(欲情と情緒)
;-------------------------------------------------
;欲情の値に応じてＶ潤増加
IF TALENT:男性 == 0 && CUP:欲情 > 0 && PALAM:Ｖ潤 < 30000
	;MAX500
	LOCAL = MIN(DOWN_PALAMLV(CUP:欲情, 2), 500)
	LOCAL += MARK:淫紋*30

	IF TALENT:容易濕
		SIF TALENT:容易濕 >= 2
			LOCAL += TALENT:容易濕 * 50
		LOCAL = UP_PALAMLV(LOCAL)
	ELSEIF TALENT:不易濕
		LOCAL = DOWN_PALAMLV(LOCAL)
	ENDIF

	CUP:Ｖ潤 += LOCAL
ENDIF
;腸液分泌体質だと肛門が湿的ます。なんということでしょう
SIF TALENT:腸液分泌体質 && CUP:欲情 > 0 && PALAM:Ａ潤 < 10000
	CUP:Ａ潤 += DOWN_PALAMLV(CUP:欲情, 1)/2

IF ARGS != "試算"
	;-------------------------------------------------
	;情緒処理（恭順と欲情）
	;-------------------------------------------------
	SELECTCASE CUP:恭順 + CUP:欲情
	CASE IS >= 60000
		FLAG:情緒 += 4 + RAND:5
	CASE IS >= 30000
		FLAG:情緒 += 3 + RAND:4
	CASE IS >= 10000
		FLAG:情緒 += 2 + RAND:3
	CASE IS >= 3000
		FLAG:情緒 += 1 + RAND:3
	CASE IS >= 500
		FLAG:情緒 += 1 + RAND:2
	ENDSELECT
	;弱点突きの情緒を追加
	IF CUP:恐怖 + CUP:反感 < 500
		IF CUP:快Ｃ && TALENT:淫核 + TALENT:Ｃ性向
			FLAG:情緒 += 2
		ELSEIF CUP:快Ｖ && TALENT:淫壷 + TALENT:Ｖ性向
			FLAG:情緒 += 2
		ELSEIF CUP:快Ａ && TALENT:尻穴狂い + TALENT:Ａ性向
			FLAG:情緒 += 2
		ELSEIF CUP:快Ｂ && TALENT:淫乳 + TALENT:Ｂ性向
			FLAG:情緒 += 2
		ELSEIF CUP:快Ｍ && TALENT:蕩唇 + TALENT:Ｍ性向
			FLAG:情緒 += 2
		ELSEIF CUP:快尿 && TALENT:尿道狂 + TALENT:尿道性感
			FLAG:情緒 += 2
		ENDIF
	ENDIF
	;SOURCEをTCVAR:660～689に記録＆CUPにUP追加
	FOR LCOUNT, 0, 30
		TCVAR:(LCOUNT + 660) += SOURCE:LCOUNT
		CUP:LCOUNT += UP:LCOUNT
	NEXT
ENDIF

VARSET SOURCE

;-------------------------------------------------
;快楽のソース
;-------------------------------------------------
@SOURCE_CVABM
;CUP:恭順,欲情,屈服にかかる補正
#DIM CUP_ELSE
;部位特化素質がある場合のBOUNS
#DIM CUP_SP

CUP:快Ｃ += SOURCE:Ｃ快楽
CUP:快Ｖ += SOURCE:Ｖ快楽
CUP:快Ａ += SOURCE:Ａ快楽
CUP:快Ｂ += SOURCE:Ｂ快楽
CUP:快Ｍ += SOURCE:Ｍ快楽
CUP:快尿 += SOURCE:尿道快楽

;快楽以外は副次効果なので獲得量を抑える
CUP_ELSE = DOWN_PALAMLV(SOURCE:Ｃ快楽 + SOURCE:Ｖ快楽 + SOURCE:Ａ快楽 + SOURCE:Ｂ快楽 + SOURCE:Ｍ快楽 + SOURCE:尿道快楽)

;欲望が大きいほど、快楽により得る恭順,欲情,屈服が増える
CUP:恭順 += CUP_ELSE * (MIN(ABL:欲望, 10) + 2) * 2 / 150
CUP:欲情 += CUP_ELSE * (MIN(ABL:欲望, 10) + 2) * 2 / 100
CUP:屈服 += CUP_ELSE * (MIN(ABL:欲望, 10) + 10) * 2 / 400

;部位特化素質がある場合には充足とＶ潤にBOUNS
CUP_SP = GET_PALAMLV(SOURCE:Ｃ快楽*TALENT:淫核 + SOURCE:Ｖ快楽*TALENT:淫壷 + SOURCE:Ａ快楽*TALENT:尻穴狂い + SOURCE:Ｂ快楽*TALENT:淫乳 + SOURCE:Ｍ快楽*TALENT:蕩唇 + SOURCE:尿道快楽*TALENT:尿道狂) * 500
;中毒のソース：CUP:恭順,欲情,屈服に影響
SOURCE:充足 += CUP_SP * (MIN(ABL:欲望, 10) + 2) * 2 / 100
;阴部があるならＶ潤も上昇
IF TALENT:男性 == 0
	IF TALENT:容易濕
		CUP:Ｖ潤 += UP_PALAMLV(CUP_SP * (MIN(ABL:欲望, 10) + 2) * 2 / 300)
	ELSEIF TALENT:不易濕
		CUP:Ｖ潤 += DOWN_PALAMLV(CUP_SP * (MIN(ABL:欲望, 10) + 2) * 2 / 300)
	ELSE
		CUP:Ｖ潤 += CUP_SP * (MIN(ABL:欲望, 10) + 2) * 2 / 300
	ENDIF
ENDIF
;腸液分泌体質があるとCUP:快Ａに応じて肛門が湿的ます
SIF TALENT:腸液分泌体質 && PALAM:Ａ潤 < 10000
	CUP:Ａ潤 += DOWN_PALAMLV(SOURCE:Ａ快楽, 2)

;-------------------------------------------------
;快楽以外のソースの計算
;-------------------------------------------------
@SOURCE_NOTCVABM
;-------------------------------------------------
;情愛のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

SELECTCASE ABL:信頼
CASE 0 TO 5
	CUP:0:恭順 = MULTIPLY(SOURCE:情愛, 40 + ABL:信頼*10)
CASE 6 TO 10
	CUP:0:恭順 = MULTIPLY(SOURCE:情愛, 90 + (ABL:信頼 - 5)*5)
CASEELSE
	CUP:0:恭順 = MULTIPLY(SOURCE:情愛, 115 + (ABL:信頼 - 10)*1)
ENDSELECT

SELECTCASE ABL:欲望
CASE 0 TO 5
	CUP:0:欲情 = MULTIPLY(SOURCE:情愛, ABL:欲望*6)
CASE 6 TO 10
	CUP:0:欲情 = MULTIPLY(SOURCE:情愛, 30 + (ABL:欲望 - 5)*3)
CASEELSE
	CUP:0:欲情 = MULTIPLY(SOURCE:情愛, 45 + (ABL:欲望 - 10)*1)
ENDSELECT

CUP:恭順 += CUP:0:恭順
CUP:欲情 += CUP:0:欲情

;-------------------------------------------------
;性行動のソース
;-------------------------------------------------
SIF COND("絶頂", PLAYER) && TCVAR:干劲
	TIMES SOURCE:性行動, 1.20

CALL VARSET_NOTCVABM

SELECTCASE ABL:奉仕精神
CASE 0 TO 5
	CUP:0:習得 = MULTIPLY(SOURCE:性行動, 100 + ABL:奉仕精神*10)
CASE 6 TO 10
	CUP:0:習得 = MULTIPLY(SOURCE:性行動, 150 + (ABL:奉仕精神 - 5)*5)
CASEELSE
	CUP:0:習得 = MULTIPLY(SOURCE:性行動, 175 + (ABL:奉仕精神 - 10) )
ENDSELECT

CUP:習得 += CUP:0:習得

;-------------------------------------------------
;達成感のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

SELECTCASE ABL:奉仕精神
CASE 0 TO 5
	CUP:0:恭順 = MULTIPLY(SOURCE:達成感, 40 + ABL:奉仕精神*20)
CASE 6 TO 10
	CUP:0:恭順 = MULTIPLY(SOURCE:達成感, 140 + (ABL:奉仕精神 - 5)*10)
CASEELSE
	CUP:0:恭順 = MULTIPLY(SOURCE:達成感, 190 + (ABL:奉仕精神 - 10)*2)
ENDSELECT

CUP:恭順 += CUP:0:恭順

;-------------------------------------------------
;疼痛のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

CUP:0:苦痛 = SOURCE:疼痛

SIF TALENT:疼痛無視
	TIMES CUP:0:苦痛, 0.10

SELECTCASE ABL:信頼
CASE 0 TO 5
	CUP:0:恐怖 = MULTIPLY(SOURCE:疼痛, 80 - ABL:信頼*8)
CASE 6 TO 10
	CUP:0:恐怖 = MULTIPLY(SOURCE:疼痛, 40 - (ABL:信頼 - 5)*4)
CASEELSE
	CUP:0:恐怖 = MULTIPLY(SOURCE:疼痛, MAX(20 - (ABL:信頼 - 10)*1, 1) )
ENDSELECT

SELECTCASE ABL:信頼 + ABL:受虐属性
CASE 0 TO 5
	CUP:0:反感 = MULTIPLY(SOURCE:疼痛, 80 - (ABL:信頼 + ABL:受虐属性)*10)
CASE 6 TO 10
	CUP:0:反感 = MULTIPLY(SOURCE:疼痛, 30 - (ABL:信頼 + ABL:受虐属性 - 5)*5)
CASEELSE
	CUP:0:反感 = MULTIPLY(SOURCE:疼痛, 5 - (ABL:信頼 + ABL:受虐属性 - 10)*1)
ENDSELECT

SELECTCASE ABL:受虐属性
CASE 0 TO 5
	CUP:0:欲情 = MULTIPLY(SOURCE:疼痛, ABL:受虐属性*15)
CASE 6 TO 10
	CUP:0:欲情 = MULTIPLY(SOURCE:疼痛, 75 + (ABL:受虐属性 - 5)*5)
CASEELSE
	CUP:0:欲情 = MULTIPLY(SOURCE:疼痛, 100 + (ABL:受虐属性 - 10)*2)
ENDSELECT

;倒錯的
IF TALENT:倒錯的
	TIMES CUP:0:恐怖, 0.50
	TIMES CUP:0:反感, 0.50
	TIMES CUP:0:欲情, 2.00
ENDIF

;拘束の状態によりソースを変更
IF GETBIT(TEQUIP:拘束, 8)
	TIMES CUP:0:恐怖, 2.50
ELSEIF GETBIT(TEQUIP:拘束, 7)
	TIMES CUP:0:恐怖, 2.00
ELSEIF GETBIT(TEQUIP:拘束, 6)
	TIMES CUP:0:恐怖, 1.50
ENDIF

CUP:苦痛 += CUP:0:苦痛
CUP:恐怖 += CUP:0:恐怖
CUP:反感 += CUP:0:反感
;受虐属性があるとPALAM:欲情も上がる
CUP:欲情 += CUP:0:欲情
;屈服も上がる
CUP:屈服 += CUP:0:苦痛 + CUP:0:恐怖/2 + CUP:0:欲情
;-------------------------------------------------
;充足のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

SELECTCASE ABL:欲望
CASE 0 TO 5
	CUP:0:恭順 = MULTIPLY(SOURCE:充足, 20 + ABL:欲望*6)
CASE 6 TO 10
	CUP:0:恭順 = MULTIPLY(SOURCE:充足, 50 + (ABL:欲望 - 5)*4)
CASEELSE
	CUP:0:恭順 = MULTIPLY(SOURCE:充足, 70 + (ABL:欲望 - 10)*1)
ENDSELECT

SELECTCASE ABL:欲望
CASE 0 TO 5
	CUP:0:欲情 = MULTIPLY(SOURCE:充足, 30 + ABL:欲望*6)
CASE 6 TO 10
	CUP:0:欲情 = MULTIPLY(SOURCE:充足, 60 + (ABL:欲望 - 5)*4)
CASEELSE
	CUP:0:欲情 = MULTIPLY(SOURCE:充足, 80 + (ABL:欲望 - 10)*1)
ENDSELECT

CUP:恭順 += CUP:0:恭順
CUP:欲情 += CUP:0:欲情
CUP:屈服 += CUP:0:恭順 + CUP:0:欲情
;-------------------------------------------------
;不潔のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

CUP:0:欲情 = SOURCE:不潔

SELECTCASE MAX(ABL:信頼, ABL:欲望)
CASE 0 TO 5
	CUP:0:反感 = MULTIPLY(SOURCE:不潔, 100 - MAX(ABL:信頼, ABL:欲望)*10)
CASE 6 TO 10
	CUP:0:反感 = MULTIPLY(SOURCE:不潔, 50 - (MAX(ABL:信頼, ABL:欲望) - 5)*8)
CASEELSE
	CUP:0:反感 = MULTIPLY(SOURCE:不潔, 90 - (MAX(ABL:信頼, ABL:欲望) - 10)*1)
ENDSELECT

CUP:0:反感 = MULTIPLY(CUP:0:反感, 100 - ABL:受虐属性*10)

IF TALENT:汚臭無視
	CUP:0:反感 = 0
ELSEIF TALENT:倒錯的
	TIMES CUP:0:反感, 0.50
ENDIF

;ある程度反感が低い場合には欲情の香料になる
SELECTCASE CUP:0:反感
CASE IS < 100
	CUP:0:反感 = 0
	CUP:0:欲情 /= 10
CASE IS < 500
	CUP:0:欲情 /= 20
CASEELSE
	CUP:0:欲情 = 0
ENDSELECT

CUP:反感 += CUP:0:反感
CUP:欲情 += CUP:0:欲情

;-------------------------------------------------
;露出のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

CUP:0:恥情 = SOURCE:露出

SELECTCASE ABL:露出癖
CASE 0 TO 5
	CUP:0:欲情 = MULTIPLY(SOURCE:露出, 5 + ABL:露出癖*11)
CASE 6 TO 10
	CUP:0:欲情 = MULTIPLY(SOURCE:露出, 60 + (ABL:露出癖 - 5)*6)
CASEELSE
	CUP:0:欲情 = MULTIPLY(SOURCE:露出, 90 + (ABL:露出癖 - 10)*2)
ENDSELECT

SELECTCASE MAX(ABL:信頼, ABL:欲望)
CASE 0 TO 5
	CUP:0:反感 = MULTIPLY(SOURCE:露出, 30 - MAX(ABL:信頼, ABL:欲望)*4)
CASE 6 TO 10
	CUP:0:反感 = MULTIPLY(SOURCE:露出, 10 - (MAX(ABL:信頼, ABL:欲望) - 5)*1)
CASEELSE
	CUP:0:反感 = MULTIPLY(SOURCE:露出, 5 - (MAX(ABL:信頼, ABL:欲望) - 10)*1)
ENDSELECT

IF TALENT:倒錯的
	TIMES CUP:0:欲情, 1.50
	TIMES CUP:0:反感, 0.50
ENDIF
SIF TALENT:喜歡引人注目
	TIMES CUP:0:反感, 0.10

CUP:欲情 += DOWN_PALAMLV(CUP:0:欲情)
CUP:恥情 += CUP:0:恥情
CUP:反感 += CUP:0:反感

;-------------------------------------------------
;屈従のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

CUP:0:屈服 = SOURCE:屈従

CUP:屈服 += CUP:0:屈服

;-------------------------------------------------
;逸脱のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

CUP:0:欲情 = SOURCE:逸脱

SELECTCASE MAX(ABL:信頼, ABL:欲望)
CASE 0 TO 5
	CUP:0:反感 = MULTIPLY(SOURCE:逸脱, 80 - MAX(ABL:信頼, ABL:欲望)*10)
CASE 6 TO 10
	CUP:0:反感 = MULTIPLY(SOURCE:逸脱, 30 - (MAX(ABL:信頼, ABL:欲望) - 5)*5)
CASEELSE
	CUP:0:反感 = MULTIPLY(SOURCE:逸脱, 5 - (MAX(ABL:信頼, ABL:欲望) - 10)*1)
ENDSELECT


SIF TALENT:倒錯的 || TALENT:好奇心
	TIMES CUP:0:反感, 0.50
SIF ABL:受虐属性 >= 3
	TIMES CUP:0:反感, 0.50

IF TALENT:倒錯的 && ABL:受虐属性 >= 3
	TIMES CUP:0:欲情, 0.50
ELSEIF TALENT:倒錯的 || ABL:受虐属性 >= 3
	TIMES CUP:0:欲情, 0.20
ELSE
	CUP:0:欲情 = 0
ENDIF

CUP:欲情 += CUP:0:欲情
CUP:反感 += CUP:0:反感

;-------------------------------------------------
;恐惧のソース
;-------------------------------------------------
CUP:恐怖 += SOURCE:恐惧

;-------------------------------------------------
;Ｖ液体追加のソース
;-------------------------------------------------
CUP:Ｖ潤 += SOURCE:Ｖ液体追加

;-------------------------------------------------
;Ａ液体追加のソース
;-------------------------------------------------
CUP:Ａ潤 += SOURCE:Ａ液体追加

;-------------------------------------------------
;Ｐ液体追加のソース
;-------------------------------------------------
CUP:Ｐ潤 += SOURCE:Ｐ液体追加

;-------------------------------------------------
;恭順追加のソース
;-------------------------------------------------
CUP:恭順 += SOURCE:恭順追加

;-------------------------------------------------
;欲情追加のソース
;-------------------------------------------------
CUP:欲情 += SOURCE:欲情追加

;-------------------------------------------------
;反感追加のソース
;-------------------------------------------------
CALL VARSET_NOTCVABM

SELECTCASE ABL:信頼
CASE 0 TO 5
	CUP:0:反感 = MULTIPLY(SOURCE:反感追加, 100 - ABL:信頼*10)
CASE 6 TO 10
	CUP:0:反感 = MULTIPLY(SOURCE:反感追加, 50 - (ABL:信頼 - 5)*8)
CASEELSE
	CUP:0:反感 = MULTIPLY(SOURCE:反感追加, 10 - (ABL:信頼 - 10)*1)
ENDSELECT

CUP:反感 += CUP:0:反感



;-------------------------------------------------
;刻印取得の格子
;-------------------------------------------------
@MARK_GOT_CHECK
#DIM MEMO_MARK, 4

MEMO_MARK:0 = MARK:快楽刻印
MEMO_MARK:1 = MARK:苦痛刻印

VARSET LOCALS

;-------------------------------------------------
;快楽刻印
;-------------------------------------------------
SELECTCASE MAX(CUP:快Ｃ, CUP:快Ｖ, CUP:快Ａ, CUP:快Ｂ, CUP:快Ｍ, (CUP:快Ｃ + CUP:快Ｖ + CUP:快Ａ + CUP:快Ｂ + CUP:快Ｍ)/2)
CASE IS >= 30000
	SIF MARK:快楽刻印 <= 2
		LOCALS = 快楽刻印Lv3取得
	MARK:快楽刻印 = 3
CASE IS >= 10000
	SIF MARK:快楽刻印 <= 1
		MARK:快楽刻印 = 2
CASE IS >= 3000
	SIF MARK:快楽刻印 == 0
		MARK:快楽刻印 = 1
ENDSELECT

IF MARK:快楽刻印 > MEMO_MARK:0
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML 
	PRINTFORML 取得快楽刻印LV{MARK:快楽刻印}
	;冷静・感情缺乏の場合は快楽刻印による信頼アップは无
	IF ABL:信頼 < MARK:快楽刻印 - 1 && TALENT:冷静 == 0 && TALENT:感情缺乏 == 0
		ABL:信頼 += 1
		PRINTFORML 以及信頼变成LV{ABL:信頼}了
	ENDIF
	RESETCOLOR
ENDIF

;-------------------------------------------------
;苦痛刻印（推倒ている時には獲得しない）
;-------------------------------------------------
SELECTCASE CALCF("苦痛刻印獲得", TARGET)
CASE 1
	MARK:苦痛刻印 = 1
CASE 2
	MARK:苦痛刻印 = 2
CASE 3
	LOCALS = 苦痛刻印Lv3取得
	MARK:苦痛刻印 = 3
ENDSELECT

IF MARK:苦痛刻印 > MEMO_MARK:1
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML 
	PRINTFORML 取得苦痛刻印LV{MARK:苦痛刻印}
	RESETCOLOR
ENDIF

SIF MARK:快楽刻印 > MEMO_MARK:0 || MARK:苦痛刻印 > MEMO_MARK:1
	FORCEWAIT

;い移開かの刻印の3LVを得た時には喋る
SIF LOCALS != ""
	CALL KOJO_MESSAGE_MARKCNG, LOCALS


;-------------------------------------------------
;奉仕快楽経験、嗜虐快楽経験、羞恥快楽経験、被虐快楽経験の格子
;LOCALを計算に使い、LOCAL:1～LOCAL:4がそれぞれに対応している
;-------------------------------------------------
@EXP_GOT_CHECK
SIF TFLAG:进行性処理
	RETURN 0
VARSET LOCAL
LOCAL = CUP:快Ｃ + CUP:快Ｖ + CUP:快Ａ + CUP:快Ｂ + CUP:快Ｍ + CUP:快尿 + CUP:欲情

;奉仕快楽と嗜虐快楽の基本値
SELECTCASE LOCAL
CASE IS >= 30000
	LOCAL = 3
CASE IS >= 10000
	LOCAL = 2
CASE IS >= 3000
	LOCAL = 1
CASEELSE
	LOCAL = 0
ENDSELECT

IF TCVAR:奉仕快楽フラグ
	LOCAL:1 = LOCAL + TCVAR:奉仕快楽フラグ
	;飲尿PLAY
	SIF COND("飲尿フラグ")
		LOCAL:1 += 1
	;媚薬
	SIF TEQUIP:媚薬
		LOCAL:1 -= TEQUIP:媚薬/50
	;射精させたか
	SIF COND("絶頂", PLAYER)
		LOCAL:1 += 1
	;二足の草鞋補正
	SIF TCVAR:嗜虐快楽フラグ && ABL:奉仕精神 <= ABL:施虐属性
		LOCAL:1 -= 1
ENDIF

IF TCVAR:嗜虐快楽フラグ
	;嗜虐快楽はLOCAL:2
	LOCAL:2 = LOCAL + TCVAR:嗜虐快楽フラグ
	;媚薬
	SIF TEQUIP:媚薬
		LOCAL:2 -= TEQUIP:媚薬/50
	;女王様PLAY
	SIF TEQUIP:女王様PLAY
		LOCAL:2 += 1
	;射精させたか
	SIF COND("絶頂", PLAYER)
		LOCAL:2 += 1
	;二足の草鞋補正
	SIF TCVAR:奉仕快楽フラグ && ABL:奉仕精神 >= ABL:施虐属性
		LOCAL:2 -= 1

	;挣扎が成功したときは０
	SIF SELECTCOM == 280
		LOCAL:2 = 0
ENDIF

;PLAYERがMASTERでないと半減
IF PLAYER != MASTER
	LOCAL:1 /= 2
	LOCAL:2 /= 2
ENDIF

;羞恥快楽の格子
LOCAL = CUP:快Ｃ + CUP:快Ｖ + CUP:快Ａ + CUP:快Ｂ + CUP:快Ｍ + CUP:快尿 + CUP:欲情

SELECTCASE MIN(LOCAL, CUP:恥情)
CASE IS >= 30000
	LOCAL:3 = 3
CASE IS >= 10000
	LOCAL:3 = 2
CASE IS >= 3000
	LOCAL:3 = 1
CASEELSE
	LOCAL:3 = 0
ENDSELECT
LOCAL:3 += TCVAR:羞恥快楽フラグ

IF LOCAL:3
	;媚薬
	SIF TEQUIP:媚薬
		LOCAL:3 -= TEQUIP:媚薬/50
	;射精させたか
	SIF COND("絶頂", PLAYER)
		LOCAL:3 += 1
	SIF NOWEX:絶頂数合計
		LOCAL:3 += 1
ENDIF


;被虐快楽の格子
LOCAL = CUP:快Ｃ + CUP:快Ｖ + CUP:快Ａ + CUP:快Ｂ + CUP:快Ｍ + CUP:快尿 + CUP:欲情

SELECTCASE MIN(LOCAL, CUP:苦痛)
CASE IS >= 30000
	LOCAL:4 = 3
CASE IS >= 10000
	LOCAL:4 = 2
CASE IS >= 3000
	LOCAL:4 = 1
CASEELSE
	LOCAL:4 = 0
ENDSELECT
LOCAL:4 += TCVAR:被虐快楽フラグ

IF LOCAL:4
	;媚薬
	SIF TEQUIP:媚薬
		LOCAL:4 -= TEQUIP:媚薬/50
	;射精させたか
	SIF COND("絶頂", PLAYER)
		LOCAL:4 += 1
	SIF NOWEX:絶頂数合計
		LOCAL:4 += 1
ENDIF

;調教知識によるBOUNS
SELECTCASE CONDS("調教知識", PLAYER)
CASE "教育擅長"
	TIMES LOCAL:1, 2.00
CASE "ＳＭ知識"
	TIMES LOCAL:2, 2.00
	TIMES LOCAL:4, 2.00
CASE "道具使"
	TIMES LOCAL:3, 2.00
	TIMES LOCAL:4, 2.00
CASE "露出知識"
	TIMES LOCAL:3, 2.00
ENDSELECT

;助手の調教知識による補正
IF ASSI && PLAYER == MASTER
	SELECTCASE CONDS("調教知識", ASSI)
	;これについては常時効果を発揮
	CASE "露出知識"
		TIMES LOCAL:3, 2.00
	ENDSELECT
ENDIF

EXP:奉仕快楽経験 += MAX(LOCAL:1, 0)
EXP:嗜虐快楽経験 += MAX(LOCAL:2, 0)
EXP:羞恥快楽経験 += MAX(LOCAL:3, 0)
EXP:被虐快楽経験 += MAX(LOCAL:4, 0)

;情緒BOUNS
FLAG:情緒 += (LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4)/2

;以下は獲得経験の表示
SIF TFLAG:システム文スキップ
	RETURN 0

;以下はとりあえず不顯示ことに
RETURN 0

SIF LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4 > 0
	PRINTL 

SIF LOCAL:1 > 0
	PRINTFORML 奉仕快楽経験+{LOCAL:1}
SIF LOCAL:2 > 0
	PRINTFORML 嗜虐快楽経験+{LOCAL:2}
SIF LOCAL:3 > 0
	PRINTFORML 羞恥快楽経験+{LOCAL:3}
SIF LOCAL:4 > 0
	PRINTFORML 被虐快楽経験+{LOCAL:4}

