@SELF_CHECK
SIF COND("同衾イベント") || COND("同衾イベント", MASTER)
	RETURN 0
;放課後Ｈは時間がない
SIF FLAG:放課後Ｈ
	RETURN 0
;念のため、PLAYERをMASTERに直す
PLAYER = MASTER

;MASTERとの絡み
SIF ABL:欲望 && BASE:MASTER:魂 >= 100 && RAND:2
	CALL SELF_CHECK_PLAY, MASTER
;自慰
SIF ABL:欲望 && ABL:自慰中毒 && RAND:4 == 0
	CALL SELF_CHECK_PLAY, TARGET

;助手との絡み
IF CONFIG("部員間Ｈ") && ASSI && ABL:欲望 && RAND:2
	;PLAYERをASSIにする
	PLAYER = ASSI
	CALL SELF_CHECK_PLAY, ASSI
ENDIF

;PLAYERをMASTERに直す
PLAYER = MASTER


@SELF_CHECK_PLAY, ARG
;行為の回数
#DIM NUM_PLAY
#DIM PLAY_FISTING

;イベントの決定
CALL PLAY_SELF, ARG
SIF STR:イベント名 == ""
	RETURN 0

;基本回数は精力依存
NUM_PLAY = LIMIT(BASE:精力/20 + LIMIT(RAND:(BASE:精力/10 + 1), 1, BASE:精力/15), 1, 4)

SELECTCASE STR:イベント名
CASE "調教後性交"
	;Ｖ性向、淫壷によるBOUNS
	SIF TALENT:Ｖ性向
		NUM_PLAY += 1
	SIF TALENT:淫壷
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORM %CALLNAME:PLAYER%和%CALLNAME:TARGET%抑制不住尚未冷却的昂奮、
				PRINTFORML 汗流浃背地在床上交合着…
				CALL SIMULATE_TRAIN_COM, 60, NUM_PLAY
			ELSEIF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORM 興奮尚未冷却的%CALLNAME:TARGET%、
				PRINTFORML 推倒了%CALLNAME:PLAYER%貪婪地发泄着自己的欲望…
				TEQUIP:推倒 = 1
				CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY
			ELSE
				PRINTFORM 身体被玩弄得燥热的%CALLNAME:TARGET%、
				PRINTFORML 骑在疲劳的%CALLNAME:PLAYER%身上超越限界地絞榨着精液…
				TEQUIP:推倒 = 1
				CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY
			ENDIF

			CALL KOJO_EVENT, STR:イベント名

			PRINTL 
			IF ABL:信頼 + ABL:欲望 + ABL:Ｖ感覚 >= 13 && COND("欲求不満")
				PRINTFORM %CALLNAME:TARGET%用舌頭舔舐清理%CALLNAME:PLAYER%的东西的同时、
				PRINTFORMW 恋恋不舍地抬眼仰視著…
				PRINTFORMW 但%CALLNAME:PLAYER%甩開了视线、离開了房间…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ELSEIF TALENT:相思相愛 && RAND:2
				IF TALENT:PLAYER:汚臭無視 && ABL:施虐属性 >= 3 && RAND:5 == 0
					PRINTFORMW %CALLNAME:PLAYER%用舌頭漂亮地舔舐清理了看起来满足了的%CALLNAME:TARGET%的阴部、离開了房间…
					CALL SIMULATE_TRAIN_COM, 1, NUM_PLAY
				ELSE
					PRINTFORMW %CALLNAME:PLAYER%干净利落地擦拭干净了看起来满足了的%CALLNAME:TARGET%的身体、离開了房间…
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%指使满足了的%CALLNAME:TARGET%仔细地清潔了阴茎、离開了房间…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ENDIF
		ELSE
			PRINTFORML 調教结束後的%CALLNAME:TARGET%和%CALLNAME:ASSI%、受溢出的欲望驱使、娇嫩的肌肤交缠在一起蠕动着…
			PRINTFORML 连%CALLNAME:MASTER%的事都被抛之脑后的二人、
			PRINTFORM 像吃不饱的野兽一般
			IF COND("夜") == 0
				PRINTFORM 直到天黑一直
			ELSE
				PRINTFORM 一整晩都
			ENDIF
			PRINTFORML 贪婪地从对方身上索取着快楽…
			CALL SIMULATE_TRAIN_COM, 61, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW
		;受精確率の変動を確認
		CALL CHECK_NINSHIN_ALLACTOR
		
		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後Ａ性交"
	;Ａ性向、尻穴狂いによるBOUNS
	SIF TALENT:Ａ性向
		NUM_PLAY += 1
	SIF TALENT:尻穴狂い
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML 用尻穴在床上交合了好多次…
				CALL SIMULATE_TRAIN_COM, 71, NUM_PLAY
			ELSEIF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORML 肛門疼得无法冷却興奮的%CALLNAME:TARGET%、
				PRINTFORML 推倒了%CALLNAME:PLAYER%貪婪地发泄着自己的欲望…
				TEQUIP:推倒 = 1
				CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY
			ELSE
				PRINTFORML 火照った身体をもてあましている%CALLNAME:TARGET%は、
				PRINTFORML 骑在疲劳的%CALLNAME:PLAYER%身上用肛門絞榨着精液…
				TEQUIP:推倒 = 1
				CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY
			ENDIF
			CALL KOJO_EVENT, STR:イベント名

			IF ABL:信頼 + ABL:欲望 >= 13 && COND("欲求不満")
				PRINTL 
				PRINTFORMW %CALLNAME:TARGET%恋恋不舍地拉住了%CALLNAME:MASTER%的袖子
				PRINTFORMW 但%CALLNAME:PLAYER%甩開了拉住袖子的手、离開了房间…
			ENDIF
		ELSE
			PRINTFORML 調教结束後的%CALLNAME:TARGET%和%CALLNAME:ASSI%、将躁动的身体贴在了对方的肌肤上…
			PRINTFORML 受满溢的欲望驱使、此时连%CALLNAME:MASTER%的事都被抛之脑后的二人
			PRINTFORM 像吃不饱的野兽一般
			IF COND("夜") == 0
				PRINTFORM 直到天黑一直
			ELSE
				PRINTFORM 一整晩都
			ENDIF
			PRINTFORML 在贪享着%CALLNAME:TARGET%肛門的快楽…
			CALL SIMULATE_TRAIN_COM, 71, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後レズPLAY"
	;Ｃ性向、淫核によるBOUNS
	SIF TALENT:Ｃ性向
		NUM_PLAY += 1
	SIF TALENT:淫核
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML 不知多少次互相安慰着彼此燥热的身体…
				CALL SIMULATE_TRAIN_COM, 1, RAND:NUM_PLAY + 1
				CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			ELSE
				PRINTFORML 興奮冷めやらぬ%CALLNAME:TARGET%は、
				SIF BASE:PLAYER:精力 * 2 < MAXBASE:PLAYER:精力
					PRINT 疲劳的
				PRINTFORML %CALLNAME:PLAYER%推倒贪婪地摩擦搓揉着自己的秘所…
				CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			ENDIF

			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
			IF ABL:信頼 + ABL:欲望 >= 13 && COND("欲求不満")
				PRINTL 
				PRINTFORMW %CALLNAME:TARGET%恋恋不舍地拉住了%CALLNAME:MASTER%的袖子
				PRINTFORMW 但%CALLNAME:PLAYER%甩開了拉住袖子的手、离開了房间…
			ENDIF
		ELSE
			PRINTFORML 調教结束後的%CALLNAME:TARGET%和%CALLNAME:ASSI%、在情欲的引导下交缠在一起…
			PRINTFORML 受满溢的欲望驱使、此时连%CALLNAME:MASTER%的事都被抛之脑后的二人
			PRINTFORM 像吃不饱的野兽一般
			IF COND("夜") == 0
				PRINTFORM 直到天黑一直
			ELSE
				PRINTFORM 一整晩都
			ENDIF

			CALL SIMULATE_TRAIN_COM, 1, RAND:NUM_PLAY + 1
			CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY/2 + 1

			IF TALENT:扶她 && TALENT:PLAYER:扶她
				PRINTFORML 互相摩擦着秘所、吸吮着阴茎贪享着快楽…
				CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY/2 + 1
			ELSE
				PRINTFORML 互相摩擦着秘所、贪享着快楽…
			ENDIF
			CALL KOJO_EVENT, STR:イベント名

			SWAP PLAYER, TARGET
			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY/2 + 1
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY/2 + 1
			SWAP PLAYER, TARGET
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後自慰"
	NUM_PLAY += RAND:(MIN(5, ABL:自慰中毒) + 1)
	IF NUM_PLAY > 0
		DRAWLINE
		PRINTFORM 將%CALLNAME:PLAYER%离開之後、%CALLNAME:TARGET%将
		SIF TALENT:恋慕
			PRINTFORML 、想着%CALLNAME:PLAYER%的同时、

		;フィストフラグ
		PLAY_FISTING = 0

		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｃ"
			IF PENIS(TARGET)
				PRINT 陰莖上下搓动着
			ELSE
				IF RAND:2 == 0
					PRINT 阴蒂
				ELSE
					PRINT 秘所
				ENDIF
				PRINTFORM を自己的指で
				IF RAND:3 == 0
					PRINT 一边揉搓
				ELSEIF RAND:2 == 0
					PRINT 一边玩弄
				ELSE
					PRINT 一边捏揉
				ENDIF
			ENDIF
		CASE "Ｖ"
			IF RAND:2 == 0
				PRINT 阴部
			ELSE
				PRINT 秘所
			ENDIF
			IF EXP:Ｖ拳交経験 >= 5 && TALENT:処女 == 0 && ABL:Ｖ拡張 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT 用自己的拳搅动着
				ELSE
					PRINT 用自己的拳抽插着
				ENDIF
				PLAY_FISTING = 2
				NUM_PLAY = MAX(1, NUM_PLAY/2)
			ELSEIF RAND:3 == 0
				PRINT 用自己的手指玩弄着
			ELSEIF RAND:2 == 0
				PRINT 用自己的手指拨弄着
			ELSE
				PRINT 用自己的手指抽插着
			ENDIF
		CASE "Ａ"
			IF RAND:2 == 0
				PRINT 肛門
			ELSE
				PRINT 尻穴
			ENDIF
			IF EXP:Ａ拳交経験 >= 5 && ABL:Ａ拡張 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT 用自己的拳搅动着
				ELSE
					PRINT 用自己的拳抽插着
				ENDIF
				PLAY_FISTING = 2
				NUM_PLAY = MAX(1, NUM_PLAY/2)
			ELSEIF RAND:3 == 0
				PRINT 用自己的手指玩弄着
			ELSEIF RAND:2 == 0
				PRINT 用自己的手指拨弄着
			ELSE
				PRINT 用自己的手指抽插着
			ENDIF
		ENDSELECT

		PRINTFORML 自慰了{NUM_PLAY}次。

		CALL KOJO_EVENT, STR:イベント名

		PRINTFORML 自慰経験＋{NUM_PLAY}
		EXP:自慰経験 += NUM_PLAY

		PRINTFORML 絶頂経験＋{NUM_PLAY}
		EXP:絶頂経験 += NUM_PLAY
		IF TALENT:女性 == 0
			PRINTFORML 射精経験＋{NUM_PLAY}
			EXP:射精経験 += NUM_PLAY
			IF BASE:濃厚精液
				BASE:濃厚精液 = MAX(BASE:濃厚精液 - NUM_PLAY, 0)
				NOWEX:濃厚射精 = 1
				EX:濃厚射精 += 1
				TCVAR:濃厚射精 = 0
			ENDIF
		ENDIF

		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｖ"
			PRINTFORML Ｖ経験＋{NUM_PLAY + PLAY_FISTING*5}
			EXP:Ｖ経験 += NUM_PLAY + PLAY_FISTING*5
			IF PLAY_FISTING
				PRINTFORML Ｖ拡張経験＋{NUM_PLAY * 2}
				EXP:Ｖ拡張経験 += NUM_PLAY * 2
			ENDIF
			PRINTFORML 快Ｖの珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:快Ｖ += (NUM_PLAY + PLAY_FISTING*5) * 200
		CASE "Ａ"
			PRINTFORML Ａ経験＋{NUM_PLAY + PLAY_FISTING*5}
			EXP:Ａ経験 += NUM_PLAY + PLAY_FISTING*5
			IF PLAY_FISTING
				PRINTFORML Ａ拡張経験＋{NUM_PLAY * 2}
				EXP:Ａ拡張経験 += NUM_PLAY * 2
			ENDIF
			PRINTFORML 快Ａの珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:快Ａ += (NUM_PLAY + PLAY_FISTING*5) * 200
		CASEELSE
			PRINTFORML 快Ｃの珠＋{NUM_PLAY * 500}
			JUEL:快Ｃ += NUM_PLAY * 500
		ENDSELECT

		PRINTFORML 恭順の珠＋{NUM_PLAY * 100}
		JUEL:恭順 += NUM_PLAY * 100
		PRINTFORMW 欲情の珠＋{(NUM_PLAY + PLAY_FISTING*5) * 250}
		JUEL:欲情 += (NUM_PLAY + PLAY_FISTING*5) * 250

		BASE:体力 = MAX(BASE:体力 - (NUM_PLAY + PLAY_FISTING*5)*50, 1)

		IF ABL:信頼 + ABL:欲望 + ABL:自慰中毒 >= 10
			PRINTFORML 之后%CALLNAME:TARGET%把这件事報告了。
			PRINTFORML 恥情の珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:恥情 += (NUM_PLAY + PLAY_FISTING*5) * 200
		ENDIF

		SIF ABL:欲望 >= 5
			PRINTFORMW 但是无论自慰多少次、%CALLNAME:PLAYER%的欲望始终得不到满足…
	ENDIF
ENDSELECT
DRAWLINE


@PLAY_SELF, ARG
#DIM LCOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

STR:イベント名 = 
;恋慕、淫乱による調教後性交渉格子
SIF BASE:PLAYER:精力 == 0 || TALENT:淫乱 + TALENT:恋慕 == 0
	RETURN 0
;体力を使い果たした
SIF BASE:体力 < 500 || BASE:精力 <= 0
	RETURN 0
;性欲など満足
SIF COND("満足")
	RETURN 0

;助手とのPLAYが発生するか
IF ARG == ASSI && ABL:欲望 >= 5
	;どちらかが男だとダメ
	SIF TALENT:男性 || TALENT:ASSI:男性
		RETURN 0
	;二人とも兩面通吃じゃないとダメ
	SIF TALENT:兩面通吃 == 0 || TALENT:ASSI:兩面通吃 == 0
		RETURN 0
	;二人とも百合経験豊富じゃないとダメ
	SIF EXP:百合経験 < 300 || EXP:ASSI:百合経験 < 300
		RETURN 0
	;相性がよいことが条件
	SIF RELATION:(NO:ASSI) < 120 || RELATION:ASSI:(NO:TARGET) < 120
		RETURN 0
ENDIF

VARSET EVENT_TABLE

IF ARG == PLAYER
	;Ｖ性交
	CALL COM_ABLE65
	SIF RESULT && PENIS(PLAYER) && ABL:Ｖ感覚 >= 3 && TALENT:処女 + TALENT:PLAYER:童貞 == 0 && COND("女性主導生性交")
		EVENT_TABLE += "調教後性交/"
	;Ａ性交
	CALL COM_ABLE75
	SIF RESULT && PENIS(PLAYER) && EXP:PLAYER:Ａ挿入経験 && COND("Ａ熟達")
		EVENT_TABLE += "調教後Ａ性交/"
	;レズPLAY
	CALL COM_ABLE80
	SIF RESULT && COND("性別嫌悪") == 0
		EVENT_TABLE += "調教後レズPLAY/"
ELSEIF ARG == TARGET
	;自慰
	SIF ABL:自慰中毒 >= 3 && COND("身嗜み：自慰禁止") == 0 && PLAYER == MASTER
		EVENT_TABLE += "調教後自慰/"
ENDIF

STR:イベント名 = %TEXTR(EVENT_TABLE)%

RETURN 0

