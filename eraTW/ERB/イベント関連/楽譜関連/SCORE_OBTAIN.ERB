;-----------------------------------------------------------
;乐谱を随机に1つ取得
;
;RETURNF 取得した乐谱番号、取得できなかった場合は-1
;-----------------------------------------------------------
@乐谱入手_随机
#FUNCTION

RETURNF EXEC_SCORES(乐谱処理モード：乐谱を随机に１つ取得, 乐谱入手方法：随机)

;-----------------------------------------------------------
;角色番号と同じ入手条件の乐谱を随机に1つ取得
;TARGET 角色番号
;
;RETURNF 取得した乐谱番号、取得できなかった場合は-1
;-----------------------------------------------------------
@乐谱入手_演奏大成功(TARGET)
#FUNCTION

RETURNF EXEC_SCORES(乐谱処理モード：乐谱を随机に１つ取得, TARGET)

;-----------------------------------------------------------
;角色番号に関連した入手条件が「乐谱入手方法：シリーズ関連角色陥落」の乐谱を一つ取得
;TARGET 角色番号
;
;RETURNF 取得した乐谱番号、取得できなかった場合は-1
;-----------------------------------------------------------
@乐谱入手_陥落(TARGET)
#FUNCTION

COUNT = EXEC_SCORES(乐谱処理モード：角色関連シリーズを全取得, TARGET)
FOR LOCAL, 0, COUNT
	RESULT = EXEC_SCORES(乐谱処理モード：乐谱を随机に１つ取得, 乐谱入手方法：シリーズ関連角色陥落, RESULT:LOCAL)
	SIF RESULT != -1
		RETURNF RESULT
NEXT

RETURNF -1

;-----------------------------------------------------------
;角色番号に関連した入手条件が「乐谱入手方法：シリーズ関連角色弾幕勝負」の乐谱を一つ取得
;TARGET 角色番号
;
;RETURNF 取得した乐谱番号、取得できなかった場合は-1
;-----------------------------------------------------------
@乐谱入手_弾幕勝負(TARGET)
#FUNCTION

COUNT = EXEC_SCORES(乐谱処理モード：角色関連シリーズを全取得, TARGET)
FOR LOCAL, 0, COUNT
	RESULT = EXEC_SCORES(乐谱処理モード：乐谱を随机に１つ取得, 乐谱入手方法：シリーズ関連角色弾幕勝負, RESULT:LOCAL)
	SIF RESULT != -1
		RETURNF RESULT
NEXT

RETURNF -1

;-----------------------------------------------------------
;指定した入手条件の乐谱を一つ取得
;EXEC_MODE 乐谱処理モード
;
;RETURNF 取得した乐谱番号、取得できなかった場合は-1
;-----------------------------------------------------------
@乐谱入手_特殊条件(EXEC_MODE)
#FUNCTION
#DIM EXEC_MODE
RETURNF EXEC_SCORES(乐谱処理モード：乐谱を随机に１つ取得, EXEC_MODE)

;-----------------------------------------------------------
;乐谱を入手
;SCORE_NO 乐谱番号
;SKIP_CHECK チェックする場合は1
;
;RETURNF 入手済みなら0, 未入手なら1
;-----------------------------------------------------------
@OBTAIN_SCORE(SCORE_NO, SKIP_CHECK=1)
#FUNCTION
#DIM SCORE_NO	; 乐谱番号
#DIM SKIP_CHECK	; チェックする場合は1

IF HAVE_SCORE(SCORE_NO)
	;乐谱入手済みの場合、処理をスキップ
	RETURNF 0
ELSE
	RESULT = SET_SCORE(SCORE_NO, 1)
	PRINTFORML %GET_SCORE_OBTAIN_MESSAGE(SCORE_NO)%
	RETURNF 1
ENDIF

;-----------------------------------------------------------
;乐谱取得に連動して取得される乐谱のチェック
;SCORE_NO 乐谱番号
;-----------------------------------------------------------
@OBTAIN_SCORE_CHECK(SCORE_NO)
#FUNCTION
#DIM DYNAMIC SCORE_NO	; 乐谱番号

#DIM DYNAMIC SERIES_NO	; シリーズ番号

SERIES_NO = GET_SERIES_NO(SCORE_NO)

;【乐谱入手方法：シリーズ初取得】の処理
LOCAL = EXEC_SCORES(乐谱処理モード：所持数カウント, -1, SERIES_NO)
SIF 0 < LOCAL
	; 1つ以上乐谱がある場合、【シリーズ初取得】の乐谱を全て取得
	RESULT = EXEC_SCORES(乐谱処理モード：全乐谱を取得, 乐谱入手方法：シリーズ初取得, SERIES_NO)

;【乐谱入手方法：シリーズ随机コンプ】の処理
LOCAL:1 = EXEC_SCORES(乐谱処理モード：総数カウント, 乐谱入手方法：随机, SERIES_NO)
LOCAL:2 = EXEC_SCORES(乐谱処理モード：所持数カウント, 乐谱入手方法：随机, SERIES_NO)
SIF LOCAL:1 <= LOCAL:2
	RESULT = EXEC_SCORES(乐谱処理モード：全乐谱を取得, 乐谱入手方法：シリーズ随机コンプ, SERIES_NO)

;【乐谱入手方法：シリーズ完全コンプ】の処理
LOCAL:1 = EXEC_SCORES(乐谱処理モード：総数カウント, -1, SERIES_NO)
LOCAL:2 = EXEC_SCORES(乐谱処理モード：所持数カウント, -1, SERIES_NO)
LOCAL:3 = EXEC_SCORES(乐谱処理モード：総数カウント, 乐谱入手方法：シリーズ完全コンプ, SERIES_NO)
SIF (LOCAL:1 - LOCAL:3) <= LOCAL:2
	; 乐谱所持数が (総数 - 完全コンプの乐谱数) を上回っていた場合、【シリーズ完全コンプ】の乐谱を全て取得
	RESULT = EXEC_SCORES(乐谱処理モード：全乐谱を取得, 乐谱入手方法：シリーズ完全コンプ, SERIES_NO)

RETURNF 0