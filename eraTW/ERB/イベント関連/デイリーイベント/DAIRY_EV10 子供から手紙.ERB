@DAILY_CONDITION_EV10
#DIM YOME
#DIM CHILDNUM
SIF RAND:20 > 0
	RETURN -1
CALL PickRandGrownChild
YOME = RESULT
CHILDNUM = RESULT:1
VARSET RESULT

SIF YOME <= 0
	RETURN -1

CALL DAILY_EV10(YOME, CHILDNUM)
RETURN 1

@DAILY_EV10(ARG, ARG:1)
;PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%との兒童の从%CHILDNAME:ARG:(ARG:1)%来信
PRINTFORMW 从%CHILDNAME:ARG:(ARG:1)%来信
LOCAL:1 = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:好感度 > CFLAG:ARG:好感度
		LOCAL:1 ++
NEXT

TARGET = ARG
;口上専用の手紙イベント分岐（なければ通常イベント）,存在しないならRESULT = 2
IF !RAND:2
	CALL KOJO_MESSAGE_SEND("CHILD", 0, TARGET, ARG:1, 0, "DAILY_口上手紙")
	SIF RESULT == 2
		GOTO TEGAMI
ELSE
	$TEGAMI
	IF LOCAL:1 >= 3
		CALL KOJO_MESSAGE_SEND("CHILD", 0, TARGET, ARG:1, 0, "DAILY_共通手紙", "すけこまし")
		;存在しないならRESULT = 2（以下同様）
		IF RESULT == 2
			PRINTFORMW 上面寫著要好好愛護%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%……
		ENDIF
	ELSEIF !RAND:5
		CALL KOJO_MESSAGE_SEND("CHILD", 0, TARGET, ARG:1, 0, "DAILY_共通手紙", "金の無心")
		IF RESULT == 2
			PRINTFORMW 又來要錢了……
		ENDIF
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%決定送去\@ MONEY > 30000? %金額表示(30000)%# 所有的錢\@
		MONEY = MAX(MONEY - 30000,0)
	ELSE
		CALL KOJO_MESSAGE_SEND("CHILD", 0, TARGET, ARG:1, 0, "DAILY_共通手紙", "仕送り")
		IF RESULT == 2
			PRINTFORMW 收到了擔心%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%和%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的話，以及寄來的%金額表示(10000)%
		ENDIF
		MONEY += 10000
	ENDIF
ENDIF
TARGET = TARGET:1
RETURN 1
