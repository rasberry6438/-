@GO_OUT_CONDITION_EV6
IF !RAND:10
	CALL GO_OUT_EV6
ELSE
	RETURN 0
ENDIF
@GO_OUT_EV6
#DIM PAIR
#DIM LUCK
#DIM SPENT
#DIMS PREY
SPENT = 1
PREY = %\@ RAND:2 ? 狸 # 狐 \@%
PAIR = FLAG:约会的对象
LUCK = CFLAG:MASTER:今日の運勢 / 100 + TFLAG:幸運補正

PRINTFORML 道中、看到陷入了猟師的罠的%PREY%
IF (GROUPMATCH(PAIR, [[藍]], [[典]]) && PREY == "狐") || (PAIR == [[猯藏]] && PREY == "狸")
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%好像想说些什么似地盯着这边
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%解開了罠之後、%PREY%向%NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%道谢后逃走了…
	PRINTFORMW 
	RETURN 1
ELSEIF (GROUPMATCH(PAIR, [[藍]], [[典]]) && PREY == "狸") || (PAIR == [[猯藏]] && PREY == "狐")
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%只是看了罠一眼便什么都不做地离開了
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%也慌忙地跟了上去…
	PRINTFORMW 
	RETURN 1
ENDIF
PRINTFORML 這樣下去的話肯定會被捕到的…
CALL ASK_M("不要妨礙罠猟", 1, "救助", 1)
IF !RESULT
	PRINTFORML 雖然很可憐但也無能爲力
	SIF PAIR
		PRINTFORM 雖然%NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%有些不情願、但
	PRINTFORMDL \@ PAIR ? 二人還是 # %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% \@什么都不做地踏上了帰路
	PRINTFORMW
	RETURN 1
ENDIF
PRINTFORML 因爲很難可憐所以準備幫助它
PRINTFORMW 雖然受傷了但簡単處理一下應該就可以行走了
PRINTDW …………
PRINTDW ……
PRINTDW …
IF LUCK > 4
	PRINTFORMW 治療結束后的%PREY%消失在了灌木的陰影中
	PRINTFORML …然後好像叼著什麽回到了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的身邊
	PRINTFORML 一定是作爲救助的謝礼
	;最高級素材を随机
	LOCAL:0 = 625, 630, 639, 640, 643, 646
	CALL GET_ITEM(ITEMNAME:(LOCAL:(RAND:6)), 1, 2)
	PRINTFORML 
	IF !(GO_OUT_FLAG_06 || PAIR || GETBIT(TFLAG:一日一回, 9)) && LUCK > 7
		GO_OUT_FLAG_06 = DAY
		SETBIT TFLAG:一日一回, 9
		CALL MAKE_YUKIZURI(RANDOM_CHARANUM, @"妖怪%PREY%")
		CFLAG:RANDOM_CHARANUM:經常去的地方 = (MAIN_MAP == 5) ? 7 # 5
		CFLAG:RANDOM_CHARANUM:好感度 = REQUIRED_FAVOR_思慕
		CFLAG:RANDOM_CHARANUM:信賴度 = REQUIRED_TRUST_思慕
		ABL:RANDOM_CHARANUM:親密 = REQUIRED_AFFECTION_思慕
		ABL:RANDOM_CHARANUM:従順 = REQUIRED_AFFECTION_思慕
		CSTR:RANDOM_CHARANUM:路人子プロフィール１ = %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%：%PRINT_DATE_F(DAY)%、在從%GET_MAPNAME(CFLAG:MASTER:约会中)%帰家的道中救助的妖怪%PREY%
		BASE:RANDOM_CHARANUM:体力 -= 50
		PRINTFORML …哦呀！？　%PREY%的様子…！
		PRINTFORMW %PREY%就在面前変成了獣耳少女！
		CALL CHARA_STATE(RANDOM_CHARANUM)
		DRAWLINE
		CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:RANDOM_CHARANUM:処女喪失履歴%",C_PINK,1)
		PRINTFORMW 
		PRINTFORML 自稱%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的%PREY%娘向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%表述了多次的感謝
		SIF !INRANGE(TIME, 540, 1140)
			GOTO SKIP
		PRINTFORML 説是爲了道謝希望跟隨著回家…
		CALL ASK_M("今天還是自己回去吧", 1, "請跟我來", 1)
		IF RESULT
			FLAG:约会的对象 = RANDOM_CHARANUM
			CFLAG:RANDOM_CHARANUM:現在位置 = CFLAG:MASTER:現在位置
			CFLAG:RANDOM_CHARANUM:同行 = 300
			CFLAG:MASTER:同行 = 300
			CFLAG:RANDOM_CHARANUM:清醒劑 = 300
			CFLAG:RANDOM_CHARANUM:拠点来訪 = 1
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%決定帶%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%回去
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%送來了熱情的視線…
			GOTO END_PHASE
		ENDIF
		PRINTFORML 因爲受了傷所以就這樣回去較好
		$SKIP
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%說下次希望能來\@ CFLAG:RANDOM_CHARANUM:經常去的地方 == 5 ? 魔法之森 # 山的山麓 \@遊玩之後便離去了…
		CALL COLORMESSAGE(@"與%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%結識了",C_YELLOW,2,1)
		CFLAG:RANDOM_CHARANUM:現在位置 = SUKIMA()
		GOTO END_PHASE
	ENDIF
	PRINTFORML 在被救助的%PREY%的目送下\@ PAIR ? 二人 # %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% \@踏上了帰路
ELSE
	IF PAIR
		PRINTFORML 在處理傷口的時候%PREY%發狂地挠抓着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		CALL RECOVER(MASTER, -200, "体力")
		WAIT
		PRINTFORML 而且在處理結束之後%PREY%便直接衝刺逃走了…真是不知感恩！
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%
		SELECTCASE CFLAG:PAIR:性格傾向
			CASE 1
				PRINTFORMW 擔心地看著%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
			CASE 2, 3
				PRINTFORMW 看著%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%\@ CFLAG:PAIR:性格傾向 == 2 ? 哈哈大笑 # 哧哧地笑 \@了起來…
			CASE 4
				PRINTFORMW 露出了難以言喻的微妙表情…
			CASE 5
				PRINTFORMW 用憐憫的眼神看向了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		ENDSELECT
		PRINTFORML 一無所獲的二人踏上了帰路
	ELSE
		PRINTFORMW 在處理結束之後%PREY%便直接衝刺逃走了…真是不知感恩！
		PRINTFORML 只好就這樣回去了、但身後好像有人
		PRINTFORMW 回頭看去…是一個因爲獲物逃跑而憤怒的猟師…！
		PRINTFORML 
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的頭被打了！
		CALL RECOVER(MASTER, -100, "体力")
		WAIT
		PRINTFORML 被猟師説教了…
		CALL RECOVER(MASTER, -300, "気力")
		WAIT
		PRINTFORML 倒霉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%跌跌撞撞地踏上了帰路
		SPENT = 4
	ENDIF
ENDIF
$END_PHASE
PRINTFORML +{15 * SPENT}分
PRINTFORMW 
TIME += (15 * SPENT)
RETURN 1
