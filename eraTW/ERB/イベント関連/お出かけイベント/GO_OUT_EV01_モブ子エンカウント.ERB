@GO_OUT_CONDITION_EV1
RESULT = 0
;约会中は発生しない
SIF RAND:3 || FLAG:约会的对象
	RETURN 0
;その日物色済みだと無理
SIF GETBIT(TFLAG:一日一回, 9)
	RETURN 0
IF WEATHERDLC
	IF TIME:5 != 3
		SIF RAND:100 > 50
			RETURN 0
	ELSEIF RAND:100 > 75
			RETURN 0
	ENDIF
ELSE
ENDIF
IF ENCOUNTDLC
	IF SUMIPROTECT
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "憤怒", "服", "", ""
			PRINTFORMDW 「%TEXTR("嗯？！/哦呀？!/啊？！")%」
		ENDIF
		PRINTFORMW 在路上，突然感觉到了被袭击的预感，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不由得在心中感谢了堇子的加护，连忙小心的躲避了开来……
		RETURN 0
	ENDIF
ENDIF
CALL GO_OUT_EV1
RETURN RESULT

@GO_OUT_EV1
#DIM ENEMY
#DIMS ENEMY_RACE
#DIM モブ残機
#DIM LOOT
#DIM LOOT_CANDIDATE, 17 = 51, 52, 53, 55, 56, 58, 59, 81, 82, 83, 84, 121, 131, 500, 501, 502, 510
#DIM CONST CANDIDATE_N = 17
#DIM REWARD
#DIM 発情フラグ
#DIM 満月バフ効果
発情フラグ = 0
満月バフ効果 = 0
;相手が変わるとかやりたい
SELECTCASE CFLAG:MASTER:约会中
	;湖
	CASE 3
		ENEMY_RACE = 妖精
	;竹林
	CASE 4
		IF !RAND:3
			ENEMY_RACE = 因幡
		ELSE
			ENEMY_RACE = 妖精
		ENDIF
	;魔法之森
	CASE 5
		ENEMY_RACE '= TEXTR("妖怪狸/茨狸")
	;三途之川
	CASE 6
		ENEMY_RACE '= TEXTR("妖怪狐/妖怪猫")
	;山麓
	CASE 7
		ENEMY_RACE = 河童
	;山頂
	CASE 8
		ENEMY_RACE = 白狼天狗
	;地底
	CASE 9
		ENEMY_RACE = 鬼
	;月
	CASE 10
		ENEMY_RACE = 玉兎
	CASEELSE
		RETURN 0
ENDSELECT

IF 玩家立绘开关
	CALL PRINT_FACE_PLAYER, MASTER, "憤怒", "服", "", ""
	PRINTFORMDW 「%TEXTR("嗯？！/哦呀？!/啊？！")%」
ENDIF
PRINTFORMW 回来的路上、突然被%ENEMY_RACE%襲击了！
SETBIT TFLAG:一日一回, 9
;桃色の風による影響
IF (FLAG:異常気象 == 8 && TCVAR:RANDOM_CHARANUM:発情) || TCVAR:RANDOM_CHARANUM:願掛け桃色の風
	発情フラグ = 1
ENDIF
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, ENEMY_RACE, 1)
ENEMY = RANDOM_CHARANUM
;BUG 防止出现“### MOVEMENT LOCATION ERROR %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)% 的現在位置異常 ### CHARA_NO:{LOCAL}”报错提示
CFLAG:RANDOM_CHARANUM:约会中 = CFLAG:MASTER:约会中

;満月時
IF TALENT:ENEMY:動物耳 && MOON_WATCH(DAY:3) == 9
	発情フラグ = 2
	CALL COLORMESSAGE(@"满月的光芒撩拨着%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的本能…！",C_YELLOW,1)
	CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的战斗力提升了！",C_YELLOW,1)
	満月バフ効果 = 1 + RAND:(MAX(2, ABL:MASTER:戦闘能力 - ABL:ENEMY:戦闘能力))
	ABL:ENEMY:戦闘能力 += 満月バフ効果
ENDIF
IF 発情フラグ
	TCVAR:ENEMY:発情 = 1
	CFLAG:ENEMY:积攒度 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:ENEMY:欲望,200,1000)
	
	IF 発情フラグ == 2
		;満月時は危険日確定
		CFLAG:ENEMY:900 = 7
	ELSE
		SELECTCASE FLAG:抱負
			CASE 6
				;抱負が子孫繁栄で未達成の場合は危険日確定
				CFLAG:ENEMY:900 = 7
			CASE 9
				;子孫繁栄達成済み、かつ、安全日の場合は再設定
				SIF ESTRUS_CYCLE(ENEMY) <= 15
					CFLAG:ENEMY:900 = RAND:10
		ENDSELECT
	ENDIF
ENDIF

HANDICAP_FIXED = RAND:(ABL:ENEMY:戦闘能力 * 5 + 1) + 10
HANDICAP_RAND = 0

CALL KOJO_MESSAGE_SEND("DANMAKU", 0, ENEMY, 1, -1, "戦闘前")
TEQUIP:ENEMY:上半身着衣状況 = 1

CALL CHARA_STATE(ENEMY)
	DRAWLINE
	CALL COLORMESSAGE(@"処女喪失履歴：%CSTR:ENEMY:処女喪失履歴%",C_PINK,1)
	PRINTFORML 
CALL DANMAKU_OPPONENT_STATUS(ENEMY)
IF HANDICAP_FIXED
	CALL COLORMESSAGE(@"地形効果：%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的骰子値降低{HANDICAP_FIXED}",C_YELLOW,1)
	;【三粒の天滴】の効果
	IF ITEM:三粒の天滴
		HANDICAP_FIXED /= 2
		CALL COLORMESSAGE(@"因为土着神的诅咒、地形効果半減为{HANDICAP_FIXED}！",C_YELLOW,1)
	ENDIF
ENDIF
$LOOP
CALL ASK_M("返打一手",1,"逃跑",1,"规则説明",1)
SELECTCASE RESULT
	CASE 0
		IF BASE:MASTER:気力 < 1000 || BASE:MASTER:体力 < 1000
			PRINTFORML 现在因为疲劳导致战斗力下降，即便如此也要战斗吗？
			CALL ASK_YN()
			IF RESULT == 0
				IF 玩家立绘开关
					CALL PRINT_FACE_PLAYER, MASTER, "笑顔", "服", "", ""
					PRINTFORMDW 「%TEXTR("塔塔开！/嘻嘻...我一定要赢呀.../这才有意思呀！/")%」
				ENDIF
				HANDICAP_FIXED += MAX(0, 1000 - BASE:MASTER:気力)
				HANDICAP_FIXED += MAX(0, 1000 - BASE:MASTER:体力)
				CALL COLORMESSAGE(@"由于疲劳，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的骰子值下降了{HANDICAP_FIXED}",C_YELLOW,1)
			ELSE
				IF 玩家立绘开关
					CALL PRINT_FACE_PLAYER, MASTER, "笑顔", "服", "", ""
					PRINTFORMDW 「%TEXTR("润！/嘻嘻...我一定要活下去呀/你给路打油！/")%」
				ENDIF
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%一溜烟地逃跑了
				RETURN 1
		ENDIF
		ENDIF
	CASE 1
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "笑顔", "服", "", ""
			PRINTFORMDW 「%TEXTR("润！/嘻嘻...我一定要活下去呀/你给路打油！/")%」
		ENDIF
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%迅捷地逃走了
		RETURN 1
	CASE 2
		CALL SHOW_DANMAKU_RULE
		GOTO LOOP
ENDSELECT
TARGET = ENEMY
REWARD = MAX(ABL:ENEMY:戦闘能力 * 10 + RAND:5, 1)
;米とか酒とか
LOOT = LOOT_CANDIDATE:(RAND:CANDIDATE_N)
TIME += 60
CFLAG:ENEMY:面識 = 1
PRINTFORMW 勝負開始！！
CALL DANMAKU_BATTLE(TARGET, 1)

モブ残機 = RESULT
CALL RECOVER(MASTER, -500, "体力")
CALL RECOVER(MASTER, -500, "気力")
CALL KOJO_MESSAGE_SEND("DANMAKU", モブ残機, TARGET, 1, -1, "戦闘後")
SIF モブ残機 < 3
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力 * 3
SIF モブ残機 < 1
	EXP:MASTER:戦闘経験 += ABL:ENEMY:戦闘能力
ABL:ENEMY:戦闘能力 -= 満月バフ効果
IF モブ残機 > 0
	;モブ娘勝利（MASTERの敗戦）処理
	SETCOLOR C_RED
	IF TCVAR:ENEMY:発情 && HAS_PENIS(MASTER)
		;CALL KOJO_MESSAGE_SEND("DANMAKU", モブ残機, TARGET, 1, -1, "逆レ開始")
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "自慰", "裸", "", "照れ"
			PRINTFORMDW 「%TEXTR("咕？/哈？/呀？/呃啊？")%%TEXTR("不行.../不要.../快...停下.../啊...啊.../咕啊.../呜哇...")%」
		ENDIF
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%压在了动不了的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%身上
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的喉咙上、温暖的吐息吹了上来…
		PRINTFORML 还没有想明白是怎么回事的下一瞬間、性器被温熱的感触包裹住了
		PRINTFORML 战战兢兢地往下腹部一看、是狡猾地笑着的%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的小穴把%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的阴茎吞到了最根部
		IF 発情フラグ == 1
			PRINTFORM 受到桃色的风影响，
		ELSEIF 発情フラグ == 2
			PRINTFORM 満月之夜迎来発情期的
		ENDIF
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%为了消解獣性的渇望、把%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%按在身下肆意地侵犯着、蹂躙着、使用着…
		PRINTFORMW ………
		PRINTFORMW ……
		PRINTFORMW …
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "性交", "裸", "", "照れ"
			PRINTFORMDW 「%TEXTR("嗯❤.../啊❤.../嗯❤...嗯.../啊❤...啊.../咕啊❤.../呜哇...")%%TEXTR("不行❤.../不要❤.../快❤...停下.../啊❤...啊.../咕啊❤.../呜哇❤.../要去了❤")%」
		ENDIF
		PRINTFORMW 一直到满足为止、把%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%吃干抹净的%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%丢下%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%离開了
		FLAG:気絶中断 = 100 + CFLAG:MASTER:現在位置 / 100
		CFLAG:MASTER:约会中 = CFLAG:MASTER:現在位置 / 100
		CALL LOST_GYAKURE(ENEMY)
	ELSEIF ITEM:LOOT
		PRINTFORMW 输掉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被拿走了%ITEMNAME:LOOT%…
		ITEM:LOOT --
	ELSEIF MONEY:2 >= REWARD
		PRINTFORMW 输掉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被拿走了{REWARD}筹码…
		MONEY:2 -= REWARD
	ELSE
		REWARD *= 300
		PRINTFORMW 输掉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被拿走了\@ MONEY > REWARD ? %金額表示(REWARD)%# 身上的所有钱\@…
		MONEY = MAX(MONEY - REWARD, 0)
	ENDIF
	RESETCOLOR
	RETURN 1
ENDIF

;source操作他
TCVAR:ENEMY:弾幕勝負不能 = 1
CFLAG:ENEMY:弾幕勝負勝利 = 1
CFLAG:ENEMY:败犬 = 1
CFLAG:ENEMY:經常去的地方 = CFLAG:MASTER:约会中

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%面对跪地求饶的%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%…
CALL ASK_M("献上胖次作为祭品",1,"勒索筹码",1,"用身体补償吧",1, "奇跡之石", ITEM:奇跡之石)
SELECTCASE RESULT
	CASE 0
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "通常", "服", "", ""
			PRINTFORMDW 「%TEXTR("献上胖次作为祭品/败者...就要献上祭品/交出你的胖次...")%」
		ENDIF
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%让%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%把手高高举起、開始吟唱祈祷的咒语、透过衣服可以看到%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的内裤開始亮起浅浅的光輝
		PRINTFORML 超自然的火炎愈发熾热、没有伤到%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的衣服分毫、只有内裤本身燃烧殆尽了
		PRINTFORML 目睹了神威的%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%害怕地逃走了
		CALL BUFF_BASE(MASTER,BASE_TSP,500, 1)
		CALL BUFF_BASE(MASTER,BASE_精力,500, 1)
		FLAG:篤信 += 10
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		TARGET = 0
		RETURN 1
	CASE 1
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "憤怒", "服", "", ""
			PRINTFORMDW 「欧拉！给我把值钱的东西全都交出来！」
		ELSE
			PRINTFORMW 欧拉！给老子把值钱的东西全都交出来！
		ENDIF
		MONEY:2 += REWARD
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%从%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%那里抢来了{REWARD}筹码
		IF RAND:5 < ABL:ENEMY:戦闘能力
			PRINTFORMW 又额外拿到了%ITEMNAME:LOOT%
			ITEM:LOOT ++
		ENDIF
		TARGET = 0
		;DELCHARA RANDOM_CHARANUM
		;ADDCHARA RANDOM_CHARANUM
		RETURN 1
	CASE 2
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "發情", "服", "", ""
			PRINTFORMDW 「如果不想被检举到巫女那里的话...」
			PRINTFORMDW 「今天一天都要听我的话哦❤」
		ELSE
			PRINTFORML 告诉%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%、如果不想被检举到巫女那里的话今天一天都要听%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的命令
		ENDIF
		IF TALENT:ENEMY:受虐狂
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%似乎在期待着之后要发生的
			PRINTFORM %TEXTR("惩罚/惩戒/报复/处罚/惩戒")%
			PRINTFORM 她%TEXTR("胸中澎湃/坐立不安/热切期待/心跳加速")%着
			PALAM:ENEMY:欲情 = PALAMLV:3
			ABL:ENEMY:欲望 = MAX(3, ABL:ENEMY:欲望)
			CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%现在无论何时何地,只要不是在众人面前,都可以随时将她压在身下了",C_YELLOW,1) 
		ELSE
			MARK:ENEMY:反発刻印 = 1
			CALL COLORMESSAGE(@"虽然%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%身上有了反発刻印、但只要不在别人面前的话无论什么时候都可以按在地上侵犯",C_YELLOW,1)
		ENDIF
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%带着%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%意气风发地回去了
	CASE 3
		PRINTFORML 反瞪着%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的同时、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%握紧了手里的奇迹之石
		PRINTFORML 受到影响的%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%移開視線低下了头
		IF 玩家立绘开关
			CALL PRINT_FACE_PLAYER, MASTER, "發情", "服", "", ""
			PRINTFORMDW 「你也没得选...不是吗❤」
		ENDIF
		CFLAG:ENEMY:体目当て = 1
		ITEM:奇跡之石 --
		ABL:ENEMY:親密 = MAX(3, ABL:ENEMY:親密)
		ABL:ENEMY:欲望 = MAX(3, ABL:ENEMY:欲望)
		CFLAG:ENEMY:积攒度 = 1000
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把手放在了%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%的肩上、命令其今天一天都要服從自己的命令
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%老实地接受了
		CALL COLORMESSAGE(@"只要不在别人面前的话、无论什么时候都可以把%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%按在地上侵犯了",C_YELLOW,1)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%带着%NAME_OUTPUT_TRANSLATION("CALLNAME", ENEMY)%意气风发地回去了
ENDSELECT

FLAG:约会的对象 = TARGET
CFLAG:TARGET:同行 = 180
CFLAG:MASTER:同行 = 180
CFLAG:ENEMY:清醒劑 = 600
CFLAG:ENEMY:拠点来訪 = 1
CFLAG:ENEMY:来訪時間 = 0
CFLAG:ENEMY:帰宅時間 = 1440 + CFLAG:ENEMY:清醒劑
CFLAG:ENEMY:就寝時間 = 1440 + CFLAG:ENEMY:清醒劑
CFLAG:ENEMY:起床時間 = 0
CSTR:ENEMY:路人子プロフィール２ = 惨遭反击之后现在已经老实了

RETURN 1

@LOST_GYAKURE(ARG)
#DIM 性交回数
#DIM 射精回数
性交回数 = MAX(1, CFLAG:ARG:积攒度 / 100)
射精回数 = MAX(1, BASE:MASTER:精力 / 100)
BASE:MASTER:体力 = 0
BASE:MASTER:気力 = 0
;BASE:MASTER:精力 = 0
;射精処理
CALL ADD_SAMEN_V(ARG, BASE:MASTER:精力)
EXP:ARG:精液経験 += 射精回数
EXP:ARG:膣射経験 += 射精回数
EXP:ARG:Ｖ性交経験 += 性交回数
EXP:ARG:Ｖ経験 += 性交回数
EXP:MASTER:絶頂経験 += 射精回数
EXP:MASTER:射精経験 += 射精回数
EXP:MASTER:挿入経験 += 性交回数
CFLAG:ARG:积攒度 = 0
IF TALENT:ARG:処女
	CSTR:ARG:処女喪失履歴 = 发情期间袭击并抛弃了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	TALENT:ARG:処女 = 0
ENDIF
CSTR:ARG:路人子プロフィール２ = 曾将%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%彻底享用殆尽
CALL SET_SLEEP(1,MASTER,0)
CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME + 120 + RAND:120
TFLAG:106 = 0
BEGIN AFTERTRAIN

;IF TIME:2 >= 5 && TIME:2 <= 7 && TIME:5 < 2
;	CALL COLORMESSAGE(@"満月的光芒激发了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的本能…！",C_YELLOW,1)
;	CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的戦闘力提升了！",C_YELLOW,1)
;ENDIF

;モブ娘エンカウント率上昇
@MOB_BATTLE_ENCOUNT
#FUNCTION
#DIM 月の状態
IF FLAG:異常気象 == 8 && TCVAR:RANDOM_CHARANUM:発情
	RETURNF 1
ELSEIF  TCVAR:RANDOM_CHARANUM:願掛け桃色の風
	RETURNF 1
ELSE
	月の状態 = MOON_WATCH(DAY:3)
	;満月
	IF 月の状態 == 9
		SELECTCASE FLAG:抱負
			CASE 6
				;抱負が子孫繁栄
				RETURNF 1
			CASE 9
				;子孫繁栄達成済み
				SIF RAND:5 == 0
					RETURNF 1
		ENDSELECT
		SIF RAND:10 == 0
			RETURNF 1
	;新月
	ELSEIF 月の状態 == 1 && RAND:3 == 0
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0
