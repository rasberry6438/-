;=========================================================
;@GET_BASE_RESOURCE(CHARA, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分)
;	基本画像のリソースを返却する
;		RETURNS:0 にリソース、RETURNS:1 にマウスオーバーリソースをそれぞれ格納
;		CHARAにキャラクタ番号、その他の引数を入力でリソースを指定できる
;		引数を省略または空文字入力で現在のステータスから自動決定
;	リソースがあれば1を返却、無ければ0を返却
;=========================================================
@GET_BASE_RESOURCE_TRANS(CHARA,CHARATRANS, 表示形式 = "", 服装 = "", 表情 = "", 表情_マウスオーバー = "", 差分 = "")
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS 服装
#DIMS 表情
#DIMS 表情_マウスオーバー
#DIMS 差分
#DIMS 乳型 = "絶壁", "貧乳", "並乳", "巨乳", "爆乳"
#DIM CHARA_CSV_乳型
#DIM LOOP_乳型
RESULT = 0

;custom code, EagloV's alt images
VARSET RESULTS ;bug fix, fix reusing previous calls RESULTS
TRYCCALLFORM SPECIAL_BASE_IMAGE_{CHARA}
IF RESULTS != ""
	TRYCCALLFORM %RESULTS%_GET_BASE_RESOURCE(CHARA, 服装, 表情)
	CATCH
		VARSET RESULTS
	ENDCATCH
	RETURN 1
ENDIF
CATCH
ENDCATCH

;表示形式
表示形式 = %GET_BASESTYLE_TRANS(CHARA,CHARATRANS, 表示形式)%
;差分
IF 差分 == ""
	RESULTS:2 = %表情%
    TRYCCALLFORM IMAGE_I{CHARATRANS}_差分(CHARATRANS)
        差分 = %RESULTS%
	CATCH
    ENDCATCH
ENDIF

;服装
IF 服装 == ""
    IF CHECK_NUDE(CHARA)
        服装 = 裸
    ELSE
        服装 = 服
    ENDIF
    ;この関数中の裸判定が変化先のキャラで行われているため特殊服があるキャラで裸が正しく表示できない
    ;逆に言うと変化先のキャラが裸だと元のキャラが服を正しく着られないことになる
    ;そもそも変身後の服装まで正確である必要なくない？というわけでコメントアウト
    ;将来的には着替えられるといいなあ
	;TRYCCALLFORM IMAGE_I{CHARATRANS}_特殊服装(CHARATRANS)
	;	服装 += RESULTS
	;CATCH
	;ENDCATCH
ENDIF
;-------PRINTFORML %CALLNAME:CHARA%の服装：%服装%、表情：%表情%、MO表情：%表情_マウスオーバー%、差分：%差分%を確認
IF FLAG:巨乳差分表示
	CHARA_CSV_乳型 = CSVTALENT(CHARA, GETNUM(TALENT, "胸圍"))
	LOOP_乳型 = TALENT:CHARA:胸圍
	RESULT = 0

	FOR LOCAL, 0, 4
		LOOP_乳型 = MAX(-2, LOOP_乳型)
		LOOP_乳型 = MIN(2, LOOP_乳型)
	
		CALL GET表情体形完整_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳型:(LOOP_乳型+2))
		SIF !RESULT
			CALL GET表情体形拆分组合_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳型:(LOOP_乳型+2))
		
		SIF RESULT || LOOP_乳型==CHARA_CSV_乳型
			BREAK
		
		IF CHARA_CSV_乳型 > LOOP_乳型
			LOOP_乳型++
		ELSE
			LOOP_乳型--
		ENDIF
	NEXT
ELSE
	RESULT = 0
ENDIF

IF !RESULT
	CALL GET表情体形完整_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分)
ENDIF
RETURN RESULT

@GET表情体形完整_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳量 = "")
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS 服装
#DIMS 表情
#DIMS 表情_マウスオーバー
#DIMS 差分
#DIMS 乳量
#DIMS TEMP_服装

TEMP_服装 = %服装%
服装 = %乳量%%服装%
;53行参照 コメントアウト
;TRYCCALLFORM IMAGE_I{CHARATRANS}_体形(CHARATRANS)
;	服装 = %RESULTS%%服装%
;	TEMP_服装 = %RESULTS%%TEMP_服装%
;CATCH
;ENDCATCH

;表情(RESULTSに格納)
IF 表情 == ""
    CALL GET_FACETYPE_TRANS(CHARA,CHARATRANS, 表示形式, TEMP_服装, 差分)
ELSE
    RESULTS:0 = %表情%
    RESULTS:1 = %表情%
ENDIF
SIF 表情_マウスオーバー != ""
    RESULTS:1 = %表情_マウスオーバー%
;PRINTFORML 表情「%RESULTS:0%」を確認

RESULTS:0 = %表示形式%_%服装%_%RESULTS:0%_{CHARATRANS}%差分%
RESULTS:1 = %表示形式%_%服装%_%RESULTS:1%_{CHARATRANS}%差分%

;基本画像のリソースをチェック前に別途合成があるかをチェック
TRYCCALLFORM IMAGE_EX_I{CHARATRANS}_PREリソースチェック(CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳量)
CATCH
ENDCATCH
;リソースのチェック 見つからない場合は0を返す
IF !SPRITECREATED(RESULTS:0)
;	PRINTFORML リソース「%RESULTS:0%」が見つからない
    RETURN 0
ENDIF
IF !SPRITECREATED(RESULTS:1)
    RETURN 0
ENDIF
RETURN 1

@GET表情体形拆分组合_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳量)
#DIM GID
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS 服装
#DIMS 表情
#DIMS 表情_マウスオーバー
#DIMS 差分
#DIMS 乳量
#DIMS SET_RESOURCE = "BASE巨乳拆分", "マウスオーバー巨乳拆分", "BASE", "マウスオーバー", "BASE巨乳", "マウスオーバー巨乳"

SET_RESOURCE:4 = %乳量%%服装%
;53行参照 コメントアウト
;TRYCCALLFORM IMAGE_I{CHARATRANS}_体形(CHARATRANS)
;	服装 = %RESULTS%%服装%
;	SET_RESOURCE:4 = %RESULTS%%SET_RESOURCE:4%
;CATCH
;ENDCATCH

;表情(RESULTSに格納)
IF 表情 == ""
    CALL GET_FACETYPE_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 差分)
	SET_RESOURCE:2 = %RESULTS:0%
	SET_RESOURCE:3 = %RESULTS:1%
ELSE
    SET_RESOURCE:2 = %表情%
    SET_RESOURCE:3 = %表情%
ENDIF
SIF 表情_マウスオーバー != ""
    SET_RESOURCE:3 = %表情_マウスオーバー%

;get 巨乳拆分
SET_RESOURCE:0 = %表示形式%_%服装%_%SET_RESOURCE:2%_%乳量%拆分_{CHARATRANS}%差分%

TRYCCALLFORM IMAGE_EX_I{CHARATRANS}_PREリソースチェック(CHARATRANS, 表示形式, 服装, 表情, 表情_マウスオーバー, 差分, 乳量)
CATCH
ENDCATCH

IF !SPRITECREATED(SET_RESOURCE:0)
	SET_RESOURCE:0 = %表示形式%_%服装%_%乳量%拆分_{CHARATRANS}%差分%
	SIF !SPRITECREATED(SET_RESOURCE:0)
		RETURN 0
ENDIF
SET_RESOURCE:1 = %表示形式%_%服装%_%SET_RESOURCE:3%_%乳量%拆分_{CHARATRANS}%差分%
IF !SPRITECREATED(SET_RESOURCE:1)
	SET_RESOURCE:1 = %表示形式%_%服装%_%乳量%拆分_{CHARATRANS}%差分%
	SIF !SPRITECREATED(SET_RESOURCE:1)
		RETURN 0
ENDIF

;name of combined image
SET_RESOURCE:5 = %表示形式%_%SET_RESOURCE:4%_%SET_RESOURCE:3%_{CHARATRANS}%差分%
SET_RESOURCE:4 = %表示形式%_%SET_RESOURCE:4%_%SET_RESOURCE:2%_{CHARATRANS}%差分%

;get body
SET_RESOURCE:2 = %表示形式%_%服装%_%SET_RESOURCE:2%_{CHARATRANS}%差分%
SET_RESOURCE:3 = %表示形式%_%服装%_%SET_RESOURCE:3%_{CHARATRANS}%差分%

SIF !SPRITECREATED(SET_RESOURCE:2) || !SPRITECREATED(SET_RESOURCE:3)
	RETURN 0

GID = 0
CALL 画像合成(GID, SET_RESOURCE:2)
CALL 画像合成(GID, SET_RESOURCE:0)
CALL リソース登録(SET_RESOURCE:4, GID)

GID = 0
CALL 画像合成(GID, SET_RESOURCE:3)
CALL 画像合成(GID, SET_RESOURCE:1)
CALL リソース登録(SET_RESOURCE:5, GID)

RESULTS:0 = %SET_RESOURCE:4%
RESULTS:1 = %SET_RESOURCE:5%

RETURN 1


;表示形式を返却する
@GET_BASESTYLE_TRANS(CHARA,CHARATRANS, 表示形式 = "")
#FUNCTIONS
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
	
LOCAL = CFLAG:CHARATRANS:差し替え適用

IF 表示形式=="顔絵" || (FLAG:立ち絵種類 && 表示形式=="")
	SIF CFLAG:CHARATRANS:差し替え適用 > CFLAG:CHARATRANS:別顔有
		CFLAG:CHARATRANS:差し替え適用 = 0
	IF CFLAG:CHARATRANS:差し替え適用 && CFLAG:CHARATRANS:別顔有 && HAS_FACE(CHARATRANS)
		表示形式 = %\@CFLAG:CHARATRANS:差し替え適用 >= 2 ?別顔{CFLAG:CHARATRANS:差し替え適用}#別顔\@%
	ELSE
		表示形式 = 顔絵
	ENDIF
ELSEIF 表示形式=="立ち絵" || (!FLAG:立ち絵種類 && 表示形式=="")
	SIF CFLAG:CHARATRANS:差し替え適用 > CFLAG:CHARATRANS:別立ち有
		CFLAG:CHARATRANS:差し替え適用 = 0
	IF CFLAG:CHARATRANS:差し替え適用 && CFLAG:CHARATRANS:別立ち有 && HAS_STANDING(CHARATRANS)
		表示形式 = %\@CFLAG:CHARATRANS:差し替え適用 >= 2 ?別立ち{CFLAG:CHARATRANS:差し替え適用}#別立ち\@%
	ELSE
		表示形式 = 立ち絵
	ENDIF
ENDIF

CFLAG:CHARATRANS:差し替え適用 = LOCAL

RETURNF 表示形式


;裸表示条件を返却する
@CHECK_NUDE_TRANS(CHARA)
#FUNCTION
#DIM CHARA

SIF FLAG:立ち絵種類 && !TEQUIP:CHARA:上半身着衣状況
	RETURNF 1
SIF !FLAG:立ち絵種類 && !TEQUIP:CHARA:上半身着衣状況 && !TEQUIP:CHARA:下半身着衣状況
	RETURNF 1
SIF BATHROOM(CFLAG:CHARA:現在位置) && !TFLAG:泡風呂
	RETURNF 1
RETURNF 0

;=========================================================
;@GET_FACETYPE(CHARA, 表示形式, 服装, 差分 )
;	表情を現在のステータスから自動決定する
;		RETURNS:0 にリソース、RETURNS:1 にマウスオーバーリソースをそれぞれ格納
;=========================================================
@GET_FACETYPE_TRANS(CHARA,CHARATRANS, 表示形式, 服装, 差分)
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS 服装
#DIMS 差分
#DIMS TEMP, 2

IF TALENT:CHARA:機嫌 == -1 || CFLAG:CHARA:ブチギレ
	RESULTS:0 = 憤怒
	LOCAL = -1
ELSEIF TALENT:CHARA:機嫌 == 1 || BASE:CHARA:ムード == MAXBASE:CHARA:ムード || TCVAR:CHARA:泥酔
	RESULTS:0 = 笑顔
	LOCAL = 1
ELSE
	RESULTS:0 = 通常
	LOCAL = 0
ENDIF
SIF CHECK_CHARA(CHARA, "陥落")
	LOCAL ++
SIF BATHROOM(CFLAG:CHARA:現在位置) && 風呂逐出条件(CHARA)
	LOCAL --
SIF MARK:CHARA:反発刻印
	LOCAL --
SIF BASE:CHARA:憤怒 >= 500
	LOCAL --
SIF BASE:CHARA:ムード >= 650
	LOCAL ++
SELECTCASE RESULTS:0
CASE "笑顔"
;	笑顔時は表情が固まらないように
;	IF LOCAL < 0
		RESULTS:1 = 通常
;	ELSE
;		RESULTS:1 = 笑顔
;	ENDIF
CASE "憤怒"
	IF LOCAL > 0
		RESULTS:1 = 通常
	ELSE
		RESULTS:1 = 憤怒
	ENDIF
CASEELSE
	IF LOCAL > 0
		RESULTS:1 = 笑顔
	ELSEIF LOCAL < 0
		RESULTS:1 = 憤怒
	ELSE
		RESULTS:1 = 通常
	ENDIF
ENDSELECT

;表情がなければ通常
SIF !SPRITECREATED(@"%表示形式%_%服装%_%RESULTS:0%_{CHARATRANS}%差分%")
	RESULTS:0 = 通常
SIF !SPRITECREATED(@"%表示形式%_%服装%_%RESULTS:1%_{CHARATRANS}%差分%")
	RESULTS:1 = 通常

;発情用
IF TCVAR:CHARA:発情 && SPRITECREATED(@"%表示形式%_%服装%_発情_{CHARATRANS}%差分%")
	RESULTS:0 = 発情
	RESULTS:1 = 発情
ENDIF

;睡眠用
IF CFLAG:CHARA:睡眠 && SPRITECREATED(@"%表示形式%_%服装%_睡眠_{CHARATRANS}%差分%")
	RESULTS:0 = 睡眠
	RESULTS:1 = 睡眠
ENDIF
IF CFLAG:CHARA:うふふ && GETPALAMLV(PALAM:CHARA:欲情, 10) >= 7 && SPRITECREATED(@"%表示形式%_%服装%_トロ顔_{CHARATRANS}%差分%")
	RESULTS:0 = トロ顔
	RESULTS:1 = トロ顔
	IF NOWEX:CHARA:TotalEX >= 5
		RESULTS:0 = イキ顔
		RESULTS:1 = イキ顔
	ENDIF
ENDIF
;専用の特殊表情変化
TRYCALLFORM IMAGE_I{CHARATRANS}_特殊表情(CHARATRANS)
SIF RESULTS:0 == ""
	RESULTS:0 = 通常
SIF RESULTS:1 == ""
	RESULTS:1 = 通常
;以下コマンドから追加される表情
SIF FLAG:時間停止 || CHARA != TARGET || CFLAG:CHARA:睡眠
	RETURN 0

;現在の表情を一時保存
TEMP:0 = %RESULTS:0%
TEMP:1 = %RESULTS:1%

;挿入中なら性交表情
SIF IS_SEXING(CHARA) && SPRITECREATED(@"%表示形式%_%服装%_性交_{CHARATRANS}%差分%")
	RESULTS:0 = 性交

SELECTCASE SELECTCOM
	;愛撫系
	CASE 0 TO 15
		IF SELECTCOM == 9
			RESULTS:0 = オナニー
		ELSE
			RESULTS:0 = 愛撫
		ENDIF
	;性交系
	CASE 60 TO 79, 317 TO 323
		RESULTS:0 = 性交
	;カウンターまたは日常系愛撫
	CASE 313 TO 316
		IF CFLAG:CHARA:うふふ
			RESULTS:0 = 愛撫
		ELSEIF MARK:CHARA:不埒刻印 == 3
			RESULTS:1 = 愛撫
		ENDIF
ENDSELECT
;PRINTFORML 表情チェック　%表示形式%_%服装%_%RESULTS:0%_{CHARATRANS}%差分%
;表情がなければ戻す
SIF !SPRITECREATED(@"%表示形式%_%服装%_%RESULTS:0%_{CHARATRANS}%差分%")
	RESULTS:0 = %TEMP:0%
SIF !SPRITECREATED(@"%表示形式%_%服装%_%RESULTS:1%_{CHARATRANS}%差分%")
	RESULTS:1 = %TEMP:1%

;発情時2/3でマウスオーバーと表情交代
SIF RAND:3 && TCVAR:CHARA:発情 && SPRITECREATED(@"%表示形式%_%服装%_発情_{CHARATRANS}%差分%")
	SWAP RESULTS:0, RESULTS:1

;専用の特殊表情変化
TRYCALLFORM IMAGE_I{CHARATRANS}_特殊表情COM(CHARATRANS, SELECTCOM)

;性交中判定
@IS_SEXING_TRANS(ARG)
#FUNCTION

SIF !ARG
	ARG = TARGET
SIF TEQUIP:ARG:Ｖ接触部位 == MASTER || TEQUIP:ARG:Ａ接触部位 == MASTER || TEQUIP:MASTER:Ｖ接触部位 == ARG || TEQUIP:MASTER:Ａ接触部位 == ARG
	RETURNF 1
RETURNF 0

;=========================================================
;@GET_EFFECT_RESOURCE_TRANS(CHARA,CHARATRANS, エフェクト, 表示形式, 差分)
;	エフェクト画像のリソースを返却する
;		RETURNS配列 にリソースをそれぞれ格納
;		CHARAにキャラクタ番号、エフェクトに入力
;		引数を省略または空文字入力で現在のステータスから自動決定
;	リソースがあればエフェクト数を返却、無ければ0を返却
;=========================================================
@GET_EFFECT_RESOURCE_TRANS(CHARA,CHARATRANS, エフェクト = "", 表示形式 = "", 差分 = "")
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS エフェクト
#DIMS 差分
#DIM DYNAMIC エフェクト数 = 0
#DIMS DYNAMIC リソース, 100
#DIMS DYNAMIC エフェクト名

;PRINTFORML エフェクトが走ってるよ

SIF !FLAG:追加エフェクト
	RETURN 0
VARSET RESULTS

;custom code, EagloV's alt images
TRYCCALLFORM SPECIAL_EFFECT_IMAGE_{CHARATRANS}
IF RESULTS != ""
	TRYCCALLFORM %RESULTS%_GET_EFFECT_RESOURCE(CHARATRANS, エフェクト)
	CATCH
		VARSET RESULTS
	ENDCATCH
	RETURN RESULT
ENDIF
CATCH
ENDCATCH


表示形式 = %GET_BASESTYLE_TRANS(CHARA,CHARATRANS, 表示形式)%
;53行参照
;IF 差分 == ""
;	TRYCCALLFORM IMAGE_I{CHARATRANS}_差分(CHARATRANS)
;		差分 = %RESULTS%
;	CATCH
;	ENDCATCH
;ENDIF

;照れ
IF STRFIND(エフェクト, "照れ") != -1 || (エフェクト == "" && CHECK_BLUSH_TRANS(CHARA,CHARATRANS, 表示形式, 差分))
	エフェクト名 = 照れ
	TRYCCALLFORM IMAGE_I{CHARATRANS}_特殊照れ(CHARATRANS)
		エフェクト名 '= RESULTS + エフェクト名
	CATCH
	ENDCATCH
	SIF (CHECK_NUDE(CHARA) || STRFIND(エフェクト, "照れ") != -1) && SPRITECREATED(@"%表示形式%_裸%エフェクト名%_{CHARATRANS}%差分%")
		エフェクト名 '= "裸" + エフェクト名
	IF SPRITECREATED(@"%表示形式%_%エフェクト名%_{CHARATRANS}%差分%")
		リソース:(エフェクト数) = %表示形式%_%エフェクト名%_{CHARATRANS}%差分%
		エフェクト数 += 1
	ENDIF
ENDIF

;吐息
IF STRFIND(エフェクト, "吐息") != -1 || (エフェクト == "" && CHECK_EXHALATION(CHARA))
	エフェクト名 = 吐息
	TRYCCALLFORM IMAGE_I{CHARATRANS}_特殊吐息(CHARATRANS)
		エフェクト名 '= RESULTS + エフェクト名
	CATCH
	ENDCATCH
	SIF (CHECK_NUDE(CHARA) || STRFIND(エフェクト, "吐息") != -1) && SPRITECREATED(@"%表示形式%_裸%エフェクト名%_{CHARATRANS}%差分%")
		エフェクト名 '= "裸" + エフェクト名
	IF SPRITECREATED(@"%表示形式%_%エフェクト名%_{CHARATRANS}%差分%")
		リソース:(エフェクト数) = %表示形式%_%エフェクト名%_{CHARATRANS}%差分%
		エフェクト数 += 1
	ENDIF
ENDIF

;汗
IF STRFIND(エフェクト, "汗") != -1 || (エフェクト == "" && CHECK_SWEAT(CHARA))
	エフェクト名 = 汗
	TRYCCALLFORM IMAGE_I{CHARATRANS}_特殊汗(CHARA)
		エフェクト名 '= RESULTS + エフェクト名
	CATCH
	ENDCATCH
	SIF (CHECK_NUDE(CHARA) || STRFIND(エフェクト, "汗") != -1) && SPRITECREATED(@"%表示形式%_裸%エフェクト名%_{CHARATRANS}%差分%")
		エフェクト名 '= "裸" + エフェクト名
	IF SPRITECREATED(@"%表示形式%_%エフェクト名%_{CHARATRANS}%差分%")
		リソース:(エフェクト数) = %表示形式%_%エフェクト名%_{CHARATRANS}%差分%
		エフェクト数 += 1
	ENDIF
ENDIF

;特殊
RESULT = 0
TRYCALLFORM IMAGE_I{CHARATRANS}_特殊エフェクトチェック(CHARATRANS, エフェクト)
IF RESULT || エフェクト == ""
	TRYCALLFORM IMAGE_I{CHARATRANS}_特殊エフェクト(CHARATRANS)
	FOR LOCAL, 0, RESULT
		IF SPRITECREATED(@"%表示形式%_%RESULTS:LOCAL%_{CHARATRANS}%差分%")
			リソース:(エフェクト数) = %表示形式%_%RESULTS:LOCAL%_{CHARATRANS}%差分%
			エフェクト数 += 1
		ENDIF
	NEXT
ENDIF
;PRINTFORML 汎用エフェクトチェックが走ってるよ
IF CSTR:(CHARATRANS):汎用エフェクト != ""
	;PRINTFORML エフェクト「汎用エフェクト_%CSTR:CHARATRANS:汎用エフェクト%」を確認
	IF SPRITECREATED(@"汎用エフェクト_%CSTR:(CHARATRANS):汎用エフェクト%")
		;PRINTFORML あったよ
		リソース:(エフェクト数) = 汎用エフェクト_%CSTR:(CHARATRANS):汎用エフェクト%
		エフェクト数 += 1
	ENDIF
ENDIF

ARRAYCOPY "リソース", "RESULTS"
RETURN エフェクト数

;照れの条件
@CHECK_BLUSH_TRANS(CHARA,CHARATRANS, 表示形式, 差分)
#FUNCTION
#DIM CHARA
#DIM CHARATRANS
#DIMS 表示形式
#DIMS エフェクト
#DIMS 差分

SIF SPRITECREATED(@"%表示形式%_裸_発情_{CHARATRANS}%差分%") && TCVAR:CHARA:発情
	RETURNF 0

SIF TCVAR:CHARA:泥酔 || TCVAR:CHARA:媚薬 || TCVAR:CHARA:発情
	RETURNF 1
SIF GETPALAMLV(PALAM:CHARA:好意, 15) >= 10
	RETURNF 1
SIF GETPALAMLV(PALAM:CHARA:欲情, 15) + CFLAG:CHARA:溜まってる度 / 200 >= 8
	RETURNF 1
SIF CFLAG:CHARA:うふふ && (GETPALAMLV(PALAM:CHARA:欲情, 15) + CFLAG:CHARA:溜まってる度 / 200 >= 5)
	RETURNF 1
RETURNF 0

;=========================================================
;	リソースの表示2
;	表示後サイズに合わせて改行を行う
;	PRINT_IMG の拡張みたいな感じ　文中の挿絵的に使えると思う
;
;	sRes_Name	：リソースの名前文字列
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_OVERRAP(sRes_Name,sRes_Name_2,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIMS sRes_Name_2
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag

SIF GETCONFIGS("描画インターフェース") == "WINAPI"
	THROW 描画インターフェースをTextRendererに変更して下さい

SIF !FLAG:画像表示 || sRes_Name == "" || sRes_Name_2 == ""
	RETURN RESULT

iEnter_Con = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5?1#0)

SIF SPRITECREATED(sRes_Name)
	HTML_PRINT @"<p align='%sAlign%'><nobr><img src='%sRes_Name%' height='{iFont_Hei_mag}'>"
iEnter_Con++
SIF SPRITECREATED(sRes_Name_2)
	HTML_PRINT @"<p align='%sAlign%'><nobr><img src='%sRes_Name_2%' height='{iFont_Hei_mag}' ypos='-{iEnter_Con * iFont_Hei_mag / 10}'>"
iEnter_Con++
IF FLAG:時間停止
	sPrint_Text =  <p align='%sAlign%'><nobr>
	sPrint_Text += @"<img src='時間停止' height='{iFont_Hei_mag}' width='{iFont_Hei_mag +20}'"
	sPrint_Text += @"ypos='-{iEnter_Con * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF

IF FLAG:画像枠表示
	sPrint_Text =  <p align='%sAlign%'><nobr>
	sPrint_Text += @"<img src='フレーム_通常' height='{iFont_Hei_mag}'"
	sPrint_Text += @"ypos='-{iEnter_Con * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF


FOR LOCAL,iEnter_Con-1,(iSize/100)-1
	HTML_PRINT "<br>"
NEXT
RETURN RESULT

;=========================================================
;	連番画像アニメーション
;
;	sRes_Name	：リソースの名前文字列　1番目のキャラ
;	sRes_Name_2	：リソースの名前文字列　2番目のキャラ
;	sRes_Name_3	：リソースの名前文字列　アニメーションするエフェクト　この名前の後に連番数字を付ける
;	iStart		：開始番号
;	iEnd		：開始からの描画枚数
;	iAnime_Num	：全体のアニメーション繰り返し回数 省略0でオプション設定したフラグ回数になります
;	iChenge		：sRes_Nameを表示するフレーム
;	iWait		：1枚の表示時間 デフォルトで100ms(10fps)
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_TRANSANIMATION(sRes_Name,sRes_Name_2,sRes_Name_3,iStart=0,iEnd=16,iAnime_Num=1,iChenge=4,iWait=100,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIMS sRes_Name_2
#DIMS sRes_Name_3
#DIM iStart
#DIM iEnd
#DIM iAnime_Num
#DIM iChenge
#DIM iWait
#DIM iSize
#DIM txtCount
#DIM TEMP
#DIMS sAlign

SIF !FLAG:画像表示 || sRes_Name == "" || sRes_Name_2 == "" || sRes_Name_3 == ""
	RETURN RESULT

txtCount = 0
TEMP = CURRENTREDRAW()
REDRAW 0

CALL PRINT_IMAGE(sRes_Name)
PRINTFORML
TWAIT iWait, 0
CLEARLINE (iSize/100)+1

FOR LOCAL,0,iAnime_Num
	FOR LOCAL:1,iStart,(iStart+iChenge)
		CALL PRINT_IMAGE_OVERRAP(sRes_Name,@"%sRes_Name_3%{LOCAL:1}",iSize,sAlign)
		PRINTFORML ぼふんっ.%"." * txtCount%
		SIF LOCAL:1 % 3 == 0
			txtCount++
		TWAIT iWait, 0
		CLEARLINE (iSize/100)+1
	NEXT
	

	FOR LOCAL:1,(iStart+iChenge),(iStart+iEnd)
		CALL PRINT_IMAGE_OVERRAP(sRes_Name_2,@"%sRes_Name_3%{LOCAL:1}",iSize,sAlign)
		PRINTFORML ぼふんっ.%"." * txtCount%
		SIF LOCAL:1 % 3 == 0
			txtCount++
		TWAIT iWait, 0
		CLEARLINE (iSize/100)+1
	NEXT
NEXT
REDRAW TEMP

RETURN RESULT
