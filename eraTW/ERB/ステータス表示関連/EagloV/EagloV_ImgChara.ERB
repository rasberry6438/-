

; 72 永琳{
@SPECIAL_BASE_IMAGE_72
SIF CFLAG:72:差し替え適用 == 2
	RESULTS = EagloV2

@SPECIAL_EFFECT_IMAGE_72
SIF CFLAG:72:差し替え適用 == 2
	RESULTS = EagloV2

@EagloV_特殊服装_72
RESULTS = 
IF IS_BUNNY(72)
	RESULTS = 兔女郎
	RETURN 1
ELSEIF CFLAG:72:妊娠経過日数 >= 60
	RESULTS = 孕装
	RETURN 1
ELSEIF YOUBI_MATCH(DAY, "月火金")
	; 工作日，常服
	RESULTS = 
	RETURN 0
ELSEIF DAY%2 && ABL:72:露出癖 >= 3
	; 休息日。露出癖3，1/2概率衬衫；露出癖5，1/10概率裸衬衫
	IF (EQUIP:72:上半身内衣１ + EQUIP:72:上半身内衣２ == 0) || (DAY%5 == 0 && ABL:72:露出癖 >= 5)
		RESULTS = 裸衬衫
	ELSE
		RESULTS = 半透衫
	ENDIF
	RETURN 1
ENDIF
RETURN 0

@EagloV_特殊效果_72
IF EagloV_服装初步判定(72) != "服"
	RESULTS:0 = EagloV72_特效_呆毛
	RESULTS:1 = 
	RETURN 1
ENDIF
RESULTS = 
CALL EagloV_特殊服装_72
SELECTCASE RESULTS
CASE "孕装", "裸衬衫", "半透衫"
	RESULTS:0 = EagloV72_特效_呆毛
	RESULTS:1 = 
	RETURN 1
ENDSELECT
;}

; 61 妹红{
@SPECIAL_BASE_IMAGE_61
SIF CFLAG:61:差し替え適用 == 2
	RESULTS = EagloV

@SPECIAL_EFFECT_IMAGE_61
SIF CFLAG:61:差し替え適用 == 2
	RESULTS = EagloV

@EagloV_特殊效果_61
IF !CFLAG:61:诶嘿嘿 && !CFLAG:61:睡眠 && CFLAG:61:妊娠経過日数 < 20 && TALENT:61:心情 < 0 && SPRITECREATED("EagloV61_特效_香烟")
	RESULTS:0 = EagloV61_特效_香烟
	RESULTS:1 = 
	RETURN 1
ENDIF
;}

; 11 魔理沙{
@SPECIAL_BASE_IMAGE_11
SIF CFLAG:11:差し替え適用 == 3
	RESULTS = EagloV

@SPECIAL_EFFECT_IMAGE_11
SIF CFLAG:11:差し替え適用 == 3
	RESULTS = EagloV

; RETURN：0:无，1:胸罩胖次，2:连体内衣
@EagloV_特殊内衣_11
IF TALENT:11:胸圍 <= 0 && CHARISMA_TENDENCY:(NOW_PERIOD()) >= 6
	RESULTS = 創口貼
	RETURN 1
ENDIF
RETURN 0
;}

; 49 觉{
@SPECIAL_BASE_IMAGE_49
SIF CFLAG:49:差し替え適用 == 3
	RESULTS = EagloV

@SPECIAL_EFFECT_IMAGE_49
SIF CFLAG:49:差し替え適用 == 3
	RESULTS = EagloV

; 需要有图片 "EagloV表情_zun颜_49"
@EagloV_特殊表情_49
SIF CFLAG:49:诶嘿嘿 || FLAG:時間停止 || CFLAG:49:睡眠 || CFLAG:49:ブチギレ
	RETURN 0

IF TALENT:49:心情 > 0 && RAND:6 == 0
	RESULTS:0 = zun颜
	RETURN 1
ENDIF
IF GETPALAMLV(PALAM:49:欲情, 8) >= 8 && CFLAG:49:积攒度 < 200 && RAND:2
	RESULTS:0 = zun颜
	RETURN 1
ENDIF

; RETURN：0:无，1:胸罩胖次，2:连体内衣
@EagloV_特殊内衣_49
IF EagloV49_内衣創口貼()
	RESULTS = 創口貼
	RETURN 1
ENDIF

IF TALENT:49:胸圍 <= -1 && DAY%7 == 2
	RESULTS = 运动
	RETURN 1
ENDIF

@EagloV49_内衣創口貼()
#FUNCTION
RETURNF TALENT:49:胸圍 <= -1 && CHARISMA_TENDENCY:(NOW_PERIOD()) >= 6 && DAY%2 && ABL:49:露出癖 >= 3

@EagloV_特殊服装_49
#DIM G_ID
SIF TEQUIP:49:上半身裸露状態 <= 0 || TEQUIP:49:上衣脱衣完毕 != 0
	RETURN 0
SIF TALENT:49:胸圍 > 0
	RETURN 0
SIF EagloV_妊肚判定(49) != ""
	RETURN 0
SIF !SPRITECREATED("EagloV49_服躯_半脱") && SPRITECREATED("EagloV49_胖次")
	RETURN 0

IF (EQUIP:49:下半身内衣２ > 0 || EQUIP:49:下半身内衣１ > 0) && !EagloV49_内衣創口貼()
	IF !SPRITECREATED("EagloV49_服躯_有pan半脱")
		G_ID = 0
		CALL 画像合成(G_ID, "ダミー", 0, 0, 300, 300)
		CALL 画像合成(G_ID, "EagloV49_胖次")
		CALL 画像合成(G_ID, "EagloV49_服躯_半脱")
		SPRITECREATE "EagloV49_服躯_有pan半脱", G_ID
	ENDIF
	RESULTS = 有pan半脱
	RETURN 1
ELSE
	RESULTS = 半脱
	RETURN 1
ENDIF

@EagloV_特殊效果_49
; 3rd眼
IF CFLAG:49:睡眠
	LOCALS = EagloV49_特效_3rd闭目
ELSEIF (FLAG:時間停止 && EX:49:無自覚Ｃ絶頂 + EX:49:無自覚Ｖ絶頂 + EX:49:無自覚Ａ絶頂 + EX:49:無自覚Ｂ絶頂 + EX:49:無自覚Ｍ絶頂 > 30) || (TCVAR:49:烂醉 && BASE:49:酒気 > MAXBASE:49:酒気 * 2/3)
	LOCALS = EagloV49_特效_3rd啊嘿
ELSEIF FLAG:時間停止
	LOCALS = EagloV49_特效_3rd睁目
ELSEIF NOWEX:49:TotalEX > 2 
	LOCALS = EagloV49_特效_3rd啊嘿
ELSEIF TCVAR:49:烂醉
	LOCALS = EagloV49_特效_3rd闭目
ELSEIF BASE:49:憤怒 >= 900
	LOCALS = EagloV49_特效_3rd暗眼
ELSEIF CFLAG:49:心情不快 > 0
	LOCALS = EagloV49_特效_3rd闭目
ELSEIF (TALENT:49:恋慕 || TALENT:49:愛欲) && (CFLAG:49:积攒度 > 800 || CFLAG:49:诶嘿嘿 && GETPALAMLV(PALAM:49:欲情, 6) >= 6)
	IF ABL:49:親密 + RAND:(ABL:49:親密/2+1) > ABL:49:欲望
		LOCALS = EagloV49_特效_3rd心目
	ELSE
		LOCALS = EagloV49_特效_3rd暗心目
	ENDIF
ELSE
	LOCALS = EagloV49_特效_3rd睁目
ENDIF

; 3rd眼气泡
IF RAND:4 && !FLAG:時間停止
	CALL EagloV_气泡判定(49)
	IF !SPRITECREATED(@"EagloV49_特效_3rd气泡_%RESULTS%")
		RESULTS:0 = %LOCALS%
		RESULTS:1 = 
		RETURN 1
	ELSE
		RESULTS:0 = EagloV49_特效_3rd气泡_%RESULTS%
		RESULTS:1 = 
		RESULTS:2 = %LOCALS%
		RESULTS:3 = 
		RETURN 2
	ENDIF
ENDIF

RESULTS:0 = %LOCALS%
RESULTS:1 = 
RETURN 1
;}


