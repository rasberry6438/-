;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE366
SIF !TFLAG:100
	RETURN 0
SIF TARGET <= 0
	RETURN 0
;時間停止/睡眠/添い寝/诶嘿嘿中は不可
SIF SHIRAHU(TARGET) != 1
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(366)
	RETURN RESULT

SELECTCASE IRAI_CHARA_TO_PROGRESS(TARGET)
CASE "依頼未受託"
;空きスロットが無いなら不可
	SIF !SEARCH_IRAI_EMPTY()
		RETURN 0
;個々に指定された条件をみたさない時
	SIF !INT_DATA_IRAI(CFLAG:TARGET:依頼内容, "依頼受託可能条件", TARGET)
		RETURN 0
CASE "依頼中"
;依頼確認/破棄が可能
CASE "依頼達成"
;成功報告可能
CASE "期限切れ", "報告忘れ", "依頼失敗"
;失敗報告
CASE "依頼終了（成功）", "依頼終了（期限切れ）", "依頼終了（報告忘れ）", "依頼終了（失敗）"
;ここは依頼フラグの消滅待ちなんで触らない
		RETURN 0
CASEELSE
	;魔改内容，诱导逆推
	SIF CFLAG:TARGET:依頼状況 == 0 && GETBIT(诱导逆推_进程:TARGET:0, 0) && 诱导逆推_开关
		RETURN 1

		RETURN 0
ENDSELECT
RETURN 1


@COM366
;魔改内容，诱导逆推
IF CFLAG:TARGET:依頼状況 == 0 && GETBIT(诱导逆推_进程:TARGET:0, 10)
	CALL 诱导逆推_强行结束(TARGET)
	RETURN RESULT
ENDIF
IF CFLAG:TARGET:依頼状況 == 0 && GETBIT(诱导逆推_进程:TARGET:0, 0)
	CALL 诱导逆推_询问(TARGET)
	RETURN RESULT
ENDIF

SELECTCASE IRAI_CHARA_TO_PROGRESS(TARGET)
CASE "依頼未受託"
	CALL IRAI_START(TARGET)
	RETURN RESULT
CASE "依頼中"
	CALL IRAI_LOOK(TARGET)
	RETURN RESULT
CASE "依頼達成", "期限切れ", "報告忘れ", "依頼失敗"
	CALL IRAI_REPORT(TARGET)
	RETURN 1
ENDSELECT



;-------------------------------------------------
;依頼受託
;21/08/25 受諾時に一部依頼内容を表示するように変更
;-------------------------------------------------
@IRAI_START(CHARA)
#DIM CHARA
#DIM 受諾		;受託依頼が入ったスロット番号
#DIM 依頼ＩＤ	;依頼自体のID
;依頼提示時
PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%好像有事想要拜託%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "依頼提示時")
CALL SHOW_IRAI_DATA(CFLAG:CHARA:依頼内容, 0)
CALL ASK_YN("接受委託", "拒絕")
IF RESULT
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%婉言拒绝了
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "依頼非受託時")
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%看起來很可惜的樣子
	RETURN -1
ELSE
	受諾 = SEARCH_IRAI_EMPTY()
	CALL SET_IRAI_SLOT(CHARA)
	依頼ＩＤ = (CFLAG:CHARA:依頼内容) % 1000
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%接受了%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的委託
	SELECTCASE IRAI_SLOT_TO_TASKNAME(受諾)
		CASE "伝令（人物）"
			;固有依頼は提示済なのでカット
			SIF 依頼ＩＤ < 100
				PRINTFORML 得到了一封写给%NAME_OUTPUT_TRANSLATION("CALLNAME", (IRAI_SLOT_TO_PERSON(受諾)))%的信
		CASE "捜索（人物）"
			IF 依頼ＩＤ == 9
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%将失物递过来了
				PRINTFORM 物品的特徴：
			ELSE
				PRINTFORM 线索：
			ENDIF
			CALL SHOW_IRAI_HINT("捜索（人物）", IRAI_SLOT_TO_PERSON(受諾), IRAI_SUB:受諾)
		CASE "捜索（場所）"
			PRINTFORM 线索：
			CALL SHOW_IRAI_HINT("捜索（場所）", IRAI_SLOT_TO_PLACE(受諾), IRAI_SUB:受諾)
		CASE "討伐"
			PRINTFORML 弾幕勝負的对手是%NAME_OUTPUT_TRANSLATION("CALLNAME", (IRAI_SLOT_TO_PERSON(受諾)))%
		CASE "其他"
			IF 依頼ＩＤ == 17
				DUMPRAND
				RANDOMIZE IRAI_SUB:受諾
				LOCALS:0 = %IRAI_一般17_味設定(CHARA)%
				REPLACE LOCALS:0, "/", "、"
				INITRAND
				PRINTFORML 大概是一道有%RESULTS:0%味道的菜肴
				VARSET LOCALS
				RESULTS:0 =
			ELSEIF 依頼ＩＤ == 18
				CALL IRAI_CARRYOUT(CHARA, 依頼ＩＤ, "依頼条件表示")
			ENDIF
	ENDSELECT
	WAIT
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "依頼受託時")
	RETURN 1
ENDIF



;-------------------------------------------------
;依頼確認/破棄
;-------------------------------------------------
@IRAI_LOOK(CHARA)
#DIM CHARA
#DIM CHK_SLOT
CHK_SLOT = SEARCH_IRAI_SLOT(CFLAG:CHARA:依頼内容)
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "依頼確認時")
CALL SHOW_IRAI_DATA(CFLAG:CHARA:依頼内容, CHK_SLOT)
CALL ASK_YN("確認", "破棄")
SIF !RESULT
	RETURN -1
PRINTFORML 真的麼？
CALL ASK_YN
SIF RESULT
	RETURN -1
CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "依頼破棄時")
PRINTFORMW 放棄了委託。
CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼終了（失敗）")



;-------------------------------------------------
;依頼報告
;-------------------------------------------------
@IRAI_REPORT(CHARA)
#DIM CHARA
#DIM CHK_SLOT
CHK_SLOT = SEARCH_IRAI_SLOT(CFLAG:CHARA:依頼内容)
;依頼成功時
;依頼失敗時
SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "依頼達成"
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "成功報告時")
	CALL IRAI_CARRYOUT(CHARA, CFLAG:CHARA:依頼内容, "依頼成功時")
CASE "期限切れ", "報告忘れ", "依頼失敗"
	CALL SHOW_IRAI_MESSAGE(CHARA, CFLAG:CHARA:依頼内容, "失敗報告時")
	CALL IRAI_CARRYOUT(CHARA, CFLAG:CHARA:依頼内容, "依頼失敗時")
ENDSELECT

;乐谱入手処理
SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "依頼達成"
	RESULT = 乐谱入手_特殊条件(乐谱入手方法：妖精依頼成功)
	PRINTL
	SELECTCASE CHARA
	CASE 5,6,7,13,14
		RESULT = 乐谱入手_特殊条件(乐谱入手方法：妖精依頼成功)
	CASE 29
		RESULT = 乐谱入手_特殊条件(乐谱入手方法：文依頼成功)
	CASE 42
		RESULT = 乐谱入手_特殊条件(乐谱入手方法：極依頼成功)
	ENDSELECT
CASE "依頼失敗"
	IF (RAND:(100) < 30)
		PRINTL
		RESULT = 乐谱入手_特殊条件(乐谱入手方法：依賴失敗時)
	ENDIF
ENDSELECT

SELECTCASE IRAI_CHARA_TO_PROGRESS(CHARA)
CASE "依頼達成"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼終了（成功）")
CASE "期限切れ"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼終了（期限切れ）")
CASE "報告忘れ"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼終了（報告忘れ）")
CASE "依頼失敗"
	CALL CLEAR_IRAI_SLOT(CHARA, CHK_SLOT, "依頼終了（失敗）")
ENDSELECT
