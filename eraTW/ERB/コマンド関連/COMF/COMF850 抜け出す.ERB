;摆脱
@COM850
#DIM なだめ判定
#DIM 抜け出せない
なだめ判定 = 0
抜け出せない = 0
IF TCVAR:随便
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%手忙脚乱的挣扎着
	IF RAND:100 < RAND:(50 + ABL:施虐属性 * 20 + ABL:戦闘能力 * 20) 
		PRINTFORMW 但是徒劳告终
		CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,3,1)
		DOWNBASE:MASTER:気力 += 50
		DOWNBASE:MASTER:体力 += 50
		SOURCE:加虐 += 100
		TCVAR:嗜虐フラグ = 1
		TIME += 5
		RETURN 1
	ENDIF
ELSEIF TFLAG:泡風呂
	CALL 泡風呂終了判定("攻守交代時終了")
ELSEIF TFLAG:抱住模式
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%推開了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
	;其他
	IF CFLAG:TARGET:性格傾向 == 5 || !FLAG:Counter地文控制
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%非常不満…
	;気弱系
	ELSEIF CFLAG:TARGET:性格傾向 == 1
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%看上去有些難過…
	;強気系
	ELSEIF CFLAG:TARGET:性格傾向 == 2
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%発出了冷笑…
	;坦率系
	ELSEIF CFLAG:TARGET:性格傾向 == 3
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%很高興%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的反応…
	;真面目系
	ELSEIF CFLAG:TARGET:性格傾向 == 4
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%說著對不起…
	ENDIF
ELSE
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:LOCAL:积攒度 >= 500
			なだめ判定 = LOCAL
			BREAK
		ENDIF
	NEXT
	IF !なだめ判定
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%一边抚慰着%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%\@ GET_TARGETNUM() > 1 ? 她们 # \@一边拔了出来
	ELSE
		IF (TALENT:MASTER:性別 & 1) && (TALENT:性別 & 1) && TIME_PROGRESS(TFLAG:被逆推開始時間) >= 360
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", なだめ判定)%儘管還是有些不滿足但還是放過了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
			TFLAG:抱住模式 = 0
			TCVAR:抱きつき禁止時間 = 70 + MAX(10 - ABL:欲望, 0) * 3 - MAX((CFLAG:積極性 + CFLAG:害羞) * 5, 30)
		ELSEIF !TALENT:MASTER:非童貞 && TALENT:MASTER:2 & 2
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", なだめ判定)%苦苦哀求著答應
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", なだめ判定)%無論如何都想要%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的第一次
			CFLAG:なだめ判定:积攒度 += 50
			BASE:なだめ判定:気力 -= 200
			TIME += 5
			RETURN 1
		ELSEIF TALENT:MASTER:処女 || TALENT:なだめ判定:処女
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", なだめ判定)%有些難為情的放過了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
			BASE:なだめ判定:気力 -= 100
			BASE:なだめ判定:情緒 = 0
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", なだめ判定)%還沒有打算放過%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的樣子
			CALL KOJO_MESSAGE_SEND("EVENT",10,なだめ判定,3,2)
			DOWNBASE:MASTER:気力 += 100
			DOWNBASE:MASTER:体力 += 50
			TIME += 5
			RETURN 1
		ENDIF
	ENDIF
ENDIF

CALL 被逆推終了

@被逆推終了(ARGS)

FOR LOCAL,0,CHARANUM
	IF CFLAG:LOCAL:诶嘿嘿 == 2 || LOCAL == TARGET
		IF ARGS == "満足"
			CALL KOJO_MESSAGE_SEND("EVENT",30,LOCAL,2)
		ELSEIF ARGS == "疲惫"
			CALL KOJO_MESSAGE_SEND("EVENT",30,LOCAL,1)
		;被逆推モード終了
		ELSEIF CFLAG:LOCAL:诶嘿嘿 == 2 && LOCAL != 0 && !TFLAG:抱住模式 && CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置
			CALL KOJO_MESSAGE_SEND("EVENT",10,LOCAL,2,0)
		;抱住模式満足終了
		ELSEIF TFLAG:抱住模式 && TARGET == LOCAL
			CALL KOJO_MESSAGE_SEND("EVENT",10,LOCAL,2,1)
		;抱住模式強制終了
		ELSEIF TFLAG:102 == 1 && TARGET == LOCAL
			CALL KOJO_MESSAGE_SEND("EVENT",10,LOCAL,2,2)
		ENDIF
		SIF CFLAG:LOCAL:诶嘿嘿 == 2
			CALL SET_DERAY(5,LOCAL)
		BASE:LOCAL:情緒 = BASE:LOCAL:情緒 / 2
		BASE:LOCAL:理性 = MIN(BASE:LOCAL:理性 + 300, MAXBASE:LOCAL:理性)
		CFLAG:LOCAL:诶嘿嘿 = 0
		;ウフフ時系装備リセット
		CALL ENDUFUFU(LOCAL)
	ENDIF
NEXT
TFLAG:102 = 1
TFLAG:106 = 0
;MASTERとの接触位置をリセット
CALL TOUCH_SET(0,0,0,1)
[SKIPSTART]
;はだけフラグをオフ
TEQUIP:2 = 0
TEQUIP:上半身裸露状態 = 0
;角色全員の服再セッティング
FOR LOCAL,0,CHARANUM
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	;上衣脱衣完毕フラグをオフ
	TEQUIP:LOCAL:5 = 0
NEXT
[SKIPEND]

TIME += 5
RETURN 1
