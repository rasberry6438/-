;顔絵選択・生成
@MIZUKO_SETUP
#DIM GID
#DIM L_FEED
#DIM 合成数
#DIMS 生成文字列
#DIMS 合成文字列配列, 50
#DIMS 色合成
;作成済or顔絵無しなら返回
IF SPRITECREATED("水子_服_通常") || 受付水子画像文字列:0 == "顔絵無"
	RETURN
;初回選択
ELSEIF 受付水子画像文字列:0 == ""
	;PRINTFORML %STONE_UKETSUKENAME%に顔絵は
	;CALL ASK_YN("いる", "いらない")
	PRINTFORML %STONE_UKETSUKENAME%的顔絵
	CALL ASK_YN("显示", "不显示")
	IF RESULT
		受付水子画像文字列:0 = 顔絵無
		RETURN
	ENDIF
	REDRAW 0
	CLEARLINE 1
	;PRINTFORML マウスオーバーで表情確認
	PRINTFORML 鼠标悬停确认表情
	SIF FLAG:多尺寸
		默认角色画像横幅 = 180
	L_FEED = LINECOUNT
	WHILE 1
		CALL 路人店員画像作成(27, "水子")
		CALL 画像セット("顔絵_服_通常_MOB_27", 0, 0, 角色画像表示尺寸比, 0, 1, "100", STONE_UKETSUKENAME, "顔絵_服_笑顔_MOB_27")
		CALL 画像一斉表示(0, 0, 1, -1)
		;PRINT [0]気に入った　　
		;PRINTL [1]気に入らない
		PRINT [0]喜欢（似乎周目内无法更换，请确认真的喜欢再确定）　　
		PRINTL [1]不喜欢
		INPUT
		SIF !RESULT
			BREAK
		CLEARLINE LINECOUNT - L_FEED
		SPRITEDISPOSE "顔絵_服_通常_MOB_27"
		SPRITEDISPOSE "顔絵_服_笑顔_MOB_27"
		SPRITEDISPOSE "顔絵_服_憤怒_MOB_27"
		SPRITEDISPOSE "顔絵_裸_通常_MOB_27"
		SPRITEDISPOSE "顔絵_裸_笑顔_MOB_27"
		SPRITEDISPOSE "顔絵_裸_憤怒_MOB_27"
		FOR COUNT, 0, 6
			GDISPOSE 路人子店員GID:27:COUNT
			路人子店員GID:27:COUNT = 0
			路人子店員画像文字列:27:COUNT = 
		NEXT
	WEND
	REDRAW 1
	;PRINTFORMW 顔絵を設定しました
	PRINTFORMW 顔絵已設定
	;顔絵生成文字列を日替わり枠からセーブデータ保存枠に移行
	FOR COUNT, 0, 6
		受付水子画像文字列:COUNT '= 路人子店員画像文字列:27:COUNT
	NEXT
ENDIF
;MIZUKO_FACEから呼び出せるスプライトを改めて作成
;@路人子生成から流用
FOR COUNT, 0, 6
	GID = 0
	VARSET LOCAL
	VARSET 合成文字列配列,
	合成数 = 0
	CALL 画像合成(GID, "ダミー")
	CALL 路人子リソース探索(合成文字列配列, 合成数, 受付水子画像文字列:COUNT, 1)
	ARRAYSORT 合成文字列配列, BACK, 0, 合成数
	FOR LOCAL, 0, 合成数
		色合成 '= ""
		IF 合成文字列配列:LOCAL != ""
			FOR LOCAL:1, 0 , 路人子素材色種別数
				IF STRFIND(合成文字列配列:LOCAL, @"_%路人子素材色種別:(LOCAL:1)%_") != -1 && DIC_CONTAINSKEY(受付水子画像文字列:COUNT, @"%路人子素材色種別:(LOCAL:1)%色")
					色合成 = %DIC_GET(受付水子画像文字列:COUNT, @"%路人子素材色種別:(LOCAL:1)%色")%
					LOCAL:1 = 路人子素材色種別数
				ENDIF
			NEXT
			CALL 画像合成(GID, 合成文字列配列:LOCAL, 0, 0, 0, 0, 色合成)
		ENDIF
	NEXT
	SELECTCASE COUNT
		CASE 0
			CALL リソース登録("水子_服_通常", GID)
		CASE 1
			CALL リソース登録("水子_服_笑顔", GID)
		CASE 2
			CALL リソース登録("水子_服_憤怒", GID)
		CASE 3
			CALL リソース登録("水子_裸_通常", GID)
		CASE 4
			CALL リソース登録("水子_裸_笑顔", GID)
		CASE 5
			CALL リソース登録("水子_裸_憤怒", GID)
	ENDSELECT
NEXT
RETURN

;水子顔絵表示関数
@MIZUKO_FACE(表情, 着衣, マウスオーバー = "")
#DIMS 表情
#DIMS 着衣
#DIMS 表示用文字列
#DIMS マウスオーバー
#DIMS マウスオーバー文字列
SIF !FLAG:画像表示 || 受付水子画像文字列:0 == "顔絵無"
	RETURN
マウスオーバー文字列 = 
SIF !GROUPMATCH(表情, "笑顔", "憤怒")
	表情 = 通常
SIF 着衣 != "裸"
	着衣 = 服
表示用文字列 = 水子_%着衣%_%表情%
SIF GROUPMATCH(マウスオーバー, "通常", "笑顔", "憤怒")
	マウスオーバー文字列 = 水子_%着衣%_%マウスオーバー%
SIF FLAG:多尺寸
	默认角色画像横幅 = 180
CALL 画像セット(表示用文字列, 0, 0, 角色画像表示尺寸比, 0, 1, "999", STONE_UKETSUKENAME, マウスオーバー文字列)
CALL 画像一斉表示(0, 0, 1, -1)
RETURN

;周回時設定
@MIZUKO_BITE_A_DUST
;PRINTFORML \@ STONE_PLAYCOUNT:2 == 17 ? 愛し合い兒童まで作らせた # \@%STONE_UKETSUKENAME%のことも…
;CALL ASK_YN("忘れてしまった", "覚えている")
PRINTFORML \@ STONE_PLAYCOUNT:2 == 17 ? 连相亲相爱的孩子都给生下来了 # \@%STONE_UKETSUKENAME%的事也…
CALL ASK_YN("已经忘记了", "还记得")
IF !RESULT
	VARSET 受付水子画像文字列
	RETURN 0
ENDIF
RETURN 1 

