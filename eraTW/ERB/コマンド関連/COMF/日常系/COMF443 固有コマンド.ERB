;-------------------------------------------------
;固有指令
;日常系指令、レベルX
;-------------------------------------------------
@COM443
TRYCALLFORM UNIQUE_COM_%NAME_UNIQUE_COM(TARGET)%
SIF RESULT == -1
	RETURN -1
TIME += 3
RETURN 1

@COM_ABLE443
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(443)
	RETURN RESULT
SIF TARGET <= 0
	RETURN 0
SIF !SHIRAHU(TARGET)
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
SIF !ABLE_UNIQUE_COM(TARGET)
	RETURN 0
RETURN 1


@NAME_UNIQUE_COM(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "キャラデータ", CHARA, "固有指令")

@ABLE_UNIQUE_COM(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "キャラデータ", CHARA, "固有指令実行可能")


;-------------------------------------------------
;霊夢用
;-------------------------------------------------
@UNIQUE_COM_神簽契約
#DIM 神簽代
IF FLAG:神簽契約
	PRINTFORML 「不是已经签约了么」
	RETURN -1
ENDIF
IF !MAIN_MAP
	神簽代 = 30
ELSE
	神簽代 = 50
ENDIF
PRINTFORML 「乐园的可爱巫女每天都为你抽签」
PRINTFORML 「一个月50籌碼、够便宜吧？」
SIF !MAIN_MAP
	PRINTFORML 「既然是住在神社里、那么只要30就好了」
CALL ASK_M("拜托了",MONEY:2 >= 神簽代,"不用了",1)
SIF RESULT
	RETURN -1
PRINTFORMW 「貌似还差一点点啊！搬家后就签约吧」
MONEY:2 -= 神簽代
FLAG:神簽契約 = 1

;-------------------------------------------------
;先代用1
;-------------------------------------------------
@UNIQUE_COM_特殊训练
#DIM 训练
IF TALENT:恋人
    训练 = 10000
ELSEIF TALENT:恋慕
    训练 = 35000
ELSEIF TALENT:思慕
    训练 = 50000
ELSEIF !MAIN_MAP
	训练 = 80000
ELSE
	训练 = 100000
ENDIF
PRINTFORML 「嗯……既然是特殊训练……」
PRINTFORML 「一次十万元、够便宜了吧？」
PRINTFORMW 「贵？ 你在我这买不了吃亏买不了上当。」
PRINTFORMW 「这种特殊的训练方式贵是正常的、不爽不要买。」
SIF !MAIN_MAP
	PRINTFORML 「既然是住在神社里的话、那么便宜点、算你八万就好了」
SIF TALENT:恋人
    PRINTFORMW 「但是、害、我俩谁跟谁啊、再那么黑就显得我小气了、就收你一万元吧。」
SIF TALENT:恋慕
    PRINTFORMW 「嗯……怎么说也……不、这次就算你三万五好了、什么？ 没听过促销啊？」
SIF TALENT:思慕
    PRINTFORMW 「熟客啊、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%、这次便宜你了、五万元就行。」
CALL ASK_M("拜托了",MONEY >= 训练,"不用了",1)
SIF RESULT
	RETURN -1
PRINTFORMW 「那么……我把特殊训练的方法告诉你、你先自己练练吧。」
MONEY -= 训练
CALL AddEXP("戦闘経験", MASTER, 50)
CALL RECOVER(MASTER, -1200, "体力", "用先代提供的方法训练")

;-------------------------------------------------
;先代用2
;-------------------------------------------------
@UNIQUE_COM_寻找委托
#DIM 寻找
#DIM 候補番号
#DIM 候補個数
#DIM 売却個数
IF TALENT:恋人
    寻找 = 100
ELSEIF TALENT:恋慕
    寻找 = 300
ELSEIF TALENT:思慕
    寻找 = 500
ELSE
	寻找 = 1000
ENDIF
PRINTFORML 所持金:%金額表示(MONEY)%
DRAWLINE
PRINTFORMW  「想要找材料吗？ 可以哦、先订购吧。」
PRINTFORMW  「如果有什么材料的话我也会收购的」
;所持している素材道具の売却可能物・個数を列表化
LOCAL:1 = 999
FOR LOCAL:0 ,600, 700
	SELECTCASE LOCAL:0
		CASE 636
			;★持っていても売れない素材を上のCASEへ追加
		CASEELSE
			IF ITEM:(LOCAL:0)
				LOCALS:0 = [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}    
				LOCAL:1 += STRLENS(LOCALS:0)
				SIF ITEM:(LOCAL:0) < 10
					LOCAL:1 ++
				IF LOCAL:1 > 90
					IF LOCAL:1 >= 999
					;最初のみ改行しない
					ELSE
						PRINTL 
					ENDIF
					LOCAL:1 = 0
					PRINT 　　
				ENDIF
				PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}
				SIF ITEM:(LOCAL:0) < 10
					PRINT  
				PRINT 　　
				ENDIF
	ENDSELECT
	PRINTFORMW  先代突然消失了、等再过了一两分钟出现时、手上提着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%订购的材料
	PRINTFORMW  「委托费1000元、请。」
	SIF TALENT:恋人
    PRINTFORMW 「但是、害、我俩谁跟谁啊、再那么黑就显得我小气了、就收你100元吧。」
SIF TALENT:恋慕
    PRINTFORMW 「嗯……怎么说也……不、这次就算你300好了、什么？ 没听过限时特惠啊？」
    SIF TALENT:思慕
    PRINTFORMW 「熟客啊、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%、这次便宜你了、500元就行。」
    CALL ASK_M("支付委托费",MONEY >= 寻找,"不支付委托费",1)
    SELECTCASE RESULT
    CASE 0
    PRINTFORMW  「那么、下次有这种活再叫上我就是了。」
    MONEY -= 寻找
    CALL CHANGE_CFLAG(2, 145, 10)
	CALL CHANGE_CFLAG(4, 145, 5)
    CASE 1
    PRINTFORMW  「没钱……？」
    PRINTFORMW  「那么、就用这个抵偿好了……」
    PRINTFORMW  %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的腹部接了重重的一拳、痛苦地在地上蜷缩着。
    PRINTFORMW  先代不知道在什么时候离开了。
    CALL RECOVER(MASTER, -1000, "体力", "先代的腹击")
    ENDSELECT
    RETURN 1
NEXT
PRINTL 
PRINTL 　　[100] 放弃
DRAWLINE
PRINTFORML 「要卖什么呢？」
$INPUT_LOOP01
INPUT
SELECTCASE RESULT
	CASE 100
		RETURN -1
	CASE 601, 603 TO 609, 612 TO 615, 617 TO 620, 622 TO 625, 632 TO 635, 637, 640 TO 643, 646
		;★売却可能な素材を上のCASEに追加
		候補番号 = RESULT
		候補個数 = ITEM:(候補番号)
		CLEARLINE 1
		IF 候補個数
			PRINTFORM 　%金額表示(ITEMPRICE:(候補番号) / 2)%一个、怎么样？」
			SETCOLOR 0x777777
			PRINTFORML 　要卖出多少个？(输入1～{候補個数}、0为返回)
			RESETCOLOR
			PRINTL   
			PRINTBUTTON "[0] 放弃", 0
			PRINT     
			PRINTBUTTON "[1] 1个", 1
			PRINT     
			IF 候補個数 >= 5
				PRINTBUTTON "[5] 5个", 5
				PRINT     
			ENDIF
			IF 候補個数 >= 10
				PRINTBUTTON "[10] 10个", 10
				PRINT    
			ENDIF
			$INPUT_LOOP02
			INPUT
			SELECTCASE RESULT
				CASE 0
					RESTART
				CASE 1 TO 候補個数
					CLEARLINE 2
					売却個数 = RESULT
					PRINTFORMW 「这样啊、那么一共%ITEMNAME:候補番号%{売却個数}个、总计订价\\{(ITEMPRICE:(候補番号) / 2 * 売却個数)}……」
					MONEY += (ITEMPRICE:(候補番号) / 2 * 売却個数)
					ITEM:(候補番号) -= 売却個数
					RESTART
				CASEELSE
					CLEARLINE 1
					GOTO INPUT_LOOP02
			ENDSELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP01
		ENDIF
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP01
ENDSELECT
RESTART
;-------------------------------------------------
;文用
;-------------------------------------------------
@UNIQUE_COM_新聞購読
IF FLAG:新聞購読
	PRINTFORML 「不是已经订阅了么」
	RETURN -1
ENDIF
PRINTFORML 「%金額表示(3000)%就能了解这个月的幻想郷的时事！　文文新聞值得你订阅？」
CALL ASK_M("不要",1,"订阅",MONEY >= 3000,"用籌碼来订阅",MONEY:2 >= 10)
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		MONEY -= 3000
	CASE 2
		MONEY:2 -= 10
ENDSELECT
PRINTFORMW 订阅了文文。新闻。
PRINTFORMW 好像是每月初续费。
FLAG:新聞購読 = 1




;-------------------------------------------------
;天子用
;-------------------------------------------------
@UNIQUE_COM_天候操作
IF CFLAG:MASTER:現在位置 == 天界
	PRINTFORML 「这里太勉强了」
	RETURN -1
ENDIF
IF FLAG:異常気象
	PRINTFORML 「这个天气无法改变」
	RETURN -1
ENDIF
IF GETBIT(TFLAG:一日一回,6)
	PRINTFORML 「今天已经结束了」
	RETURN -1
ENDIF
PRINTFORML 「好像是因为很麻烦的様子、只是随便做了下」
$LOOP
CALL ASK_M("晴朗",1,"陰天",1,"雨",1, "雪", DAY:2 == 4, "内裤", 1)
SELECTCASE RESULT
	CASE 0
		;雨なら低確率で虹
		SIF INRANGE(TIME:5,4,7) && !RAND:7
			TIME:7 = 1
		;雪なら低確率でダイヤモンドダスト
		SIF INRANGE(TIME:5,8,13) && !RAND:7
			TIME:7 = 2
		TIME:5 = RAND:2
	CASE 1
		TIME:5 = RAND:2 + 2
	CASE 2
		;晴天を変えようとすると虹
		IF TIME:5 < 2 && !RAND:4
			TIME:7 = 1
		ELSE
			TIME:5 = 4 + RAND:4
		ENDIF
	CASE 3
		;晴天を変えようとするとダイヤモンドダスト
		IF TIME:5 < 2 && !RAND:4
			TIME:7 = 2
		ELSE
			TIME:5 = 8 + RAND:6
		ENDIF
	CASE 4
		PRINTFORML 「你是笨蛋麼？」
		RETURN -1
ENDSELECT

DOWNBASE:気力 = 300
SETBIT TFLAG:一日一回,6

;(天候パッチ)風速を0にする
WIND_VELOCITY = 0
;(天候パッチ)1日が終わるまで天候変更を禁止する
FORBIDDEN_CHANGE_WEATHER = 1


;-------------------------------------------------
;椛用
;-------------------------------------------------
@UNIQUE_COM_千里眼
PRINTFORML 「要看哪里呢？」
CALL BROAD_HITOSAGASI


;-------------------------------------------------
;琪露諾用
;-------------------------------------------------
;冰淇淋の値段は季節により変動する（夏が一番高い）
@UNIQUE_COM_买冰淇淋
#DIM 冰淇淋ねだん
#DIM パックねだん
SELECTCASE DAY:2
CASE 1
	冰淇淋ねだん = 400
CASE 2
	冰淇淋ねだん = 500
CASE 3
	冰淇淋ねだん = 300
CASE 4
	冰淇淋ねだん = 200
ENDSELECT
パックねだん = 冰淇淋ねだん * 9

DRAWLINE
PRINTL 「要冰淇淋麼？」
PRINTFORML 所持金:%金額表示(MONEY)%
DRAWLINE
CALL EX_SHOPPING(GETNUM(ITEM, "冰淇淋"), "冰淇淋", "又甜又好吃的冰淇淋", 冰淇淋ねだん, 1)
CALL EX_SHOPPING(GETNUM(ITEM, "冰淇淋"), "混合装冰淇淋", "大家一起吃吧（10個）", パックねだん, 10, GETNUM(ITEM, "冰淇淋") + 1)
DRAWLINE
PRINTL [999] 算了
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "冰淇淋")
	CALL ITEM_CARTON(GETNUM(ITEM, "冰淇淋"), 冰淇淋ねだん, 1)
ELSEIF RESULT == GETNUM(ITEM, "冰淇淋") + 1
	CALL ITEM_CARTON(GETNUM(ITEM, "冰淇淋"), パックねだん, 10)
ENDIF
RESTART

;-------------------------------------------------
;咲夜用
;-------------------------------------------------
@UNIQUE_COM_红魔馆出差服务
PRINTFORML 「哎呀、有什麼困難麼？」
PRINTFORML 
CALL ASK_M("沒有請求", 1, "掃除代理", !TFLAG:上门清扫, "澆水代理", ITEM:150)
SELECTCASE RESULT
CASE 0
	RETURN -1
CASE 1
	PRINTFORML 是委托清扫%GET_MAPNAME(MAIN_MAP)%么？
	CALL PAY_YEN_OR_CM(3000,10)
	SIF !RESULT
		RETURN -1
	PRINTFORML 委托了上门清扫服务。
	TFLAG:上门清扫 = 1
CASE 2
	PRINTFORML 委托家庭菜園的浇水么（这一整个月）
	CALL PAY_YEN_OR_CM(15000,70)
	SIF !RESULT
		RETURN -1
	PRINTFORML 委托了浇水。
	FLAG:澆水代行 = 1
ENDSELECT
RETURN 1



;-------------------------------------------------
;納茲琳用
;-------------------------------------------------
@UNIQUE_COM_灵摆查找
#DIM DYNAMIC CNT_TALK
#DIM DYNAMIC CNT_DOWSING
#DIM CAN_SLOT
#DIM CHK_SLOT
#DIM TMP_ID
IF !CNT_TALK
	PRINTFORML 「嗯。想找東西麼」
	PRINTFORML 「這確實是我的專業領域」
	PRINTFORMW 「那麼、請詳細的說明一下吧」
	CNT_TALK ++
	CAN_SLOT = CAN_DOWSING()
	IF !CAN_SLOT
		PRINTFORML 「喂喂、你說的真的是特別想找的東西麼探測術完全沒反應啊？」
		PRINTFORMW 「不是來看笑話的吧？真是的…」
		RETURN
	ENDIF
ELSE
	PRINTFORMW 「還有什麼麼？」
ENDIF

FOR CHK_SLOT, 1, 10
	SIF !GETBIT(CAN_SLOT, CHK_SLOT)
		CONTINUE
	SIF IRAI_EX:CHK_SLOT
		SETCOLOR C_GRAY
	PRINTFORML [{CHK_SLOT}] - 依頼{CHK_SLOT}　＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "依頼名", IRAI_SLOT_TO_CLIENT(CHK_SLOT))%＞
	RESETCOLOR
NEXT
PRINTFORML 
PRINTFORML [0] - 放棄
DO
	INPUT
	CHK_SLOT = RESULT
	SELECTCASE CHK_SLOT
	CASE 1 TO 9
		SIF !GETBIT(CAN_SLOT, CHK_SLOT)
			CONTINUE
		IF IRAI_EX:CHK_SLOT
			PRINTFORML 「喂喂、这个依頼不是已经調查过了吗」
			CONTINUE
		ENDIF
		PRINTFORML 「原來如此」
		PRINTFORML 「要使用探測術的話多少需要供奉一些、沒問題吧？」
		CALL ASK_M("還是算了", 1, @"施捨%金額表示(1000)%", MONEY >= 1000, "施捨5CM", MONEY:2 >= 5)
		SELECTCASE RESULT
		CASE 0
			PRINTFORMW 「哦呀、那太遺憾了」
			BREAK
		CASE 1
			MONEY -= 1000
		CASE 2
			MONEY:2 -= 5
		ENDSELECT
		CNT_DOWSING ++
		SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
		CASE "捜索（人物）"
			CNT_DOWSING ++
			TMP_ID = IRAI_SLOT_TO_PERSON(CHK_SLOT)
			IRAI_EX:CHK_SLOT = TMP_ID
			IF CALLNAME:TMP_ID == "納茲琳"
			;ナズ口上は人物に対する反応分岐が多いがつくれない
			;流石に自分が相手ならこんな感じになるだろう
				PRINTFORML 「從剛才開始反應就一直搖擺不定」
				PRINTFORML 「這個委託難道是和我有関連的東西麼？」
				PRINTFORMW 「不由得有這種感覺」
				BREAK
			ENDIF
			CALL DOWSING_TSUBUYAKI
			PRINTFORML 「得到結果了」
			PRINTFORML 「%NAME_OUTPUT_TRANSLATION("CALLNAME", TMP_ID)%是関鍵人物」
			PRINTFORML 「要順便算出所在地麼？」
			CALL ASK_M("還是算了", 1, @"施捨%金額表示(500)%", MONEY >= 500, "施捨3CM", MONEY:2 >= 3)
			SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「哦呀、那太遺憾了」
				BREAK
			CASE 1
				MONEY -= 500
			CASE 2
				MONEY:2 -= 3
			ENDSELECT
			IF CFLAG:TMP_ID:現在位置 == SUKIMA()
				PRINTFORML 「嗯ー、反応不穩定啊」
				PRINTFORMW 「現在很難馬上見面啊」
			ELSE
				PRINTFORMW 「她現在在“%STR:(5000 + GET_MAPID(CFLAG:TMP_ID:現在位置))%”的附近」
			ENDIF
		CASE "捜索（場所）"
			CNT_DOWSING ++
			TMP_ID = IRAI_SLOT_TO_PLACE(CHK_SLOT)
			IRAI_EX:CHK_SLOT = TMP_ID + 5000
			CALL DOWSING_TSUBUYAKI
			PRINTFORML 「得到結果了」
			PRINTFORML 「你的目標場所是“%STR:(5000 + GET_MAPID(TMP_ID))%”」
			PRINTFORML 「需要更加詳細的情報麼？」
			
			CALL ASK_M("還是算了", 1, @"施捨%金額表示(2000)%", MONEY >= 2000, "施捨10CM", MONEY:2 >= 10)
			SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「哦呀、那太遺憾了」
				BREAK
			CASE 1
				MONEY -= 2000
			CASE 2
				MONEY:2 -= 10
			ENDSELECT
			IRAI_EX:CHK_SLOT = TMP_ID
			PRINTFORMW 「%STR:(5000 + GET_MAPID(TMP_ID))%附近、特別是“%GET_PLACENAME(TMP_ID)%”的反応非常強」
		CASEELSE
			PRINTFORMW 「啊嘞？……嗯ー、讀取出了問題」
			PRINTFORML 
			PRINTFORML 　发生错误中(请提供日志)
			PRINTFORML 　点击生成错误记录
			PRINTFORMW ◆从这里開始
			PRINTFORML IRAI_SLOT:CHK_SLOT　{IRAI_SLOT:CHK_SLOT}
			PRINTFORML IRAI_DATA:CHK_SLOT　{IRAI_DATA:CHK_SLOT}
			CALL SHOW_IRAI_DATA(IRAI_SLOT:CHK_SLOT, CHK_SLOT)
			PRINTFORML 
			PRINTFORML ◆到这里结束
			PRINTFORMW 　复制粘贴
		ENDSELECT
		
	CASE 0
		IF CNT_DOWSING
			PRINTDATAL
				DATA 「好像沒派上用場？」
				DATA 「祈禱自己幸運吧」
				DATA 「已經可以了麼？」
			ENDDATA
			PRINTFORMW 「嘛、請耐心尋找」
		ELSE
			PRINTFORMW 「請不要再愚弄我了」
		ENDIF
		RETURN
	CASEELSE
		$RETRY
		PRINTFORMW 「嗯、什麼？」
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;受託済み依頼の灵摆查找可否判定
;戻り値はBITで各依頼の灵摆查找可否
@CAN_DOWSING()
#FUNCTION
#DIM CHK_SLOT
#DIM DYNAMIC CAN_SLOT
FOR CHK_SLOT, 1, 10
	SIF !IRAI_SLOT_CAN_DOWSING(CHK_SLOT)
		CONTINUE
	SETBIT CAN_SLOT, CHK_SLOT
NEXT
RETURNF CAN_SLOT

;個々の委托接口の灵摆查找可否判定
@IRAI_SLOT_CAN_DOWSING(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF IRAI_SLOT_TO_PROGRESS(CHK_SLOT) != "依頼中"
	RETURNF 0
SIF !GROUPMATCH(IRAI_SLOT_TO_TASKNAME(CHK_SLOT), "捜索（人物）", "捜索（場所）")
	RETURNF 0
RETURNF CHK_SLOT

;灵摆查找中の独り言
@DOWSING_TSUBUYAKI
#DIMS COMMENT, 3
COMMENT:0 '= "「這……這是」"
STRDATA COMMENT:1
	DATA 「……已经缩小了不少了啊」
	DATA 「唔、但是这様不科学啊」
	DATA 「哦呀、是这様吗」
	DATA 「那么、这様做的话…………」
ENDDATA
STRDATA COMMENT:2
	DATA 「……原来是这么回事啊」
	DATA 「什么嘛、这不是很簡単的吗」
	DATA 「很好、就是这个了」
	DATA 「是在这里吗」
ENDDATA
CALL PRINT_GROUP(COMMENT, 3)





;-------------------------------------------------
;斯塔用
;-------------------------------------------------
@UNIQUE_COM_探访迹象
PRINTFORML 「嗯ー、在找人麼？」
PRINTFORML 「這樣啊……要給我零花錢？」
CALL ASK_M("還是算了", 1, @"給%金額表示(500)%", MONEY >= 500, "給3CM", MONEY:2 >= 3)
SELECTCASE RESULT
CASE 0
	PRINTDATAW
		DATAFORM 「嗚ー、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%真小氣」
		DATAFORM 「……哈啊」
		DATAFORM 斯塔 萨菲雅默默地盯著這邊
	ENDDATA
	RETURN
CASE 1
	MONEY -= 500
CASE 2
	MONEY:2 -= 3
ENDSELECT
PRINTDATAW
	DATAFORM 「哇啊ー」
	DATAFORM 「太好了ー」
	DATAFORM 「是零花錢ー」
	DATAFORM 「我喜歡通情達理的人」
	DATAFORM 斯塔 萨菲雅非常開心
ENDDATA
PRINTFORML 「那麼開始吧♪」
PRINTFORMW 「嗯……」
PRINTFORML 
CALL HITOSAGASI
PRINTFORMW 

;-------------------------------------------------
;魔理沙用
;交友関係広そうだし、耳は早かろう
;-------------------------------------------------
@UNIQUE_COM_帮助别人
#DIM PREV_DAY
#DIM IRAI_ABLE
#DIM CLIENT_ID
PREV_DAY = DAY
PRINTFORMW 「誒？“想知道正在困擾的女孩子”？」

CALL CAN_IRAI_START
IRAI_ABLE = RESULT
IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(TARGET) == "依頼未受託"
	PRINTFORML 「有啊、就在你面前！」
	CALL IRAI_START(TARGET)
	SIF IRAI_CHARA_TO_PROGRESS(TARGET) != "依頼中"
		PRINTFORMW 「什麼啊、一般來說這個劇情走向是拒絕的麼？」
	RETURN
ENDIF

CLIENT_ID = SEARCH_UNIQUE_COM_帮助别人()
SELECTCASE CLIENT_ID
CASE 0
	PRINTFORML 「嗯ー」
	PRINTFORMW 「據我所知好像沒有啊」
CASEELSE
	PRINTFORML 「%TEXTR("嗯——/那個——/確實……")%」
	PRINTDATAW
		DATAFORM 「%NAME_OUTPUT_TRANSLATION("CALLNAME", CLIENT_ID)%好像那麼說過」
		DATAFORM 「%NAME_OUTPUT_TRANSLATION("CALLNAME", CLIENT_ID)%好像說過些什麼」
		DATAFORM 「聽說了%NAME_OUTPUT_TRANSLATION("CALLNAME", CLIENT_ID)%有些困擾的傳聞」
		DATAFORM 「去%NAME_OUTPUT_TRANSLATION("CALLNAME", CLIENT_ID)%的所在地看看怎麼樣？」
		DATAFORM 「我覺得去見見%NAME_OUTPUT_TRANSLATION("CALLNAME", CLIENT_ID)%比較好」
	ENDDATA
ENDSELECT

@SEARCH_UNIQUE_COM_帮助别人
#FUNCTION
#DIM DYNAMIC CLIENT_ID, 人物数量上限
#DIM DYNAMIC CLIENT_CNT
FOR LOCAL:0, 1, CHARANUM
	SIF IRAI_CHARA_TO_PROGRESS(LOCAL:0) != "依頼未受託"
		CONTINUE
	CLIENT_ID:CLIENT_CNT = LOCAL:0
	CLIENT_CNT ++
NEXT
SIF !CLIENT_CNT
	RETURNF 0
RETURNF CLIENT_ID:(RAND:CLIENT_CNT)

;-------------------------------------------------
;荷取用
;-------------------------------------------------
@UNIQUE_COM_購買道具
#DIM 買うもの
#DIM 割引
TFLAG:194 = 1
割引 = 66
;購入条件設定
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:河童の道具屋")
	IF RESULT
		CALL ITEMDATA(LOCAL, "SALES")
		ITEMSALES:LOCAL = RESULT
	ENDIF
NEXT
DRAWLINE
CALL LIST_ITEM("河童道具")
DRAWLINE
PRINTL 「今天的话有优惠哦」
PRINTFORML 请选择要購买的道具（所持金:%金額表示(MONEY)%）
DRAWLINE
CALL SHOW_SHOPPING("河童の道具屋", "河童道具", 割引)
DRAWLINE
;未実装
;PRINTL [990] - 道具的改造　[991] - 道具の開発
PRINTL [990] - 道具的改造
PRINTL [999] - 返回　　　　　
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == 990
	CALL TOOL_CUSTOM
	SIF RESULT
		RETURN RESULT
ELSEIF RESULT == 991
	;未実装
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "SHOP:河童の道具屋", 割引)
	IF RESULT && ITEMSALES:買うもの >= 0 && ITEM:買うもの < 所持数制限
		CALL ITEMDATA(買うもの, "購入", 割引)
		SIF RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF


;道具改造関数
;現状では泳池だけだが
;改造道具が増えるとさらに肥大化するのが目に見えるので独立
;購入でRETURN -1、購入しない時はRETURN 0
;何もせずに末尾に到達すると購入した扱いになります
@TOOL_CUSTOM
#DIM 泡改造前提条件
泡改造前提条件 = GETBIT (FLAG:泳池, 0) && GETBIT (FLAG:泳池, 1) && GETBIT (FLAG:泳池, 2)
DRAWLINE
PRINTFORML 请选择要改造的道具（所持金:%金額表示(MONEY)%）
DRAWLINE
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "TYPE:改造可能")
	SIF RESULT && ITEM:LOCAL
		PRINTFORML [{LOCAL, 3}] - %ITEMNAME:LOCAL%
NEXT
PRINT [999] - 果然還是放棄
INPUT
SELECTCASE RESULT
CASE 999
	RETURN
CASE GETNUM(ITEM, "簡易泳池")
	SIF !ITEM:簡易泳池
		RETURN
	DRAWLINE
	PRINTL 「どう改造する？」
	PRINT 現在的状態：
	DRAWLINE
	SIF GETBIT (FLAG:泳池, 0)
		PRINT [自走马达]
	SIF GETBIT (FLAG:泳池, 1)
		PRINT [温水機能]
	SIF GETBIT (FLAG:泳池, 2)
		PRINT [喷淋装置]
	SIF GETBIT (FLAG:泳池, 3)
		PRINT [不思議的泡]
	PRINTL
	
	IF !GETBIT (FLAG:泳池, 0)
		PRINTFORML [  1] - 马达（%金額表示(5000)%）：消除移动负担
	ELSE
		SETCOLOR C_GRAY
		PRINTL [ 済] - 马达：消除移动负担
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:泳池, 1)
		PRINTFORML [  2] - 温水機能（%金額表示(5000)%）：在广阔的地方放松
	ELSE
		SETCOLOR C_GRAY
		PRINTL [ 済] - 温水機能：在广阔的地方放松・可以玩浴室PLAY
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:泳池, 2)
		PRINTFORML [  3] - 淋浴（%金額表示(5000)%）：可以淋浴・也可以浇水
	ELSE
		SETCOLOR C_GRAY
		PRINTL [ 済] - 淋浴：可以淋浴・也可以浇水
		RESETCOLOR
	ENDIF
	IF 泡改造前提条件 == 0
		PRINTFORML [  4] - 三个都改造（每个%金額表示(5000)%）
		PRINTL [  ?] - ？？？？？
	ELSEIF !GETBIT (FLAG:泳池, 3)
		PRINTFORML [  5] - 不可思议的泡（%金額表示(5000)%）：若鷺姫可以自由移动
	ELSE
		SETCOLOR C_GRAY
		PRINTL [ 済] - 不可思议的泡：若鷺姫可以自由移动
		RESETCOLOR
		PRINTL 　　　　无法做更进一步的改造了
	ENDIF
	
	PRINTL [999] - 還是算了
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF !GETBIT (FLAG:泳池, 0) && MONEY >= 5000
			PRINTL 移動的負担消失了！
			SETBIT FLAG:泳池, 0
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 2
		IF !GETBIT (FLAG:泳池, 1) && MONEY >= 5000
			PRINTL 可以在更多的地方放松了！
			PRINTL 可以进行浴室PLAY了！
			SETBIT FLAG:泳池, 1
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 3
		IF !GETBIT (FLAG:泳池, 2) && MONEY >= 5000
			PRINTL 可以使用淋浴了！
			PRINTL 灌水也变得轻松愉快了！
			SETBIT FLAG:泳池, 2
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 4
		IF 泡改造前提条件 == 0 && MONEY >= 5000 * (!GETBIT (FLAG:泳池, 0) + !GETBIT (FLAG:泳池, 1) + !GETBIT (FLAG:泳池, 2))
			PRINTL 追加了各种各様的功能！
			MONEY -= 5000 * (!GETBIT (FLAG:泳池, 0) + !GETBIT (FLAG:泳池, 1) + !GETBIT (FLAG:泳池, 2))
			SETBIT FLAG:泳池, 0
			SETBIT FLAG:泳池, 1
			SETBIT FLAG:泳池, 2
		ELSE
			RETURN
		ENDIF
	CASE 5
		IF !GETBIT (FLAG:泳池, 3) && 泡改造前提条件 && MONEY >= 5000
			PRINTL 为若鹭姬购买了不可思议的泡！
			SETBIT FLAG:泳池, 3
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	ENDSELECT
CASEELSE
	RETURN
ENDSELECT
RETURN -1

;-------------------------------------------------
;鈴仙と帝用
;-------------------------------------------------
@UNIQUE_COM_買薬
#DIM 買うもの
DRAWLINE
CALL LIST_ITEM("うさぎ薬品")
DRAWLINE
PRINTFORML 请选择要購买的道具 所持金:%金額表示(MONEY)%
DRAWLINE
PRINTL
VARSET LOCALS
CALL EX_SHOPPING(GETNUM(ITEM, "潤滑液"), "瓶装润滑乳液", "装有润滑乳液的大瓶子。大概能用5次的様子。", 1000, 5)
CALL EX_SHOPPING(GETNUM(ITEM, "避孕套"), "避孕套优惠包", "内有50個。", 1000, 50)
CALL EX_SHOPPING(GETNUM(ITEM, "口服避孕薬"), "口服避孕药（内有5片）", "光明的计划生育 ", 5000, 5)
CALL ITEM_SHOPPING(GETNUM(ITEM, "催情薬"), "更容易催生好感")
CALL ITEM_SHOPPING(GETNUM(ITEM, "睡眠薬"), "不要用它来做坏事情哟？")
CALL ITEM_SHOPPING(GETNUM(ITEM, "栄養飲料"), "含有1000mg牛磺酸")
CALL ITEM_SHOPPING(GETNUM(ITEM, "精力剤"), "肮脏的有钱人专用")
CALL ITEM_SHOPPING(GETNUM(ITEM, "姜黄飲料"), "在飲酒前服用")
CALL ITEM_SHOPPING(GETNUM(ITEM, "绷帯"), "在受伤或想隐藏的地方卷起来吧")
CALL ITEM_SHOPPING(GETNUM(ITEM, "栄養強化剤"), "混在食物中来补充不足容易缺乏的营养")
SIF TARGET == [[帝]] && !ITEM:兎符「開運大紋」
	CALL ITEM_SHOPPING(GETNUM(ITEM, "兎符「開運大紋」"), "会发生十分幸运的色情事件")
DRAWLINE
PRINTL [999] 算了
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "兎符「開運大紋」") && TARGET == [[帝]] && !ITEM:兎符「開運大紋」
	CALL BOTTAKURI
ELSEIF RESULT == GETNUM(ITEM, "潤滑液")
	CALL ITEM_CARTON(RESULT, 1000, 5)
ELSEIF RESULT == GETNUM(ITEM, "避孕套")
	CALL ITEM_CARTON(RESULT, 1000, 50)
ELSEIF RESULT == GETNUM(ITEM, "口服避孕薬")
	CALL ITEM_CARTON(RESULT, 5000, 5)
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "TYPE:うさぎ薬品")
	IF RESULT && ITEMSALES:買うもの >= 1 && ITEM:買うもの < 所持数制限
		CALL ITEMDATA(買うもの, "購入")
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF
RESTART

@BOTTAKURI
SELECTCASE FLAG:開運大紋購入回数
CASE 0
	LOCAL = 1000
CASE 1 TO 3
	LOCAL = 10000
CASE 3 TO 10
	LOCAL = 20000
CASE 11 TO 20
	LOCAL = 30000
CASEELSE
	LOCAL = 50000
ENDSELECT
SIF TALENT:TARGET:恋慕 && LOCAL >= 20000
	LOCAL /= 2

SIF !FLAG:開運大紋購入回数
	PRINTFORML 「想要試試的話現在只需要%金額表示(LOCAL)%！就可以買走！」
IF LOCAL <= 10000
ELSE
	IF TALENT:TARGET:恋慕
		PRINTFORML 「是%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%只需要%金額表示(LOCAL)%就行」
	ELSE
		PRINTFORML 「如果這樣的話需要%金額表示(LOCAL)%」
	ENDIF
	CALL ASK_M("別開玩笑了",1, "買下", MONEY >= LOCAL, "你有那麼說的立場麼？", CFLAG:TARGET:被掌握把柄 && !CFLAG:TARGET:掌握把柄)
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
		CASE 2
			PRINTFORMW 利用把柄免费交换到了
			ITEM:130 ++
			CFLAG:TARGET:被掌握把柄 = 0
			RETURN
	ENDSELECT
ENDIF

IF MONEY >= LOCAL
	ITEM:130 ++
	MONEY -= LOCAL
	FLAG:開運大紋購入回数 ++
	PRINTFORMW 支付\\{LOCAL}購買了兎符「開運大紋」
ENDIF

;-------------------------------------------------
;みすちー用
;-------------------------------------------------
@UNIQUE_COM_路边摊吃饭
#DIM 同行人数カウント
#DIM 飲酒可能人数
#DIM ATSUKAN
#DIM OOMORI
#DIM KAIKEI
#DIM KAKAKU,2

同行人数カウント = 1
飲酒可能人数 = 0
ATSUKAN = 0
KAIKEI = 0
OOMORI = 0
VARSET KAKAKU
SIF CAN_DRINK(MASTER)
	飲酒可能人数 = 1
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:同行
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:空腹時刻)
	 	CONTINUE
	同行人数カウント ++
	SIF CAN_DRINK(LOCAL)
		飲酒可能人数 ++
NEXT
PRINTFORML 「歡迎光臨、{同行人数カウント}位客人」
KAKAKU:0 = 3 * 同行人数カウント
KAKAKU:1 = 5 * 同行人数カウント
CALL ASK_M("烤串拼盤　每人3CM",MONEY:2 >= KAKAKU:0 && TIME_PROGRESS(TCVAR:MASTER:空腹時刻),"烤串拼盤（大）　每人5CM", MONEY:2 >= KAKAKU:1 && TIME_PROGRESS(TCVAR:MASTER:空腹時刻),"打包　5CM",MONEY:2 >= 5 && !ITEM:八目鰻烤串,"取消",1)
IF RESULT == 3
	PRINTFORML 「歡迎下次再來」
	RETURN -1
ENDIF
IF RESULT == 2
	ITEM:八目鰻烤串 ++
	MONEY:2 -= 5
	PRINTFORML 「趁著還沒冷的時候吃吧」
	SOURCE:歓楽 += 200
	TFLAG:98 = 1
	TIME += 5
	RETURN 1
ENDIF
MONEY:2 -= KAKAKU:RESULT
KAIKEI = KAKAKU:RESULT

SIF RESULT == 1
	OOMORI = 1

IF MONEY:2 >= 2 * 飲酒可能人数
	PRINTFORML 「要每人花2籌碼燙酒麼？」（酒気・気力回復量上升）
	CALL ASK_YN
	IF !RESULT
		ATSUKAN = 1
		KAIKEI += 2 * 飲酒可能人数
		MONEY:2 -= 2 * 飲酒可能人数
	ENDIF
ENDIF
;信頼
TFLAG:98 += 3
;同行中全員で回す

FOR LOCAL,0,CHARANUM
	SIF !CFLAG:LOCAL:同行 && LOCAL
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:空腹時刻)
	 	CONTINUE
	CALL EAT_YATSUME(LOCAL,OOMORI,ATSUKAN)
NEXT

SOURCE:歓楽 += KAIKEI * 100
PRINTFORML 「總共{KAIKEI}籌碼」
PRINTFORML 「歡迎下次再來」
TIME += 30

@EAT_YATSUME(対象,ARG,ARG:1)
#DIM 回復前体力
#DIM 回復前気力
#DIM 回復前精力
#DIM 回復後体力
#DIM 回復後気力
#DIM 回復後精力
#DIM 対象

回復前体力 = BASE:対象:体力
回復前気力 = BASE:対象:気力
回復前精力 = BASE:対象:精力

;大盛りの場合
IF ARG == 1
	CALL 満腹度上昇(対象,"主食")
	CALL RECOVER_PERMIL(対象,100,"体力")
	CALL RECOVER_PERMIL(対象,200,"気力")
	CALL RECOVER_PERMIL(対象,100,"精力")
ELSE
	CALL 満腹度上昇(対象,"軽食")
	CALL RECOVER_PERMIL(対象,50,"体力")
	CALL RECOVER_PERMIL(対象,100,"気力")
	CALL RECOVER_PERMIL(対象,50,"精力")
ENDIF
IF 対象
;固定で獲得するソース
SOURCE:対象:歓楽 = 1000
SOURCE:対象:情愛 = 300

;ABL:欲望をみる
IF ABL:対象:欲望 <= 1
	SOURCE:対象:歓楽 += (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 3
	SOURCE:対象:歓楽 += 400 + (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 5
	SOURCE:対象:歓楽 += 800 + (ABL:対象:欲望 * 60)
ELSEIF ABL:対象:欲望 <= 8
	SOURCE:対象:歓楽 += 1000 + (ABL:対象:欲望 * 75)
ELSEIF ABL:対象:欲望 <= 11
	SOURCE:対象:歓楽 += 1700 + (ABL:対象:欲望 * 75)
ELSE
	SOURCE:対象:歓楽 += 2500 + (ABL:対象:欲望 * 35)
ENDIF

;ABL:対象:奉仕精神をみる
IF ABL:対象:奉仕精神 <= 1
	SOURCE:対象:情愛 += (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 3
	SOURCE:対象:情愛 += 200 + (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 5
	SOURCE:対象:情愛 += 500 + (ABL:対象:奉仕精神 * 30)
ELSEIF ABL:対象:奉仕精神 <= 8
	SOURCE:対象:情愛 += 700 + (ABL:対象:奉仕精神 * 45)
ELSEIF ABL:対象:奉仕精神 <= 11
	SOURCE:対象:情愛 += 1200 + (ABL:対象:奉仕精神 * 45)
ELSE
	SOURCE:対象:情愛 += 1800 + (ABL:対象:奉仕精神 * 20)
ENDIF

;好感度をみる
IF CFLAG:対象:2 <= 500
	SOURCE:対象:歓楽 += CFLAG:対象:2
ELSEIF CFLAG:対象:2 <= 5000
	SOURCE:対象:歓楽 += 750 + (CFLAG:対象:2 - 500) / 3
ELSE
	SOURCE:対象:歓楽 += 2000 + (CFLAG:対象:2 - 5000) / 4
ENDIF
SIF SOURCE:対象:歓楽 < 0
	SOURCE:対象:歓楽 = 0
SOURCE:対象:受動 = 200 + 110 * ABL:対象:従順
SOURCE:対象:征服 = 200 + 110 * ABL:対象:施虐属性

SIF TALENT:大胃王
	TIMES SOURCE:対象:歓楽 , 1.50
ENDIF
;熱燗頼んだ場合
IF ARG:1 && CAN_DRINK(対象)
	TIMES SOURCE:対象:歓楽 , 1.10
	CALL RECOVER_PERMIL(対象,50,"気力")
	SIF 対象 != MASTER
		CALL KIGEN_CHANGE(対象,50,1)
	CALL DRUNKEN(対象,52)
ENDIF

回復後体力 = BASE:対象:体力
回復後気力 = BASE:対象:気力
回復後精力 = BASE:対象:精力

;PRINTFORML 体力+{ABS(回復後体力 - 回復前体力)}（%NAME_OUTPUT_TRANSLATION("CALLNAME", 対象)%）
;PRINTFORML 気力+{ABS(回復後気力 - 回復前気力)}（%NAME_OUTPUT_TRANSLATION("CALLNAME", 対象)%）
SIF ABS(回復後精力 - 回復前精力) > 0
;	PRINTFORML 精力+{ABS(回復後精力 - 回復前精力)}（%NAME_OUTPUT_TRANSLATION("CALLNAME", 対象)%）
;PRINTFORMW 
TIME += 20
SIF ARG == 1
	TIME += 10

;-------------------------------------------------
;小傘用
;-------------------------------------------------
@UNIQUE_COM_傘の修理
PRINTFORML 小傘修理好了折畳傘
IF ITEM:壊掉的傘
	PRINTFORMW 「再小心一点使用呀」
	DOWNBASE:気力 = 200
	SOURCE:歓楽 += 1000
	ITEM:壊掉的傘 = 0
	ITEM:折畳傘 = 1
	TIME += 45
ELSE
	PRINTFORMW 「要小心使用啊」
	DOWNBASE:気力 = 100
	SOURCE:歓楽 += 1000 + ABL:親密 * 50
	TFLAG:好感度BONUS = FLAG:傘耐久度 / 10
	TFLAG:98 = FLAG:傘耐久度 / 1000
	FLAG:傘耐久度 = 0
ENDIF
SETBIT TFLAG:一日一回, 13
TIME += 30

;-------------------------------------------------
;永琳用
;-------------------------------------------------
@UNIQUE_COM_生薬を売る
#DIM 候補番号
#DIM 候補個数
#DIM 売却個数

PRINTFORML 所持金:%金額表示(MONEY)%
DRAWLINE
;所持している素材道具の売却可能物・個数を列表化
LOCAL:1 = 999
FOR LOCAL:0 ,600, 700
	IF UNIQUE_COM_HARBAL_SALES(LOCAL:0)
		IF ITEM:(LOCAL:0)
			LOCALS:0 = [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}    
			LOCAL:1 += STRLENS(LOCALS:0)
			SIF ITEM:(LOCAL:0) < 10
				LOCAL:1 ++
			IF LOCAL:1 > 90
				IF LOCAL:1 >= 999
				;最初のみ改行しない
				ELSE
					PRINTL 
				ENDIF
				LOCAL:1 = 0
				PRINT 　　
			ENDIF
			PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}
			SIF ITEM:(LOCAL:0) < 10
				PRINT  
			PRINT 　　
		ENDIF
	ENDIF
NEXT
PRINTL 
PRINTL 　　[100] 放弃
DRAWLINE
PRINTFORML 「要卖什么呢？」
$INPUT_LOOP01
INPUT
候補番号 = RESULT

IF 候補番号 == 100
;100は先送り、売却不能物、600未満および700以上の入力は無効化する
ELSEIF UNIQUE_COM_HARBAL_SALES(候補番号) == 0 || 候補番号 < 600 || 候補番号 > 699
	CLEARLINE 1
	GOTO INPUT_LOOP01
ENDIF
SELECTCASE 候補番号
	CASE 100
		RETURN -1
	CASEELSE
		候補個数 = ITEM:(候補番号)
		CLEARLINE 1
		IF 候補個数
			SELECTCASE 候補番号
				CASE 601, 612, 635
					PRINTFORML 「%ITEMNAME:候補番号%啊。有着強烈的毒性、所以根据使用的方法的不同……对吧。
				CASE 603, 618
					PRINTFORML 「%ITEMNAME:候補番号%啊。作为強壮的生薬非常有名……呼呼、想用在谁身上呢？
				CASE 605, 624
					PRINTFORML 「%ITEMNAME:候補番号%啊。如果普通人尝了这个东西的话、会变成非常了不得的事态、所以请注意一下。
				CASE 634
					PRINTFORML 「%ITEMNAME:候補番号%啊。连発掘都会啊、不是挺多才多芸的嘛。
				CASE 646
					PRINTFORML 「%ITEMNAME:候補番号%啊。连伐採都会啊、不是挺多才多芸的嘛。
				CASE 606, 609, 615, 625, 640, 643
					PRINTFORML 「%ITEMNAME:候補番号%…！　真亏你能把这种东西弄到手啊。
				CASEELSE
					PRINTFORM 「%ITEMNAME:候補番号%啊。
			ENDSELECT
			PRINTFORM 　%金額表示(ITEMPRICE:(候補番号) / 2)%一个、怎么样？」
			SETCOLOR 0x777777
			PRINTFORML 　要卖多少個？(输入1～{候補個数}、0为返回)
			RESETCOLOR
			PRINTL   
			PRINTBUTTON "[0] 放弃", 0
			PRINT     
			PRINTBUTTON "[1] 1個", 1
			PRINT     
			IF 候補個数 >= 5
				PRINTBUTTON "[5] 5個", 5
				PRINT     
			ENDIF
			IF 候補個数 >= 10
				PRINTBUTTON "[10] 10個", 10
				PRINT    
			ENDIF
			$INPUT_LOOP02
			INPUT
			SELECTCASE RESULT
				CASE 0
					RESTART
				CASE 1 TO 候補個数
					CLEARLINE 2
					売却個数 = RESULT
					PRINTFORMW 「非常感谢。那么%ITEMNAME:候補番号%{売却個数}个、总计花\\{(ITEMPRICE:(候補番号) / 2 * 売却個数)}买下来了哦」
					MONEY += (ITEMPRICE:(候補番号) / 2 * 売却個数)
					ITEM:(候補番号) -= 売却個数
					RESTART
				CASEELSE
					CLEARLINE 1
					GOTO INPUT_LOOP02
			ENDSELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP01
		ENDIF
ENDSELECT
RESTART

;売却列表に載せない道具を定義
@UNIQUE_COM_HARBAL_SALES(ARG)
#FUNCTION
SELECTCASE ARG
	;★ここに売却列表に載せない道具を記入
	CASE 600, 602, 610, 611, 616, 621, 626 TO 631, 636, 638, 639, 642, 644, 645, 647, 648, 660 TO 669
		RETURNF 0
	;まだ未登録の道具
	CASE 649 TO 659, 670 TO 699
		RETURNF 0
	CASEELSE
		RETURNF ARG
ENDSELECT

;-------------------------------------------------
;潤美用
;-------------------------------------------------
@UNIQUE_COM_魚の交換
#DIM PREV_DAY
#DIM CNG_NUM

IF PREV_DAY == DAY
	PRINTFORMW 「干嘛？今天不是已经跟你交換过了吗？」
	RETURN
ENDIF
CNG_NUM = GET_INT(0, "キャラデータ", TARGET, "固有指令：魚の交換比率")

PRINTFORML 「想要好吃的魚？」
PRINTFORML 「让我想想。有普通的魚{CNG_NUM}尾的话跟你交換一尾也可以哟」
PRINTFORML 「怎么样？」
CALL ASK_M("还是算了", 1, "交換", ITEM:魚 >= CNG_NUM)
SELECTCASE RESULT
CASE 0
	PRINTFORML 「这样啊」
CASE 1
	PRINTFORML 「谢谢惠顾。1、2、3……嗯、確实数量没问题」
	PRINTFORML 「来、拿着、你的魚！」
	PREV_DAY = DAY
	CALL LOST_ITEM("魚", CNG_NUM, 1)
	CALL GET_ITEM("好吃的魚", 1, 2)
ENDSELECT


@UNIQUE_COM_FISH_CHANGE_RATE
#FUNCTION
RETURNF 


;-------------------------------------------------
;瓔花用
;-------------------------------------------------
@UNIQUE_COM_今日の積み石テーマ

	;PRINTFORMW 「今日の積石竞赛のカップル部門のテーマが知りたいの？」
	PRINTFORMW 「想知道今天的积石比赛情侣组的主题？」
IF STONE_PLAYCOUNT:2 == 16 && (STONE_UKETSUKEDAY + 14 - DAY:0) > 0 && (STONE_UKETSUKEDAY + 14 - DAY:0) < 15
	;PRINTFORMW 「残念だけど…{STONE_UKETSUKEDAY + 14 - DAY:0}日後まで積石竞赛はお休みなんだよねー」
	;PRINTFORMW 「なんでって……それは%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%が一番よーく知っているんじゃない？」
	PRINTFORMW 「很遗憾…积石竞赛要休息到{STONE_UKETSUKEDAY + 14 - DAY:0}日后ー」
	PRINTFORMW 「要问为什么……%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不该是最清楚的那个吗？」
ELSE
	SELECTCASE DAY:3
		CASE 1
			;PRINTFORMW 「今日のコンテスト名は『積み石は春風とともに』……春がテーマだね」
			;PRINTFORML
			;PRINTFORML 「春が好きな子が得意だよ。逆に夏秋冬が好きな子は苦手な傾向があるみたい…」
			;PRINTFORMW 「春のイメージが強い花が好きな子も得意かも？」
			;PRINTFORMW 「四季テーマは定期的にやってるからこの季節が苦手でも別の季節のときにチャレンジしてほしいな」
			PRINTFORMW 「今天的比赛名为『垒石伴春风』……以春天为主题」
			PRINTFORML
			PRINTFORML 「喜欢春天的孩子很擅长哦。相反喜欢夏秋冬的孩子好像有不擅长的倾向…」
			PRINTFORMW 「喜欢春天印象强烈的花的孩子可能也很擅长？」
			PRINTFORMW 「四季主题是定期做的，即使不擅长这个季节，也希望能在别的季节挑战一下」
		CASE 2
			;PRINTFORMW 「今日のコンテスト名は『積み上げる皿が一枚足りない…』……お皿がテーマだね」
			;PRINTFORML
			;PRINTFORML 「私以外の水子はご飯を食べないから食器は積み上げるためのものでしかないんだよねー」
			;PRINTFORMW 「食器とかお皿が好きな子が得意だね。小刀とかお皿以外の食器好きだと影響しない気がするよ」
			;PRINTFORMW 「あと料理が得意だったりお皿をよく扱う女僕さんなんかも有利みたい」
			PRINTFORMW 「今天的比赛名为『堆积的盘子少一个…』……以盘子为主题」
			PRINTFORML
			PRINTFORML 「除了我以外的水子都不吃饭，所以餐具只是用来堆积的ー」
			PRINTFORMW 「喜欢餐具和盘子的孩子很擅长呢。喜欢小刀和盘子以外的餐具的话感觉不会有影响哦」
			PRINTFORMW 「另外擅长料理经常端盘子的女仆之类的似乎也很有利」
		CASE 3
			;PRINTFORMW 「今日のコンテスト名は『こんどの積み石はこわい』……ホラーがテーマだね」
			;PRINTFORML
			;PRINTFORML 「こら、水子の存在がそもそもホラーとか言わない」
			;PRINTFORMW 「まあ分かりやすく血とか闇とか血とかそういうのが好きだと得意みたい」
			;PRINTFORMW 「ちなみに傘の妖怪がチャレンジしたことがあるんだけどその子はテーマ加点はなかったよ」
			PRINTFORMW 「今天的比赛名为『这次的积石好可怕』……以恐怖为主题」
			PRINTFORML
			PRINTFORML 「嚯啦、水子的存在根本就称不上恐怖」
			PRINTFORMW 「嘛，简单易懂，喜欢血、黑暗、类似血的东西的人好像很擅长」
			PRINTFORMW 「顺便说一下，伞的妖怪曾经挑战过，但那个孩子没有主题加分哦」
		CASE 4
			;PRINTFORMW 「今日のコンテスト名は『題名のない積石竞赛』……音楽がテーマだね」
			;PRINTFORML
			;PRINTFORML 「積み石しながら音楽を歌ったり奏でたりしてもらうんだ」
			;PRINTFORMW 「だから当然音楽や楽器が得意だったり、好きだと評価が上がるよー」
			PRINTFORMW 「今天的比赛名为『无题的积石竞赛』……以音乐为主题」
			PRINTFORML
			PRINTFORML 「一边堆着石头一边唱歌演奏」
			PRINTFORMW 「所以当然擅长、喜欢音乐和乐器的人评价都会上升哦ー」
		CASE 5
			;PRINTFORMW 「今日のコンテスト名は『ゆめかわメルヘン積み石』……メルヘンがテーマだね」
			;PRINTFORML
			;PRINTFORML 「可愛的ものが好きだと得意みたい。逆に帥氣的ものや可怕的ものが好きだと少し苦手だね」
			;PRINTFORMW 「可愛的ものが好きって恥ずかしくて言えない子でもハートとか粉色が好きならセーフだよ」
			;PRINTFORMW 「細かいところだと…ゆめかわって名前の街道実は夢要素もちょっぴり加点対象だね」
			PRINTFORMW 「今天的比赛名为『梦河童话堆石』……以童话为主题」
			PRINTFORML
			PRINTFORML 「喜欢可爱东西的就会很擅长。相反，喜欢帅气或恐怖的东西的话会有点不擅长呢」
			PRINTFORMW 「即使是因为害羞不好意思说喜欢可爱东西的孩子，只要喜欢心形或者粉色也是可以算的哦」
			PRINTFORMW 「细节上的话…就像名字一样，其实梦的要素也是加分对象」
		CASE 6
			;PRINTFORMW 「今日のコンテスト名は『水子にも衣装』……布飾がテーマだね」
			;PRINTFORML
			;PRINTFORML 「布を使って積み石を飾り立てるデコり大会だね」
			;PRINTFORMW 「人形とか布娃娃好きだと稍微得意になるみたい」
			;PRINTFORMW 「ちなみに針が好きでも得意になる傾向があるんだけど…その針って本当に布飾用？」
			PRINTFORMW 「今天的比赛名为『水子也要穿衣服』……以布饰为主题」
			PRINTFORML
			PRINTFORML 「这是一场用布料装饰积石的装饰大会」
			PRINTFORMW 「喜欢人偶和布娃娃的话好像会变得有点擅长」
			PRINTFORMW 「顺便说一下，即使喜欢针也有擅长的倾向…那个针真的是布饰用的吗？」
		CASE 7
			;PRINTFORMW 「今日のコンテスト名は『虹色に積み上げて』……多彩がテーマだね」
			;PRINTFORML
			;PRINTFORML 「特定の色だけではなく多彩なのが条件だから特別な美的センスが問われると評判だよ」
			;PRINTFORMW 「好きな色が…というよりは嫌いな色が無い人が評価上げやすい傾向にあるかな」
			PRINTFORMW 「今天的比赛名为『堆积彩虹』……以多彩为主题」
			PRINTFORML
			PRINTFORML 「因为条件是多彩，不仅仅是特定的颜色，所以被认为需要特别的审美感」
			PRINTFORMW 「与其说有喜欢的颜色…不如说没有讨厌的颜色的人更容易得到好评吧」
		CASE 8
			;PRINTFORMW 「今日のコンテスト名は『積み石は夏風とともに』……夏がテーマだね」
			;PRINTFORML
			;PRINTFORML 「夏が好きな子が得意だよ。逆に春秋冬が好きな子は苦手な傾向があるみたい…」
			;PRINTFORMW 「夏のイメージが強い花が好きな子も得意かも？」
			;PRINTFORMW 「四季テーマは定期的にやってるからこの季節が苦手でも別の季節のときにチャレンジしてほしいな」
			PRINTFORMW 「今天的比赛名为『垛石伴夏风』……以夏天为主题」
			PRINTFORML
			PRINTFORML 「喜欢夏天的孩子很擅长哦。相反喜欢春秋冬的孩子似乎有不擅长的倾向…」
			PRINTFORMW 「喜欢夏天印象强烈的花的孩子可能也很擅长？」
			PRINTFORMW 「四季主题是定期做的，即使不擅长这个季节，也希望能在别的季节挑战一下」
		CASE 9
			;PRINTFORMW 「今日のコンテスト名は『大陸風積み石』……まんま大陸風がテーマだね」
			;PRINTFORML
			;PRINTFORML 「当然和風や洋風が好きだと苦手みたいだね」
			;PRINTFORMW 「ううう…大陸風っていうと…あのこわーい邪仙のイメージが…」
			PRINTFORMW 「今天的比赛名为『中国风积石』……就是以中国风为主题」
			PRINTFORML
			PRINTFORML 「当然喜欢和风和西式的话就不擅长了」
			PRINTFORMW 「唔…说起中国风…就会想起那个可怕的邪仙…」
		CASE 10
			;PRINTFORMW 「今日のコンテスト名は『積み石動物園』……動物がテーマだね」
			;PRINTFORML
			;PRINTFORML 「動物っぽい積み石作品を作るのは分かりやすくて大人気テーマなんだよ」
			;PRINTFORMW 「注意点としては分かりやすくアニマルイメージの四足歩行動物と鳥みたいなのと虫や魚ではそれぞれ得点が違うって所かな」
			;PRINTFORMW 「動物の仲間だって分かってても虫のことアニマルに入れたくなくない？」
			PRINTFORMW 「今天的比赛名为『积石动物园』……以动物为主题」
			PRINTFORML
			PRINTFORML 「制作动物般的堆石作品是通俗易懂的超人气主题哦」
			PRINTFORMW 「需要注意的是，简单易懂的四足步行动物形象、鸟之类的动物、昆虫和鱼类的得分各不相同」
			PRINTFORMW 「即使知道都是动物伙伴，也不想把虫子归进动物一类里吗？」
		CASE 11
			;PRINTFORMW 「今日のコンテスト名は『積み石バトル！！』……戦いがテーマだね」
			;PRINTFORML
			;PRINTFORML 「戦いがテーマだから…戦いとか刃物や銃が得意な人が得意かなー」
			;PRINTFORMW 「逆に刃物や弾や血生臭いのが苦手な私達水子みたいなのはあんま得意なテーマじゃないかも」
			;PRINTFORMW 「ちなみに刃物と銃のどちらかは得意だけどもう片方が苦手…みたいな子は心配しちゃうかもだけど…」
			;PRINTFORMW 「加点と減点が合わさって得点無し！なんてことは無いから安心してね。ちゃんと加点が優先されるから」
			PRINTFORMW 「今天的比赛名为『积石对战！！』……以战斗为主题」
			PRINTFORML
			PRINTFORML 「因为是以战斗为主题的…所以擅长战斗和刀枪的人比较拿手ー」
			PRINTFORMW 「相反，像我们水子这样不喜欢刀具、子弹和血腥的人，也许就不是很擅长这种主题」
			PRINTFORMW 「顺便说一下，只擅长刀具和火枪其中一种而不擅长另一种的…像这样的孩子可能会担心…」
			PRINTFORMW 「不会出现加分和减分合在一起就没有得分的情况，放心吧。肯定会优先加分的」
		CASE 12
			;PRINTFORMW 「今日のコンテスト名は『ROCK TO THE FUTURE』……未来がテーマだね」
			;PRINTFORML
			;PRINTFORML 「ま、水子に未来なんてないんだけど…」
			;PRINTFORMW 「機械仕掛けとか未来を感じさせるのが好きだと得意で、逆に懷舊好きは苦戦しちゃうみたい」
			;PRINTFORMW 「ま、水子には過去なんてのもほぼ存在して無いんだけど…」
			PRINTFORMW 「今天的比赛名为『ROCK TO THE FUTURE』……以未来为主题」
			PRINTFORML
			PRINTFORML 「嘛、虽然水子没有未来…」
			PRINTFORMW 「喜欢机械装置和未来感的很擅长、相反喜欢复古的人就会陷入苦战」
			PRINTFORMW 「嘛、虽然对水子来说过去什么的也几乎是不存在的…」
		CASE 13
			;PRINTFORMW 「今日のコンテスト名は『積み石は秋風とともに』……秋がテーマだね」
			;PRINTFORML
			;PRINTFORML 「秋が好きな子が得意だよ。逆に春夏冬が好きな子は苦手な傾向があるみたい…」
			;PRINTFORMW 「秋のイメージが強い叶子が好きな子も得意かも？」
			;PRINTFORMW 「四季テーマは定期的にやってるからこの季節が苦手でも別の季節のときにチャレンジしてほしいな」
			PRINTFORMW 「今天的比赛名为『堆石伴秋风』……以秋天为主题」
			PRINTFORML
			PRINTFORML 「喜欢秋天的孩子很擅长哦。相反喜欢春夏冬的孩子似乎有不擅长的倾向…」
			PRINTFORMW 「喜欢秋天印象强烈的叶子的孩子可能也很擅长？」
			PRINTFORMW 「四季主题是定期做的，即使不擅长这个季节，也希望能在别的季节挑战一下」
		CASE 14
			;PRINTFORMW 「今日のコンテスト名は『ONCE UPON TSUMIISHI』……懷舊がテーマだね」
			;PRINTFORML
			;PRINTFORML 「ま、水子に過去なんてほぼないんだけど…」
			;PRINTFORMW 「逝去的とか昔を感じさせるのが好きだと得意で、逆に未来的なもの好きは苦戦しちゃうみたい」
			;PRINTFORMW 「ま、水子には未来なんてのも存在して無いんだけど…」
			PRINTFORMW 「今天的比赛名为『ONCE UPON TSUMIISHI』……以复古为主题」
			PRINTFORML
			PRINTFORML 「嘛、虽然水子几乎没有过去…」
			PRINTFORMW 「喜欢失去感或者复古感的很擅长、相反，喜欢未来事物的人就会陷入苦战」
			PRINTFORMW 「嘛、虽然对水子来说也不存在未来什么的…」
		CASE 15
			;PRINTFORMW 「今日のコンテスト名は『積み石マーケット』……高級がテーマだね」
			;PRINTFORML
			;PRINTFORML 「積み石バイヤーが打分担当で、高値をつけてでも買いたい作品を選ぶんだ」
			;PRINTFORMW 「やっぱり高いものを普段から好んでる人ほどうまく作れるみたい」
			;PRINTFORMW 「あっ！高いものを好んでようが貧乏神はやめといた方が良いと思う…」
			PRINTFORMW 「今天的比赛名为『积石市场』……以高级为主题」
			PRINTFORML
			PRINTFORML 「积石买家负责评分、他们会出高价挑选自己想买的作品」
			PRINTFORMW 「果然还是平时喜欢贵东西的人能做得更好」
			PRINTFORMW 「啊！即使很喜欢贵的东西但贫穷神还是放弃比较好…」
		CASE 16
			;PRINTFORMW 「今日のコンテスト名は『ウルトラスーパーデラックス積み石』……誇張がテーマだね」
			;PRINTFORML
			;PRINTFORML 「本来慎ましくあるべきの積み石を誇張にするというある意味喧嘩を売ってるテーマだね」
			;PRINTFORMW 「誇張なものが好きだと得意で樸素とか繊細なのが好きだと苦手な傾向があるよ」
			;PRINTFORMW 「あと白黒灰みたいな樸素的色が好きでも苦手みたい…」
			PRINTFORMW 「今天的比赛名为『超级超级豪华堆积石』……以华丽为主题」
			PRINTFORML
			PRINTFORML 「把本来应该谨慎的砌石弄得很花哨，从某种意义上来说是在找茬的主题」
			PRINTFORMW 「喜欢华丽东西的就比较擅长，喜欢简单细腻东西的会有不擅长的倾向」
			PRINTFORMW 「还有就是喜欢黑白灰这样简单颜色的好像也不擅长…」
		CASE 17
			;PRINTFORMW 「今日のコンテスト名は『和風積み石』……そのまんま和風がテーマだね」
			;PRINTFORML
			;PRINTFORML 「当然大陸風や洋風が好きだと苦手みたいだね」
			;PRINTFORMW 「三途の河も扱い的には和風…なのかなぁ」
			PRINTFORMW 「今天的比赛名为『和风积石』……就是这样以和风为主题」
			PRINTFORML
			PRINTFORML 「当然喜欢中国风和西式的话就不擅长了」
			PRINTFORMW 「三途之川也可以看作是和风…的吧」
		CASE 18
			;PRINTFORMW 「今日のコンテスト名は『石と仲良くなろう』……そのまんま鉱石がテーマだね」
			;PRINTFORML
			;PRINTFORML 「積み石で扱う普通の石だけでなく、色んな石を使って積み石してもらうよ！」
			;PRINTFORMW 「宝石盗もうとしたら地蔵様に押しつぶされるからやめとくのをお勧めするね」
			PRINTFORMW 「今天的比赛名为『和石头成为好朋友吧』……就这样以矿石为主题」
			PRINTFORML
			PRINTFORML 「不只是用普通的石头进行堆积、还要用各种各样的石头来积石哟！」
			PRINTFORMW 「如果想偷宝石的话会被地藏大人压碎的，所以建议你不要做这种事」
		CASE 19
			;PRINTFORMW 「今日のコンテスト名は『ロジカル積み石システム』……知性がテーマだね」
			;PRINTFORML
			;PRINTFORML 「賢い人が本気で賢く作ったら一体どんな積み石が出来るんだろう…って発想で生まれたテーマだよ」
			;PRINTFORMW 「賢い人は勿論、本を好んで読んでる人も得意みたい」
			;PRINTFORMW 「私も積み石の本があったら頑張って読むんだけどなー」
			PRINTFORMW 「今天的比赛名为『逻辑积石系统』……以智慧为主题」
			PRINTFORML
			PRINTFORML 「聪明人如果认真聪明地做的话，到底会做出什么样的积石呢…这是在这样的想法中诞生的主题哦」
			PRINTFORMW 「聪明人自不必说，喜欢看书的人似乎也很擅长」
			PRINTFORMW 「如果有积石的书，我也会努力读的ー」
		CASE 20
			;PRINTFORMW 「今日のコンテスト名は『積み石は冬風とともに』……夏がテーマだね」
			;PRINTFORML
			;PRINTFORML 「冬が好きな子が得意だよ。逆に春夏秋が好きな子は苦手な傾向があるみたい…」
			;PRINTFORMW 「冬のイメージが強い氷とか雪が好きな子も得意かも？」
			;PRINTFORMW 「四季テーマは定期的にやってるからこの季節が苦手でも別の季節のときにチャレンジしてほしいな」
			PRINTFORMW 「今天的比赛名为『积石伴冬风』……以冬天为主题」
			PRINTFORML
			PRINTFORML 「喜欢冬天的孩子很擅长哦。相反喜欢春夏秋的孩子似乎有不擅长的倾向…」
			PRINTFORMW 「喜欢冬季印象强烈的冰和雪的孩子可能也很擅长？」
			PRINTFORMW 「四季主题是定期做的，即使不擅长这个季节，也希望能在别的季节挑战一下」
		CASE 21
			;PRINTFORMW 「今日のコンテスト名は『ツみ石こンてすトカッぷル部もん』……カオスがテーマだね」
			;PRINTFORML
			;PRINTFORML 「混沌な感じが好きな子が得意で逆に秩序的な感じが好きな子が苦手みたい」
			;PRINTFORMW 「明日は秩序テーマだから、こういうのが苦手なら明日改めてってのもありかも」
			PRINTFORMW 「今天的比赛名为『即使竟塞氢铝族』……以混沌为主题」
			PRINTFORML
			PRINTFORML 「喜欢混沌感的孩子很擅长，相反喜欢秩序感的孩子不擅长」
			PRINTFORMW 「明天是秩序主题、如果不擅长这个的话明天再来吧」
		CASE 22
			;PRINTFORMW 「今日のコンテスト名は『積石竞赛カップル部門』……秩序がテーマだね」
			;PRINTFORML
			;PRINTFORML 「秩序な感じが好きな子が得意で逆に混沌的な感じが好きな子が苦手みたい」
			;PRINTFORMW 「混沌な感じが好きな人は……昨日だったんだよねぇ残念」
			PRINTFORMW 「今天的比赛名为『积石竞赛情侣组』……以秩序为主题」
			PRINTFORML
			PRINTFORML 「喜欢秩序感的孩子很擅长，相反喜欢混沌感的孩子不擅长」
			PRINTFORMW 「喜欢混沌感的人……是昨天呢，真遗憾」
		CASE 23
			;PRINTFORMW 「今日のコンテスト名は『洋風積み石』……そのまんま洋風がテーマだね」
			;PRINTFORML
			;PRINTFORML 「当然大陸風や和風が好きだと苦手みたいだね」
			;PRINTFORMW 「洋風積み石といえばストーンヘンジってやつだって眼镜かけた女の子が言ってたけど…」
			;PRINTFORMW 「ストーンヘンジ…チャレンジしてみる？」
			PRINTFORMW 「今天的比赛名为『西式积石』……就是这样以西式为主题」
			PRINTFORML
			PRINTFORML 「当然喜欢中国风和和风的话就不擅长了」
			PRINTFORMW 「一个戴眼镜的女孩说过，说起西式积石就是巨石阵…」
			PRINTFORMW 「巨石阵…想挑战一下吗？」
		CASE 24
			;PRINTFORMW 「今日のコンテスト名は『積み石の星になれ』……天体がテーマだね」
			;PRINTFORML
			;PRINTFORML 「星空がテーマなのですけど太陽が好きでも活躍できるよ」
			;PRINTFORMW 「ただ闇は天体のイメージがありそうですけど加点も減点も無いので注意してねー」
			PRINTFORMW 「今天的比赛名为『成为积石之星』……以天体为主题」
			PRINTFORML
			PRINTFORML 「虽然是以星空为主题，但喜欢太阳的也可以大显身手哦」
			PRINTFORMW 「虽说纯粹的黑暗似乎会给人一种天体的印象，但既不是加分项也不是减分项，请注意哦ー」
		CASE 25
			;PRINTFORMW 「今日のコンテスト名は『水の子コンテスト』……水がテーマだね」
			;PRINTFORML
			;PRINTFORML 「ある意味最も身近な存在である水がテーマだよ」
			;PRINTFORMW 「水棲生物好きが強いかも？」
			;PRINTFORMW 「ちなみに三途の河に潜るのは絶対にやめた方が良いよ。命が惜しいならね」
			PRINTFORMW 「今天的比赛名为『水之子竞赛』……以水为主题」
			PRINTFORML
			PRINTFORML 「从某种意义上来说主题是我们最亲近的水」
			PRINTFORMW 「喜欢水栖生物的会很强吧？」
			PRINTFORMW 「顺便说一句，要命的话千万别往三途之川里钻」
		CASE 26
			;PRINTFORMW 「今日のコンテスト名は『守矢プレゼンツ積石竞赛』……守矢がテーマだね」
			;PRINTFORML
			;PRINTFORML 「今日は守矢の方々が出資している特別回…だから会場に広告がいーっぱい！」
			;PRINTFORMW 「守矢は範囲狭窄と思いきや、風とか大地とかあと巫女とか神霊好きでも得意な傾向があるみたい」
			;PRINTFORMW 「ちなみに青蛙と蛇のどちらかは得意だけどもう片方が苦手…みたいな子は心配しちゃうかもだけど…」
			;PRINTFORMW 「加点と減点が合わさって得点無し！なんてことは無いから安心してね。ちゃんと加点が優先されるから」
			PRINTFORMW 「今天的比赛名为『守矢赞助积石竞赛』……以守矢为主题」
			PRINTFORML
			PRINTFORML 「今天是守矢的各位出资的特别回…所以会场里广告非常多！」
			PRINTFORMW 「原以为守矢的范围很小、没想到喜欢风、大地、巫女、神灵的都有擅长的倾向」
			PRINTFORMW 「顺便说一下，只喜欢青蛙和蛇其中一个而不喜欢另一个的…像这样的孩子可能会担心…」
			PRINTFORMW 「不会出现加分和减分合在一起就没有得分的情况，放心吧。肯定会优先加分的」
		CASE 27
			;PRINTFORMW 「今日のコンテスト名は『マジカルストーン蔷薇ンシング』……魔法使がテーマだね」
			;PRINTFORML
			;PRINTFORML 「そのまま魔法や魔女に精通してる人が勿論強いは強いんだけど…」
			;PRINTFORMW 「評価するのは私達水子だからね！カラスや箒だけでも魔女扱いするよ」
			;PRINTFORMW 「何なら書物だけでも魔法使判定しちゃうかも」
			PRINTFORMW 「今天的比赛名为『魔法石平衡』……以魔法使为主题」
			PRINTFORML
			PRINTFORML 「就那样精通魔法和魔女的人当然很强，不过…」
			PRINTFORMW 「因为评分的是我们水子！就算只有乌鸦和扫帚也会被当做魔女对待的哦」
			PRINTFORMW 「只要有书就可能会被判定为魔法使」
		CASE 28
			;PRINTFORMW 「今日のコンテスト名は『兔子之日』……文字街道バニーがテーマだね」
			;PRINTFORML
			;PRINTFORML 「毎月２８日は兔子之日！ということでそれに合わせた企画なんだけど……」
			;PRINTFORMW 「いったい何なんだろうね兔子之日って……私も着せられてるし……」
			;PRINTFORMW 「えーっと……とりあえずバニーに関連するものが好きな人が有利だよ」
			;PRINTFORMW 「月とか……あと卡片とか……勛章とか……」
			;PRINTFORMW 「あと恥ずかしい服装が苦手な人はダメみたいだね」
			PRINTFORMW 「今天的比赛名为『兔子之日』……顾名思义，以兔子为主题」
			PRINTFORML
			PRINTFORML 「每个月的２８号都是兔子之日！所以才会有相应的企划……」
			PRINTFORMW 「兔子之日到底是怎么回事呢……我也被要求穿上了……」
			PRINTFORMW 「嗯ー……总之喜欢兔子有关东西的人比较有利哦」
			PRINTFORMW 「月亮啦……还有卡片啦……勛章啦……」
			PRINTFORMW 「还有，不擅长羞耻服装的人好像不行呢」
		CASE 29
			;PRINTFORMW 「今日のコンテスト名は『自然のマリアージュ、石と植物の調和』……植物がテーマだね」
			;PRINTFORML
			;PRINTFORML 「植物自体が好きだととてもうまくて、そうでなくても花や植物がなにかしら好きならって感じだね」
			;PRINTFORMW 「ちなみに蘑菇は植物ではありません！ざんねーん！」
			PRINTFORMW 「今天的比赛名为『自然的契合、石头和植物的协调』……以植物为主题」
			PRINTFORML
			PRINTFORML 「喜欢植物本身就很好，即使不是那样，如果喜欢什么花和植物的话也行」
			PRINTFORMW 「顺便说一下，蘑菇不是植物！真遗憾！」
		CASE 30
			;PRINTFORMW 「今日のコンテスト名は『自然のマリアージュ、空と風の調和』……風がテーマだね」
			;PRINTFORML
			;PRINTFORML 「風そのものや空を飛ぶいきものが好きだと有利みたい」
			;PRINTFORMW 「ただの風じゃなくて空って部分も重要だから地底好きな人は苦戦するかもしれないね」
			;PRINTFORMW 「天体が好きなのは別に補正かからないからそこは注意かも」
			PRINTFORMW 「今天的比赛名为『自然的契合、天空与风的协调』……以风为主题」
			PRINTFORML
			PRINTFORML 「喜欢风本身或者在空中飞翔的生物的话好像很有利」
			PRINTFORMW 「不仅仅是风，天空的部分也很重要，所以喜欢地底的人可能会陷入苦战」
			PRINTFORMW 「喜欢天体是没有修正的，这一点可能需要注意」
		CASE 31
			;PRINTFORMW 「今日は何でもありの特別な日！」
			;PRINTFORML
			;PRINTFORML 「他の日のテーマの中から一つ選んで、そのテーマで積み石を作ることができるよ」
			;PRINTFORMW 「優勝したらその指定した日のテーマで優勝したことになるんだよね」
			;PRINTFORMW 「つまりうっかりやり忘れたら次は３１日後ってならないための救済処置だね！」
			PRINTFORMW 「今天是什么都有的特别的日子！」
			PRINTFORML
			PRINTFORML 「你可以从其他日子的主题中选择一个、用那个主题制作积石哦」
			PRINTFORMW 「获胜的话就等于获得了那个指定日期主题的冠军」
			PRINTFORMW 「也就是说，这是一种补救措施，如果不小心忘记了，下次就要等到31天后了！」
	ENDSELECT
ENDIF
;-------------------------------------------------
;龍用
;-------------------------------------------------
@UNIQUE_COM_龍珠の取引
#DIM ORB_PRICE
#DIM SALES

IF !ITEM:龍珠
	;PRINTFORML 龍珠を持っていない…
	PRINTFORML 没有龙珠…
	RETURN -1
ENDIF

;売価は筹码交易レート×90×√話術Lvで決定
ORB_PRICE = CHARISMA_NOW() * 90
SELECTCASE ABL:MASTER:話術技能
	CASE 0
		TIMES ORB_PRICE, 1.0
	CASE 1
		TIMES ORB_PRICE, 1.2
	CASE 2
		TIMES ORB_PRICE, 1.4
	CASE 3
		TIMES ORB_PRICE, 1.7
	CASE 4
		TIMES ORB_PRICE, 2.0
	CASE 5
		TIMES ORB_PRICE, 2.2
	CASE 6
		TIMES ORB_PRICE, 2.4
	CASEELSE
		TIMES ORB_PRICE, 2.6
ENDSELECT

PRINTL 
PRINTDATAW
	;DATAFORM 「持て余しているならその龍珠、引き取ろう。無論、それなりの対価は支払うよ」
	;DATAFORM 「ん？　そんなものを買い取ってどうするのかって？　はてさて、どうしたものか？」
	;DATAFORM 「どこから掘り出してきたものやら。まあ、使えるモノであることに変わりはない」
	;DATAFORM 「それを手にしているということは、那个に見込まれたということか。いやはや、人は見かけによらないものだね」
	;DATAFORM 「誰かにそそのかされて…というわけでもなさそうだ。では、取引といこうか」
	;DATAFORM 「レアアースの供給元は多いに越したことはないしな。松鼠クヘッジ、というやつさ」
	DATAFORM 「如果是多到有剩，那颗龙珠我就收下了。当然、我会支付相应的报酬的」
	DATAFORM 「嗯？　你问我买这些东西有什么用？　哎呀哎呀、该怎么说呢？」
	DATAFORM 「不知从哪里挖来的东西。好吧、是可以用的东西这一点是不会变的」
	DATAFORM 「能够手拿着那个、也就是说你被它认可了吗。哎呀哎呀，人不可貌相啊」
	DATAFORM 「好像也不是在某人的唆使下…那么、做个交易吧」
	DATAFORM 「稀有资源的供给源越多越好。就是所谓的风险对冲」
ENDDATA
;PRINTFORML 「龍珠１個を%金額表示(ORB_PRICE)%で買い取ろう。どうかな？」
PRINTFORML 「用%金額表示(ORB_PRICE)%买一个龙珠。怎么样？」
PRINTL 
DRAWLINE
PRINTFORML 所持金:%金額表示(MONEY)%　　現在的￥/筹码レート:%金額表示(CHARISMA_NOW())%←→1CM
SETCOLOR 0x777777
;PRINTFORML 　何個売りますか？(1～{ITEM:龍珠}の個数を入力、0で返回)
PRINTFORML 　卖几个？(输入1～{ITEM:龍珠}的个数、0返回)
RESETCOLOR
PRINTL   
PRINTBUTTON "[0] 放弃", 0
PRINT     
PRINTBUTTON "[1] 1个", 1
PRINT     
IF ITEM:龍珠 >= 5
	PRINTBUTTON "[5] 5个", 5
	PRINT     
ENDIF
IF ITEM:龍珠 >= 10
	PRINTBUTTON "[10] 10个", 10
	PRINT    
ENDIF
$INPUT_LOOP01
INPUT
SELECTCASE RESULT
	CASE 0
		CLEARLINE 2
		;PRINTFORMW 「そうか。取引額は筹码交易レートに応じた時価だから、その気になったらまた来てくれ」
		PRINTFORMW 「是吗。交易额是根据筹码交易汇率的时价、如果你有意的话再来吧」
		RETURN
	CASE 1 TO ITEM:龍珠
		CLEARLINE 2
		SALES = RESULT
		;PRINTFORMW 「それでは龍珠を{SALES}つ、%金額表示(ORB_PRICE * SALES)%で買い取らせてもらおう」
		PRINTFORMW 「那么让我以%金額表示(ORB_PRICE * SALES)%的价格买下{SALES}个龙珠吧」
		CALL GET_MONEY_YEN(ORB_PRICE * SALES, 1)
		CALL LOST_ITEM("龍珠", SALES, 2)
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP01
ENDSELECT

