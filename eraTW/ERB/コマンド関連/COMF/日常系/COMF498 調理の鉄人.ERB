@COM498
#DIMS 対戦相手
#DIMS 相手の料理
#DIM DISH_TYPE_NUM
#DIM SCORE, 3
#DIM JUDGE, 3
#DIM TOTAL_PLAYER
#DIM TOTAL_ENEMY
#DIM ENEMY_LV
#DIM C_ID
#DIMS THEME
#DIMS TASTE_TREND_GOOD
#DIMS TASTE_TREND_BAD
#DIMS DISH_NAME_ENEMY
#DIMS DISH_TASTE_ENEMY
#DIM BASEPOINT_TEMP
#DIMS WINNER
#DIM DishEXP
#DIM FirstWinBonus

PRINTFORML 请选择对战对手
IF !TARGET == CFLAG:MASTER
	CALL ASK_M("新手",1,"入门",1,"高手",1,"老手",1,CALLNAME:TARGET,1,"取消",1)
ELSE
	CALL ASK_M("新手",1,"入门",1,"高手",1,"老手",1,"取消",1)
ENDIF
SELECTCASE RESULT
	CASE 0
		対戦相手 = 新手料理人
		ENEMY_LV = 1
	CASE 1
		対戦相手 = 入门料理人
		ENEMY_LV = 3
	CASE 2
		対戦相手 = 高手料理人
		ENEMY_LV = 4
	CASE 3
		対戦相手 = 老手料理人
		ENEMY_LV = 5
	CASE 4
		SIF TARGET == CFLAG:MASTER
			RETURN -1
		対戦相手 = %CALLNAME:TARGET%
		ENEMY_LV = ABL:TARGET:料理技能
	CASEELSE
		RETURN -1
ENDSELECT
SETBIT TFLAG:一日一回, 14
DISH_TYPE_NUM = RAND:2 + 1
;レベル不足の時は軽食のみ
SIF ABL:MASTER:料理技能 < 2
	DISH_TYPE_NUM = 1

SELECTCASE DISH_TYPE_NUM
	CASE 1
		DISH_TYPE = 軽食
	CASE 2
		DISH_TYPE = 主食
ENDSELECT
IF DISH_TYPE == "軽食"
	THEME = %TEXTR("和食/洋食/中華/肉料理/魚料理/汁物/油炸食品/煮物/開胃/家庭料理")%
ELSEIF DISH_TYPE == "主食"
	THEME = %TEXTR("和食/洋食/中華/肉料理/魚料理/麺類/鍋物/汁物/油炸食品/謎料理/家庭料理")%
ENDIF

PRINTFORML 「好，各位观众！接下来即将开幕的是大家期待已久的——料理铁人！今日灶台上燃起的火焰，究竟会点燃胜利的意志，还是化作败北的泪水呢！」
IF 対戦相手 == CALLNAME:TARGET
	PRINTFORML 「本次的主题是【%THEME%】。若我的记忆无误，说到%THEME%当属此女！」
ELSE
	PRINTFORML 「本次的主题是【%THEME%】。若我的记忆无误，说到%THEME%当属此男！」
ENDIF
PRINTFORML 「请大家用热烈的掌声欢迎！%THEME%的铁人，%対戦相手%！」
SIF THEME == "家庭料理"
	THEME = 家庭的
PRINTFORML 「而挑战者则是%CALLNAME:MASTER%！」
PRINTFORML 「评审团由以下诸位组成」

$JUDGE_SELECT
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
ARRAYCOPY "SHAFFLE_ARRAY" , "JUDGE"

;テーマの味が嫌いな審査員だった場合再抽選
;対戦相手,なりきり時は自分だった場合も再抽選
FOR LOCAL, 0 , 3
	C_ID = JUDGE:LOCAL + 1
	SIF STRCOUNT(GET_STR(, "キャラデータ", C_ID, "料理：讨厌的味道"), THEME)
		GOTO JUDGE_SELECT
	SIF C_ID == TARGET
		GOTO JUDGE_SELECT
	SIF C_ID == FLAG:なりきり
		GOTO JUDGE_SELECT
NEXT

FOR LOCAL, 0 , 3
	JUDGE:LOCAL += 1
	C_ID = JUDGE:LOCAL
	CALL PRINT_FACE, C_ID, "通常", "服", "", ""
	PRINTFORML 
	PRINTFORML 【评审员 No.{LOCAL + 1} %CALLNAME:C_ID%】
	PRINTL 喜欢的味道：
	CALL SHOW_TASTE(GET_STR(, "キャラデータ", C_ID, "料理：喜欢的味道"), "POSITIVE", ABL:C_ID:親密 < 3)
	PRINTL 讨厌的味道：
	CALL SHOW_TASTE(GET_STR(, "キャラデータ", C_ID, "料理：讨厌的味道"), "NEGATIVE", ABL:C_ID:親密 < 3)
	PRINTFORMW 
	CALL JUDGE_RESEARCH(C_ID,"GET")
NEXT

TASTE_TREND_GOOD =
TASTE_TREND_BAD =

;好きな味、嫌いな味の内気を付けるべきものを提示
CALL JUDGE_RESEARCH(C_ID,"OUT")
PRINTFORM 「本次料理铁人的关键...
FOR LOCAL, 0, 6
	IF LOCAL < 3
		PRINTFORM [%RESULTS:LOCAL%]
		TASTE_TREND_GOOD += RESULTS:LOCAL + "/"
		SIF LOCAL == 2
			PRINTFORM 味道的运用将是关键！反之，
	ELSEIF LOCAL > 2 
		PRINTFORM [%RESULTS:LOCAL%]
		TASTE_TREND_BAD += RESULTS:LOCAL + "/"
		SIF LOCAL == 5
			PRINTFORM 味道需要特别注意！」
	ENDIF
NEXT
PRINTFORMW 

{
PRINTFORML 「%TEXTR("胜负关键取决于食材中凝聚的灵魂与驾驭天赋的才能！挑战者将展现怎样的奇迹之皿，敬请拭目以待！
/这座厨房即是今日战场。两位料理人将在此施展毕生绝技，这场激战请诸位尽情欣赏！
/究竟谁能夺得最终桂冠？在有限时间里诞生的至高料理——诸位请看！")%」
}
PRINTFORMW 「那么……料理开始……Allez cuisine(アレ・キュイジーヌ)！」

;相手の料理を生成する
CALL ENEMY_DISH_MENU(対戦相手, DISH_TYPE_NUM, THEME, ENEMY_LV, TASTE_TREND_GOOD, TASTE_TREND_BAD)
DISH_NAME_ENEMY = %RESULTS:0%
DISH_TASTE_ENEMY = %RESULTS:1%

CALL COLORMESSAGE(@"【%DISH_TYPE%】【%THEME%】(若偏离主题将失去资格)",C_YELLOW,1)
DRAWLINE
CALL COOKING_MATCH_SELECT(DISH_TYPE_NUM, THEME)
IF RESULTS == "弃权"
	PRINTFORML 哦呀挑战者宣布弃权了！
	WINNER = 弃权
	GOTO PLAYER_LOSE
ENDIF

BASEPOINT_TEMP = DISH_BASEPOINT
CALL ENEMY_COOKING(DISH_NAME_ENEMY, DISH_TASTE_ENEMY, ENEMY_LV, 対戦相手)
DISH_NAME_ENEMY = %RESULTS%

PRINTFORML 双方都完成了料理！
PRINTFORMW 那么请从%対戦相手%的%DISH_NAME_ENEMY%开始品鉴！

TOTAL_ENEMY = 0
FOR LOCAL, 0 , 3
	C_ID = JUDGE:LOCAL
	CALL PRINT_FACE, C_ID, "通常", "服", "", ""
	CALL REVIEW_DISH(C_ID, DISH_TASTE_ENEMY)
	SCORE:LOCAL = TCVAR:C_ID:料理評価値/10
	PRINTFORMW 【评审员 No.{LOCAL + 1} %CALLNAME:C_ID%】 {SCORE:LOCAL}分
	TOTAL_ENEMY += SCORE:LOCAL
NEXT
CALL COLORMESSAGE(@"总分：{TOTAL_ENEMY}分",C_YELLOW,1)
DISH_BASEPOINT = BASEPOINT_TEMP

PRINTFORMW 挑战者的%DISH_NAME%会获得怎样的评价呢！？

IF STRFIND(DISH_TASTE, THEME) == -1
	PRINTFORML 糟糕！？
	PRINTFORML 没有包含【%THEME%】！失去资格！
	GOTO PLAYER_LOSE
ENDIF

TOTAL_PLAYER = 0
FOR LOCAL, 0 , 3
	C_ID = JUDGE:LOCAL
	CALL PRINT_FACE, C_ID, "通常", "服", "", ""
	CALL REVIEW_DISH(C_ID, DISH_TASTE)
	SCORE:LOCAL = TCVAR:C_ID:料理評価値 / 10
	IF SCORE:LOCAL >= 80
		SETCOLOR C_GREEN
		FONTBOLD
		{
		PRINTFORML %CALLNAME:C_ID%的背景中%TEXTR("红魔馆爆炸了
		/妖怪之山喷发了/铃奈庵起火了
		/辉针城坠落导致博丽神社倒塌了/动物灵和埴轮跳起了桑巴
		/月亮弹跳着嵌进了赫卡提亚的脑袋
		/守矢神社裂成两半发射了核热造神「非想天则」
		/血池地狱变成钴蓝色并长出了椰子树
		/浮现出巨大键山雏的脸庞
		/发射了七色魔炮")%
		}
		RESETCOLOR
		FONTREGULAR
	ENDIF
	PRINTFORMW 【评审员 No.{LOCAL + 1} %CALLNAME:C_ID%】 {SCORE:LOCAL}分
	TOTAL_PLAYER += SCORE:LOCAL
NEXT
CALL COLORMESSAGE(@"总分：{TOTAL_PLAYER}分",C_YELLOW,2)
IF TOTAL_PLAYER > TOTAL_ENEMY
	WINNER = 勝利
	PRINTFORMW 挑战者胜利了！
ELSE
	WINNER = 敗北
	$PLAYER_LOSE
	PRINTFORMW %対戦相手%胜利了！
ENDIF
CALL RESET_DISH

IF WINNER == "勝利"
	IF !CFLAG:TARGET:料理勝負勝利 && 対戦相手 == CALLNAME:TARGET
		CALL COLORMESSAGE(@"【%CALLNAME:TARGET%初次胜利】",C_YELLOW,1)
		CFLAG:TARGET:料理勝負勝利 = 1
		SELECTCASE ABL:TARGET:料理技能
			CASE 0
				FirstWinBonus = 1
			CASE 1
				FirstWinBonus = 3
			CASE 2
				FirstWinBonus = 5
			CASE 3
				FirstWinBonus = 10
			CASE 4
				FirstWinBonus = 20
			CASE 5
				FirstWinBonus = 30
			CASEELSE
				FirstWinBonus = 50
		ENDSELECT
		EXP:MASTER:料理経験 += FirstWinBonus
	ENDIF
	DishEXP = MAX(ENEMY_LV , 1)
	SIF ABL:MASTER:料理技能 < ENEMY_LV
		DishEXP *= 2
	EXP:MASTER:料理経験 += DishEXP
ELSEIF WINNER == "敗北"
	EXP:MASTER:料理経験 += 1
ENDIF
DOWNBASE:MASTER:体力 = 100
DOWNBASE:MASTER:気力 = 100
TIME += 90
RETURN 1

;-------------------------------------------------
;料理の選択
;-------------------------------------------------
@COOKING_MATCH_SELECT(DISH_TYPE_NUM, THEME)
#DIM DISH_TYPE_NUM
#DIM MAXMENU
#DIM MENUCOUNT
#DIM SELECT_MENU
#DIM SELECT_ING
#DIM 料理結果
#DIM REQUIRED_LV
#DIMS THEME

CALL COOKING_UI(DISH_TYPE_NUM, SELECT_MENU, SELECT_ING, 0, 0, 0, THEME)
PRINTFORML [ 0] 弃权
INPUT
;取消
RESULTS =
IF RESULT == 0
	RESULTS = 弃权
ELSEIF RESULT == 9 && SELECT_MENU
	CALL DISHDATA(SELECT_MENU, DISH_TYPE, 0)
	IF RESULT != 10
		CLEARLINE 14 + COOKING_LCOUNT
		RESTART
	ENDIF
	CALL DISH_TASTE_ADD(SELECT_ING)
	DISH_RANK = 食材レベル(SELECT_ING)
	;DISHNAMEは口上でのみ使用してください
	DISHNAME = %DISH_NAME%
	FLAG:料理 = SELECT_MENU
	SELECT_MENU = 0
	PRINTFORML 使用食材：%ITEMNAME:SELECT_ING%
	ITEM:SELECT_ING --
	CALL DISH_COOKING(0)
ELSE
	;料理選択
	IF INRANGE(RESULT, 101, 400 + CHARANUM)
		SELECT_MENU = RESULT
		CALL DISHDATA(RESULT, DISH_TYPE)
		;表示外なら選択リセット
		IF !RESULT
			SELECT_MENU = 0
			SELECT_ING = -1
		;作れないなら材料だけリセットして表示する
		ELSEIF RESULT < 10
			SELECT_ING = -1
		;作れるなら材料を設定
		ELSEIF RESULT == 10
			RESULT = 0
			;選択中の材料があったら選択を保持する
			FOR LOCAL, 1, 11
				IF DISH_ING:LOCAL == SELECT_ING && SELECT_ING >= 0
					RESULT = 1
					BREAK
				ENDIF
			NEXT
			;選択中の材料がなければ材料を再設定
			IF !RESULT
				FOR LOCAL, 1, 11
					IF ITEM:(DISH_ING:LOCAL)
						SELECT_ING = DISH_ING:LOCAL
						BREAK
					ENDIF
				NEXT
			ENDIF
		ENDIF
	ELSEIF INRANGE(RESULT, 11, 20) && ITEM:(DISH_ING:(RESULT - 10))
		SELECT_ING = DISH_ING:(RESULT - 10)
	ENDIF
	CLEARLINE 15 + COOKING_LCOUNT
	RESTART
ENDIF
[SKIPSTART]
キイチゴ/カキ/ナシ/スイカ/バナナ/ドリアン/ラズベリー/ライチ/ブルーベリー/ラズベリー/
リンゴ/スモモ/ブドウ/パイナップル/キウイ/アンズ/イチジク

ケーキ
プリン
タルト
羊羹
パフェ
団子
餅
ゼリー
スムージー

SELECTCASE DISH_TYPE_NUM
	CASE 3
		相手の料理 = %TEXTR("")%
ENDSELECT
[SKIPEND]

;-------------------------------------------------
;審査員の好みリサーチ
;-------------------------------------------------
@JUDGE_RESEARCH(C_ID, MODE)
#DIM C_ID
#DIMS TASTE_STR              	;表示対象の味データ
#DIMS TASTE_LIST_PO, 100		;好きな味リスト
#DIMS TASTE_LIST_NE, 100		;嫌いな味リスト
#DIM  TASTE_CNT_PO,  100	;好きな味リスト各要素の重複個数
#DIM  TASTE_CNT_NE,  100	;嫌いな味リスト各要素の重複個数
#DIM  TASTE_NUM_PO			;好きな味の種類の総数
#DIM  TASTE_NUM_NE			;嫌いな味の種類の総数
#DIMS RESULTS_TASTE,6		;最終的な好みの傾向(3位まで)
#DIM D_ID					;リピート用
#DIMS MODE

IF MODE == "GET"
	;味リストの取得
	TASTE_LIST_PO += GET_STR(, "キャラデータ", C_ID, "料理：喜欢的味道")
	TASTE_LIST_NE += GET_STR(, "キャラデータ", C_ID, "料理：讨厌的味道")
	
ELSEIF MODE == "OUT"
	CALLF CNT_SPLIT(TASTE_LIST_PO, TASTE_LIST_PO, TASTE_CNT_PO, TASTE_NUM_PO)
	CALLF CNT_SPLIT(TASTE_LIST_NE, TASTE_LIST_NE, TASTE_CNT_NE, TASTE_NUM_NE)
	
	FOR LOCAL, 0 , TASTE_NUM_PO
		FOR D_ID, 0, (TASTE_NUM_PO-1)
			IF TASTE_CNT_PO:D_ID < TASTE_CNT_PO:(D_ID+1)
				RESULT = TASTE_CNT_PO:D_ID
				TASTE_CNT_PO:D_ID = TASTE_CNT_PO:(D_ID+1)
				TASTE_CNT_PO:(D_ID+1) = RESULT
				RESULTS = %TASTE_LIST_PO:D_ID%
				TASTE_LIST_PO:D_ID = %TASTE_LIST_PO:(D_ID+1)%
				TASTE_LIST_PO:(D_ID+1) = %RESULTS%
			ENDIF
		NEXT
	NEXT
	
	FOR LOCAL, 0, TASTE_NUM_NE
		FOR D_ID, 0, (TASTE_NUM_NE-1)
			IF TASTE_CNT_NE:D_ID < TASTE_CNT_NE:(D_ID+1)
				RESULT = TASTE_CNT_NE:D_ID
				TASTE_CNT_NE:D_ID = TASTE_CNT_NE:(D_ID+1)
				TASTE_CNT_NE:(D_ID+1) = RESULT
				RESULTS = %TASTE_LIST_NE:D_ID%
				TASTE_LIST_NE:D_ID = %TASTE_LIST_NE:(D_ID+1)%
				TASTE_LIST_NE:(D_ID+1) = %RESULTS%
			ENDIF
		NEXT
	NEXT
	
	FOR LOCAL, 0, 3
		RESULTS_TASTE:LOCAL = %TASTE_LIST_PO:LOCAL%
		RESULTS_TASTE:(LOCAL+3) = %TASTE_LIST_NE:LOCAL%
	NEXT
	;審査員味リストをリセット
	VARSET TASTE_LIST_PO
	VARSET TASTE_LIST_NE
	ARRAYCOPY "RESULTS_TASTE" , "RESULTS"
ENDIF

;-------------------------------------------------
;対戦相手の料理選択
;-------------------------------------------------
@ENEMY_DISH_MENU(対戦相手, DISH_TYPE_NUM, THEME, ENEMY_LV, DISH_GOOD, DISH_BAD)
#DIMS 対戦相手
#DIM DISH_NUM		;料理番号
#DIM DISH_TYPE_NUM	;料理タイプ番号
#DIMS THEME			;テーマの味
#DIM ENEMY_LV
#DIMS DISH_GOOD
#DIMS DISH_BAD
#DIMS DISH_NAME_ENEMY	;料理名リスト
#DIM LOOP_CHECK
DISH_NAME_ENEMY =
;料理データを呼ぶ
$LOOP
FOR LOCAL, 1 + DISH_TYPE_NUM * 100, 100 + DISH_TYPE_NUM * 100
	CALL DISHDATA(LOCAL, DISH_TYPE, 0)
	IF RESULT
		IF STRCOUNT(DISH_TASTE, THEME)
			LOCALS = {LOCAL}/
			DISH_NAME_ENEMY += LOCALS
			;料理技能が4以上ならより有効な料理を選ぶ可能性が高くなる
			IF ENEMY_LV >= 4
				SIF STR_MULTI_COUNT(DISH_TASTE,DISH_GOOD)
					DISH_NAME_ENEMY += LOCALS * (ENEMY_LV - 3) * 2
				SIF STR_MULTI_COUNT(DISH_TASTE,DISH_BAD) && LOOP_CHECK == 0
					DISH_NAME_ENEMY = %REPLACE(DISH_NAME_ENEMY, LOCALS, "")%
			ENDIF
			CONTINUE
		ELSE
			CONTINUE
		ENDIF
	ENDIF
NEXT
IF DISH_NAME_ENEMY == ""
	LOOP_CHECK = 1
	GOTO LOOP
ENDIF

DISH_NAME_ENEMY = %TEXTR(DISH_NAME_ENEMY)%
DISH_NUM = TOINT(DISH_NAME_ENEMY)
CALL DISHDATA(DISH_NUM, DISH_TYPE, 0)
IF 対戦相手 == CALLNAME:TARGET
	CALL DISH_TASTE_FROM_CHARA(TARGET)
ENDIF
RESULTS:0 = %DISH_NAME%
RESULTS:1 = %DISH_TASTE%
RETURN 0

;-------------------------------------------------
;対戦相手の料理を生成する
;-------------------------------------------------
@ENEMY_COOKING(DISH_NAME_ENEMY, DISH_TASTE_ENEMY, ENEMY_LV, 対戦相手)
#DIM BONUS点
#DIM 減点
#DIMS DISH_NAME_ENEMY
#DIMS DISH_TASTE_ENEMY
#DIM ENEMY_LV
#DIMS 対戦相手
#DIM DISH_RANK_ENEMY

;念の為最終チェック
SIF DISH_NAME_ENEMY == ""
	THROW
SIF DISH_TYPE == ""
	THROW

DISH_TASTE_ENEMY += "できたて/"
DISH_TASTE_ENEMY += DISH_NAME_ENEMY + "/"
DISH_TASTE_ENEMY += DISH_TYPE + "/"
;[中立温度]は夏だけ冷たい（そば,うどん等）
IF STRCOUNT(DISH_TASTE_ENEMY, "中立温度/")
	IF GET_MONTH() == "夏之月"
		DISH_TASTE_ENEMY '= REPLACE(DISH_TASTE_ENEMY, "中立温度\/", "冷/")
	ELSE
		DISH_TASTE_ENEMY '= REPLACE(DISH_TASTE_ENEMY, "中立温度\/", "温暖/")
	ENDIF
ENDIF
;鮮度設定,調理時間を追加しておく
DISH_LIMIT = DISH_MAXLIMIT + DISH_TIME

;表示用の料理名設定
EX_DISH_NAME = 
IF STRCOUNT(DISH_TASTE_ENEMY, "女僕流/")
	EX_DISH_NAME += "女僕流"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "天狗流/")
	EX_DISH_NAME += "天狗流"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "冥界風/")
	EX_DISH_NAME += "冥界風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "天界風/")
	EX_DISH_NAME += "天界風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "地底風/")
	EX_DISH_NAME += "地底風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "地獄風/")
	EX_DISH_NAME += "地獄風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "畜生流/")
	EX_DISH_NAME += "畜生流"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "月都風/")
	EX_DISH_NAME += "月都風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "玉兎流/")
	EX_DISH_NAME += "玉兎流"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "魔界風/")
	EX_DISH_NAME += "魔界風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "夢幻風/")
	EX_DISH_NAME += "夢幻風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "外界風/")
	EX_DISH_NAME += "外界風"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "化学的/")
	EX_DISH_NAME += "化学的"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "小人族秘伝的味道/")
	EX_DISH_NAME += "小人族秘伝の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "稗田家秘伝の味/")
	EX_DISH_NAME += "稗田家秘伝の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "純化的味道/")
	EX_DISH_NAME += "純化的"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "逆転/")
	EX_DISH_NAME += "逆転した"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "大火力/")
	EX_DISH_NAME += "大火力"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "氷結/")
	EX_DISH_NAME += "氷結"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "心跳味/")
	EX_DISH_NAME += "心跳"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "未知の味/")
	EX_DISH_NAME += "不思議な"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "闪耀/")
	EX_DISH_NAME += "闪光"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "生命力/")
	EX_DISH_NAME += "妖精の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "仙術/")
	EX_DISH_NAME += "仙人の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "鬼の魔力/")
	EX_DISH_NAME += "鬼の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "血の魔力/")
	EX_DISH_NAME += "吸血鬼の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "魔力/")
	EX_DISH_NAME += "魔法の"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "妖力/")
	EX_DISH_NAME += "妖气的"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "霊障/")
	EX_DISH_NAME += "霊的"
ELSEIF STRCOUNT(DISH_TASTE_ENEMY, "秘術/")
	EX_DISH_NAME += "神秘的"
ENDIF
EX_DISH_NAME += DISH_NAME_ENEMY

BONUS点 = 0
減点 = 0

SIF STRCOUNT(DISH_TASTE_ENEMY, "旬/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE_ENEMY, "玉兎流/") && STRCOUNT(DISH_TASTE, "胡蘿卜/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE_ENEMY, "プロの味/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE_ENEMY, "闪耀/")
	BONUS点 += 1
;得意料理や切れ味は該当していない場合消去されているのでこれでOK,得意料理は"/"を外す
SIF STRCOUNT(DISH_TASTE_ENEMY, "得意料理")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE_ENEMY, "切れ味/")
	BONUS点 += 1
SIF STRCOUNT(DISH_TASTE_ENEMY, "美味的水/")
	BONUS点 += 1
;嫉妬,隠し味の効果は食べる時

DISH_RANK_ENEMY = 1
SIF ENEMY_LV >= 3
	DISH_RANK_ENEMY = 3


DISH_BASEPOINT = ENEMY_LV + ENEMY_LV + DISH_RANK_ENEMY + BONUS点 - 減点
DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
RESULTS = %EX_DISH_NAME%

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
@COM_ABLE498
;時間停止中
SIF FLAG:時間停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(498)
	RETURN RESULT
;厨房なのか
SIF !KITCHEN(CFLAG:MASTER:現在位置)
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:うふふ
	RETURN 0
;添い寝中
SIF CFLAG:添い寝中
	RETURN 0
;イタズラ中
SIF CFLAG:MASTER:イタズラ
	RETURN 0
;既に料理を作っている
SIF DISH_NAME != ""
	RETURN 0
SIF GETBIT(TFLAG:一日一回, 14)
	RETURN 0
RETURN 1