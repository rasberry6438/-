;-------------------------------------------------
;任憑擺佈
;日常系指令、レベル3
;-------------------------------------------------
@COM360
TIME += 5
;気力ゼロのときは変動しない（任憑擺佈しかできないため）
SIF BASE:MASTER:気力 <= 0
	RETURN 1
;通常はプラス,施虐狂だと反転
IF TALENT:TARGET:施虐狂
	LOCAL = -1
ELSE
	LOCAL = 1
ENDIF
;指向が反転中は3倍
IF COUNTER_SIKOU:TARGET:(TCVAR:TARGET:Counter行動) > 0 && TALENT:TARGET:施虐狂
	LOCAL *= 3
ELSEIF COUNTER_SIKOU:TARGET:(TCVAR:TARGET:Counter行動) < 0
	LOCAL *= 3
ENDIF
;［坦率］［学習快］は2倍
SIF TALENT:TARGET:態度 < 0 || TALENT:TARGET:習得速度 > 0
	LOCAL *= 2
;最大影響度10
COUNTER_SIKOU:TARGET:(TCVAR:TARGET:Counter行動) = LIMIT(COUNTER_SIKOU:TARGET:(TCVAR:TARGET:Counter行動) + LOCAL, -10, 10)
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE360
;停止中は不可
SIF FLAG:70
	RETURN 0
;なすがまま実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(360)
	RETURN RESULT
SIF CFLAG:诶嘿嘿 != 2
	RETURN 0
;おねだり中は不可
SIF INRANGE(TCVAR:Counter行動, 40, 49)
	RETURN 0
RETURN 1
