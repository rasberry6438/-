;-------------------------------------------------
;ラジオをいじる
;日常系指令、レベル1
;-------------------------------------------------
@COM496
#DIMS 大雨情報
#DIMS 大雪情報
#DIMS 暴風情報
#DIMS 雷情報
#DIMS 低温情報
#DIMS 雪崩情報
#DIMS 低気圧位置
#DIMS 線状降水帯位置
#DIMS 積乱雲位置
#DIMS 台風勢力
#DIMS 早期注意情報

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

大雨情報 = %GET_WEATHER_WARNING_HEAVY_RAIN()%
大雪情報 = %GET_WEATHER_WARNING_HEAVY_SNOWFALL()%
暴風情報 = %GET_WEATHER_WARNING_STORM()%
雷情報 = %GET_WEATHER_WARNING_LIGHTNING()%
低温情報 = %GET_WEATHER_WARNING_COLD()%
雪崩情報 = %GET_WEATHER_WARNING_AVALANCHE()%
早期注意情報 = %GET_WEATHER_ADVISORY_probability_subsequent_WARNING()%

SELECTCASE IS_TYPHOON:1
	CASE 5
		台風勢力 = 猛烈的
	CASE 4
		台風勢力 = 非常强的
	CASE 3
		台風勢力 = 强的
	CASEELSE
		台風勢力 = 
ENDSELECT

[IF_DEBUG]
SELECTCASE TIME:0
	CASE 345 TO 359, 375 TO 389, 405 TO 419, 435 TO 449, 465 TO 479, 525 TO 539, 585 TO 599, 705 TO 719, 735 TO 749, 825 TO 839, 885 TO 899, 945 TO 959, 1005 TO 1019, 1065 TO 1079, 1125 TO 1139, 1185 TO 1199, 1245 TO 1259, 1305 TO 1319, 1365 TO 1379
	;天気予報(15分間)(05:45 06:15 06:45 07:15 07:45 08:45 09:45 11:45 12:15 13:45 14:45 15:45 16:45 17:45 18:45 19:45 20:45 21:45 22:45)
		DEBUGPRINTFORML 現在的放送：天気予報
	CASE 300 TO 329, 360 TO 374, 420 TO 434, 540 TO 554, 600 TO 614, 660 TO 674, 720 TO 734, 780 TO 794, 840 TO 854, 900 TO 914, 1020 TO 1034, 1140 TO 1184, 1200 TO 1214, 1260 TO 1274, 1380 TO 1394, 0 TO 14, 60 TO 74, 120 TO 134, 180 TO 194, 240 TO 254
	;ニュース(原則15分間)(05:00~05:29 06:00 07:00 09:00 10:00 11:00 12:00 13:00 14:00 15:00 17:00 19:00~19:44 20:00 21:00 23:00 24:00 01:00 02:00 03:00 04:00)
		DEBUGPRINTFORML 現在的放送：ニュース
	CASE 960 TO 989
	;気象通報(16:00~16:29)
		DEBUGPRINTFORML 現在的放送：気象通報
	CASEELSE
		DEBUGPRINTFORML 現在的放送：不明
ENDSELECT

DEBUGPRINTFORML %大雨情報%
DEBUGPRINTFORML %大雪情報%
DEBUGPRINTFORML %暴風情報%
DEBUGPRINTFORML %雷情報%
DEBUGPRINTFORML %低温情報%
DEBUGPRINTFORML %雪崩情報%
DEBUGPRINTFORML %早期注意情報%

[ENDIF]

PRINTW 摆弄了一下收音机的旋钮
PRINTL

PRINTL ┏━━━━━━┓
PRINT ┃电源［
IF 大雨情報 == "大雨特別警報"
	PRINT □□
ELSEIF 大雨情報 == "大雨警報(土砂災害警戒情報)" || 大雪情報 == "大雪特別警報" || 暴風情報 == "暴風雪特別警報" || 暴風情報 == "暴風特別警報"
	IF CONTINUOUS_RAINFALL >= 10
		SETCOLOR C_気象_土砂災害警戒情報
		PRINT ■
		RESETCOLOR
		PRINT □
	ELSE
		SETCOLOR C_気象_土砂災害警戒情報
		PRINT ■■
		RESETCOLOR
	ENDIF
ELSEIF 大雨情報 == "大雨警報" || 大雪情報 == "大雪警報" || 暴風情報 == "暴風雪警報" || 暴風情報 == "暴風警報" || 雷情報 == "雷注意報(竜巻注意情報)" || 雪崩情報 == "なだれ注意報"
	SETCOLOR C_気象_警報
	PRINT ■■
	RESETCOLOR
ELSEIF 大雨情報 == "大雨注意報" || 大雪情報 == "大雪注意報" || 暴風情報 == "風雪注意報" || 暴風情報 == "強風注意報" || 雷情報 == "雷注意報"
	SELECTCASE 早期注意情報
		CASE "警報級の可能性［高］"
			SETCOLOR C_気象_注意報
			PRINT ■■
			RESETCOLOR
		CASE "警報級の可能性［中］"
			SETCOLOR C_気象_注意報
			PRINT ■
			RESETCOLOR
			PRINT ■
		CASEELSE
			SETCOLOR C_気象_注意報
			PRINT ■
			SETCOLOR C_GREEN
			PRINT ■
			RESETCOLOR
	ENDSELECT
ELSE
	SELECTCASE 早期注意情報
		CASE "警報級の可能性［高］"
			PRINT ■■
		CASE "警報級の可能性［中］"
			SETCOLOR C_GREEN
			PRINT ■
			RESETCOLOR
			PRINT ■
		CASEELSE
			SETCOLOR C_GREEN
			PRINT ■■
			RESETCOLOR
	ENDSELECT
ENDIF
PRINTL ］┃
PRINTL ┗━━━━━━┛
;(天候パッチ)雷発生時はノイズが混じる
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1
	PRINT ……
	FONTBOLD
	PRINT 嘎吱嘎吱！
	FONTREGULAR
	PRINT ………
	FONTBOLD
	PRINT 滋滋滋
	FONTREGULAR
	PRINTL ……
ELSEIF GET_FRONTAL_THUNDERSTORM() == 1 || GET_HEAT_THUNDERSTORM() == 1
	PRINT ……
	FONTBOLD
	PRINT 嘎吱
	FONTREGULAR
	PRINTL ………　……
ELSE
	PRINTL ……　………　……
ENDIF
IF	ABL:MASTER:教養 < 2
	PRINT ……
	FONTBOLD
	PRINT ^&$^%$^&@#$！
	FONTREGULAR
	PRINT ………
	FONTBOLD
	PRINT @#%**&^%@q$&？？
	FONTREGULAR
	PRINTL ……
	IF 玩家立绘开关
		CALL PRINT_FACE_PLAYER, MASTER, "憤怒", "服", "", ""
		PRINTFORMDW 「啥是%TEXTR("中心气压/气旋/冷暖锋/湾台风/冷涡/强冷空气/积雨云")%？啥是%TEXTR("汇率波动/农作物嫁接")%啊？？」
		PRINTFORMDW 「我果然是个文盲...气笑了」
		PRINTFORMDW 「看来要好好提升一下自己的教养」
	ELSE
		PRINTFORMDL 收音机里晦涩且生僻的专业词汇让你摸不着头脑
		PRINTFORMDW 看来要好好提升一下教养才能听懂收音机内容呢
	ENDIF
	RETURN 1
ENDIF
SELECTCASE TIME:0
	CASE 345 TO 359, 375 TO 389, 405 TO 419, 435 TO 449, 465 TO 479, 525 TO 539, 585 TO 599, 705 TO 719, 735 TO 749, 825 TO 839, 885 TO 899, 945 TO 959, 1005 TO 1019, 1065 TO 1079, 1125 TO 1139, 1185 TO 1199, 1245 TO 1259, 1305 TO 1319, 1365 TO 1379
	;天気予報(15分間)(05:45 06:15 06:45 07:15 07:45 08:45 09:45 11:45 12:15 13:45 14:45 15:45 16:45 17:45 18:45 19:45 20:45 21:45 22:45)
		SELECTCASE 低気圧位置
			CASE "台風遠方"
				PRINTFORML …的南海域存在%台風勢力%台风…号……
			CASE "台風勢力圏"
				PRINTFORML …近海的%台風勢力%台风……中心气压…hPa…
			CASE "台風接近強風域", "台風接近暴風域"
				PRINTFORM …%台風勢力%台风…号
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%…数十年一遇的强度…正在接近…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%…维持强度…正在接近…
				ELSE
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%…正在接近…
				ENDIF
			CASE "台風最接近"
				PRINTFORM …%台風勢力%台风…号
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……以数十年一遇的强度…最接近…请严加警戒…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……最接近…请严加警戒…
				ELSE
					PRINTFORML ……最接近…请充分警戒…
				ENDIF
			CASE "台風通過暴風域", "台風通過強風域"
				PRINTFORM …%台風勢力%台风…号
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……保持破纪录的强度…通过…请严加警戒…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……维持强度…通过中…
				ELSE
					PRINTFORML ……正在通过…
				ENDIF
			CASE "寒冷渦遠方", "寒冷渦接近"
				SELECTCASE DAY:2
					CASE 1, 3, 4
						PRINTFORML …冷涡向日本海方向移动……
					CASE 2
						PRINTFORML …上空有强冷空气流入……
				ENDSELECT
			CASE "二つ玉低気圧遠方", "日本海低気圧遠方"
				PRINTFORML …日本海方向…低压正在……
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML …日本海急速发展的低压……
				PRINTFORML …山区天气可能骤变……
			CASE "日本海低気圧接近", "日本海低気圧通過中"
				PRINTFORML …日本海发展的低压……
			CASE "南岸低気圧遠方"
				PRINTFORML …中国东海…低压正在……
			CASE "南岸低気圧接近"
				PRINTFORML …太平洋侧发展的低压……
			CASE "温暖前線遠方"
				IF TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 <= DAY:3 + 1
					PRINTFORML …明天以后锋面将……
				ELSE
					PRINTFORML …周中将有低压……
				ENDIF
			CASE "温暖前線接近"
				PRINTFORML …因暖锋接近西部将……
			CASE "暖域"
				PRINTFORML …冷锋过境时可能……
			CASE "寒冷前線通過"
				PRINTFORML …局部地区有骤雨……
			CASEELSE
				SELECTCASE DAY:2
					CASE 1,3
						IF IS_RAINY_SEASON1:0 == 1 || IS_RAINY_SEASON2 == 1
							PRINTFORML …天空阴沉多云……
						ELSE
							PRINTFORML …天空平静晴朗……
						ENDIF
					CASE 2
						IF POWER_WARM_AIR == 1
							PRINTFORML …太平洋高压势力减弱……
						ELSE
							PRINTFORML …全国晴空夏日……
						ENDIF
					CASE 4
						PRINTFORML …西高东低…冬季型气压配置……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……遭遇前所未有的大雨……
					CASE 1
						PRINTFORML …出现数十年一遇的降雨量……
					CASE 2
						PRINTFORML ……地区发布…大雨特别警报……
					CASE 3
						PRINTFORML ……安全避难已困难…请采取保命行动……
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …过去数年土砂灾害风险最高……
					CASE 1
						PRINTFORML …气象台…地区土砂灾害风险……
				ENDSELECT
			CASE "大雨警報"
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML …行动不便者请……
					CASE 1
						PRINTFORML …请趁天亮前避难……
					CASE 2
						PRINTFORML …气象台…地区发布大雨洪水警报……
				ENDSELECT
			CASE "大雨注意報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …请确认居住区灾害地图……
					CASE 1
						PRINTFORML …气象台…地区发布大雨注意报……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雪情報
			CASE "大雪特別警報"
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML …遭遇豪雪级降雪……
					CASE 1
						PRINTFORML …数十年一遇的大雪……
					CASE 2
						PRINTFORML …地区发布大雪特别警报……
				ENDSELECT
			CASE "大雪警報"
				IF CONTINUOUS_SNOWFALL >= 20
					PRINTFORML ……气象台…地区发布显著大雪……
				ELSE
					PRINTFORML ……地区发布大雪警报……
				ENDIF
			CASE "大雪注意報"
				PRINTFORML ……地区发布大雪注意报……
		ENDSELECT
		
		SELECTCASE 暴風情報
			CASE "暴風雪特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地区发布…暴风雪特别警报……
					CASE 1
						PRINTFORML …数十年一遇的猛吹雪……
					CASE 2
						PRINTFORML …史无前例的暴风雪…请避免不必要外出……
					CASE 3
						PRINTFORML …猛烈暴风雪可能随时……
				ENDSELECT
			CASE "暴風特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地区发布…暴风特别警报……
					CASE 1
						PRINTFORML …遭遇前所未有的狂风……
					CASE 2
						PRINTFORML …堪比湾台风的…破纪录……
					CASE 3
						PRINTFORML …狂风肆虐…请确保人身安全……
				ENDSELECT
			CASE "暴風雪警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …地区发布暴风雪警报…请避免外出……
					CASE 1
						PRINTFORML …数年一遇的猛吹雪……
				ENDSELECT
			CASE "暴風警報"
				PRINTFORML …地区发布暴风警报…请尽早避难……
			CASE "風雪注意報"
				PRINTFORML ……地区发布风雪注意报……
			CASE "強風注意報"
				PRINTFORML ……地区发布强风注意报……
		ENDSELECT
		
		SELECTCASE 雷情報
			CASE "雷注意報(竜巻注意情報)"
					PRINTFORML …大气状态极不稳定……
					PRINTFORML …地区龙卷风…风险升高……
			CASE "雷注意報"
				IF DAY:2 == 2 && (IS_COLD_LOW || 低気圧位置 == "台風勢力圏")
					PRINTFORML …大气状态极不稳定……
				ELSE
					PRINTFORML …大气状态趋于不稳定……
				ENDIF
		ENDSELECT
		
		SELECTCASE 雪崩情報
			CASE "なだれ注意報"
				PRINTFORML ……可能发生雪崩……
		ENDSELECT
		
		SELECTCASE 低温情報
			CASE "低温注意報"
				PRINTFORML …地区气温低于常年……
		ENDSELECT
		
	CASE 300 TO 329, 360 TO 374, 420 TO 434, 540 TO 554, 600 TO 614, 660 TO 674, 720 TO 734, 780 TO 794, 840 TO 854, 900 TO 914, 1020 TO 1034, 1140 TO 1184, 1200 TO 1214, 1260 TO 1274, 1380 TO 1394, 0 TO 14, 60 TO 74, 120 TO 134, 180 TO 194, 240 TO 254
	;ニュース(原則15分間)(05:00~05:29 06:00 07:00 09:00 10:00 11:00 12:00 13:00 14:00 15:00 17:00 19:00~19:44 20:00 21:00 23:00 24:00 01:00 02:00 03:00 04:00)
		SELECTCASE 低気圧位置
			CASE "台風遠方"
				PRINTFORML …的南海域存在%台風勢力%台风…号……
			CASE "台風勢力圏"
				PRINTFORML …近海的%台風勢力%台风……中心气压…hPa…
			CASE "台風接近強風域", "台風接近暴風域", "台風最接近", "台風通過暴風域", "台風通過強風域"
				PRINTFORM …%台風勢力%台风…号
				IF IS_TYPHOON:1 >= 3
					PRINTFORML ……维持强度…正在接近…
				ELSE
					PRINTFORML ……将于近日…逐渐接近…
				ENDIF
			CASE "寒冷渦遠方", "寒冷渦接近"
				SELECTCASE DAY:2
					CASE 1, 3, 4
						PRINTFORML …冷涡向日本海方向移动……
					CASE 2
						PRINTFORML …上空有强冷空气流入……
				ENDSELECT
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML …山区连续发生遇险事件……
		ENDSELECT
		
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……遭遇前所未有的大雨……
					CASE 1
						PRINTFORML …出现数十年一遇的降雨量……
					CASE 2
						PRINTFORML ……地区发布…大雨特别警报……
					CASE 3
						PRINTFORML ……警戒等级已升至5级……
					CASE 4
						PRINTFORML ……安全避难已困难…请采取保命行动……
				ENDSELECT
				
				SELECTCASE CONTINUOUS_RAINFALL
					CASE IS >= 40
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …政府指定为…特大灾害……
							CASE 1
								PRINTFORML …各地交通中断…救援队正在……
						ENDSELECT
					CASE IS >= 20
						SELECTCASE RAND:3
							CASE 0
								PRINTFORML …山体滑坡导致…户居民…受困……
							CASE 1
								PRINTFORML …部分地区已出现破纪录降雨……
							CASE 2
								PRINTFORML …河流超过泛滥危险水位……
						ENDSELECT
					CASE IS >= 10
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …市发布短时强降雨警报……
							CASE 1
								PRINTFORML …出现数年一遇的大雨……
						ENDSELECT
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……警戒等级升至4级……
					CASE 1
						PRINTFORML …过去数年土砂灾害风险最高……
					CASE 2
						PRINTFORML …气象台…地区土砂灾害风险……
					CASE 3
						PRINTFORML …市向…户居民发布避难指示……
					CASE 4
						PRINTFORML …线路全线暂停运行……
				ENDSELECT
				
			CASE "大雨警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML …行动不便者请……
					CASE 1
						PRINTFORML …请趁天亮前避难……
					CASE 2
						PRINTFORML …气象台…地区发布大雨洪水警报……
					CASE 3
						PRINTFORML …市要求…户老年居民提前避难……
					CASE 4
						PRINTFORML ……警戒等级升至2级……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雪情報
			CASE "大雪特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML …遭遇豪雪级降雪……
					CASE 1
						PRINTFORML …数十年一遇的大雪……
					CASE 2
						PRINTFORML …地区发布大雪特别警报……
					CASE 3
						PRINTFORML …因破纪录大雪各地出现…孤立……
				ENDSELECT
			CASE "大雪警報"
				IF CONTINUOUS_SNOWFALL >= 20
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML ……气象台…地区发布显著大雪……
						CASE 1
							PRINTFORML …大雪引发大规模交通瘫痪……
					ENDSELECT
				ELSE
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML …气象台…地区发布大雪警报……
						CASE 1
							PRINTFORML …大雪影响各地交通系统……
					ENDSELECT
				ENDIF
		ENDSELECT
		
		SELECTCASE 暴風情報
			CASE "暴風雪特別警報"
				SELECTCASE RAND:6
					CASE 0
						PRINTFORML ……地区发布…暴风雪特别警报……
					CASE 1
						PRINTFORML …数十年一遇的猛吹雪……
					CASE 2
						PRINTFORML …史无前例的暴风雪…请避免不必要外出……
					CASE 3
						PRINTFORML …猛烈暴风雪可能随时……
					CASE 4
						PRINTFORML …多地人员受困…请求救援……
					CASE 5
						PRINTFORML …高速公路多辆汽车被困……
				ENDSELECT
			CASE "暴風特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地区发布…暴风特别警报……
					CASE 1
						PRINTFORML …遭遇前所未有的狂风……
					CASE 2
						PRINTFORML …堪比湾台风的…破纪录……
					CASE 3
						PRINTFORML …狂风肆虐…请确保人身安全……
				ENDSELECT
			CASE "暴風雪警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …地区发布暴风雪警报…请避免外出……
					CASE 1
						PRINTFORML …数年一遇的猛吹雪……
				ENDSELECT
			CASE "暴風警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …气象台…地区发布暴风警报……
					CASE 1
						PRINTFORML …狂风期间避难危险…请尽早撤离……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 雷情報
			CASE "雷注意報(竜巻注意情報)"
				PRINTFORML …大气状态极不稳定……
				PRINTFORML …地区龙卷风…风险升高……
			CASE "雷注意報"
				IF DAY:2 == 2 && (IS_COLD_LOW || 低気圧位置 == "台風勢力圏")
					PRINTFORML …大气状态极不稳定……
				ENDIF
		ENDSELECT
		
		IF POSSIBLITY_AVALANCHE >= 1
			PRINTFORML …各地频发雪崩灾害……
		ENDIF
		
		SIF IS_RAINY_SEASON1:1 == 1
			PRINTFORML …气象台宣布…西日本梅雨结束……
		
		SELECTCASE IS_COLD_CROP_DAMAGE
			CASE IS >= 3
				PRINTFORML …因破纪录寒害…紧急启动……
			CASE IS >= 1
				PRINTFORML …天气异常影响农作物生长……
		ENDSELECT
		
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML …今日日元汇率……
			CASE 1
				PRINTFORML …接下来是各地新闻……
			CASE 2
				PRINTFORML …根据…报道……
		ENDSELECT
		
	CASE 960 TO 989
	;気象通報(16:00~16:29)
		PRINTFORML …发布…{DAY:3}日正午气象通报……
		SELECTCASE RAND:54
			CASE 0
				PRINTFORML ……继续为您播报…{DAY:3}日正午天气……
			CASE 1
				PRINTFORML …富士山…気温……
			CASEELSE
				{
				PRINTFORML …%TEXTR("/石垣岛/那霸/名濑/鹿儿岛/福江/严原/足摺岬/室户岬/松山/滨田/西乡/大阪/潮岬/
				八丈岛/大岛/御前崎/铫子/前桥/小名滨/轮岛/相川/仙台/宫古/秋田/函馆/浦河/根室/稚内/
				波罗奈斯克/北克里松鼠克/哈巴罗夫斯克/鲁德纳亚松鼠普塔/符拉迪沃斯托克/
				首尔/郁陵岛/釜山/木浦/济州岛/台北/恒春/长春/北京/大连/青岛/
				上海/武汉/厦门/香港/浴室高/马尼拉/父岛/南鸟岛/
				")%…風力……hPa……
				}
		ENDSELECT
		
		PRINTFORML …接下来是渔业气象
		
		SELECTCASE 低気圧位置
			CASE "台風遠方", "台風勢力圏", "台風接近強風域", "台風接近暴風域", "台風最接近", "台風通過暴風域", "台風通過強風域"
				PRINTFORML ……存在中心气压…hPa的…号台风……正以…公里时速移动……
			CASE "寒冷渦遠方", "寒冷渦接近"
				PRINTFORML ……存在中心气压…hPa的低气压……几乎停滞……
			CASE "二つ玉低気圧遠方", "日本海低気圧遠方"
				PRINTFORML 黄海…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "日本海低気圧接近", "日本海低気圧通過中"
				PRINTFORML 日本海…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML 日本海…存在中心气压…hPa的低气压……正以…公里时速移动……
				PRINTFORML 本州…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "南岸低気圧遠方"
				PRINTFORML 中国东海…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "南岸低気圧接近"
				PRINTFORML 本州…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "温暖前線遠方"
				PRINTFORML 长江下游…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "温暖前線接近"
				PRINTFORML 西日本…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "暖域"
				PRINTFORML 北陆地区…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "寒冷前線通過"
				PRINTFORML 三陆近海…存在中心气压…hPa的低气压……正以…公里时速移动……
			CASE "低気圧未発生"
				PRINTFORML …的…存在中心气压…hPa的高气压……几乎停滞……
		ENDSELECT
	CASEELSE
		IF GET_LOCATION_AREA_TYPHOON() == 1 && IS_TYPHOON:1 >= 5
			SELECTCASE RAND:5
				CASE 0
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …强烈台风…号正以破纪录强度接近……
					ELSE
						PRINTFORML …强烈台风…号正在接近……
					ENDIF
				CASE 1
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …台风…号已造成各地重大损失……
					ELSE
						PRINTFORML …台风…号已造成各地灾情……
					ENDIF
				CASE 2
					PRINTFORML …线路全线暂停运行……
				CASE 3
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …堪比海湾台风的…请确保人身安全……
					ELSE
						PRINTFORML …请尽快完成避难准备……
					ENDIF
				CASE 4
					PRINTFORML …航空运输受到重大影响……
			ENDSELECT
		ENDIF
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……遭遇前所未有的大雨……
					CASE 1
						PRINTFORML …出现数十年一遇的降雨量……
					CASE 2
						PRINTFORML ……地区发布…大雨特别警报……
					CASE 3
						PRINTFORML ……警戒等级已升至5级……
					CASE 4
						PRINTFORML ……安全避难已困难…请采取保命行动……
				ENDSELECT
				
				SELECTCASE CONTINUOUS_RAINFALL
					CASE IS >= 40
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …政府指定为…特大灾害……
							CASE 1
								PRINTFORML …各地交通中断…救援队正在……
						ENDSELECT
					CASE IS >= 20
						SELECTCASE RAND:3
							CASE 0
								PRINTFORML …山体滑坡导致…户居民…受困……
							CASE 1
								PRINTFORML …部分地区已出现破纪录降雨……
							CASE 2
								PRINTFORML …河流超过泛滥危险水位……
						ENDSELECT
					CASE IS >= 10
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …市发布短时强降雨警报……
							CASE 1
								PRINTFORML …出现数年一遇的大雨……
						ENDSELECT
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……警戒等级升至4级……
					CASE 1
						PRINTFORML …过去数年土砂灾害风险最高……
					CASE 2
						PRINTFORML …气象台…地区土砂灾害风险……
					CASE 3
						PRINTFORML …市向…户居民发布避难指示……
					CASE 4
						PRINTFORML …线路全线暂停运行……
				ENDSELECT
			CASEELSE
				IF 暴風情報 == "暴風雪特別警報"
					SELECTCASE RAND:6
						CASE 0
							PRINTFORML ……地区发布…暴风雪特别警报……
						CASE 1
							PRINTFORML …数十年一遇的猛吹雪……
						CASE 2
							PRINTFORML …史无前例的暴风雪…请避免不必要外出……
						CASE 3
							PRINTFORML …猛烈暴风雪可能随时……
						CASE 4
							PRINTFORML …多地人员受困…请求救援……
						CASE 5
							PRINTFORML …高速公路多辆汽车被困……
					ENDSELECT
				ELSEIF 大雪情報 == "大雪特別警報"
					SELECTCASE RAND:4
						CASE 0
							PRINTFORML …遭遇豪雪级降雪……
						CASE 1
							PRINTFORML …数十年一遇的大雪……
						CASE 2
							PRINTFORML …地区发布大雪特别警报……
						CASE 3
							PRINTFORML …因破纪录大雪各地出现…孤立……
					ENDSELECT
				ELSEIF 大雪情報 == "大雪警報" && CONTINUOUS_SNOWFALL >= 20
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML ……气象台…地区发布显著大雪……
						CASE 1
							PRINTFORML …大雪引发大规模交通瘫痪……
					ENDSELECT
				ELSEIF 雷情報 == "雷注意報(竜巻注意情報)"
					SELECTCASE RAND:3
						CASE 0
							PRINTFORML …地区龙卷风…风险升高……
						CASE 1
							PRINTFORML …积雨云正在接近…请前往安全场所……
						CASE 2
							PRINTFORML …各地出现突发性大风和冰雹灾害……
					ENDSELECT
				ELSEIF POSSIBLITY_AVALANCHE >= 1
					PRINTFORML …各地频发雪崩灾害……
				ELSE
					PRINTFORML … ……………… ……
				ENDIF
		ENDSELECT
ENDSELECT
IF 玩家立绘开关
	PRINTFORML
	CALL PRINT_FACE_PLAYER, MASTER, "笑顔", "服", "", ""
	PRINTFORMDW 「唔姆唔姆...」
	PRINTFORMDW 「%TEXTR("原来如此♪/这样啊♪/还有这种事啊♪/得做好准备呢♪")%」
ENDIF
TIME += 5

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE496
SIF !TFLAG:100
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
;ウフフ中は不可
SIF CFLAG:诶嘿嘿
	RETURN 0
;睡眠恶作剧中は不可
SIF CFLAG:恶作剧
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;约会中
;SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:現在位置 != OMANEKIBEYA()
;	RETURN 0

RETURN 1
