;-------------------------------------------------
;就寝
;日常系指令、レベル1
;-------------------------------------------------
@COM402
#DIM 労働量
#DIM 収益

;魔改睡前自动扫地
CALL AUTO_SWEEP_BEFORE_SLEEP_ASK

IF FLAG:内職 && BASE:MASTER:気力 > 300 && BASE:MASTER:TSP > 0
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%在睡覺之前使用剩餘的TSP做了些副業
	労働量 = MIN(BASE:MASTER:気力, BASE:MASTER:TSP /2)
	収益 = 労働量 * (5 + RAND:5) / 10
	MONEY += 収益
	PRINTFORMW 掙了%金額表示(収益)%
ENDIF
IF FLAG:自動縁結び && !FLAG:祈願内容 && !CFLAG:陪睡中
	PRINTFORM 睡觉前
	IF MONEY >= 1000
		CALL GANKAKE_0(FLAG:自動縁結び)
		MONEY -= 1000
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%向神祈祷能和%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:自動縁結び))%变得更亲近
		SIF FLAG:祈願内容 < 0
			PRINTFORMW ……但这个願望被一股不思議的力量抹去了…
	ELSE
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%向神祈祷能和%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:自動縁結び))%成为好友、但是没有香火钱
	ENDIF
ENDIF

CALL KIKENBIHOUMON

LOCAL:10 = 0
FOR LOCAL, 1, MAXCHILD + 1
	SIF CHILDNAME:TARGET:LOCAL == ""
		CONTINUE
	SIF CHILD_INDEPENDENT:TARGET:LOCAL
		CONTINUE
	SIF DAY - CHILD_BIRTHDAY:TARGET:LOCAL >= CHILD_よちよち
		LOCAL:10 ++
NEXT

IF 兒童の睡眠時間(TARGET) && CFLAG:出産経過日 >= CHILD_よちよち && LOCAL:10 && FLAG:兒童描写 && !INRANGE(TIME, 540, 840)
	PRINTFORMDL 一家人睡在了一起…
	CALL KOJO_MESSAGE_SEND("EVENT", 0, TARGET, 0, 0, "CHILD_RAISING_OYASUMI")
	TFLAG:193 = 1
	RESETCOLOR
ENDIF

CALL OYASUMI

RETURN 1

;就寝
@COM_ABLE402
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(402)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室設定
SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:311 && CFLAG:MASTER:現在位置 != OMANEKIBEYA() && CFLAG:陪睡中 == 0
	RETURN 0
;起床後12時間以上経過してないとダメ（デバッグ中）
IF FLAG:1005 == 0
SIF NEMUKE() < 720
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
ENDIF
RETURN 1

@OYASUMI
IF TARGET > 0
	CFLAG:同行 = 0
	CFLAG:陪睡中 = 0
	CFLAG:MASTER:同行 = 0
	CFLAG:MASTER:陪睡中 = 0
ENDIF

IF CFLAG:MASTER:現在位置 == CFLAG:MASTER:311
	CALL SET_SLEEP(1,MASTER,0)
ELSE
	CALL SET_SLEEP(1,TARGET,1)
	SIF CFLAG:TARGET:衰弱
		CFLAG:TARGET:衰弱 = 0
ENDIF

CALL SET_SNEAK(0,TARGET,1)
;CALL SET_TOGETHER(0,TARGET,1)

CFLAG:MASTER:就寝時間 = DAY * 1440 + TIME
BEGIN AFTERTRAIN
