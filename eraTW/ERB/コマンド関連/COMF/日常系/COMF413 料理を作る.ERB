;-------------------------------------------------
;製作料理
;日常系指令、レベル1
;DISH_TYPE_NUM     料理の種別,1=軽食,2=主食,3=甜点,4=練習,LOCAL保存しておく
;-------------------------------------------------
@COM413
#DIM DISH_TYPE_NUM
#DIM MAXMENU
#DIM MENUCOUNT
#DIM SELECT_MENU
#DIM SELECT_ING
#DIM SELECT_DRG
#DIM SELECT_DRG_2
#DIM 練習不可
#DIM 料理結果
#DIM 従順係数
#DIM 好感度係数
#DIM 練習モード
#DIMS CONST TENKABUTSU, 8 = "", "なし", "栄養強化剤", "莓子酒", "黑朗姆酒", "白兰地VS", "白兰地VSOP", "白兰地XO"
#DIMS CONST SHIKOMI, 6 = "", "なし", "媚薬", "利尿剤", "催情薬", "睡眠薬",
COOKING_LCOUNT = 0
練習モード = 0
VARSET RESULTS
;助手設定
IF SHIRAHU(TARGET) == 1
	DISH_ASSI = TARGET
ELSE
	DISH_ASSI = 0
ENDIF
;練習判定
IF (!DISH_ASSI || LEARNED_MENU:DISH_ASSI >= 100 || ABL:TARGET:親密 < 5)
	練習不可 = 1
ELSE
	練習不可 = 0
ENDIF
;種別修整
IF !DISH_TYPE_NUM
	DISH_TYPE_NUM = 1
ELSEIF DISH_TYPE_NUM == 4 && 練習不可
	DISH_TYPE_NUM = 1
ENDIF
;材料修整,0番はローターなので
IF SELECT_ING == 0
	SIF !INRANGE(SELECT_MENU, 351, 360)
		SELECT_ING = -1
ENDIF
;仕込み修整
SIF DISH_ASSI
	SELECT_DRG = 0
;DISH_TYPEとメニューの範囲設定
SELECTCASE DISH_TYPE_NUM
CASE 1
	DISH_TYPE = 軽食
	MAXMENU = 100
CASE 2
	DISH_TYPE = 主食
	MAXMENU = 100
CASE 3
	DISH_TYPE = 甜点
	MAXMENU = 50
CASE 4
	練習モード = 1
	MAXMENU = 2
	;練習メニューは記憶しない
	SIF SELECT_MENU - 400 != DISH_ASSI
		SELECT_MENU = 0
ENDSELECT
;材料修整PLUS
IF SELECT_MENU > 0 && (STRLENS(DISH_TYPE) > 0 || DISH_TYPE_NUM == 4)
	CALL DISHDATA(SELECT_MENU, DISH_TYPE, 練習モード)
	;表示外なら選択リセット
	IF !RESULT
		SELECT_MENU = 0
		SELECT_ING = -1
	;作れないなら材料だけリセットして表示する
	ELSEIF RESULT < 10
		SELECT_ING = -1
	;作れるなら材料を設定
	ELSEIF RESULT == 10
		RESULT = 0
		;選択中の材料があったら選択を保持する
		FOR LOCAL, 1, 11
			IF DISH_ING:LOCAL == SELECT_ING && SELECT_ING >= 0
				RESULT = 1
				BREAK
			ENDIF
		NEXT
		;選択中の材料がなければ材料を再設定
		IF !RESULT
			FOR LOCAL, 1, 11
				IF ITEM:(DISH_ING:LOCAL)
					SELECT_ING = DISH_ING:LOCAL
					BREAK
				ENDIF
			NEXT
		ENDIF
	ENDIF
ENDIF


CALL COOKING_UI(DISH_TYPE_NUM, SELECT_MENU, SELECT_ING, SELECT_DRG, SELECT_DRG_2, 練習モード)

IF !練習モード
	PRINTFORML [ 7]随意去做%DISH_TYPE% 吧
ELSE
	PRINTL 
ENDIF
PRINTFORML [ 8]确认委托
PRINTFORML [ 0]放棄
INPUT

;放棄
IF !RESULT
	CALL RESET_DISH
	RETURN -1
;DISH_TYPE選択
ELSEIF INRANGE(RESULT, 1, 3)
	DISH_TYPE_NUM = RESULT
;練習
ELSEIF RESULT == 4 && !練習不可
	DISH_TYPE_NUM = RESULT
;料理選択
ELSEIF INRANGE(RESULT, 101, 400 + CHARANUM)
	SELECT_MENU = RESULT
	CALL DISHDATA(RESULT, DISH_TYPE, 練習モード)
	;表示外なら選択リセット
	IF !RESULT
		SELECT_MENU = 0
		SELECT_ING = -1
	;作れないなら材料だけリセットして表示する
	ELSEIF RESULT < 10
		SELECT_ING = -1
	;作れるなら材料を設定
	ELSEIF RESULT == 10
		RESULT = 0
		;選択中の材料があったら選択を保持する
		FOR LOCAL, 1, 11
			IF DISH_ING:LOCAL == SELECT_ING && SELECT_ING >= 0
				RESULT = 1
				BREAK
			ENDIF
		NEXT
		;選択中の材料がなければ材料を再設定
		IF !RESULT
			FOR LOCAL, 1, 11
				IF ITEM:(DISH_ING:LOCAL)
					SELECT_ING = DISH_ING:LOCAL
					BREAK
				ENDIF
			NEXT
		ENDIF
	ENDIF
;材料選択
ELSEIF INRANGE(RESULT, 11, 20) && ITEM:(DISH_ING:(RESULT - 10))
	SELECT_ING = DISH_ING:(RESULT - 10)
;仕込み選択
ELSEIF INRANGE(RESULT, 21, 30) && !DISH_ASSI
	LOCALS = %SHIKOMI:(RESULT - 20)%
	IF RESULT == 21
		SELECT_DRG = 0
	ELSEIF ITEM:GETNUM(ITEM, LOCALS) > 0
		SELECT_DRG = GETNUM(ITEM,LOCALS)
	ENDIF
;添加物選択
;(酒造パッチ)洋酒を添加できる
ELSEIF INRANGE(RESULT, 31, 40)
	LOCALS = %TENKABUTSU:(RESULT - 30)%
	IF RESULT == 31
		SELECT_DRG_2 = 0
	ELSEIF ITEM:GETNUM(ITEM, LOCALS) > 0
		SELECT_DRG_2 = GETNUM(ITEM,LOCALS)
	ENDIF
;おまかせ料理
ELSEIF RESULT == 7 && !練習モード
	SELECT_MENU = 0
	CALL FISHER_YATES_SHAFFLE(400 + CHARANUM)
	FOR LOCAL, 1, 400 + CHARANUM + 1
		CALL DISHDATA(SHAFFLE_ARRAY:LOCAL + 100, DISH_TYPE)
		SIF STRCOUNT(DISH_TASTE, "过季/")
			CONTINUE
		SIF STRCOUNT(DISH_TASTE, "謎料理/")
			CONTINUE
		IF RESULT == 10
			SELECT_MENU = SHAFFLE_ARRAY:LOCAL + 100
			FOR LOCAL:1, 1, 11
				IF DISH_ING:(LOCAL:1) >= 0
					IF ITEM:(DISH_ING:(LOCAL:1))
						SELECT_ING = DISH_ING:(LOCAL:1)
						BREAK
					ENDIF
				ENDIF
			NEXT
			BREAK
		ENDIF
	NEXT
	IF SELECT_MENU
		GOTO COOK
	ELSE
		PRINTFORMW 没有可制作的%DISH_TYPE%...
		COOKING_LCOUNT ++
	ENDIF
ELSEIF RESULT == 8
	CALL SHOW_ALL_IRAI_SLOT
	WAIT
	RESTART
;料理を作る
ELSEIF RESULT == 9 && SELECT_MENU
	$COOK
	CALL DISHDATA(SELECT_MENU, DISH_TYPE, 練習モード)
	IF RESULT != 10
		CLEARLINE 17 + COOKING_LCOUNT
		RESTART
	ENDIF
	CALL DISH_TASTE_ADD(SELECT_ING)
	DISH_DRG = SELECT_DRG
	DISH_DRG_2 = SELECT_DRG_2
	DISH_RANK = 食材レベル(SELECT_ING)
	;DISHNAMEは口上でのみ使用してください
	DISHNAME = %DISH_NAME%
	FLAG:料理 = SELECT_MENU
	SELECT_MENU = 0
	PRINTFORML 使用食材：%ITEMNAME:SELECT_ING%
	CALL DISH_COOKING(練習モード)
	ITEM:SELECT_ING --
	SIF SELECT_DRG
		ITEM:SELECT_DRG --
	SIF SELECT_DRG_2
		ITEM:SELECT_DRG_2 --
	SELECTCASE DISH_TYPE
	CASE "軽食"
		DOWNBASE:MASTER:体力 = 50
		DOWNBASE:MASTER:気力 = 50
	CASE "主食"
		DOWNBASE:MASTER:体力 = 100
		DOWNBASE:MASTER:気力 = 100
	CASE "甜点"
		DOWNBASE:MASTER:体力 = 100
		DOWNBASE:MASTER:気力 = 100
	ENDSELECT
	EXP:MASTER:料理経験 ++
	SIF ITEM:調理指南書 && !RAND:2
		EXP:MASTER:料理経験 ++
	SIF 練習モード
		EXP:MASTER:料理経験 ++
	STAIN:MASTER:口 = 0
	STAIN:MASTER:手 = 0
	CFLAG:MASTER:口内精液 = 0
	CFLAG:MASTER:脸上精液 = 0
	CFLAG:MASTER:手上精液 = 0
	TIME += DISH_TIME
	SIF CFLAG:MASTER:同行
		CFLAG:MASTER:同行 += DISH_TIME
	;助手へ与える効果
	IF DISH_ASSI
		;獲得ソース
		IF ABL:DISH_ASSI:従順 <= 1
			従順係数 = 0
		ELSEIF ABL:DISH_ASSI:従順 <= 3
			従順係数 = 1
		ELSEIF ABL:DISH_ASSI:従順 <= 5
			従順係数 = 2
		ELSEIF ABL:DISH_ASSI:従順 <= 8
			従順係数 = 3
		ELSEIF ABL:DISH_ASSI:従順 <= 11
			従順係数 = 4
		ELSEIF ABL:DISH_ASSI:従順 <= 14
			従順係数 = 5
		ELSE
			従順係数 = 6
		ENDIF
		IF CFLAG:DISH_ASSI:好感度 < 100
			好感度係数 = 0
		ELSEIF CFLAG:DISH_ASSI:好感度 < 500
			好感度係数 = 1
		ELSEIF CFLAG:DISH_ASSI:好感度 < 1000
			好感度係数 = 2
		ELSEIF CFLAG:DISH_ASSI:好感度 < 5000
			好感度係数 = 4
		ELSEIF CFLAG:DISH_ASSI:好感度 < 10000
			好感度係数 = 6
		ELSEIF CFLAG:DISH_ASSI:好感度 < 30000
			好感度係数 = 8
		ELSE
			好感度係数 = 10
		ENDIF
		;歓楽だけなので割と高効率,そもそも親密が必要なので
		SOURCE:DISH_ASSI:歓楽 = 300 + 30 * (従順係数 + 好感度係数) * DISH_TIME
		;信頼
		TFLAG:98 = 2 + (ABL:DISH_ASSI:料理技能 / 2)
		SELECTCASE DISH_TYPE
		CASE "軽食"
			DOWNBASE:DISH_ASSI:体力 = 50
			DOWNBASE:DISH_ASSI:気力 = 50
		CASE "主食"
			DOWNBASE:DISH_ASSI:体力 = 100
			DOWNBASE:DISH_ASSI:気力 = 100
		CASE "甜点"
			DOWNBASE:DISH_ASSI:体力 = 100
			DOWNBASE:DISH_ASSI:気力 = 100
		ENDSELECT
		;ちょっと複雑だけど要するとこう→ 1 + RAND:(基本点 / 4) + 習得速度
		EXP:DISH_ASSI:料理経験 += MAX(1 + RAND:(MAX((DISH_BASEPOINT - 300) / 80, 1)) + TALENT:DISH_ASSI:習得速度, 1)
		SIF ITEM:調理指南書 && !RAND:2
			EXP:DISH_ASSI:料理経験 ++
		IF ABL:MASTER:料理技能 <= ABL:DISH_ASSI:料理技能
			EXP:MASTER:料理経験 ++
		ELSEIF ABL:MASTER:料理技能 > ABL:DISH_ASSI:料理技能
			EXP:DISH_ASSI:料理経験 ++
		ENDIF
		SIF CFLAG:DISH_ASSI:同行
			CFLAG:DISH_ASSI:同行 += DISH_TIME
		STAIN:DISH_ASSI:口 = 0
		STAIN:DISH_ASSI:手 = 0
		CFLAG:DISH_ASSI:口内精液 = 0
		CFLAG:DISH_ASSI:脸上精液 = 0
		CFLAG:DISH_ASSI:手上精液 = 0
	ENDIF
	;魔改超级幽幽子大人，100%出现模式，无视其它条件必然出现每天一次。
	IF superYuyukoSamaMode == 2 && superYuyukoSamaIsHungry >= 1
		CALL superYuyukoSama
		PRINTW
		RETURN -1
	;概率出现模式，superYuyukoSamaIsHungry，开启后每天加一（即每天增加1%概率），关闭后每天重置为0（即0%概率）
	;!CAN_TAKARI(66)只在正常幽幽子无法出现的时候才能出现
	ELSEIF (RAND:100 + superYuyukoSamaIsHungry) >= 100 && !CAN_TAKARI(66) && superYuyukoSamaMode > 0
		CALL superYuyukoSama
		PRINTW
		RETURN -1
	ENDIF
	RETURN 1
ENDIF
CLEARLINE 17 + COOKING_LCOUNT
RESTART

;-------------------------------------------------
;料理を生成する関数,といっても大半の情報はすでに設定済み
;ここでは調理部分の処理をする
;-------------------------------------------------
@DISH_COOKING(練習モード)
#DIM 道具力
#DIM BONUS点
#DIM 減点
#DIM 練習モード
;念の為最終チェック
SIF DISH_NAME == ""
	THROW
SIF DISH_TYPE == ""
	THROW
SIF !DISH_TIME
	THROW

PRINTFORMW \@ DISH_ASSI ? %CALLNAME:DISH_ASSI%と # \@【%DISH_NAME%】を作ります
PRINTFORML ・
PRINTFORML ・・・
SIF DISH_TIME > 10
	PRINTFORML ・・・・・・
SIF DISH_TIME > 30
	PRINTFORML ・・・・・・・・・・・・
;たかり
IF DISH_TYPE == "軽食"
	CALL COOKING_TAKARI([[幽幽子]], 5)
	CALL COOKING_TAKARI([[紫苑]], 5)
ELSEIF DISH_TYPE == "主食"
	CALL COOKING_TAKARI([[幽幽子]], 10)
	CALL COOKING_TAKARI([[紫苑]], 10)
ENDIF

;TASTE設定
SIF FLAG:扮演
	CALL DISH_TASTE_FROM_CHARA(FLAG:扮演)
SIF DISH_ASSI && !練習モード
	CALL DISH_TASTE_FROM_CHARA(DISH_ASSI)
DISH_TASTE += "できたて/"
DISH_TASTE += DISH_NAME + "/"
DISH_TASTE += DISH_TYPE + "/"
SIF 練習モード
	DISH_TASTE += "練習/"
;[練習]は过季を消す
SIF STRCOUNT(DISH_TASTE, "練習/")
	DISH_TASTE '= REPLACE(DISH_TASTE, "过季\/", "")
;[中立温度]は夏だけ冷（荞麦麺,乌冬麺等）
IF STRCOUNT(DISH_TASTE, "中立温度/")
	IF GET_MONTH() == "夏之月"
		DISH_TASTE '= REPLACE(DISH_TASTE, "中立温度\/", "冷/")
	ELSE
		DISH_TASTE '= REPLACE(DISH_TASTE, "中立温度\/", "温暖/")
	ENDIF
ENDIF
;鮮度設定,調理時間を追加しておく
DISH_LIMIT = DISH_MAXLIMIT + DISH_TIME

;表示用の料理名設定
EX_DISH_NAME = 
IF DISH_ASSI
	IF 練習モード
		EX_DISH_NAME += CALLNAME:DISH_ASSI + "教学用的"
	ELSE
		EX_DISH_NAME += "和" + CALLNAME:DISH_ASSI + "一起做的"
	ENDIF
ENDIF
IF STRCOUNT(DISH_TASTE, "女僕流/")
	EX_DISH_NAME += "女僕流"
ELSEIF STRCOUNT(DISH_TASTE, "天狗流/")
	EX_DISH_NAME += "天狗流"
ELSEIF STRCOUNT(DISH_TASTE, "冥界風/")
	EX_DISH_NAME += "冥界風"
ELSEIF STRCOUNT(DISH_TASTE, "天界風/")
	EX_DISH_NAME += "天界風"
ELSEIF STRCOUNT(DISH_TASTE, "地底風/")
	EX_DISH_NAME += "地底風"
ELSEIF STRCOUNT(DISH_TASTE, "地獄風/")
	EX_DISH_NAME += "地獄風"
ELSEIF STRCOUNT(DISH_TASTE, "畜生流/")
	EX_DISH_NAME += "畜生流"
ELSEIF STRCOUNT(DISH_TASTE, "月都風/")
	EX_DISH_NAME += "月都風"
ELSEIF STRCOUNT(DISH_TASTE, "玉兎流/")
	EX_DISH_NAME += "玉兎流"
ELSEIF STRCOUNT(DISH_TASTE, "魔界風/")
	EX_DISH_NAME += "魔界風"
ELSEIF STRCOUNT(DISH_TASTE, "夢幻風/")
	EX_DISH_NAME += "夢幻風"
ELSEIF STRCOUNT(DISH_TASTE, "外界風/")
	EX_DISH_NAME += "外界風"
ELSEIF STRCOUNT(DISH_TASTE, "化学的/")
	EX_DISH_NAME += "化学的"
ELSEIF STRCOUNT(DISH_TASTE, "小人族秘伝的味道/")
	EX_DISH_NAME += "小人族秘伝の"
ELSEIF STRCOUNT(DISH_TASTE, "稗田家秘伝の味/")
	EX_DISH_NAME += "稗田家秘伝の"
ELSEIF STRCOUNT(DISH_TASTE, "純化的味道/")
	EX_DISH_NAME += "純化的"
ELSEIF STRCOUNT(DISH_TASTE, "逆転/")
	EX_DISH_NAME += "逆転した"
ELSEIF STRCOUNT(DISH_TASTE, "大火力/")
	EX_DISH_NAME += "大火力"
ELSEIF STRCOUNT(DISH_TASTE, "氷結/")
	EX_DISH_NAME += "氷結"
ELSEIF STRCOUNT(DISH_TASTE, "心跳味/")
	EX_DISH_NAME += "心跳"
ELSEIF STRCOUNT(DISH_TASTE, "未知の味/")
	EX_DISH_NAME += "不思議な"
ELSEIF STRCOUNT(DISH_TASTE, "闪耀/")
	EX_DISH_NAME += "闪光"
ELSEIF STRCOUNT(DISH_TASTE, "生命力/")
	EX_DISH_NAME += "妖精の"
ELSEIF STRCOUNT(DISH_TASTE, "仙術/")
	EX_DISH_NAME += "仙人の"
ELSEIF STRCOUNT(DISH_TASTE, "鬼の魔力/")
	EX_DISH_NAME += "鬼の"
ELSEIF STRCOUNT(DISH_TASTE, "血の魔力/")
	EX_DISH_NAME += "吸血鬼の"
ELSEIF STRCOUNT(DISH_TASTE, "魔力/")
	EX_DISH_NAME += "魔法の"
ELSEIF STRCOUNT(DISH_TASTE, "妖力/")
	EX_DISH_NAME += "妖气的"
ELSEIF STRCOUNT(DISH_TASTE, "霊障/")
	EX_DISH_NAME += "霊的"
ELSEIF STRCOUNT(DISH_TASTE, "秘術/")
	EX_DISH_NAME += "神秘的"
ENDIF
IF STRCOUNT(DISH_TASTE, "去掉蔬菜的/")
	IF DISH_NAME == "炒蔬菜"
		EX_DISH_NAME += "去掉蔬菜的炒蔬菜"
	ELSEIF DISH_NAME == "涼拌菜"
		EX_DISH_NAME += "没有蔬菜的凉拌菜"
	ELSEIF DISH_NAME == "漬物大碗飯"
		EX_DISH_NAME += "大碗飯"
	ELSEIF DISH_NAME == "奶酪番茄沙拉"
		EX_DISH_NAME += "牡蛎沙拉"
	ELSE
		EX_DISH_NAME += "去掉蔬菜的"
		EX_DISH_NAME += DISH_NAME
	ENDIF
ELSE
	EX_DISH_NAME += DISH_NAME
ENDIF
PRINTFORML ＜%EX_DISH_NAME%＞
CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")

;基本点数計算
道具力 = ITEM:银杏案板
BONUS点 = 0
減点 = 0
SIF STRCOUNT(DISH_TASTE, "道具の共鳴/")
	TIMES 道具力, 2.0

SIF STRCOUNT(DISH_TASTE, "旬/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "玉兎流/") && STRCOUNT(DISH_TASTE, "胡蘿卜/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "プロの味/")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "闪耀/")
	BONUS点 += 1
;得意料理や切れ味は該当していない場合消去されているのでこれでOK,得意料理は"/"を外す
SIF STRCOUNT(DISH_TASTE, "得意料理")
	BONUS点 += 2
SIF STRCOUNT(DISH_TASTE, "切れ味/")
	BONUS点 += 1
SIF STRCOUNT(DISH_TASTE, "美味的水/")
	BONUS点 += 1
;嫉妬,隠し味の効果は食べる時

SIF DISH_DRG
	減点 += 1
IF DISH_DRG_2
	IF ITEM:DISH_DRG_2 == ITEM:栄養強化剤
		減点 += 1
	;(酒造パッチ)洋酒にBONUS点
	ELSEIF ITEM:DISH_DRG_2 == ITEM:莓子酒 || ITEM:DISH_DRG_2 == ITEM:黑朗姆酒 || ITEM:DISH_DRG_2 == ITEM:白兰地VS
		BONUS点 += 2
	ELSEIF ITEM:DISH_DRG_2 == ITEM:白兰地VSOP
		BONUS点 += 3
	ELSEIF ITEM:DISH_DRG_2 == ITEM:白兰地XO
		BONUS点 += 4
	ENDIF
ENDIF

;DISH_ASSIが0ならMASTERだから分岐いらないんだけども一応
IF DISH_ASSI
	DISH_BASEPOINT = ABL:MASTER:料理技能 + ABL:DISH_ASSI:料理技能 + DISH_RANK + 道具力 + BONUS点 - 減点
	DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
	PRINTFORM 成品：
	CALL 出来栄え(DISH_BASEPOINT)
	PRINTFORML 　／%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的技能({ABL:MASTER:料理技能}) + %NAME_OUTPUT_TRANSLATION("CALLNAME", DISH_ASSI)%的技能({ABL:DISH_ASSI:料理技能}) + 食材的品质({DISH_RANK}) \@ 道具力 ? + 道具的品质({道具力}) # \@ \@ BONUS点 ? + BONUS点({BONUS点}) # \@ \@ 減点 ? - 添加物({減点}) # \@
ELSE
	DISH_BASEPOINT = ABL:MASTER:料理技能 + ABL:MASTER:料理技能 + DISH_RANK + 道具力 + BONUS点 - 減点
	DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
	PRINTFORM 成品：
	CALL 出来栄え(DISH_BASEPOINT)
	PRINTFORML 　／%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%の技能 x 2({ABL:MASTER:料理技能 * 2}) + 食材的品质({DISH_RANK}) \@ 道具力 ? + 道具的品质({道具力}) # \@ \@ BONUS点 ? + BONUS点({BONUS点}) # \@ \@ 減点 ? - 添加物({減点}) # \@
ENDIF
PRINTL 

;練習結果,初期状態だと4～7回必要。料理技能が上がるにつれ習得速度が上昇していく。
IF 練習モード
	LEARNED_MENU:DISH_ASSI += 20 + (TALENT:MASTER:習得速度 * 5) + RAND:(MAX(ABL:MASTER:料理技能 * 25,1))
	PRINTFORM 　習熟度：
	FOR LOCAL, 1, 10
		IF LOCAL <= LEARNED_MENU:DISH_ASSI / 10
			SETCOLOR 0x64FFDC
			PRINT ★
		ELSE
			RESETCOLOR
			PRINT ☆
		ENDIF
	NEXT
	RESETCOLOR
	PRINTL 
	SIF LEARNED_MENU:DISH_ASSI >= 100
		CALL LEARNING_SP_DISH(DISH_ASSI, DISH_NAME)
ENDIF

IF STRCOUNT(DISH_TASTE, "跳蛋/")
	TFLAG:194 = 100
ELSEIF DISH_TYPE == "甜点"
	TFLAG:194 = 3
ELSEIF DISH_TYPE == "主食"
	TFLAG:194 = 2
ELSE
	TFLAG:194 = 1
ENDIF

;WAIT


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE413
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;掃除実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(413)
	RETURN RESULT

;厨房なのか
SIF !KITCHEN(CFLAG:MASTER:現在位置)
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;恶作剧中
SIF CFLAG:MASTER:恶作剧
	RETURN 0
;既に料理を作っている
SIF DISH_NAME != ""
	RETURN 0
RETURN 1
