;-------------------------------------------------
;外出
;日常系指令、レベル1
;-------------------------------------------------
@COM405
VARSET LOCAL
FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):同行
		LOCAL:1 ++
		SIF CFLAG:LOCAL:衰弱 || BASE:LOCAL:気力 == 0
			LOCAL:2 ++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:工作量
			LOCAL:3 ++
		SIF CFLAG:LOCAL:態度 < 1 && CAN_IRAI_CHARA_GO_TOGATHER(LOCAL)
			LOCAL:4 ++
	ENDIF
NEXT

IF NEMUKE() >= 720
	DRAWLINE
	PRINTL 在睡意来袭时还是别外出比较好吧
	RETURN -1
ELSEIF LOCAL:1 > 2
	DRAWLINE
	PRINTL 不能带三人以上外出约会
	RETURN -1
ELSEIF LOCAL:2 || BASE:MASTER:気力 == 0
	DRAWLINE
	PRINTFORML 因为身体不适所以无法外出\@ BASE:MASTER:気力 == 0 ? （気力為零） # （同行者身体不适）\@
	RETURN -1
ELSEIF LOCAL:3 && FESTIVAL() == ""
	DRAWLINE
	PRINTL 因为同行者处于工作日所以无法外出
	RETURN -1
ELSEIF LOCAL:4
	DRAWLINE
	PRINTL 被说了有时间出门不如早点完成依頼…
	RETURN -1
;(天候パッチ)悪天候時も外出可能に
;ELSEIF TIME:5 == 9 || TIME:5 == 5 || WIND_VELOCITY >= 2
;	DRAWLINE
;	PRINTL 因为天气恶劣所以无法外出
;	RETURN -1
ELSEIF TALENT:MASTER:100 < -4 
	DRAWLINE
	PRINTL 用这様的身体外出是很危険的
	RETURN -1
ENDIF
IF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP)
	PRINTFORML 首先往%NAME_FROM_PLACE(ENTRANCE)%移动
	CALL SKIP_MOVE(CFLAG:MASTER:現在位置, ENTRANCE)
ENDIF
LOCAL:3 = 0
DRAWLINE
CALL OTHERREGIONS("外出")
LOCAL:2 = RESULT
LOCAL = 0
IF LOCAL:2 == MAIN_MAP || LOCAL:2 == 100
	FLAG:约会的对象 = 0
	RETURN -1
ENDIF
RETURN 1

@COM_ABLE405
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(405)
	RETURN RESULT
;携带中は不可
SIF TFLAG:運搬
	RETURN 0
SIF CFLAG:MASTER:陪睡中
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
SIF CFLAG:MASTER:恶作剧
	RETURN 0
;参道設定
;SIF !CHK_ENTRANCE(CFLAG:MASTER:現在位置, MAIN_MAP)
;	RETURN 0
SIF !AT_HOME(MASTER)
	RETURN 0
RETURN 1
