;※※※※※※※※※※※※※※※※※※※※※※※※※
;奉仕系指令＠椛＆ナズ口上構文お借りしました
;※※※※※※※※※※※※※※※※※※※※※※※※※
;==================================================
;80,手淫
;==================================================
@M_KOJO_MESSAGE_COM_K30_80
CALL M_KOJO_MESSAGE_COM_K30_80_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_80_1
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;時姦中
	IF FLAG:70
		PRINTFORMW 
		RETURN 1
	;マグロスイッチオン
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		PRINTFORMW 「用手…摩擦吗？」
		PRINTFORMW 「嗯，握着没关系吗？」
		PRINTFORMW 「…啊，对不起，不应该用那么大的力量……」
		RETURN 1
	;基本セット
	;これでMASTERが射精する
	ELSEIF NOWEX:MASTER:11
		;慣れてない
		IF EXP:手淫経験 < 10
			PRINTFORML 「手已经黏糊糊的了…」
			PRINTFORMW 「啊，再快点摩擦就好了吗？」
		;顔射待ち
		ELSEIF ABL:精液中毒 >= 5 && ABL:技巧 > 3 && RAND:2 == 1
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 「%K30_C_NAME(MASTER)%心情好的话，才是最重要的……」
					PRINTFORML 「那个…我想用嘴给你做…好想为你舔啊」
					PRINTFORMW 「……啊，出来了吧？那么……请给到我这里——啊—呜」
				CASE 1
					PRINTFORML 「不需要忍耐…就这样用手？　还是在脸上?嘴巴也可以哦?」
					PRINTFORMDL 映姬伸出软舌，舔了舔前列腺液渗出的铃口
					PRINTFORMW 「哼哼~、有你的味道♥ …想要，就这样让你…射出来♥」
				CASE 2
					PRINTFORML 「啊，好像马上就要出来了吧…不忍耐也可以哦？」
					PRINTFORMDW 映姬用陶醉的眼神对待着爱郎的肉棒
					PRINTFORML 「我…想要%K30_C_NAME(MASTER)%的精液、因为已，已经不能忍耐了……」
					PRINTFORMW 「就这样，请多给我一些吧♥　―――啊ー嗯」
			ENDSELECT
		;手馴れた手つき
		ELSEIF ABL:技巧 > 3
			SELECTCASE RAND:3
				CASE 0
					PRINTFORML 「在这里，这样擦的话…」
					PRINTFORML 「哼哼~、看起来很舒服呢…怎么样？ 我进步了吧？」
					PRINTFORMW 「所以，我知道已经受不了了…就请都射出来吧♥」
				CASE 1
					PRINTFORML 「嗯…要去了吗？」
					PRINTFORMW 「不用忍耐，没关系的哦…」
				CASE 2
					PRINTFORMW 「啊，刺激不够吗？那么，就这样…」
					PRINTFORMDL 映姬用另一只手温柔地握住爱郎的玉袋以催促射精…
					PRINTFORMW 「精液想出来的话，请不要客气呀，射出来吧？ 你看……♥」
			ENDSELECT
		;戸惑いながら
		ELSEIF RAND:2 == 1
			PRINTFORML 「嗯，出来吧…？」
			PRINTFORML 「我知道了…就这样用手搓着…」
			PRINTFORMDW 映姬一副认真的表情埋头于摩擦着……
		ELSE
			PRINTFORML 「这个，这样可以吗？不会痛吗？」
			PRINTFORML 「从铃口里渗透出来了一些…是马上就要出来了呢」
			PRINTFORMW 「嗯，我知道了，那就这样…」
		ENDIF
	;通常
	;何度も射精した
	ELSEIF TCVAR:30:396 > 4 && RAND:3 == 1 && ABL:指 > 2
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 「啊…已经变硬了…」
				PRINTFORMDW 看着不管射出多少次都会耸起的肉棒，映姬很高兴哦
				PRINTFORMW 「安心…%K30_C_NAME(MASTER)%，我的手会让你很舒服呢……♥」
			CASE 1
				PRINTFORML 「好厉害，%K30_C_NAME(MASTER)%…这么的…壮硕…」
				PRINTFORMDW 映姬一边佩服一边处理着%CALLNAME:MASTER%的肉棒……
		ENDSELECT
	;勃起きってない
	ELSEIF BASE:MASTER:勃起 < 1000 && RAND:3== 1
		PRINTFORML 「啊、变得软绵绵的了…」
		PRINTFORMW 「马上就让你再次雄起……♥」
	;指技量高め
	ELSEIF ABL:指 > 3 && RAND:2 == 1
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML 「不可思议…明明只是用手帮你做的，连我也心跳得厉害…」
				PRINTFORMDW 映姫出神地对%CALLNAME:MASTER%的阴茎处理着
				PRINTFORML 「好像能传达出%CALLNAME:MASTER%的心情呢……」
			CASE 1
				PRINTFORML 「用手的话能知道你的反应…很開心。例如，像这样…」
				PRINTFORMDL 映姬握着肉棒的大拇指…轻软地揉搓里筋附近…
				PRINTFORMW 「你看，看起来很舒服呢♪那么，接下来……」
				PRINTFORML 「……这是感谢你一直以来为我做的事的回礼。是真的哦？」
				PRINTFORMDW 在用手指玩弄阴茎的同时，映姬笑起来。
			CASE 2
				PRINTFORML 「不给它弄得更温暖一点的话，心情就不会好起来吧？」
				PRINTFORMDW 映姬为了增加阴茎的滑腻，在龟头上滴了好几次香唾
				PRINTFORMDL 严肃的阎王大人的香艳举动让%CALLNAME:MASTER%欲火飙升…。
				PRINTFORMW 「即使这样…下流也可以。这种事小女子我，只为%CALLNAME:MASTER%才做…」
		ENDSELECT
	ELSE
		SELECTCASE RAND:3
			CASE 0
				SELECTCASE  TALENT:MASTER:形状
					;未熟・粗チン
					CASE 1, 2
						PRINTFORMW 「这样看来总觉得很可爱……」
						SIF TALENT:処女 == 0
							PRINTFORMW 「我不在乎大小…」
					;健壮的
					CASE 3
						PRINTFORMW 「你这里果然很大啊……」
						SIF TALENT:処女 == 0
							PRINTFORMW 「那个，像那样…在我里面……♥」
					;光る
					CASE 4
						PRINTFORMW 「你这里果然很奇怪啊……」
						SIF TALENT:処女 == 0
							PRINTFORMW 「好几次…如果被插上的话，我的也…会发光吗……」
					;长疣的・巨根
					CASEELSE
						PRINTFORMW 「无论何时看你的都有点害怕……」
						SIF TALENT:処女 == 0
							PRINTFORMW 「最初…做的时候也是，真的…挺疼得……」
				ENDSELECT
			CASE 1
				PRINTFORML 「一直在一跳一跳抽搐呢……」
				PRINTFORMW 「而且，也很烫……」
			CASE 2
				;準備出来て経験那个ば1/2でおねだりして欲しいよね
				IF (ABL:30:Ｖ感覚 >= 3 || ABL:30:Ａ感覚 >= 3) && BASE:MASTER:勃起 >= 1000 && RAND:2 ==1
					;潤滑と経験だけ見るガバ判定だけど
					IF PALAM:潤滑 < PALAMLV:2
						PRINTFORMW 「哼哼~…变大了♪接下来，我也……♥」
					ELSE
						PRINTFORML 「……呼呼、变大了」
						PRINTFORMW 「这样的话…对我的\@ TCVAR:30:391 > TCVAR:30:393 ? 这里 # 屁股 \@也…呢♥」
						SIF TCVAR:30:391 > TCVAR:30:392
							PRINTFORMDW 映姫有些害羞地指着%SLIT(, 1)%
					ENDIF
				ELSE
					PRINTFORMW 「是用手比较好吧？ 还是……」
				ENDIF
		ENDSELECT
	ENDIF
ENDIF
RETURN 1

;==================================================
;81,口交
;==================================================
;映姫様は説教好き→舌と口をよく動かす
;映姫様は[献身的]に[鼓舞]をする素質がある
;以上から映姫様は口交大好き設定になってます
;アニメ付けたついでに分岐増
;-------------------------------------------------
@M_KOJO_MESSAGE_COM_K30_81
IF SHIRAHU(30) 
	CALL K30_BLOW_JOB_ANIME(2)
	PRINTFORML 
ENDIF
CALL M_KOJO_MESSAGE_COM_K30_81_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_81_1
#DIM 射出, 2
#DIM 箇所
VARSET 射出
;清洁口交用＠地の文からコピペ
;箇所 = TCVAR:112 MASTERが直前のターンに已射精部位FLAG 1=膣内 2=肛門
;射出:0 = 前ターン中出しされたTARGET_ID
;射出:1 = 前ターンＶorA挿入されていたTARGET_ID
IF TCVAR:MASTER:112 == 1
	射出:0 = MASTER_POSE(SET_V,SET_P,1)
	箇所 = 1
ELSEIF TCVAR:MASTER:112 == 2
	射出:0 = MASTER_POSE(SET_A,SET_P,1)
	箇所 = 2
ENDIF
IF MASTER_POSE(SET_V,SET_P,1)
	射出:1 = MASTER_POSE(SET_V,SET_P,1)
	箇所 = 1
ELSEIF MASTER_POSE(SET_A,SET_P,1)
	射出:1 = MASTER_POSE(SET_A,SET_P,1)
	箇所 = 2
ENDIF
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;時姦中
	IF FLAG:70
		PRINTFORMW 
		RETURN 1
	;マグロスイッチオン
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		PRINTFORML 「哎呀…？」
		PRINTFORMW 「做这种下流的事总觉得快没原则了，那个……舔这个就好了吧……」
		PRINTFORMDL 看着%CALLNAME:MASTER%的%TINKO(PLAYER)%映姬不安地问，接着叹了口气
		PRINTFORMW 「只、只为你而做哟…那么，失礼了……嗯唔♥」
		PRINTFORMDW 映姬满脸通红、怯生生地伸出软舌，小心翼翼地从%TINKO_SIMPLE(0)%的前端開始舔…
		RETURN 1
	;基本セット
	;清洁口交
	ELSEIF 射出:0
		;映姫様に中出しした
		IF 射出:0 == 30
			IF ABL:精液中毒 >= 5
				SELECTCASE RAND:4 + (INRANGE(TCVAR:21, 1, 3) * 4)
					CASE 0
						PRINTFORML 「啾…哈姆，嗯…啾……已经没有剩下了呢」
						PRINTFORMW 「……还不够……。要不要再来一回呢？」
					CASE 1
						PRINTFORML 「嗯…漂亮地弄干净了……但是，如果可以的话，请再给我一次…怎么样？」
						PRINTFORMW 「我的\@ TCVAR:102 == 1 ? 这个 # 屁股 \@那么，下次…我会让你舒服的…♥」
					CASE 2
						PRINTFORML 「…是，漂亮地弄干净了♪」
						PRINTFORMW 「但是，\@ BASE:MASTER:勃起 >= 1000 ? 滴答作响 # 看起来还很精神 \@是啊…」
						PRINTFORMW 「接下来…可以给我吗？」
					;こ れ は ひ ど い
					CASE 3
						PRINTFORML 「诶~…\@ TCVAR:102 == 1 ? 自，自己 # 屁、屁股 \@味道的…感想？」
						PRINTFORMW 「非说不可的话……只能说你的精液很好喝…」
						PRINTFORMW 「什么也不说……不行吗？」
						PRINTFORMW 「……那个…\@ TCVAR:102 == 1 ? 稍微…酸了# 稍微…有点苦 \@…是」
					CASE 4
						PRINTFORMW 「确实…射出来之后会变得敏感吧？那么现在的话……啊呜」
						PRINTFORMDL 映姬提取剩下的精液后，再次巧妙地使用香舌继续攻击龟头……
						PRINTFORMDW 比平时更加敏锐的快感和映姬的侍奉让%CALLNAME:MASTER%爽的两眼泛白…
						CALL HPH_PRINT("（呵呵…好厉害的表情……也许还有点可爱呢HPH）", "W")
					CASE 5
						PRINTFORMW 「已经变干净了…明明还不够吃…」
						PRINTFORML 「………！　对了对了，这里还足够……啊嗯♥」
						PRINTFORMDL 映姬松開嘴，用手指一插被中出了的\@ TCVAR:102 == 1 ? %SLIT(30)% # 肛門 \@，把精液刮出来
						PRINTFORMW 「嗯~…呼呼呼~♥　这个琼浆，可不能就这样……」
						PRINTFORML 「啊ー呜、啾卟、嘞咯…♥」
						PRINTFORMDL 竟然把到手的精液涂在阴茎上，再次继续口交
						PRINTFORMDW 太淫荡的映姬的样子和灵活软舌令%CALLNAME:MASTER%\@ RAND:2 ? 好不容易才忍住连射 # 的阴茎很快就恢复了力量 \@…
					CASE 6
						PRINTFORML 「嗯~…♥　嗯啾~、嗯姆~……啾噜噜~！」
						PRINTFORMDL 尽管暂时放開了嘴，但映姬马上又開始口交了，已经变成没有%CALLNAME:MASTER%的肉棒就活不下去的女人了
						LOCALS:0 '= "", "一边吸着阴茎发出咕嘟咕嘟的声音", "被卵蛋贴着玉颊", "把阴茎吞到极限，顶到喉咙深处"
						PRINTFORMDW %CALLNAME:MASTER%の%LOCALS:(TCVAR:21)%被继续侍奉，不管不顾，一直干下去…
						PRINTFORML （%K30_C_NAME(MASTER)%的…小鸡鸡，很好吃…♥）
						PRINTFORML （\@ TCVAR:102 == 1 ? 在肚子里 # 屁股 \@光靠，不够……嘴里，也想要牛奶呀…）
						PRINTFORMW （射嘴里吧…给映姬，射满满的…映姬没有你的精华已经不行了…♥）
					CASE 7
						PRINTFORML 「嗯~♥　这样，干净了吗…？」
						PRINTFORML 「啊~…又冒出来了，前列腺液…」
						PRINTFORML 「这个也会给你弄干净的……哈呜♥　啾噜…」
						PRINTFORMDW 在映姬的热心服务下…%CALLNAME:MASTER%的胯间好像永远也不会休息
				ENDSELECT
			ELSEIF ABL:精液中毒 >= 2
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML 「啊，又硬了……」
						PRINTFORMW 「\@ TCVAR:102 == 1 ? 这里 # 屁股 \@明明只给了那一个……♥」
						PRINTFORMW 「还会……做吧？」
					CASE 1
						PRINTFORML 「啊…啾卟…哈、啾……」
						PRINTFORMW 「……就这样拿出来吗？」
					CASE 2
						PRINTFORML 「诶~…\@ TCVAR:102 == 1 ? 自，自己 # 屁、屁股 \@味道的…感想？」
						PRINTFORML 「别，别问奇怪的事…而且各种各样的…混在一起…」
						PRINTFORMW 「那种事我才不知道……」
				ENDSELECT
			ELSE
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML 「来吧…啾卟…啊姆，啾……」
						PRINTFORMW 「舒服吗？」
					CASE 1
						PRINTFORML 「哈姆…、啾噜…呃……」
						PRINTFORMW 「最好是擦掉，擦掉不好吗？」
					CASE 2
						PRINTFORML 「诶~…\@ TCVAR:102 == 1 ? 自，自己 # 屁、屁股 \@味道的…感想？」
						PRINTFORMW 「……请，请不要问奇怪的事情！」
				ENDSELECT
			ENDIF
		;違う人に中出し
		ELSE
			SELECTCASE ABL:精液中毒
				CASE IS <= 1
					PRINTFORMW 「我说，也……不太愿意…做这样的事…」
				CASE 2, 3, 4
					PRINTFORML 「虽然想帮忙打起精神来……」
					PRINTFORMW 「还是稍微有点抵触啊…我知道了」
				CASE IS > 4
					SELECTCASE RAND:7 
						CASE 0, 4
							PRINTFORMW 「嗯呼…啾卟…噜噜噜……」
							PRINTFORML 「啊~、已经够了？」
							PRINTFORMW 「我明明也想要吃的……」
						CASE 1, 5
							PRINTFORML 「是，给你漂亮地弄干净了。」
							PRINTFORMW 「%K30_C_NAME(射出:0)%的味道，已经没剩下了……」
						CASE 2, 6
							PRINTFORML 「真是的，总是让我做这种事……」
							PRINTFORMW 「这样就满足了么？？」
							PRINTFORMW 「……下一个总该不是%K30_C_NAME(射出:0)%……请给…我的……」
						CASE 3
							PRINTFORML 「净让我做这种事……那就稍微问一下」
							PRINTFORMW 「我的嘴和%K30_C_NAME(射出:0)%の\@ 箇所 == 1 ? 那里 # 屁股 \@…哪个更舒服？」
							PRINTFORMDL 映姬笑着看完了无法回答的%CALLNAME:MASTER%，眼睛却没有笑意
							PRINTFORMDW 从背后可以感受到%CALLNAME:(射出:0)%的冷眼…
					ENDSELECT
			ENDSELECT
		ENDIF
	;これでMASTERが射精する
	ELSEIF NOWEX:MASTER:11
		;慣れてない
		IF EXP:精飲経験 < 20
			SELECTCASE RAND:3
				CASE 0
					PRINTFORML 「诶？　出来了…？」
					PRINTFORMW 「就这样含着就行了…？　嗯…啾噗…」
				CASE 1
					PRINTFORMW 「啊，总觉得有点害怕…！？」
					PRINTFORMW 「诶、不能松開嘴吗？　嗯…啾噜…」
				CASE 2
					PRINTFORMW 「哈姆……啾噜、啾噜……嗯！？」
					PRINTFORMDW 技巧虽拙劣，但映姬极其忘我的侍奉，让%CALLNAME:MASTER%的忍耐不起作用…
			ENDSELECT
		;口内射精
		ELSEIF 射出:1 == 30
			SELECTCASE ABL:精液中毒
				CASE 0, 1
					PRINTFORML （诶~！？　在嘴里…射出来！？）
					PRINTFORMDW 映姬虽然很吃惊，但还是为即将射精的阴茎服务……
				CASE 2, 3, 4
					IF RAND:2 == 1
						PRINTFORML （好厉害的，气味……）
						PRINTFORMW （但是，我不讨厌…为什么呢…）
					ELSE
						PRINTFORML 「啾噜……嗯姆……哈……」
						PRINTFORMW （咕，在嘴里面，抖的好厉害……）
					ENDIF
				CASE IS > 4
					SELECTCASE RAND:4
						CASE 0
							PRINTFORML 「啾噗…rero……噗哈~」
							PRINTFORMW 「你更喜欢嘴吗？真是没办法♥」
							PRINTFORMW 「肉棒牛奶，我会全部喝掉的……请给我，更多……♥　啊姆~、啾噜…」
						CASE 1
							PRINTFORML 「嗯姆~、啾噗……嗯卟、啾噜…」
							PRINTFORMDW 直到刚才自己的\@ 箇所 == 1 ? 膣内 # 肛門 \@里还插入着的肉棒映姬也不在意污秽，一直在淫靡的吮舔着……
							PRINTFORMW （好想吃……快点，快点……想要……♥）
						CASE 2
							PRINTFORML （\@ 箇所 == 1 ? 在这里 # 屁股 \@不射出来吗……）
							PRINTFORMW 「……我明白了。但是，不把精华多射出来些是不行的哦？…啊，只是……」
							PRINTFORML 「啾吧、嗯嗯~…啾噜……嗯姆~♥」
							PRINTFORMDW 映姬内心深处的開关打開了，饥渴地含着阴茎…露出一脸荡漾的样子
						CASE 3
							PRINTFORMDL %CALLNAME:MASTER%突然停止了活塞，被抹满\@ 箇所 == 1 ? 愛液 # 腸液 \@的阴茎缓缓地伸到映姬的眼前
							PRINTFORMW 「…是。想放进嘴里吧♥」
							PRINTFORML 「诶诶、好的…啊ー姆~♥」
							PRINTFORML 「嗯卟……啾噜噜~！　叽啵、啾啵……」
							PRINTFORMDW 映姬娴熟地叼着肮脏的阴茎，朝着终点加速香口的动作…
					ENDSELECT
			ENDSELECT
		;誰かから抜いて口内射精
		ELSEIF 射出:1 > 0
			SELECTCASE ABL:精液中毒
				CASE IS <= 1
					PRINTFORML 「嗯嗯~！？　…嗯呃…姆咕~…」
					PRINTFORMW （讨厌…总觉得有点奇怪的味道……）
				CASE 2 TO 4
					PRINTFORML 「嗯，嗯啾……嗯，嗯……」
					PRINTFORMDW 映姬的口交没有平时献身的样子。果然内心还有抵抗啊…
				CASE IS > 4
					SELECTCASE RAND:4
						CASE 0
							PRINTFORML （%K30_C_NAME(MASTER)%的精液、射映姬嘴里…想要…满满的♥）
							PRINTFORMDW %CALLNAME:(射出:1)%の\@ 箇所 == 1 ? 膣内 # 肛門 \@还没有来得及射出来、映姬就吸住阴茎催促射精…
						CASE 1
							PRINTFORML （%K30_C_NAME(射出:1)%の\@ 箇所 == 1 ? 啊那里 # 屁股 \@比起，我的嘴里……更好哟♥）
							PRINTFORMDW 映姬怀着奇妙的优越感，将爆发前的阴茎吞下……
						CASE 2
							PRINTFORML 「叽噗、叽卟卟~！　啾噜、叽噗~……」
							PRINTFORMDW %SAMEN_DESCRIPTION(MASTER)%要独占似的，映姬更加淫荡激烈地动着香舌和樱唇…
						CASE 3
							PRINTFORML 「嗯啾…啾呜呜呜~♥　啾噜~……啾噜……啾噜噜~！」
							PRINTFORMDW 映姫的嘴%CALLNAME:(射出:1)%の\@ 箇所 == 1 ? 膣 # 肛門 \@用不同的柔软处包裹着阴茎…
							PRINTFORML （%K30_C_NAME(MASTER)%会给我吧……%K30_C_NAME(射出:1)%不是……我的，给我的嘴♥）
							PRINTFORMDW 奉献的喜悦快要达到的同时，映姬激烈地吸著快要射精的阴茎…
					ENDSELECT
			ENDSELECT
		;精液中毒高め
		ELSEIF ABL:精液中毒 > 5 && RAND:3 != 1
			SELECTCASE RAND:5
				CASE 0
					PRINTFORML 「是吗…嗯唔，唔呼……你也喜欢吗♪」
					PRINTFORMDL 记住了精液味道的映姬感受到了射精的预兆，一边含着阴茎一边对%CALLNAME:MASTER%说话
					PRINTFORMDW 映姬的香舌碰到了铃口，意想不到的快感袭击了%CALLNAME:MASTER%
					PRINTFORML （%K30_C_NAME(MASTER)%、看起来很称心…♥）
					PRINTFORMDW 欣赏到了%CALLNAME:MASTER%的反应，映姬满足地进一步加快了服务的步伐……
				CASE 1
					CALL HPH_PRINT("「不用忍耐哦…没关系，我会马上让你快乐的HPH　嗯姆~…啾噜…」", "L")
					PRINTFORMDW 映姬慈爱着舔着试图忍耐的阴茎，真空口交一样紧紧吞着……
					CALL HPH_PRINT("「嗯啾…HPH　滋兹~……嗯呼…啊姆~HPH」", "L")
					PRINTFORMDW 映姬的猛烈地口淫，受不了的%CALLNAME:MASTER%已经到了极限…
				CASE 2
					PRINTFORML 「嗯~………叽噗……、滋兹~……呜呜」
					PRINTFORMDL 映姬的香舌灵活地将%CALLNAME:MASTER%逼入绝境…
					PRINTFORMDW 因为平时说教时经常动嘴，所以自然而然地锻炼了吗？
					CALL HPH_PRINT("「啾卟、啊姆HPH　……嗯姆~HPH　咕噗~！　滋兹~…」", "L")
					PRINTFORMDW 映姬的软舌好像也察觉到了射精近在眼前，像最后的冲刺一样，奉仕变得更加激烈……
				CASE 3
					CALL HPH_PRINT("「啾卟、啊姆~HPH　……嗯啾~HPH　啾噜~…」", "L")
					PRINTFORMDL 映姬一边时不时地抬头眼神询问%CALLNAME:MASTER%的情况，一边继续全心服务…轻拢慢捻抹复挑
					PRINTFORML （%K30_C_NAME(MASTER)%的精液…♥　想要，满满的…♥）
					PRINTFORMDW 完全被精液俘虏的映姬的口交服务，伴随着强烈的快感催促着%CALLNAME:MASTER%射精……
				CASE 4
					CALL HPH_PRINT("（！！　…给我吧、给映姬…淫荡的嘴巴HPH　灌的满满的HPHHPH）", "L")
					PRINTFORMDW 映姬感受到射精的征兆后，搂着%CALLNAME:MASTER%的腰，将阴茎含到了极限
					CALL HPH_PRINT("「嗯呃~………嗯嗯ーHPH　嗯呃~……………嗯呼呼~HPH」", "L")
					PRINTFORMDW 映姬目泛泪光，阴茎仍固定在喉咙深处，眼白上翻催促射精…
			ENDSELECT
		;それ以外
		ELSE
			SELECTCASE RAND:3
				CASE 0
					PRINTFORML 「啊，好大啊…嗯…、拿出来吧……」
					PRINTFORMDW 映姬含着龟头，像魅魔一样要把阳气吸出来似的催促射精
				CASE 1
					PRINTFORMW 「不用忍耐，也可以的…让我们一起舒服吧…」
					PRINTFORMW 「啊姆、嗯…啾…嗯、滋兹」
				CASE 2
					PRINTFORML 「要出来了吗？嗯，没关系的……\@ ABL:精液中毒 > 4 ? 快点让我喝你的“琼浆玉液”吧 # 我会好好地全喝掉牛奶的 \@……」
					PRINTFORMW 「所以，不要客气，都射出来吧…」
			ENDSELECT
		ENDIF
	;何度も射精した
	ELSEIF TCVAR:30:396 > 4 && RAND:2 && ABL:舌 > 2
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 「呵呵，还不满足啊，这孩子……♪」
				PRINTFORMDW 眼泛爱心地看着正在恢复硬度的阴茎后，映姬不断吻着肉棒前端……
			CASE 1
				PRINTFORML 「嗯呜……啊呜，嗯…♪　已经，这样了…」
				PRINTFORML 「连这种事情，都为你做了，%CALLNAME:MASTER%，请用我的嘴，变得更舒服……」
		ENDSELECT
	;勃起きってない
	ELSEIF BASE:MASTER:勃起 < 1000 && RAND:2 == 1 && ABL:舌 > 2
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 「啊，还很柔软的…」
				PRINTFORMW 「是、我会叫醒你的……用我的嘴♪　嗯姆、啾~…」
			CASE 1
				PRINTFORML 「哼哼~、如果是这个大小的话…啊ー姆♪　嗯、呼、啾噜…」
				PRINTFORMDW 映姬把阴茎含的比平时更深，并同时刺激龟头和枪杆
				PRINTFORMW 「嗯姆……嗯呼、……嗯呃~………」
		ENDSELECT
	;舌技能高め＠なげーよ、確率下げよ
	ELSEIF ABL:30:舌 > 2 && RAND:3 == 1
		SELECTCASE RAND:5
			CASE 0
				PRINTFORMDL 仔细为龟头服务的映姬，发出了“叽啵”的一声，放開了嘴
				PRINTFORMW 「啊，%K30_C_NAME(MASTER)%……偶尔忍耐一下吧？」
				PRINTFORMDL 映姬伸长香舌，从阴茎的根部開始到尿道、背部用舌尖慢慢地舔起来…
				PRINTFORMDW 受到刺激而涌出的先行汁液映姬也只是用细舌轻舔着而已，持续着令人着急的侍奉……绛唇渐轻巧，云步转虚徐
				PRINTFORMDL 对焦急样子的%CALLNAME:MASTER%，映姬妖艳地微笑着…
				CALL HPH_PRINT("「总是被欺负，这是回击哦，你平时总是这样開心吧？HPH」", "W")
			CASE 1
				PRINTFORMDL 热心侍奉的映姬越来越可爱，慰劳般地摸了摸她的头
				CALL HPH_PRINT("「！？………嗯呼HPH　嗯呼呼~…HPHHPH」", "L")
				PRINTFORMDL 继续抚摸的话，她的笑颜就更加淫靡，侍奉也会更加热情
				PRINTFORMDW 也许是立场上不经常被夸奖，映姬看起来很開心…
				CALL HPH_PRINT(@"「嗯~HPH　嗯~HPH　哈姆~……啾噜噜~！」", "L")
				CALL HPH_PRINT(@"（%K30_C_NAME(MASTER)%…很高兴HPH　能用我的嘴…HPH）", "L")
				CALL HPH_PRINT(@"（更加…舒服了HPH　希望能得到更多、更多、更多的奖励…HPH）", "W")
			CASE 2
				CALL HPH_PRINT("「啊姆~…嗯~HPH　嗯~HPH　嗯啾………滋兹~」", "L")
				PRINTFORMDW 映姬似乎迫不及待地要精饮，热心地继续为阴茎侍奉……
				PRINTFORMDL 这算是被阎王爷施以涩情的『拔舌』之刑么，%CALLNAME:MASTER%在想无聊的事情。
				CALL HPH_PRINT("「嗯啾~HPH　滋兹~……嗯啾、嗯呼…HPH」", "W")
			CASE 3
				CALL HPH_PRINT("「啾噜噜地！　嗯~HPH　嗯呼…HPH　啾噜地！」", "L")
				PRINTFORMDL 映姫%TEXTR("发出特别大的下流声音/用柔软的颊肉抚慰/不停地转动香舌")%同时含着%TINKO(MASTER)%
				PRINTFORMDW 和往常不同，被人看到了不体面的样子，%CALLNAME:MASTER%的兴奋会更加高涨
				PRINTFORML 「噗哈~……%K30_C_NAME(MASTER)%、怎么样…刚才的，很舒服…吗？」
				PRINTFORMW 「我也是，为了让你…高兴，做了很多调查…很努力哦…♥」
				PRINTFORMDL 一本正经和献身的性格，甚至是平时说个不停锻炼过的粉嫩软舌
				PRINTFORMDW 难不成想说阎王大人的朱唇是为他而生的，真是至福…
			CASE 4
				PRINTFORMDL 映姬沉浸其中，吮吸%TINKO(MASTER)%……。
				CALL HPH_PRINT("「啊姆~、叽噗……嗯呼HPH　叽噗HPH　……啾啵~」", "W")
				PRINTFORML 「哈啊…哈啊……啊~、哼哼、半途而废不满意吗?　……那样的话…♪」
				PRINTFORMDW 说完，映姬含住阴茎，開始深喉
				PRINTFORMDL 用喉咙将龟头紧紧地咬住，与平时不一样的快感让%CALLNAME:MASTER%坐立不安…
				PRINTFORMDW 虽然担心会不会很痛苦，但映姬倒不如说陶醉于深喉服务……朱唇咽入香喉时，明眸渐開横秋水
		ENDSELECT
	;通常
	ELSE
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML 「啊姆~、嗯噗~……啊姆~……嗯姆~」
				PRINTFORMDW 看着只为自己献身侍奉的映姫，%CALLNAME:MASTER%获得了深深的满足感
			CASE 1
				PRINTFORML 「嗯姆…嗯呃~………叽噗~………哈啊~…哈啊…」
				;準備出来て経験那个ば1/2でおねだりして欲しいよね
				IF (ABL:30:Ｖ感覚 >= 3 || ABL:30:Ａ感覚 >= 3) &&( BASE:MASTER:勃起 >= 1000 && RAND:2 ==1)
					;潤滑と経験だけ見るガバ判定だけど
					IF PALAM:潤滑 < PALAMLV:2
						PRINTFORMW 「哼哼~…变大了♪　接下来，我也…舔一下♥」
					ELSE
						PRINTFORML 「……呼呼、变大了」
						PRINTFORMW 「吶、%K30_C_NAME(MASTER)%……\@ TCVAR:30:391 > TCVAR:30:392 ? 这里 # 屁股 \@…准备好了哦♥」
						SIF TCVAR:30:391 > TCVAR:30:392
							PRINTFORMDW 映姫有些害羞地指着%SLIT(, 1)%
					ENDIF
				ELSE
					PRINTFORMW 「怎么办？ 用嘴继续吗？ 还是…」
				ENDIF
			CASE 2
				PRINTFORML 「嗯……嗯啾，呸咯，啊呜…」
				PRINTFORMDL 映姬没有疲惫的样子，一直舔着%TINKO(PLAYER)%
				PRINTFORMDW 考虑到平时不停说教的体力，这点小事也根本不算什么…
		ENDSELECT
	ENDIF
ENDIF
RETURN 1

;==================================================
;82,乳交＠尺寸変えると全て破綻するけどヨシ
;==================================================
@M_KOJO_MESSAGE_COM_K30_82
CALL M_KOJO_MESSAGE_COM_K30_82_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_82_1
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;時姦中
	IF FLAG:70
		PRINTFORMW 
		RETURN 1
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		PRINTFORMW 「榨乳？……」
		PRINTFORMW 「就算你对我说『用胸』夹住…呜，这太下流了」
		PRINTFORMW 「…………………………」
		PRINTFORMW 「是吗…最相爱的人才做，情侣间……我明白了」
		PRINTFORMW 「………………」
		PRINTFORMW 「………」
		PRINTFORMDW 鬱屈 + 5
		RETURN 1
	;マグロスイッチオン
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	;これでMASTERが射精する
	ELSEIF NOWEX:MASTER:11
		IF ABL:30:胸 < 2 && RAND:3 != 1
			PRINTFORMW 「要出来吗？……那么，就这样…」
		ELSEIF PALAM:欲情 >= PALAMLV:5 && RAND:2 == 1 && ABL:30:胸 > 1
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 「呼呼、在我这里捅的话…♥」
					PRINTFORMDL 映姬用乳头摩擦里筋，享受阴茎颤颤悠悠的运动……鬓垂香颈云遮藕，粉著兰胸雪压梅
					PRINTFORMW 「啊…好像要出来了……?啊哈♥那么，就那样，请吧…♥」
				CASE 1
					PRINTFORML 「这个，我也…我的乳头…好舒服呀…♥」
					PRINTFORMW 「你也要去吗?好啊，对映姬的欧派…不要客气地挂满吧…♥」
			ENDSELECT
		ELSE
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 「好像已经，想要发射了…我的胸口，很舒服吗？呼呼，看你还敢不敢说映姬是“阎萝王”」
					PRINTFORML 「…………♪」
					PRINTFORMW 「请，就这样，射在胸前…♥」
				CASE 1
					PRINTFORML 「好啊，满满的射在我的胸口上……」
					PRINTFORMW 「用我……来变得快乐……」
			ENDSELECT
		ENDIF
	ELSEIF ABL:30:胸 < 2 && RAND:3 != 1 && (EX:Ｂ絶頂 * 10000 + PALAM:快Ｂ + PALAM:欲情 < 30000)
		PRINTDATAW
			DATAFORM （%K30_C_NAME(MASTER)%我想让你更高兴…）
			DATAFORM 「很难啊…这个…」
			DATAFORM 「做得好吗…」
		ENDDATA
	;勃起きってない
	ELSEIF BASE:MASTER:勃起 < 1000 && RAND:2 == 1
			PRINTFORML 「啊…还很柔软的……」
			PRINTFORMW 「呵呵，那我会打起精神的」
	ELSEIF PALAM:欲情 >= PALAMLV:5 && RAND:2 == 1 && ABL:30:胸 > 1
		SELECTCASE RAND:2
			CASE 0
				PRINTFORML 「哼哼~、哪一个葡萄、更舒服呢♥」
				PRINTFORMDL 映姬左右的乳头交替慢慢地蹭%TINKO(MASTER)%…皓腕高抬身宛转，销魂双乳耸罗衣
				PRINTFORMDW 为了让刺激而挺尖的乳头贴在背面和铃铛口上，映姬继续全心全意地服务着……夫婿调酥绮窗下，金茎几点露珠悬
			CASE 1
				PRINTFORMDL 映姬说%TINKO(MASTER)%放在左胸上后，用手从上面按住慢慢地移动身体……
				PRINTFORMDW 被光滑如玉的纤手和小小软酥的胸部夹住，%TINKO(MASTER)%更加一柱擎天…。
				PRINTFORML 「这也是夹在胸前的吧？」
				PRINTFORMW 「%K30_C_NAME(MASTER)%、想让你高兴……怎么样，舒服吗？」
		ENDSELECT
	ELSE
		SELECTCASE RAND:13
			CASE 0 TO 3
				;準備出来て経験那个ば1/2でおねだりして欲しいよね
				IF (ABL:30:Ｖ感覚 >= 3 || ABL:30:Ａ感覚 >= 3) &&( BASE:MASTER:勃起 >= 1000 && RAND:2 ==1)
					;潤滑と経験だけ見るガバ判定だけど
					IF PALAM:潤滑 < PALAMLV:2
						PRINTFORMW 「你好像准备好了…接下来是我，对吧…♪」
					ELSE
						PRINTFORML 「……呼呼、变大了」
						PRINTFORMW 「吶、这样的话……我的\@ TCVAR:30:391 > TCVAR:30:392 ? 这里 # 屁股 \@给……怎么样？♥」
						SIF TCVAR:30:391 > TCVAR:30:392
							PRINTFORMDW 映姫有些害羞地指着%SLIT(, 1)%
					ENDIF
				ELSE
					PRINTFORMW 「虽然很小，但如果能让你高兴的话…」
				ENDIF
			CASE 4 TO 7
				PRINTFORMW 「想让你舒服起来…努力着哦？」
			CASE 8 TO 11
				PRINTFORMW 「即使夹不住、也有能做到的事」
			CASE 12
				PRINTFORML 「不要放弃，我再试一次。」
				PRINTFORMDW  映姬不服输，用腋下夹住%TINKO(MASTER)%，就算用腋穴侍奉，也要让%CALLNAME:MASTER%舒服
				PRINTFORMDL 苗条纤细的映姬努力让肢体的脂肪夹住……
				PRINTFORML 「…………」
				PRINTFORMDW 映姬把%CALLNAME:MASTER%的龟头上涂满了唾沫，比平时更专注于服务……水晶帘下恣窥张，半臂才遮菽乳香
		ENDSELECT
	ENDIF
ENDIF
RETURN 1
;稍微だけ胸部の大きさを気にする映姫様のEXTRASOURCE関数
@M_KOJO_EXTRASOURCE_COM_K30_82
SIF STRCOUNT(CSTR:30:0, "82/")
	RETURN
SOURCE:鬱屈 += 5
RETURN

;==================================================
;83,素股
;極口上とてもえっちで良かったので素股→挿入を真似してます
;丸パクリは那个なので、攻守交代をはさんでそれっぽくする。ついでに隙あらば肛門挿入を狙ったりするように
;==================================================
@M_KOJO_MESSAGE_COM_K30_83
CALL M_KOJO_MESSAGE_COM_K30_83_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_83_1
#DIM Ｖ可否
#DIM Ａ可否
#DIM 交代可
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;時姦中
	IF FLAG:70
		PRINTFORMW 
		RETURN 1
	;マグロスイッチオン
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	;初めて
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		IF TALENT:処女 == 0
			PRINTFORML 「明明是这个姿势……却不是插入…」
		ELSE
			PRINTFORML 「从上面跨过去，怎么这样…」
			PRINTFORMW 「哎！　……疑，疑似要进去了的感觉，唔…」
		ENDIF
		PRINTFORMW 「嗯……这样摩擦……你会……舒服吗？」
		RETURN 1
	ENDIF
	;挿入可条件を算出
	CALL COM_ABLE24
	交代可 = RESULT && INROOM(CFLAG:現在位置) && !FLAG:99
	CALL COM_ABLE65
	Ｖ可否 = RESULT && 交代可 && BASE:気力 >= 100 && BASE:体力 >= 600 && !TALENT:処女
	CALL COM_ABLE66
	Ａ可否 = RESULT && 交代可 && BASE:気力 >= 100 && BASE:体力 >= 600
	SIF !Ｖ可否 || GET_TARGETNUM() > 1 || NOWEX:MASTER:射精
		GOTO NORMAL
	SIF  !(TALENT:妊娠願望 || GETBIT(CFLAG:1502, 4)) || TCVAR:口服避孕薬
		GOTO NORMAL
	;素股→子作りックス
	IF !TCVAR:口服避孕薬 && ESTRUS_CYCLE(30) >= 100 && PALAM:潤滑 >= PALAMLV:5 && (BASE:MASTER:射精 > (MAXBASE:MASTER:射精 / 2))
		PRINTFORML 「吶，看…♥ 这里，很热…已经，很粘稠了…」
		PRINTFORML 「然而…不给映姬插进去吗？好想要啊♥」
		PRINTFORMDW 映姬滴着爱蜜，拼命地侍奉着，却总觉得有些不耐烦地扭动着腰。
		PRINTFORML 「嗯嗯~…♥　现在，不上不下的……已经，马上…」
		PRINTFORMDW %CALLNAME:MASTER%\@ TCVAR:MASTER:要去了 ? 的极限接近 # 没有多少余裕 \@意识到这一点，映姬也到了忍耐的极限…
		IF !EX:膣内精液 && ONCE("素股挿入")
			PRINTFORML 「不记得了吗…%K30_C_NAME(MASTER)%」
			PRINTFORML 「今天是…两个人的宝宝，的诞生日」
			PRINTFORMW 「让我当妈妈…重要的日子，的说♥」
		ELSE
			PRINTFORML 「已经忘记了吗？　%K30_C_NAME(MASTER)%、今天射出的话…不是这里吧?」
			PRINTFORMW 「我，最重要的地方…婴儿的房间必须，塞满…♥」
		ENDIF
		IF TEQUIP:MASTER:避孕套
			TEQUIP:MASTER:避孕套 = 0
			PRINTFORML 「所以，把这种东西摘下来…」
			PRINTFORMD 映姬剥下避孕套
			LOCAL = 0
		ENDIF
		PRINTDATADL
			DATAFORM \@ LOCAL ? 映姫将 # \@%CALLNAME:MASTER%紧紧地抱住，用%SLIT(30,1)%夹住阴茎
			DATAFORM \@ LOCAL ? 映姫对 # \@%CALLNAME:MASTER%重新跨坐，让湿溜溜的阴茎顺势滑进去%SLIT(30,1)%
		ENDDATA
		PRINTDATADL
			DATAFORM 就这样紧紧地搂着%CALLNAME:MASTER%，骚浪地開始扭动腰肢
			DATAFORM 膣肉欢喜地颤抖着，妖娆地缠着整根阴茎
		ENDDATA
		IF FLAG:画像表示 && FLAG:挿絵表示
			CALL PRINT_IMAGE_ANIMATION_正常位("正常位_挿入")
			PRINTFORML 
		ENDIF
		IF TCVAR:MASTER:要去了
			PRINTFORML 「哈……嗯~♥　呼、嗯♥好喜欢啊这个肉棒，要飞起来了！飞了啊　…请就这样…给我，在这里，给我♥」
			PRINTFORML 「射出来，之前，我是不会……的♥」
		ELSE
			PRINTFORML 「啊啊…♥　啊嗯~♥　就这样，交给我吧♥」
			PRINTFORML 「小宝宝的材料，全部，都收到了♥小映姬是个想要肉棒牛奶的小骚货！给我！给我吧~♥」
		ENDIF
		PRINTFORMW
		PRINTFORMDL 映姬似乎脑子里只剩下榨取种子了
		PRINTFORMDL %CALLNAME:MASTER%放弃挣扎了，只能把自己交给眼泛爱心的映姬…
		PRINTFORMW 
		;ステータスを被逆推Ｖ騎乗位に移行
		CFLAG:MASTER:诶嘿嘿 = 2
		CFLAG:诶嘿嘿 = 2
		TFLAG:COMABLE管理 = 3
		TCVAR:30:26 = 0
		CALL CCOMF85(30)
		SELECTCCOM:0:1 = 85
		TCVAR:次回Counter = TCVAR:MASTER:要去了 ? 86 # 88
		CFLAG:积攒度 = MAX(CFLAG:积攒度, 150)
		TCVAR:30:362 = 1
		RETURN 1
	ENDIF
	$NORMAL
	;これでMASTERが射精する
	IF NOWEX:MASTER:11
		;子作り未解禁or幼児育児中で避妊無しで性器にぶっかけ
		IF TEQUIP:MASTER:避孕套 == 0 && K30_CREAMPIE() > 1
			PRINTFORMW 「诶……要中出了吗…不行！等等…」
			PRINTFORMW 「射在那里的话……不是很危险吗！ …啊…」
		ELSE
			SELECTCASE RAND:2 
				CASE 0
					PRINTFORML 「……没有忍耐…吗？」
					PRINTFORMW 「发射出来就行了♥ 乖乖，很舒服吧…射的大腿上满满的……」
				CASE 1
					PRINTFORML 「一跳一跳的……是啊，因为是我……」
					PRINTFORMW 「就这样…发射出来，好烫……好多…♥」
			ENDSELECT
		ENDIF
		RETURN 1
	;技巧高い 1/3で下選択肢
	ELSEIF (ABL:腰 >= 3 || ABL:技巧 >= 5) && !RAND:3 == 1
		SELECTCASE RAND:3 
			CASE 0
				PRINTFORML 「啊嗯~、嗯呼…%K30_C_NAME(MASTER)%、啾地、唔…♥」
				PRINTFORMDW %CALLNAME:MASTER%将映姬以对面座位的体位抱着，映姬也热情地活动腰部，%SLIT(30)%紧紧贴合……
				PRINTFORMW 「呼啊~…、我喜欢，这个♥ 和你…紧紧地粘在一起……♥好爱你呀」
			CASE 1
				PRINTFORML 「啊、哈啊~………硬硬的……在摩擦呀♥　……唔啊啊！」
				PRINTFORMDL 从映姬的%SLIT()%不断地溢出代替润滑液的爱液
				PRINTFORMDW 奉仕的兴奋似乎就这样转化为快乐……
				PRINTFORMW 「你……可以的……全部，我的腿穴来……快乐地给你做♥」
			CASE 2
				PRINTFORML 「…我想让你更加…快活……♥」
				PRINTFORMDW 映姬挪动身体用大腿夹住阴茎，開始动了腰身……
				PRINTFORMDW 光滑的大腿舒服得不得了……每次突然摩擦映姬的性器都会让她发出娇声…
		ENDSELECT
	;技巧低い 
	ELSEIF (ABL:腰 <= 1 || ABL:技巧 <= 2) && RAND:3 == 1
		PRINTFORML 「啊……你的，那个…滑出去了，别闹了…」
		PRINTFORMW 「比想象中…好难啊，这个……」
		RETURN 1
	ELSE
		SELECTCASE RAND:3 
			CASE 0
				PRINTFORML 「啊、啊，这里…这里，好快乐啊！　……呼啊啊~♥」
				PRINTFORMDL 似乎喜欢雁首挂在阴蒂上的感觉，映姬不断重复着动作……
			CASE 1
				PRINTFORML 「啊啊呃、哈啊~♥　……%K30_C_NAME(MASTER)%…这里，舒服吗……」
				PRINTFORMDW 映姬把手放在%CALLNAME:MASTER%的阴茎上，与阴唇夹住，继续服务…
			CASE 2
				PRINTFORML 「…哈啊、哈啊…嗯嗯~！……%K30_C_NAME(MASTER)%，还在，忍耐吗…？」
				IF TALENT:処女 != 0
					PRINTFORMW 「如果是你的话……」
				ELSEIF ABL:Ｖ感覚 <= 1
					PRINTFORMW 「虽然我还很害怕，但是如果是你的话……」
				ELSEIF PALAM:欲情 >= PALAMLV:5
					PRINTFORMW 「我，想要，更多的…♥」
				ELSEIF PALAM:潤滑 > PALAMLV:2 
					PRINTFORMW 「我，什么时候，都可以……♥」
				ELSE
					PRINTFORMW 「在我那里，感觉好快乐好舒服啊……♥」
				ENDIF
		ENDSELECT
	ENDIF
	SIF !Ａ可否 || GET_TARGETNUM() > 1
		RETURN 1
	;尻素股→Ａ挿入
	IF ABL:Ａ感覚 >= 5 && PALAM:欲情 >= PALAMLV:10
		PRINTFORML
		PRINTFORML 「啊呋~…♥　……嗯嗯~、呜……嗯♥」
		PRINTFORMDL 虽然映姬继续尽心奉仕，但总觉得有些不够
		PRINTFORML 「吶、%K30_C_NAME(MASTER)%……♥」
		PRINTDATA
			DATAFORM 「你的这里，也喜欢吧…
			DATAFORM 「更多更多，更舒服的事…怎么样
			DATAFORM 「这种做法也是…有啊…
		ENDDATA
		CALL HPH_PRINT("HPH」", "W")
		PRINTFORMDL %CALLNAME:MASTER%的阴茎从%SLIT()%引导到臀部的裂口，映姬再次開始动起来了腰身。
		PRINTFORMDL 被映姬柔软的手和光滑的臀部夹着，沉浸在与%SLIT()%完全不同的快感中…
		CALL HPH_PRINT("「很厉害，吧HPH　我也可以HPH　非常，舒服…好吗HPH」", "L")
		CFLAG:积攒度 += 50
		IF CFLAG:积攒度 >= 250
			PRINTFORMW 
			PRINTFORMDL 激烈的尻素股令%CALLNAME:MASTER%\@ TCVAR:MASTER:要去了 ? 已经接近极限了 # 的阴茎越来越滚烫 \@
			PRINTFORMDL 映姬把阴茎贴在屁股上的过程中，也变得更加情欲高涨…
			IF TCVAR:MASTER:要去了
				{
				LOCALS = %TEXTR(@"中出吧…是啊HPH　那么，请在这里，屁股，请给我…HPH
						/啊啊啊…HPH　果然，这里！屁股，想要的HPHHPH
						/不行，还不行HPH　因为你的、要射出来、是这里HPH")%
				}
			ELSE
				{
				LOCALS = %TEXTR(@"哈、唔…HPH　啊、啊啊~HPH　这样，可不够啊！」\n「请给我、%K30_C_NAME(MASTER)%HPH　屁股上…请给我HPH
						/啊呜~…HPH　这个、也不错，但是里面……我想要在屁股里…HPH
						/更多…舒服、的…HPH　屁股，更……呐HPH　更多~更多哈♥我要被你弄死了HPH")%
				}
			ENDIF
			CALL HPH_PRINT(@"「%LOCALS%」", "W")
			PRINTFORMDL 无法忍耐的映姬把贴着的手稍微挪開了一点
			CALL HPH_PRINT(@"「嗯~、嗯嗯~……嘠呼HPH」", "L")
			PRINTFORMDW 就这样，菊门迅速地饮下阴茎，紧紧地夹住等待已久的东西…
			IF FLAG:画像表示 && FLAG:挿絵表示
				CALL PRINT_IMAGE_ANIMATION_正常位("Ａ正常位_挿入")
				PRINTFORML 
			ENDIF
			IF TCVAR:MASTER:要去了
				CALL HPH_PRINT("「嗯呼~…HPH　好厉害、太猛了…HPH」", "L")
				CALL HPH_PRINT("「这里，射出的话…会非常舒服哦HPHHPH」", "L")
				PRINTFORMDL 映姬露出放浪的表情，像冲刺一样開始动腰…
			ELSE
				CALL HPH_PRINT("「啊哈HPH　屁股，非常……快乐HPH」", "L")
				CALL HPH_PRINT(@"「%K30_C_NAME(MASTER)%HPH　动，动起来，动起来，是吧…HPH」", "L")
				PRINTFORMDL 被肛门的快乐所吸引，映姬忘我地摇着腰…
			ENDIF
			PRINTFORMW
			PRINTFORMDL 完全被剥夺了主导权的%CALLNAME:MASTER%只能听凭映姬的摆布
			PRINTFORMW
			;ステータスを被逆推Ａ騎乗位に移行
			CFLAG:MASTER:诶嘿嘿 = 2
			CFLAG:诶嘿嘿 = 2
			TFLAG:COMABLE管理 = 3
			TCVAR:30:26 = 0
			CALL CCOMF87(30)
			SELECTCCOM:0:1 = 87
			TCVAR:次回Counter = 89
			BASE:気力 += 40
			RETURN
		ELSE
			CALL HPH_PRINT("「不用客气…小映姬淫荡的屁股HPH　随时，都可以临幸…HPH」", "W")
		ENDIF
	ENDIF
ENDIF
RETURN 1

;==================================================
;84,泡泡浴
;==================================================
@M_KOJO_MESSAGE_COM_K30_84
CALL M_KOJO_MESSAGE_COM_K30_84_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_84_1
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;マグロスイッチオン
	IF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		PRINTFORML 「……特意为了这个……。连这样的道具都买齐了。」
		PRINTFORMW 「…诶诶，什么都不说了。那怎么办才好呢？」
		PRINTFORML 「把肥皂打起泡……这样的感觉？」
		PRINTFORMW 「真是太羞耻了，这个……」、
		RETURN 1
	ENDIF
	;これでMASTERが射精する
	IF NOWEX:MASTER:11
		SELECTCASE RAND:2 
			CASE 0
				PRINTFORML 「……各种各样的磨擦…很快乐，对吧」
				PRINTFORMW 「你也是吧?……我……那么，变舒服了…给我，很多…挂着…♥」
			CASE 1
				PRINTFORML 「没有，忍耐吗…射出来，就行了…」
				PRINTFORMW 「即使变脏了，也能马上洗好…就给我…浇上火热的白浊吧………♥」
		ENDSELECT
	ELSE
	;地の文も平文のみ、他の指令とも被るから手抜き
		SELECTCASE RAND:2 
			CASE 0
				PRINTFORML 「就这样……抱在怀里好吗？」
				PRINTFORML 「滑溜溜得很厉害……嗯，这个，这样啊……嗯，♥嗯」
				PRINTFORMW 「%K30_C_NAME(MASTER)%的那个…硬硬的…奇怪，好舒服……」
			CASE 1
				PRINTFORML 「挺起胸膛这样擦…要动啊……那么，你仰着身子……」
				PRINTFORML 「嗯，正好…两人粘在一起的话，会有各种各样的舒服摩擦……啊嗯♥」
		ENDSELECT
	ENDIF
ENDIF
RETURN 1

;==================================================
;85,足交
;良さがわからないので否定的になってしまう。好きな人にはごめんなさい
;構文読んでたら切り替え方法わかったので、映姫様が隙あらばしゃぶりつくようにしてみる
;→EXTRASOURCEを使い指令ごと書き換えてしゃぶりつくように
;==================================================
;==================================================
;Optional: Determines if the standard description should be skipped.
;Return 0 if description and KOJO should be shown, 1 if description should be hidden and KOJO shown, 2 if description should be shown and KOJO hidden, and 3 if both description and KOJO should be hidden.
;==================================================
@M_KOJO_MESSAGE_MESSAGECHECK_COM_K30_85
IF TFLAG:193 == 99
	DRAWLINE
	PRINTFORMDL 映姫对%CALLNAME:MASTER%的%TINKO(MASTER)%用美丽的足底摩挲着…
	PRINTFORMDL ……半途停下了
	PRINTFORMDL 映姬用好像会说话的眼睛看了看突然起勃的阴茎…
	PRINTFORMW 
	RETURN 1
ELSEIF TCVAR:MASTER:112 && !(FLAG:70 || TCVAR:30:烂醉 || CFLAG:MASTER:恶作剧 == 2)
	DRAWLINE
	PRINTFORMDL %CALLNAME:MASTER%对%\@ TCVAR:MASTER:112 == 1 ? 在阴道里排了很多 # 射精 \@%后的阴茎，让%CALLNAME:TARGET%的玉足用里侧抚摸着
	PRINTFORMDL %CALLNAME:TARGET%总觉得很难做…
	PRINTFORMW 
	RETURN 1
ELSE
	RETURN 0
ENDIF

@M_KOJO_MESSAGE_COM_K30_85
CALL M_KOJO_MESSAGE_COM_K30_85_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_85_1
#DIM Ｆ可否
#DIM 交代可
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;小分岐
	;-------------------------------------------------
	;初めて
	;記入チェック
	LOCAL:1 = 1
	;-------------------------------------------------
	;時姦中
	IF FLAG:70
		PRINTFORMW 
		RETURN 1
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		CALL K30_GASPING_MAGURO
		RETURN 1
	ELSEIF LOCAL:1 && FIRSTTIME(SELECTCOM)
		PRINTFORML 「这是…真的不太明白」
		PRINTFORMW 「你这样过瘾吗…？」
		RETURN 1
	ENDIF
	;攻守交代条件クリア＆余裕と経験が那个ばしゃぶりつく
	CALL COM_ABLE24
	交代可 = RESULT && !FLAG:99 && GET_TARGETNUM() == 1 
	CALL COM_ABLE81
	Ｆ可否 = (RESULT && 交代可 && ABL:TARGET:舌 >= 3 && ABL:精液中毒 >= 5 && BASE:体力 > 600 && BASE:気力 > 100)
	;これでMASTERが射精する
	IF NOWEX:MASTER:11
		;フラグ立ってたら指令も無視してしゃぶりつく
		IF	TFLAG:193 == 99
			PRINTDATAL
				DATAFORM 「…和现在想做的不一样。 不是这个吗」
				DATAFORM 「…那个…现在不想」
				DATAFORM 「%K30_C_NAME(MASTER)%、这样的话…我不能满足哦」
			ENDDATA
			PRINTDATAL
				DATAFORM 「是的，我想做的是这个……我想这样做…」
				DATAFORM 「好像被放置在那里了，不。我不想忍耐…」
				DATALIST
					DATAFORM 「我也可以偶尔任性一下」
					DATAFORM 「我想更多的舔舐，想要在嘴里…」
				ENDLIST
			ENDDATA
			PRINTFORML 「对不起、%K30_C_NAME(MASTER)%。我会让你舒服一点……啊姆~♥」
			CALL K30_BLOW_JOB_ANIME(0)
			PRINTFORML 「啾，啾噜…啾噜噜！　滋咕滋咕～～♥♥」
			PRINTFORMDL 映姬竟然无视%CALLNAME:MASTER%的意向擅自開始了口交
			PRINTFORMDW 舔着雁首舔着里筋的浓密服务，%CALLNAME:MASTER%一眨眼就被逼到绝境……
			PRINTFORML 「啾噜、啾噜……咕啵♥　咕啵♥　啾噜噜♥」
			PRINTFORMDL 从溢出的先走汁中感受到射精的预兆，映姬的口淫越来越浓厚了
			PRINTFORMDW %CALLNAME:MASTER%在感受到全部被吸尽的快感的同时，向映姬所希望的地方倾泻…
			RETURN 1
		ELSEIF ABL:精液中毒 > 2 && RAND:2
			IF RAND:2 == 1
				PRINTFORML 「嗯……出来吧，是吗？」
				PRINTFORMW 「那个，不觉得可惜吗…？」
				PRINTFORMW 「我…想要啊……」
			ELSE
				PRINTFORML 「要去了…现在？　这样行吗」
				PRINTFORMW 「%K30_C_NAME(MASTER)%舒服，就好…」
				PRINTFORMW 「想给你做更多不同的服务…」
			ENDIF
		ELSE
			PRINTFORML 「这样就很舒服了呢…」
			PRINTFORMW 「快要出来了吗？ 那么，就这样动起来…」
		ENDIF
	;射精直前だと随机で勝手に口内射精を要求
	ELSEIF Ｆ可否 && TCVAR:MASTER:要去了 && RAND:3 == 1
		PRINTFORML 「…………」
		PRINTFORMDL 映姬一脸认真地继续着足交，不知为什么突然停下了玉足
		PRINTFORMDL 快感突然被阻止，生气的阴茎无情地抽动着…
		PRINTFORMW 
		PRINTDATAL
			DATAFORM 「很无聊，这样的…」
			DATAFORM 「对不起……果然，还是忍不住…」
			DATAFORM 「不是这样的，是想这样的吧…」
		ENDDATA
		PRINTDATAL LOCAL
			DATAFORM 「你重要的地方，不要草率地对待…」
			DATAFORM 「男女和睦相处，互相舒服比较好哦」
			DATAFORM 「很多，要射出来的……放在这样的地方会不会不一样吗？」
		ENDDATA
		PRINTFORMDL 映姬匍匐着把脸朝%CALLNAME:MASTER%的胯部凑过来。…
		PRINTFORMW 「\@ LOCAL ? 所以呢， # \@我想这样给你………哈啊唔♥」
		PRINTFORMDL 一口气把阴茎张口吃到根部，映姬一边发出猥亵的声音一边開始了口交
		CALL K30_BLOW_JOB_ANIME(1)
		PRINTFORML 
		PRINTFORML 「啾啵~……啾噜噜♥　嗯呜……嗯嗯~～～♥♥」
		PRINTFORML 「啊呋~……我想这样做♥　吃到嘴里，想要……吶，可以吧…♥」
		PRINTFORMW 
		PRINTFORMDL 映姬从%CALLNAME:MASTER%那里完全夺取了主导权，咬着肉棒
		PRINTFORMDL 看来在射在口中之前，是不会离開的……
		PRINTFORMW 
		;ステータスを被逆推口交に移行
		CFLAG:积攒度 = MAX(CFLAG:积攒度, 100)
		CFLAG:MASTER:诶嘿嘿 = 2
		CFLAG:诶嘿嘿 = 2
		TFLAG:COMABLE管理 = 3
		TCVAR:30:26 = 0
		CALL CCOMF51(30)
		SELECTCCOM:0:1 = 51
		TCVAR:次回Counter = 51
		TCVAR:30:361 = TIME
		TCVAR:30:362 = 3
	;口交以外で射精直後の場合勝手に清洁口交を始める
	ELSEIF Ｆ可否 && TCVAR:MASTER:112 && TCVAR:MASTER:112 != 4
		PRINTFORML 「用脚，做吗…？」
		PRINTFORMDW 映姬好像有什么抵触，慢慢地把脚伸向阴茎，但到了那里就停了下来…
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML 「刚拿出来擦一下，连我的脚都黏糊糊的。」
				PRINTFORML 「那样的话周围也会变脏吧？」
				PRINTFORMW 「所以呢…我想先给你弄干净…♥」
			CASE 1
				PRINTFORML 「刚射出来，那里……变得非常敏感了吧？」
				PRINTFORML 「用脚做那个什么的…太难了」
				PRINTFORMW 「取而代之的是，温柔的……我会包容你的……♥」
			CASE 2
				PRINTFORML 「对不起……我想用嘴给你做」
				PRINTFORML 「小鸡鸡…把弄脏的东西清理干净」
				PRINTFORMW 「最后剩下的部分，会给你安排的……喜欢…♥」
		ENDSELECT
		PRINTFORML 「首先，这边啊……嘞咯、啾~……啾卟，啾♥」
		PRINTFORMDL 映姬用嘴代替脚靠近%CALLNAME:MASTER%的胯部，仔细用心地舔去溅在%TEXTR("腿/玉袋/")%上的精液
		PRINTFORMW 
		PRINTFORML 「嗯……变漂亮了♥」
		PRINTFORML 「那么，也会把欧金金收拾得漂漂亮亮的……啊唔♥」
		CALL K30_BLOW_JOB_ANIME(1)
		PRINTFORML 
		SELECTCASE TCVAR:MASTER:112
			CASE 1, 6
				PRINTFORMD 涂在爱液和精液上，散发出浓厚的性臭
			CASE 2
				PRINTFORMD 涂在精液和肠液上有一点异臭
			CASEELSE
				PRINTFORMD 被先走汁和精液弄黏糊糊的
		ENDSELECT
		PRINTFORMDL 阴茎被映姫%TEXTR("毫不犹豫地/疼爱地/開心地")%咽下，開始舔舐…
		PRINTFORMW 
		PRINTFORMDL 完全被映姬掌握了节奏，%CALLNAME:MASTER%就这样转为被动
		PRINTFORMW 
		;ステータスを被逆推口交に移行
		CFLAG:积攒度 = MAX(CFLAG:积攒度, 60)
		CFLAG:MASTER:诶嘿嘿 = 2
		CFLAG:诶嘿嘿 = 2
		TFLAG:COMABLE管理 = 3
		TCVAR:30:26 = 0
		CALL CCOMF51(30)
		SELECTCCOM:0:1 = 51
		TCVAR:次回Counter = 51
		TCVAR:30:361 = TIME
		TCVAR:30:362 = 4
	ELSEIF TCVAR:MASTER:112
		IF ABL:精液中毒 < 3 || TCVAR:MASTER:112 == 4
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 「明明已经射了那么多了…」
					PRINTFORMW 「用脚紧紧地捏住…没关系吗？」
				CASE 1
					PRINTFORML 「诶诶哆、那个…因为你的…全身变得软绵绵的」
					PRINTFORMW 「脚很难夹住……」
			ENDSELECT
		ELSE
			SELECTCASE RAND:2
				CASE 0
					PRINTFORML 「收拾残局，应该有更不同的方法吧？
					PRINTFORMW 「比如说，你看…用嘴，之类的…」
				CASE 1
					PRINTFORML 「怎么说呢，我不太愿意」
					PRINTFORML 「好不容易把牛奶取出来」
					PRINTFORMW 「想更用心地照顾你……」
			ENDSELECT
		ENDIF
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 1
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML 「这、这样吗……？」
				PRINTFORMW 「诶~…轻轻地，踩上…的？　大丈夫？」
			CASE 1
				PRINTFORML 「竟然说要用脚…」
				PRINTFORMW 映姬突然顽皮的露出嗜虐的笑容
			CASE 2
				PRINTFORMW 「原来……你喜欢这样做啊……呼呼呼…」
		ENDSELECT
	ELSE
		SELECTCASE RAND:3
			CASE 0
				PRINTFORMW 「果然还是不太明白力量的大小…」
			CASE 1
				PRINTFORML 「总觉得好像在处理杂项…」
				PRINTFORMW 「诶……那样好吗？」
			CASE 2
				PRINTFORML 「那个…不是用嘴…什么的，真的可以吗…」
				PRINTFORMW 「如果疼的话，请马上告诉我」
		ENDSELECT
	ENDIF
ENDIF
RETURN 1

;足交を断固拒否して咥えたがる映姫様のEXTRASOURCE関数
@M_KOJO_EXTRASOURCE_COM_K30_85
;色々条件の精査
;SIF TCVAR:MASTER:要去了
;	GOTO TEST
SIF !TCVAR:MASTER:要去了 || GET_TARGETNUM() != 1
	RETURN
SIF FLAG:99 || !SHIRAHU(30) || EXP:口淫経験 < 100
	RETURN
CALL COM_ABLE81
SIF !(RESULT && ABL:TARGET:舌 >= 3 && ABL:精液中毒 >= 5)
	RETURN
;积攒度/精愛味覚に応じて3～60%ぐらいの適当判定
SIF (CFLAG:积攒度 / 100) + (TALENT:精愛味覚 * 3) < RAND:20
	RETURN
;ここまで抜けたら実行。口交のソース/経験等の加算と修正
;$TEST
;継続行動はリセット
CALL TOUCH_SET(0,0,0,1)
CALL TOUCH_SET(SET_FROM_M, SET_P, 30)
CSTR:MASTER:10 = /
CALL COM81
;映姫様の快感を2倍、大体イくようにする
SOURCE:快Ｍ *= 2
DOWNBASE:体力 -= 10
DOWNBASE:気力 -= 150
SOURCE:加虐 = 0
SOURCE:征服 = 0
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 -= 600
IF HETEROSEX(TARGET,MASTER)
	EXP:(50 + HETEROSEX(TARGET,MASTER)) -= 4
	EXP:MASTER:(50 + HETEROSEX(TARGET,MASTER)) -= 4
ENDIF
SIF TALENT:施虐狂 || ABL:施虐属性
	EXP:嗜虐快楽経験 --
;口上側に渡すフラグ
TFLAG:193 = 99
RETURN

;==================================================
;88,真空口交
;色々茶番を入れてフェラチオコマンドと差別化を図る
;TFLAG:192：疑似射精回数
;TFLAG:193：直前の我慢ゲージ 0-通常 1-限界 2-射精直後
;==================================================
;==================================================
;Optional: Determines if the standard description should be skipped.
;Return 0 if description and KOJO should be shown, 1 if description should be hidden and KOJO shown, 2 if description should be shown and KOJO hidden, and 3 if both description and KOJO should be hidden.
;==================================================
@M_KOJO_MESSAGE_MESSAGECHECK_COM_K30_88
RETURN 1

@M_KOJO_MESSAGE_COM_K30_88
IF TFLAG:192 < 0 && !TCVAR:烂醉
	CALL TRAIN_MESSAGE
	CALL K30_BLOW_JOB_ANIME(2)
	SIF TCVAR:30:385 || TCVAR:30:强行
		CALL K30_GASPING_MAGURO
ELSE
	CALL K30_D_LINE
	CALL M_KOJO_MESSAGE_COM_K30_88_1
ENDIF
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_88_1
#DIM 射出, 2
#DIM 箇所
#DIM 分岐
#DIM 回数
#DIM L_COUNT
IF TCVAR:烂醉
	PRINTFORMDL 让迷迷糊糊的映姬张嘴,让她给%TINKO(MASTER)%口交
	PRINTFORMW
	PRINTFORML 「呜呜...咕咚,咕咚咕咚...」
	PRINTFORMDW 映姬满含着阴茎,像在吮吸热酒一样吞下前液...
	RETURN 1
ENDIF
VARSET 射出
;箇所 = TCVAR:112 MASTERが直前のターンに射精した箇所フラグ 1=膣内 2=アナル
;射出:0 = 前ターン中出しされたTARGET_ID
;射出:1 = 前ターンＶorA挿入されていたTARGET_ID
IF TCVAR:MASTER:112 == 1
	射出:0 = MASTER_POSE(SET_V, SET_P, 1)
	箇所 = 1
ELSEIF TCVAR:MASTER:112 == 2
	射出:0 = MASTER_POSE(SET_A, SET_P, 1)
	箇所 = 2
ENDIF
IF MASTER_POSE(SET_V, SET_P, 1)
	射出:1 = MASTER_POSE(SET_V, SET_P, 1)
	箇所 = 1
ELSEIF MASTER_POSE(SET_A, SET_P, 1)
	射出:1 = MASTER_POSE(SET_A, SET_P, 1)
	箇所 = 2
ENDIF
回数 = (TFLAG:193 == 2)
;カウンター地の文を固定
TCVAR:30:21 = 3
;地の文分岐はフェラチオコマンドからコピペ
IF 射出:0
	PRINTFORMDL %CALLNAME:(射出:0)%の\@ 箇所 < 2 ? おまんこ # 肛門 \@にたっぷりと射精してから%TINKO(MASTER)%を引き抜き、
    PRINTFORMDL 把沾满了精液的%TINKO(MASTER)%塞进了映姬嘴里...
    PRINTFORMDL 映姬吸着阴茎发出下流的声音,脸颊凹陷
    分岐 = 1 + (射出:0 == 30)
ELSEIF 射出:1
    IF 射出:1== 30
		PRINTFORMDL 射精の直前に\@ 箇所 == 1 ? ヴァギナ # 菊穴 \@から%TINKO(MASTER)%を抜き映姫にしっかりと咥えさせた…
        PRINTFORMDL 映姬迫不及待地吸着阴茎,仿佛等不及想吞下精液。
		分岐 = 3
    ELSE
        PRINTFORMDL 在高潮前一刻从%CALLNAME:(射出:1)%的\@ 箇所 == 1 ? 雌穴 # 尻穴 \@拔出,用力把%TINKO(MASTER)%塞进了映姬嘴里...
        PRINTFORMDL 映姬抱住你的腰牢牢吸附着阴茎。
        分岐 = 4
    ENDIF
ELSE
    LOCALS '= "", "即将喷发的", "刚射过的"
    IF MASTER_POSE(SET_M, SET_P, 1) == 30
        PRINTFORMDL 映姬更加贪婪地吸吮着口中的%LOCALS:(TFLAG:193)%阴茎
    ELSE
        PRINTFORMDL 一靠近%LOCALS:(TFLAG:193)%阴茎映姬就本能地吞下吸吮
    ENDIF
    PRINTFORMDL 从她仿佛要榨干精液的样子看,完全感受不到平日作为阎魔的威严和谨慎...
    分岐 = 5
ENDIF 
CALL K30_BLOW_JOB_ANIME(0)
分岐 = !TFLAG:192 ? 0 # 分岐
SELECTCASE 分岐
CASE 0
        PRINTDATAL
            DATAFORM 「唔啊,咕咚...咕嘟嘟!唔嗯嗯♥ 咕嘟,咕嘟嘟...」 
            DATAFORM 「呜嗯,呜呜嗯♥ 咕噜,咕嘟嘟嘟...咕嘟嘟!咕咕...♥」
            DATAFORM 「呜噗♥ 咕嘟,咕噗噗...咕嘟嘟嘟!咕嘟,咕嘟...」
        ENDDATA
        PRINTDATADW
            DATAFORM 映姬扭曲着端庄的脸蛋,吸附着阴茎...
            DATAFORM 映姬红润的嘴唇发出淫靡的吮吸声,彼此的兴奋越烧越旺...
            DATAFORM 映姬锻炼良好的舌头和口腔仿佛要将%CALLNAME:MASTER%的性器整根吸入腹中...
        ENDDATA
        RETURN
    CASE 1
        PRINTDATAW
            DATAFORM (只让%K30_C_NAME(射出:0)%独享不行...)\n(我也要...多要些...)
            DATAFORM (我的小嘴...能接待住后续吗?)\n(..................)\n(我也想要...好多好多...)
            DATAFORM (...比%K30_C_NAME(射出:0)%更...舒服的话)\n(说不定他就...只给我了)
        ENDDATA
    CASE 2
        PRINTDATAW
            DATAFORM (啊哈♥ 小嘴也...有份啦♥)\n(啊...还想要...更多更多♥)\n(想要的心情...越来越强烈了♥)
            DATAFORM (得好好清理干净...♥)\n(全部、全部...吸出来...弄干净♥)
            DATAFORM (...\@ 箇所 < 2 ? 小穴 # 小屁股 \@只有的话...总觉得寂寞)\n(%K30_C_NAME(MASTER)%、小嘴也...可以有份吧♥)
        ENDDATA
CASE 3
        PRINTDATAW
            DATAFORM (%K30_C_NAME(MASTER)%更喜欢...这里吗?)\n(嘻嘻...♥ 那我得更努力了♥)
            DATAFORM (啊...要出来了吗?)\n(..................)\n(一次...好像不太够呢♥) 
            DATAFORM (%K30_C_NAME(MASTER)%...看起来很舒服的样子...♥)\n(想再...努力一些♥)\n(想更更多地...用小嘴取悦你...♥)
        ENDDATA
    CASE 4 
        PRINTDATAW
            DATAFORM （啊...♥ 不是%K30_C_NAME(射出:1)%,是我♥）\n（%K30_C_NAME(MASTER)%的...精液,只给我一个人...♥）
            DATAFORM （%K30_C_NAME(MASTER)%...不要再忍耐,射在我身上♥）\n（只给我...只射进我嘴里,给我好多好多...♥）
            DATAFORM （虽然对%K30_C_NAME(射出:1)%不好意思...）\n（现在该我了...♥ 该我让%K30_C_NAME(MASTER)%...舒服起来的时候了♥）
        ENDDATA
    CASE 5
        PRINTDATAW
            DATAFORM (啊哈...♥ %K30_C_NAME(MASTER)%...我爱你♥)\n(不仅%K30_C_NAME(MASTER)%,连%K30_C_NAME(MASTER)%的小弟弟都...超爱的♥)
            DATAFORM (想要...快给我,我想要...♥)\n(%K30_C_NAME(MASTER)%的精液,很多很多...都想要...♥) 
            DATAFORM (啊...♥ 嘴都要化了...♥)\n(如果%K30_C_NAME(MASTER)%射在里面...会更加舒服吧...)\n(更更多...♥)
        ENDDATA 
ENDSELECT
SELECTCASE TFLAG:193
CASE 0
    PRINTFORMDL 映姬高超的舌技很快就将%CALLNAME:MASTER%逼至极限
    PRINTFORMDW 本来还有余力的%CALLNAME:MASTER%,但被映姬用力吮吸铃口后很轻松就达到了高潮...
CASE 1
    PRINTFORMDL 本就已近高潮的阴茎在映姬的侍奉下猛烈跳动
    PRINTFORMDW 和映姬大力吸吮前液的同时,一股热流也涌上了阴茎...
CASE 2
    PRINTFORMDL 映姬细心地吮吸着刚射过还很敏感的粘膜
    PRINTFORMDL 阴茎很快就重新聚集了力量,准备进行下一次喷发
    PRINTFORMDW 看到%CALLNAME:MASTER%的反应,映姬喜形于色,于是发出更大声的吮吸声... 
ENDSELECT
;射精1回なら通常の絶頂口上に合流
SIF TFLAG:192 == 1
		RETURN 1
;映姫様の連続ターン
分岐 = RAND:3 + 5
FOR L_COUNT, 1, TFLAG:192
	CALL K30_D_LINE
	PRINTFORML
	PRINTFORMDL Ｃ経験 +1(%CALLNAME:MASTER%)
	PRINTFORMDL 絶頂経験 +1(%CALLNAME:MASTER%)
	PRINTFORMDL 射精経験 +1(%CALLNAME:MASTER%)
	WAIT
	CALL COLORMESSAGE, @"【口内%\@ TFLAG:194 ? 大量 # \@%%\@ TALENT:MASTER:濃厚精液 ? 濃厚 # \@%射精】", C_CREAM, 1, 0
	分岐 = 分岐 > 6 ? 5 # 分岐 + 1
    PRINTFORMDL %CALLNAME:MASTER%再度向映姬嘴里%\@ L_COUNT > 1 ? %TEXTR("再次/又一次/再再次/更多")% # \@%射精了 
	CALL K30_BLOW_JOB_ANIME(分岐)
	SELECTCASE 分岐
CASE 5
            PRINTFORMDL 映姬一边用舌头爱抚着龟头,一边接受着%CALLNAME:MASTER%%SAMEN_DESCRIPTION(MASTER)%,诱导着他进行更多的射精...
            PRINTDATAW
                DATAFORM 「啊啊...好舒服...♥」
                DATAFORM 「我会...全部吃干净的...♥」
                DATAFORM 「嘻嘻...:哈啊: 叭嗒、呜啾...♥」
            ENDDATA
        CASE 6
            PRINTFORMDL 映姬一边用舌头戳刺着%CALLNAME:MASTER%尿道口,%SAMEN_DESCRIPTION(MASTER,1)%喷发而出,
            PRINTFORMDL 从张开的嘴巴里%SAMEN_DESCRIPTION(MASTER)%滴滴答答流了出来...
            PRINTDATAW
                DATAFORM 「溢出来的部分,再给我的话...就没关系了♥」
                DATAFORM 「嘻嘻♥ 跳动得这么厉害...♥」
                DATAFORM 「碰一碰就...源源不断地出来啊...♥」
            ENDDATA
        CASE 7
            PRINTFORMDL 映姬用嘴唇抹净所有%SAMEN_DESCRIPTION(MASTER)%,全部吞入口中,然后
            PRINTFORMDL 将%SAMEN_DESCRIPTION(MASTER)%吐在双手上展示给%CALLNAME:MASTER%看...
            PRINTDATAW
                DATAFORM 「%K30_C_NAME(MASTER)%...我全吃掉了♥」
                DATAFORM 「这是,我的小嘴让您舒服的证据吧...♥」
                DATAFORM 「好烫啊...♥ 感觉手心快要烫伤了...♥」
            ENDDATA 
	ENDSELECT
	PRINTFORML 
	PRINTFORMD ...映姬%TEXTR("舔掉溢出来的精液/擦去嘴边的精液/吞下剩余的所有精液")%后,
    PRINTFORMDW %TEXTR("盯着/亲了亲/用手玩弄着")%变得%TEXTR("柔软的/萎靡的/缩小的")%阴茎...
	
	IF TFLAG:192 - 1 == L_COUNT
		PRINTDATAL
            DATAFORM 「这就是最后了...」\n「我会让您最舒服...♥」
            DATAFORM 「那么,最后一次...尽情地射出来吧...♥」
            DATAFORM 「最后...想射在脸上,可以吗...♥」\n「那我就...全都吃掉好了♥」
            DATAFORM 「还想更多地...感受%K30_C_NAME(MASTER)%」\n「这次要...满脸都是♥」
            DATAFORM 「我对自己的舌头...还是很有自信的」\n「可以完完全全...吸干%K30_C_NAME(MASTER)%...♥」
        ENDDATA
    ELSE
        PRINTDATAL
            DATAFORM 「还要...还要更多,好想要♥」
            DATAFORM 「还射得不够...」\n「%K30_C_NAME(MASTER)%就随我这么做好不好...♥」
            DATAFORM 「再来一次...可以吧?」\n「我不会让您说不的...♥」
            DATAFORM 「还不够,还要更多,拜托了...♥」
            DATAFORM 「我会...再把它弄大的...♥」
            DATAFORM 「还不想停下来...」\n「拜托,可以吗...♥」
            DATAFORM 「您平时不是...还能再来的吗♥」
            DATAFORM 「......嘻嘻♥」\n「加油!♥ 加油!♥」
            DATAFORM 「嘻嘻...♥」\n「很快就...会再次精神起来的♥」
            DATAFORM 「我啊...」\n「最喜欢%K30_C_NAME(MASTER)%和%K30_C_NAME(MASTER)%的小弟弟了♥」
            DATAFORM 「...我们继续吧,好不好♥」
            DATAFORM 「啊哈♥ 还很有精神...♥」\n「%K30_C_NAME(MASTER)%(这点程度)...完全没问题对吧♥」
            DATAFORM 「第%TOKANJIT(回数 + L_COUNT + 1)%次,也想要...射在小嘴里♥」\n「所以...再来一次♥」
        ENDDATA 
	ENDIF
	CALL K30_BLOW_JOB_ANIME(8)
    PRINTFORMW 「%TEXTR("嗯.../啊嗯.../呜嗯...")%♥ %TEXTR("吱吱.../嗯嗯...吱/嘎吱、嘎吱...吱嗯")%! %TEXTR("咕咚~.../吱吱吱.../呜吱...♥ 吱吱:红心:")%♥」
    PRINTFORMDL 映姬%TEXTR("马不停蹄地/再次/立刻")%含住了%CALLNAME:MASTER%的阴茎
    IF TFLAG:192 - 1 == L_COUNT
        PRINTFORMDL 这样下去,会被吸干净的...
        PRINTFORMDW 映姬像要把%CALLNAME:MASTER%榨干一样的吸吮,让%CALLNAME:MASTER%产生了这样的错觉
    ELSE
        PRINTDATADL
            DATAFORM 看来闫魔大人的舌头和嘴巴,并不打算让%CALLNAME:MASTER%的阴茎休息...
            DATAFORM 变得更加敏感的龟头被吸吮,酥酥麻麻的快感袭上了%CALLNAME:MASTER%...
            DATAFORM 粘满精液的映姬嘴巴发出更大声的水声,紧紧吸附在%CALLNAME:MASTER%身上...
            DATAFORM 映姬发出下流的声音吸吮着%CALLNAME:MASTER%,阴茎不由自主地再次站立起来...
        ENDDATA
        PRINTFORMDW %CALLNAME:MASTER%很快就无法再忍耐,连续第%TOKANJIT(回数 + L_COUNT + 1)%次高潮就要来临了
    ENDIF 
NEXT
RETURN 1

;TINKOに吸い付いて搾り尽くす映姫様のEXTRASOURCE関数
@M_KOJO_EXTRASOURCE_COM_K30_88
;条件精査-恋慕＆口ステータスが「精液を味わうと身体が疼いてしまう」
TFLAG:192 = -1
SIF !TALENT:恋慕 || !SHIRAHU(30) || TCVAR:30:385 || TCVAR:30:强行
	RETURN
TFLAG:192 ++
SIF FLAG:99 || TEQUIP:MASTER:避孕套
	RETURN
SIF EXP:精飲経験 < 151 || ABL:精液中毒 < 5 || RAND:2
	RETURN
;ここまで抜けたら実行。即射精の処理 + 口上側に渡すフラグ設定
TFLAG:192 = 1 + TCVAR:MASTER:要去了 + (EXP:精飲経験 > 300 && ABL:精液中毒 >= 10) + TALENT:精愛味覚
SIF GROUPMATCH(TCVAR:MASTER:112, 1, 2) && MASTER_POSE((TCVAR:MASTER:112) + 5, SET_P, 1) != 30
	TFLAG:192 ++
TFLAG:193 = TCVAR:MASTER:112 ? 2 # 0
TFLAG:193 = TCVAR:MASTER:要去了 ? 1 # TFLAG:193
TIME += (TFLAG:192 - 1) * 5
BASE:MASTER:射精 = MAXBASE:MASTER:射精
TCVAR:MASTER:要去了 = 1
SOURCE:反感 = 0
SOURCE:MASTER:快Ｃ += 50 * TFLAG:192
RETURN

;==================================================
;89,喝套内精液
;==================================================
;==================================================
;Optional: Determines if the standard description should be skipped.
;Return 0 if description and KOJO should be shown, 1 if description should be hidden and KOJO shown, 2 if description should be shown and KOJO hidden, and 3 if both description and KOJO should be hidden.
;==================================================
@M_KOJO_MESSAGE_MESSAGECHECK_COM_K30_89
IF FLAG:70 || CFLAG:MASTER:恶作剧 == 2 
	RETURN 0
ELSE
	CALL K30_D_LINE
	RETURN 1
ENDIF

@M_KOJO_MESSAGE_COM_K30_89
CALL M_KOJO_MESSAGE_COM_K30_89_1
RETURN RESULT

@M_KOJO_MESSAGE_COM_K30_89_1
;-------------------------------------------------
;記入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;-------------------------------------------------
IF LOCAL
	;時姦睡姦
	IF FLAG:70 || CFLAG:MASTER:恶作剧 == 2 
		RETURN 1
	ELSEIF TCVAR:30:385 || TCVAR:30:强行 || TCVAR:30:烂醉
		PRINTFORMDL 从积存种子的避孕套中将带橡胶臭地精液灌进映姬的嘴里
		PRINTFORMDL 但是映姬马上就把含在嘴里的东西吐了出来…
		PRINTFORMW 
		IF TCVAR:30:烂醉
			PRINTFORMW 「呕~哕%TEXTR("…臭～死～了～/、奇怪的味道…/、等一下…这是什么～")%」
		ELSEIF TCVAR:30:强行
			PRINTFORMW 「真是的…你还想让我做什么…？」
		ELSEIF ABL:精液中毒 < 5 
			PRINTFORMW 「这个味道…不行，真的……不讲理…」
		ELSE
			PRINTFORMW 「这样的…又不是理所当然要喝…」
		ENDIF
		RETURN 1
	ENDIF
	PRINTFORMD %CALLNAME:MASTER%让映姬跪坐、
	SELECTCASE TFLAG:193
		CASE IS > 300
			PRINTD 满满的,都快撑破了
		CASE IS > 150
			PRINTD 在前端充分地
	ENDSELECT
	PRINTFORMDL 把积存了子种的避孕套推到嘴边
	IF TFLAG:194 == 590
		PRINTFORMDL 映姬把带着橡胶臭的精液在自己口中滚动着啜饮
		PRINTFORMDL 但是，脸上浮现出不满的表情，怎么也喝不完…
		PRINTFORMW 
		PRINTFORML 「…………嗯、嗯呼呼♥」
		PRINTFORMDL 突然映姬露出恶作剧般的微笑，把脸凑向%CALLNAME:MASTER%的胯座
		CALL K30_BLOW_JOB_ANIME(0)
		PRINTFORML 「嗯…啾噜♥　嗯啾……啾噜噜！」
		PRINTFORMDW 嘴里积满了唾沫和精液，開始剧烈地嘬吸阴茎……
		PRINTFORML
		PRINTFORMW 「…噗哈♥」
		PRINTFORML 「%K30_C_NAME(MASTER)%、我……对你的味道\@ TALENT:精愛味覚 ? 非常喜欢 # 不讨厌 \@但是」
		PRINTFORML 「真正喜欢的是…看你愉快的表情和隐忍的表情」
		PRINTFORML 「用喉咙和嘴唇感受你的……小鸡鸡，让你快乐」
		PRINTFORMW 「这样的话，我也会很幸福……也非常開心…♥」
		PRINTFORML 
		PRINTFORML 「所以我不喜欢这么拐弯抹角」
		PRINTFORMW 「%K30_C_NAME(MASTER)%、我想让你直接感受♥」
		IF NOWEX:MASTER:射精
			PRINTFORML 「啊ー姆…啾噜、叽啵！　叽啵……滋兹～♥♥」
			PRINTFORMDL 映姬仿佛连呼吸都舍不得似的，叼着阴茎不放
			PRINTFORMDW 转眼间%CALLNAME:MASTER%就迎来了极限，在映姬的口中释放了积存已久的欲望…
		ELSE
			PRINTFORML 「…啊姆♥　啾噜！　啾噜噜♥　啾噗……」
			PRINTFORMDL 映姬滴垂着混合白浊的口水，一边热切地口交侍奉着
			PRINTFORMDW %CALLNAME:MASTER%把一切交给映姬，感受这腰下飘荡的极致快感…
		ENDIF
	ELSEIF TALENT:精愛味覚
		PRINTFORMDL 映姬很高兴地舔掉了橡皮臭味的精液，\@ TALENT:MASTER:濃厚精液 ? 就像咀嚼一样 # 在舌间转动着 \@品味……
		PRINTFORMW 
		PRINTFORML 「嗯……嗯咕、嗯………噗哈♥」
		PRINTFORMW 「嗨！谢谢款待…\@ TALENT:MASTER:濃厚精液 ? 浓厚牛奶 # 美味しく \@我開动了♥」
		PRINTFORML 「但是，只有这个的话嘴会变得寂寞呢…」
		PRINTFORML 「刚榨的热的…更想大口大口地吃…♥」
		PRINTFORMDW 映姬一边舔着舌头一边向%CALLNAME:MASTER%的%TINKO()%投去炙热的视线……
	ELSEIF ABL:精液中毒 < 5 || EXP:口淫経験 < 100
		PRINTFORMDL 映姬皱着眉头，嘴里含着橡胶臭味的精液…
		PRINTFORMW 
		PRINTFORML 「嗯嗯…呃、嗯姆……唔哕…呕」
		PRINTDATAW
			DATAFORM 「总觉得喝了……这个，有点…」
			DATAFORM 「…臭得厉害…讨厌」
			DATAFORM 「这有什么意义吗…」\n「如果你告诉我的话，我会直接用嘴给你做的…？」
		ENDDATA
	ELSE
		PRINTFORMDL 映姬带着微妙的表情，喝下了橡胶味的精液。…
		PRINTFORMW
		PRINTFORML 「嗯咕……嗯、嗯嗯……嗯」
		PRINTFORML 「…嗯唔呜、总算…咽下去了，但是」
		PRINTFORMW 「\@ TALENT:MASTER:濃厚精液 ? 太…太浓了 # 精臭味……很重 \@、真是太糟糕了」
		PRINTFORML 「而且呢，我觉得这个味道…并不是很喜欢」
		PRINTFORML 「我真正喜欢的是…♥」
		PRINTFORMDW 映姬停顿了一下，热切地注视着%CALLNAME:MASTER%的股间…
	ENDIF
ENDIF
RETURN 1
;精飲よりTINKOに奉仕するのが好きな映姫様のEXTRASOURCE関数
@M_KOJO_EXTRASOURCE_COM_K30_89
;条件精査
SIF FLAG:99 || !TALENT:恋慕 || TCVAR:30:385 || TCVAR:30:强行
	RETURN
SIF !SHIRAHU(30) || EXP:口淫経験 < 100 || ABL:舌 < 3
	RETURN
CALL COM_ABLE81
SIF !RESULT
	RETURN
;积攒度＆舌技能に応じた適当判定
SIF (CFLAG:积攒度 / 200) + ABL:舌 < RAND:14
	RETURN
;ここまで抜けたら実行。口交のソース/経験等の加算と修正
CALL TOUCH_SET(SET_FROM_M, SET_P, 30)
CSTR:MASTER:10 = /
CALL COM81
SOURCE:快Ｍ += 20
SOURCE:MASTER:快Ｃ += 30
;口上側に渡すフラグ
TFLAG:194 = 590
RETURN
