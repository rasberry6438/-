;================================
;custom shop button
;カスタムSHOPボタン
;y is the id of the button, range from 0 to 9
;yはボタンのID、0~9に対応
;使用する際はSKIPSTART~SKIPENDを外してください。
;================================
;judge the button can be shown, if return 1, it will show
;これをRETURN 1するとボタンが表示される
@KOJO_CUSTOM_BUTTON_CONDITION_K30_0
SIF !GETBIT(CFLAG:30:1803, 8)
	RETURN 0
custom_button_name:30:0 = 口上色変更
RETURN 1

;when the button is clicked
;ボタンがクリックされた場合
@KOJO_CUSTOM_BUTTON_K30_0
#DIM K_COLOR = 0x337D5C, 0x52C487, 0x7FD8A7, GETDEFCOLOR(), 0xFF93FB
LOCALS '= "冷静的", "普通的", "稍微高调的", "无个性的", "色情的"
LOCAL = FINDELEMENT(K_COLOR, CFLAG:30:1501, 0)
LOCAL = INRANGE(LOCAL, 0, 3) ? LOCAL + 1 # 0
CFLAG:30:1501 = K_COLOR:LOCAL
SETCOLOR CFLAG:30:1501
PRINTFORML 「已更改为%LOCALS:LOCAL%声音」
RESETCOLOR
RETURN 1

;================================
;カスタム素質
;テンプレの便宜上、独立した関数としてまとめました。
;使用する際はSKIPSTART~SKIPENDを外してください。
;================================
@KOJO_CUSTOM_TALENT_SET_K30

;表示判定 真=表示/偽=非表示
CUSTOM_TALENT:30:0 = GETBIT(CFLAG:1910, 7)
;素質名
CUSTOM_TALENT_NAME:30:0 = 肛门一线天
;(省略可)表示色、0/通常 1/ピンク 2/赤色+太字
CUSTOM_TALENT_COLOR:30:0 = 0xFFCCFF
;(省略可)表示タイプ 1/種族 2/性的 3/身体的 4/精神的 5/技術的 他は「その他」
CUSTOM_TALENT_TYPE:30:0 = 3

CUSTOM_TALENT:30:1 = !INRANGE(TCVAR:30:363, -1, 0)
CUSTOM_TALENT_NAME:30:1 = %\@ TCVAR:30:363 < 0 ? 早晨 # 早安咬 \@%满足
CUSTOM_TALENT_COLOR:30:1 = 0xFFCCFF
CUSTOM_TALENT_TYPE:30:1 = 0

;================================
;カスタムコマンド0_接受无休止的说教
;================================
;コマンド名
@KOJO_COM_NAME_K30_0
RESULTS = 接受无休止的说教

;コマンド使用条件。通常のCOM_ABLE関数と同じくRETURN 1すれば実行可能としてコマンドが表示される
@KOJO_COM_ABLE_K30_0
SIF SHIRAHU(MASTER) != 1 || SHIRAHU(30) != 1 || CFLAG:30:1701 < 10 || TFLAG:COMABLE管理 > 1
	RETURN 0
SIF GET_TARGETNUM() > 1 || CHK_DATENOW(CFLAG:30:约会中) || MARK:30:反発刻印 > 1
	RETURN 0
SIF NEMUKE() > 240 || !CHARA_HOLIDAY(30) || K30_CHECK_HEN_T()
	RETURN 0
RETURN 1

;コマンド実行内容。経験・SOURCE変動を記述
;RETURN 1=コマンド実行 / RETURN 0=キャンセル(SOURCE計算SKIP) / RETURN -1=コマンド取り消し
@KOJO_COM_K30_0
#DIM お怒り
お怒り = CFLAG:ブチギレ || MARK:反発刻印
PRINTFORML …你真的要接受吗？（%\@ お怒り ? 作为平息映姬的愤怒的代价 # 就这样 \@%结束这一天）
CALL ASK_YN
SIF RESULT
	RETURN -1
;好感度低いなら効果強め、でも効率考えると使う意味なし
TFLAG:194 = 720 - NEMUKE() / 60
SOURCE:情愛 = 75 * TFLAG:194
SOURCE:達成 = 150 * TFLAG:194
IF CFLAG:30:好感度 <= 500
	TIMES SOURCE:情愛, 1.50
	TIMES SOURCE:達成, 1.50
ELSEIF CFLAG:30:好感度 <= 5000
	TIMES SOURCE:情愛, 1.25
	TIMES SOURCE:達成, 1.25
ENDIF
BASE:MASTER:体力 = 0
BASE:MASTER:気力 = 0
IF お怒り
	SOURCE:情愛 = 0
	SOURCE:達成 /= 3
	CFLAG:ブチギレ = 0
	CFLAG:心情不快 = 0
	BASE:憤怒 = 0
	BASE:MASTER:体力 = -500
	TFLAG:193 = 1
	IF MARK:反発刻印
		SOURCE:達成 = 0
		MARK:反発刻印 = 0
		TFLAG:193 = 2
		BASE:MASTER:体力 = -2500
	ENDIF
ENDIF
TFLAG:194 = MIN(TFLAG:194, 6)
TIME += TFLAG:194  * 60
RETURN 1

;コマンドメッセージ。口上色が指定される。
;Vの値は(270 + Y)。SELECTCOM/PREVCOMの代入値も同じ
@M_KOJO_MESSAGE_COM_K30_270
#DIM TEMP
#DIM P_REDRAW
#DIM L_COUNT
#DIM LOOP_C
#DIM LINE_C
{
#DIMS WORDS =	 "首先", "再次强调", "坦率地说", "客观地看待",
				 "比如说", "话题转变", "重要的是", "反思自己",
				 "是的，您稍微", "借此机会", "反思自己", "如果继续这样下去"
}
{
#DIMS MODIFY =	"永远地", "无论何时", "无尽地", "永不停歇", "无限地",
				"无限制地", "一直", "不断地", "专心致志地", "仍然",
				"无穷尽地", "持续地", "不知疲倦地", "无止境的", "永不结束",
				"直到点击为止", "依然", "无法停止地", "持续不断地", "不间断地"
}
CALL FISHER_YATES_SHAFFLE(12)
CALLF FUNC_FISHER_YATES_SHAFFLE(LOCAL, 20)
VARSET LOCALS
TEMP = 0
MODIFY:(LOCAL:0) '= ""
IF TFLAG:193
	PRINTFORMDL 映姬开始%TEXTR("猛烈地/如烈火/如奔流")%地训诫...
ELSE
	PRINTFORMDL 听取了%CALLNAME:MASTER%的愿望，映姬%TEXTR("兴高采烈地/欣喜若狂地/仿佛心领神会地/")%开始训诫...
ENDIF
PRINTW
PRINTFORML 「！！」
IF !TFLAG:193
	PRINTDATAW 
		DATAFORM 「太棒了...这种高尚的心态令人钦佩」\n「重新审视了%K30_C_NAME(MASTER)%」
		DATAFORM 「没想到你这么想听我的教诲...我没有意识到」\n「善行的重要性，%K30_C_NAME(MASTER)%似乎也终于明白了」
		DATAFORM 「%K30_C_NAME(MASTER)%变得如此高尚...」\n「我的日常行为也感到稍许回报」
	ENDDATA
	PRINTFORML 「...好吧」
	PRINTFORML 「幸好今天我时间比较空」
	PRINTFORML 「%TEXTR("我会比平常更加用心，/会花更多时间，/会包括之前遗漏的内容，认真地")%%K30_C_NAME(MASTER)%传授教诲」
ELSE
	PRINTDATAW 
		DATAFORM 「终于...终于有了改变的决心啊」
		DATAFORM 「如果有反省的意愿，我也不能不帮忙」
		DATAFORM 「在我忍耐的极限之前，你自己意识到是最好的」
	ENDDATA
	PRINTFORML 「今天我的时间也比较空...你不用客气」
	PRINTDATAL 
		DATAFORM 「那么，可能会有些长，请认真听讲」
		DATAFORM 「那么，从何处开始呢...让我们一边传授教诲一边整理吧」
		DATAFORM 「那么...为了不让反省止步于此，让我们认真传授教诲吧」
	ENDDATA
ENDIF
PRINTL

P_REDRAW = CURRENTREDRAW()
LINE_C = LINECOUNT
REDRAW 0
WHILE 1
	FOR LOOP_C, 0, 5
		IF !TEMP
			LOCALS:99 = 好吗？
		ELSEIF TEMP % 2
			LOCALS:99 = 
		ELSE
			SIF !LOOP_C
				LOCAL:99 = LOCAL:99 == 11 ? 0 # LOCAL:99 + 1
			LOCALS:99 = \@ !LOOP_C ? %WORDS:(SHAFFLE_ARRAY:(LOCAL:99))% # %LOCALS:99% \@
		ENDIF
		LOCALS:0 = 「%LOCALS:99%%UNICODE(0x2026) * (9 - STRLENSU(LOCALS:99))%」
		FOR L_COUNT, 1, 4
			LOCALS:L_COUNT '= \@ LOOP_C >= L_COUNT ? 「%UNICODE(0x2026) * ((4 - L_COUNT) * 2 + 1)%」 # \@
		NEXT
		LOCALS:4 '= \@ LOOP_C == 4 ? （%MODIFY:(LOCAL:TEMP)%少女説教中…） # \@
		FOR L_COUNT, 0, 5
			IF L_COUNT < 4
				PRINTFORML %LOCALS:L_COUNT%
			ELSE
				PRINTFORMDL %LOCALS:L_COUNT%
			ENDIF
		NEXT
		TWAIT 350 + ((LOOP_C == 4) * 1050), 0
		SIF GETKEY(1) && TEMP > 1
			GOTO LOOP_BREAK
		CLEARLINE LINECOUNT - LINE_C
	NEXT
	CLEARLINE LINECOUNT - LINE_C
	TEMP = TEMP < 19 ? TEMP + 1 # 1
WEND
$LOOP_BREAK
REDRAW P_REDRAW
LOCALS:0 '= "「………」", "「……………」", "「…………哎呀？」"
CALL PRINT_GROUP(LOCALS, 3, 350)
IF TFLAG:193
	PRINTFORMDL …{TFLAG:194}个小时的持续训诫，映姬的心情好像好转了
	PRINTFORMDW 映姬%\@ TFLAG:193 > 1 ? 失去了反発刻印LV1 # 的怒气平息了 \@%
ELSE
	PRINTFORMDL …{TFLAG:194}个小时的持续训诫，映姬看起来并不感到疲倦
	PRINTFORMDL 相反，她比平时更有活力，非常享受地继续训诫
ENDIF
PRINTFORML
PRINTFORMDW 但是，%CALLNAME:MASTER%已经到达极限了...
RETURN 1

;================================
;カスタムコマンド1_スイッチ操作
;================================
@KOJO_COM_NAME_K30_1
RESULTS = 遥控震动棒

@KOJO_COM_ABLE_K30_1
SIF !TEQUIP:30:肛用振動棒 || !SHIRAHU(30)
	RETURN 0
SIF TFLAG:COMABLE管理 > 1 || CFLAG:30:诶嘿嘿 || CFLAG:MASTER:诶嘿嘿
	RETURN 0
SIF TCVAR:30:364 < 1 || !TCVAR:30:365 || TIME:1 - TCVAR:30:368 < 10
	RETURN 0
RETURN 1

@KOJO_COM_K30_1
;強弱切替え
TCVAR:30:365 = TCVAR:30:365 == 1 ? 2 # 1
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
TFLAG:理性削り = 1
;-------------------------------------------------
;適当にソース追加処理
;-------------------------------------------------
;弱
IF TCVAR:30:365 == 1
	SOURCE:露出 = 50
	SOURCE:屈従 = 50
	SOURCE:欲情 = 100
	SOURCE:受動 = 100
	SOURCE:反感 = 10
	LOCAL = 2
;強
ELSE
	SOURCE:露出 = 150
	SOURCE:屈従 = 150
	SOURCE:欲情 = 50
	SOURCE:受動 = 50
	SOURCE:反感 = 20
	LOCAL = 5
ENDIF
SIF HITOKE(1)
	TIMES SOURCE:露出 , 2.50
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ａ経験 ++
EXP:PLAYER:道具使用経験 ++
TIME += LOCAL
TCVAR:30:368 = TIME:1 + LOCAL 
RETURN 1

@M_KOJO_MESSAGE_COM_K30_271
#DIM TGT
LOCAL = K30_FIND_AROUND()
TGT = GROUPMATCH(LOCAL, 49, RANDOM_CHARANUM) ? 0 # LOCAL
IF TCVAR:30:365 == 1
	PRINTFORMDL %CALLNAME:MASTER%将怀中的开关调回到「弱」。
	IF NOWEX:Ａ絶頂
		PRINTFORMDL 但是映姬激动的身体已经无法再忍受绝顶的感觉...
		CALL K30_PRINT_VIBE_DATE
		PRINTFORMW
		STRDATA LOCALS
			DATAFORM 「%K30_C_NAME(MASTER)%，现在...就算放轻松，也为时已晚了HPH」\n「我已经...无法停下来了...HPH」
			DATAFORM 「...‘等一下’，你是说？」\n「这样的话...如果不让我解脱...我会受不了的HPH」
			DATAFORM 「...在这里，根本做不到...憋住HPH」\n「就这样，慢慢地...就这样...HPH」
		ENDDATA
		CALL HPH_PRINT, LOCALS, "W"
		RETURN 1
	ELSE
		PRINTDATADL
			DATAFORM 映姬整理了凌乱的容貌并深呼吸
			DATAFORM 从强烈的快感中解脱出来，映姬松了口气
			DATAFORM 映姬擦去额头上的汗，深吸一口气
		ENDDATA
		IF K30_BE_SEEN() && !TFLAG:约会道中
			PRINTFORMDL 确认了没有被察觉，\@ TGT ? %CALLNAME:TGT% # 任何人 \@ 察觉，显得安心下来
		ELSE
			PRINTFORMDL ...但是，映姬转向%CALLNAME:MASTER%的目光似乎有些失望
		ENDIF
	ENDIF
	CALL K30_PRINT_VIBE_DATE
	PRINTFORMW
	CALL HPH_PRINT, @"「呼HPH 呼呼HPH」", "L"
	PRINTFORML
	IF K30_BE_SEEN() && !TFLAG:约会道中
		STRDATA LOCALS
			DATAFORM 「没事的……%K30_C_NAME(TGT,2)%没有注意到…HPH」\n「是的，没事的……无论多少次…没关系，因为…HPH」
			DATAFORM 「…比平时，更加…敏感，感觉…HPH」\n「在%K30_C_NAME(TGT,2)%面前…做这种事情，我…」\n「已经，无法回头了…对吧HPH」
			DATAFORM 「即使在%K30_C_NAME(TGT,2)%面前…也毫不手软，对吧，%K30_C_NAME(MASTER)%HPH」\n「…多亏了你，我…脑袋，都要化掉了…HPH」
		ENDDATA
	ELSE
		STRDATA LOCALS
			DATAFORM 「……稍微，平静下来了」\n「但是……」\n「想到，下次，什么时候会来…HPH」
			DATAFORM 「之前一直在想，会持续到什么时候，但是…」\n「一旦变得虚弱…」\n「…就会想要…HPH」
			DATAFORM 「呼…HPH」\n「成功，忍住了…HPH」\n「下一个奖励，会…期待的…HPH」
		ENDDATA
	ENDIF
	CALL HPH_PRINT, LOCALS, "W"
ELSE
	PRINTFORMDL %CALLNAME:MASTER%将怀中的开关拨到了“强”…
	PRINTFORMW
	CALL K30_PRINT_VIBE_DATE
	FONTSTYLE 3
	PRINTFORML
	LOCALS = %TEXTR("啊゛啊゛゛唔/哈゛啊゛啊/啊゛咿゛咿/啊゛゛哈/唔゛噢HPH　啊゛啊HPHHPH　啊゛哈")%HPHHPHHPH
	IF K30_BE_SEEN() && !TFLAG:约会道中
		CALL HPH_PRINT, @"（%LOCALS%）", "L"
		FONTREGULAR
		PRINTFORML
		PRINTFORMDW …就在最后一刻，映姬忍住了声音
	ELSE
		CALL HPH_PRINT, @"「%LOCALS%」", "L"
		FONTREGULAR
		PRINTFORML
		PRINTFORMDW …传出了一种完全不像是阎魔大王的声音
	ENDIF
	PRINTFORML
	IF TCVAR:30:382 
		PRINTDATADL
			DATAFORM 刚刚高潮过的身体遭受追击，映姬挣扎着试图忍耐
			DATAFORM 敏感的身体再次受到追击，映姬呆立不动
			DATAFORM 难以忍受无尽的快感，映姬拒绝摇头
		ENDDATA
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		PRINTDATADL
			DATAFORM 映姬用颤抖的身体慢慢后退，试图逃离\@ TGT ? %CALLNAME:TGT% # 周围 \@的目光
			DATAFORM 映姬明显表现出可疑行为，试图不被\@ TGT ? %CALLNAME:TGT% # 周围 \@察觉
			DATAFORM 映姬在人群中不自然地扭动身体，在\@ TGT ?%CALLNAME:TGT%面前 # 周围人的目光下 \@ 试图掩饰自己的焦虑
		ENDDATA
	ELSE
		PRINTDATADL
			DATAFORM 映姬抱紧自己的身体，拼命忍耐
			DATAFORM 映姬屏住呼吸，完全动弹不得
			DATAFORM 映姬尴尬地夹紧大腿，紧握裙摆
		ENDDATA
	ENDIF
	IF NOWEX:Ａ絶頂
		PRINTFORMDL 但肛门震动器的强烈震动无情地逼近映姬...
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		PRINTFORMDL 忍受快感却忌讳他人目光的她，让%CALLNAME:MASTER%的虐待心得到了满足
	ELSE
		PRINTFORMDL 察觉周围无人，映姬毫不犹豫地发出欢愉的尖叫
	ENDIF
	PRINTFORML
	IF NOWEX:Ａ絶頂
		TFLAG:194 = 19
		STRDATA LOCALS
			DATAFORM 「啊啊…！！ 不行了HPH 等一下HPH 等一下HPHHPH」
			DATAFORM 「不行了HPH 太厉害了HPH 太厉害了HPH 不行了HPHHPH」
			DATAFORM 「啊啊…HPH 啊HPHHPH 啊啊～～HPHHPH」
			DATAFORM 「要飞起来了HPH 头晕HPH 变得奇怪了HPH」
		ENDDATA
		IF K30_BE_SEEN() && !TFLAG:约会道中
			CALL HPH_PRINT, @"（%LOCALS%）", "L"
			CALL HPH_PRINT, @"（要高潮了HPH %K30_C_NAME(TGT,2)%面前高潮了！用屁股高潮了HPH高潮了HPHHPH）", "W"
		ELSE
			CALL HPH_PRINT, @"「%LOCALS%」", "L"
			CALL HPH_PRINT, @"「感觉好极了HPH 屁股HPH 太舒服了HPHHPH」", "W"
		ENDIF
	ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
		CALL HPH_PRINT, @"（%K30_C_NAME(MASTER)%！？ %TEXTR("现在、这里/这么、地方/这里、狙击")%，太过分了HPHHPH）", "L"
		CALL HPH_PRINT, @"（%K30_C_NAME(TGT,2)%在这里！这边！看着呢HPHHPH）", "L"
		CALL HPH_PRINT, @"（啊啊HPH 这感觉…太舒服了%\@ TCVAR:30:382 ? ，停不下来HPH 一直，停不下来 # HPH 这种…忍不住了，忍不住 \@%HPHHPH）", "W"
	ELSE
		STRDATA LOCALS
			DATAFORM 「…声音！！ 快结束了HPH 太激烈了HPHHPH」
			DATAFORM 「…太厉害了HPH 太厉害了HPH 很刺激HPH 无法停止HPHHPH」
			DATAFORM 「…明知道…不行HPH 感觉，太过了…真的，不行了HPHHPH」
		ENDDATA
		CALL HPH_PRINT, LOCALS, "W"
	ENDIF
ENDIF
RETURN 1
