;==================================================
;ARG为1，玩家有多少个恋人，针对多恋人补丁
;ARG为2，玩家在地灵殿之外有多少个恋人，针对多恋人补丁
;ARG:1为1是恋人，为2是恋慕
;返回-1，代表函数未正确启动
;-------------------------------------------------
@K36_MASTER_LOVER_NUMBER(ARG,ARG:1)
#FUNCTION
#DIM NUMBER = 0
;声明变量NUMBER并赋值0
#DIM NUM = 1
;声明变量NUM并赋值1
NUMBER = 0

IF ARG == 1
	IF ARG:1 == 1
		FOR NUM, 1, CHARANUM
			IF TALENT:NUM:恋人
				NUMBER ++
			ENDIF
		NEXT
	ELSEIF ARG:1 == 2
		FOR NUM, 1, CHARANUM
			IF TALENT:NUM:恋慕
				NUMBER ++
			ENDIF
		NEXT
	ELSE
		NUMBER = -1
	ENDIF
ELSEIF ARG == 2
	IF ARG:1 == 1
		FOR NUM, 1, CHARANUM
			IF TALENT:NUM:恋人 && !(NUM == 36 || NUM == 37 || NUM == 38 || NUM == 49)
			;除了地灵殿众人以外的恋人
				NUMBER ++
			ENDIF
		NEXT
	ELSEIF ARG:1 == 2
		FOR NUM, 1, CHARANUM
			IF TALENT:NUM:恋慕 && !(NUM == 36 || NUM == 37 || NUM == 38 || NUM == 49)
			;除了地灵殿众人以外的恋慕
				NUMBER ++
			ENDIF
		NEXT
	ELSE
		NUMBER = -1
	ENDIF
ELSE
	NUMBER = -1
ENDIF

RETURNF NUMBER

;==================================================
;参照典的口上
;返回0，代表函数未正确启动
;-------------------------------------------------
@K36_FIND_AROUND()
#FUNCTION
#DIM NUMBER = 0
;声明变量NUMBER并赋值0
IF GET_TARGETNUM()
	FOR NUMBER,1,(GET_TARGETNUM() + 1)
		SIF TARGET:NUMBER == TARGET:0
			CONTINUE
		SIF (CFLAG:(TARGET:NUMBER):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:NUMBER):現在位置 != CFLAG:36:現在位置)
			CONTINUE
		SIF TARGET:NUMBER == 49
		;觉
			SETBIT CFLAG:36:1012,0
		SIF TARGET:NUMBER == 37
		;空
			SETBIT CFLAG:36:1012,1
		SIF TARGET:NUMBER == 38
		;恋
			SETBIT CFLAG:36:1012,2
		SIF TARGET:NUMBER == 24
		;橙
			SETBIT CFLAG:36:1012,3
	NEXT
	RETURNF 1
ELSE
	RETURNF 0
ENDIF