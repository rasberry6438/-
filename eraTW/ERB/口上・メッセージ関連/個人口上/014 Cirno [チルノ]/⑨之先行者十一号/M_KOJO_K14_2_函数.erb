;===========================================================
;冰之妖精的控制界面！
;用于UPDATE和初见。
;===========================================================
@CIRNO_UPDATE
SETCOLOR 0x51c4d3
$BEGIN
PRINTL
SELECTCASE RAND:10
	CASE 0
		PRINTFORML	❉❉❉(ᗜᴗᗜ)❉❉❉
	CASE 1
		PRINTFORML	❉❉❉(ᗜ_ᗜ)❉❉❉
	CASE 2
		PRINTFORML	❉❉❉(ᗜ ̮ᗜ)❉❉❉
	CASE 3
		PRINTFORML	❉❉❉(ᗜ‸ᗜ)❉❉❉
	CASEELSE
		PRINTFORML	❉❉❉(ᗜˬᗜ)❉❉❉
ENDSELECT
PRINTL
PRINTFORML	此处为琪露诺口上的调整界面。
PRINTFORML	本口上关于角色的性格和行为有许多个人的理解、且包含一定的暴力表现、还请见谅。
PRINTFORML	请选择需要调整的内容。
PRINTFORML	[1] 角色数据
PRINTFORML	[2] 暴力表现
PRINTFORML	[3] 选项分歧
PRINTFORML	[9] 关于此口上
PRINTFORML	[0] 没有了
INPUT
SELECTCASE RESULT
	CASE 1
		$SWITCH_1
		IF CFLAG:14:1000 != 1
			PRINTFORML	依照本口上的默认设置、琪露诺的素质与角色相性将发生变动。
		ELSE
			PRINTFORML	依照本口上的默认设置、琪露诺的素质与角色相性本应发生变动。
		ENDIF
		PRINTFORML	允许此变动吗？
		PRINTFORML	[1] 允许
		PRINTFORML	[2] 阻止（或还原）
		PRINTFORML	[3] 仅阻止涉及其他角色的部分
		SIF !CFLAG:14:面識
			PRINTFORML	[4] 保留（见面时决定）
		PRINTFORML	[0] 了解此变动
		INPUT
		SELECTCASE RESULT
			CASE 1
				CALL CIRNO_ADJUST(0)
				CALL CIRNO_ADJUST(1)
				CALL CIRNO_ADJUST(2)
				PRINTFORML	已完成默认的素质与角色相性变动。
				PRINTL
			CASE 2
				CALL CIRNO_ADJUST(0)
				IF CFLAG:14:1000 != 1
					PRINTFORML	不会改变琪露诺的素质与角色相性。
					PRINTFORMW	请宽容看待口上内容因此出现的少许矛盾。
					PRINTL
				ELSE
					PRINTFORML	已将本口上改变的素质与角色相性还原。
					CFLAG:14:1000 = 1
					PRINTL
				ENDIF
			CASE 3
				CALL CIRNO_ADJUST(0)
				CALL CIRNO_ADJUST(1)
				PRINTFORML	已完成不涉及其他角色的素质与角色相性变动。
				PRINTL
			CASE 4
				CFLAG:14:1000 = 3
				PRINTFORML	将等待到初见时再决定。
				PRINTFORML	如果已经见过面，则是保留之前的选择结果。
				PRINTL
			CASE 0
				PRINTFORML	出于情节安排的需要以及对角色的自我理解、本口上为琪露诺添加了叛逆与开朗素质、并且将在取得恋人时更换傲慢素质。
				PRINTFORML	同时、调整了琪露诺与部分角色的相性如下：
				PRINTFORML	去除了莉莉白、莉莉黑、莉格露、妹红和蕾蒂的特殊相性（X→100）、这一变化是双向的；
				PRINTFORML	降低了对三月精的相性（150→120）、提升了对拉瓦尔的相性（100→150）、提升了对文的相性（100→150）；
				PRINTFORML	提升了文对琪露诺的相性（100→150）、提升了隐崎奈对琪露诺的相性（100→120）；
				PRINTFORML	以及、混杂了一点私心地、基于大妖精口上的内容倾向、降低了大妖精对琪露诺的相性（300→150）。
				PRINTFORML	以上为默认的变动内容。尽管相性这个数据似乎没有什么特别的作用、但、图个看着舒服吧大概。
				PRINTFORML	如果无法接受改变其他角色数据的行为，请选择阻止涉及其他角色的部分变动。
			CASEELSE
				GOTO SWITCH_1
		ENDSELECT
		GOTO BEGIN
	CASE 2
		$SWITCH_2
		PRINTFORML	所谓暴力表现、即是基于角色活泼好动性格、结合角色原作表现所决定的口上特性。
		PRINTFORML	一言以蔽之、就是傻子会打人。
		PRINTFORML	由于算法的限制、打得还挺疼、通常情况下造成的伤害可达900点体力。
		PRINTFORML	虽然这类表现并不多见、但可能有部分玩家依然不能适应。
		PRINTFORML	因此、这里给出一个将伤害降低到纯粹画风程度的选项。
		PRINTFORML	以及为保持一致、将同时关闭琪露诺的冷气效果。
		PRINTFORML	[1] 降低
		PRINTFORML	[0] 不变
		INPUT
		SELECTCASE RESULT
			CASE 1
				IF CFLAG:14:1001 == 0
					PRINTFORML	将降低造成的伤害。
					CFLAG:14:1001 = 1
				ELSE
					PRINTFORML	将依然降低造成的伤害。
				ENDIF
			CASE 0
				IF CFLAG:14:1001 == 0
					PRINTFORML	将依然造成正常数值的伤害。
				ELSE
					PRINTFORML	将重置为正常数值的伤害。
					CFLAG:14:1001 = 0
				ENDIF
		ENDSELECT
		GOTO BEGIN
	CASE 3
		$SWITCH_3
		PRINTFORML	本口上默认在部分内容中设置有选项分歧。
		PRINTFORML	但考虑到玩家游玩习惯的区别、可能会有觉得选项太多很麻烦的、或是觉得随机比较不容易漏掉内容的。
		PRINTFORML	因此、这里给出一个关闭选项分歧的选项。这样讲是否像是某种悖论？
		PRINTFORML	[0] 关闭
		PRINTFORML	[1] 保持
		INPUT
		SELECTCASE RESULT
		ENDSELECT
		GOTO BEGIN
	CASE 9
		$SWITCH_9
		PRINTFORM	首先鸣谢
		CALL COLORMESSAGE(" 花椰菜 ", 0xb6dc65, 0, 3)
		PRINTFORML	在各方面提供的一切帮助。
		PRINTFORML	（花椰菜是一名在4.842及之前版本活跃并成果丰富的口上汉化者。）
		PRINTFORML	（其较新的翻译作品包括典、辉夜、勇仪、正邪、露米娅等数十名角色。）
		PRINTFORML	（也曾制作过诸如白色情人节回礼等补丁。）
		PRINTFORML	
		PRINTFORML	此琪露诺口上由纸蛇冰沙制作，在一开始，主要是出于对「原有琪露诺口上竟只是大妖精扮演模式的挂件」的不满而动笔。
		PRINTFORML	在写作过程中，重新点燃了对角色的热情，于是才在经历颇多变故的情况下、还得以坚持至今。
		PRINTFORML	所以我认为写口上还是得为了喜欢这个角色才行。
		PRINTFORML	同时感谢以往在「制作口上教学频道」认识到并遗留下来的各位朋友一直以来的支持与鼓励。
		PRINTFORML	还有nosoma，一个写袿姬写一年写不出一个系的无名之辈，带我见识到这个游戏、从而得以开始。
		PRINTFORML	此口上距离完成还有相当长的一段距离，请无需期待。
		GOTO BEGIN
	CASE 0
		;关闭调整界面
		PRINTFORML	
	CASEELSE
		GOTO BEGIN
	ENDSELECT
RESETCOLOR

;===========================================================
;冰之妖精的数据调整！
;用于UPDATE和初见。
;ARG	0：还原	1：影响自己	2：影响别人
;===========================================================
@CIRNO_ADJUST(ARG = 0)
IF ARG == 1
	;⑨的原有数据：幼儿体型、容易湿、V敏感、贫乳；傲慢、好奇心、喜欢引人注目、幼稚；采集Lv2、学习慢、无知。
	;这个是加了叛逆。
	TALENT:14:11 = 1
	;这个是加了开朗。
	TALENT:14:24 = 1
	;变成恋人的时候会再将傲慢转化为傲娇（无知）或献身的（非无知）。
	;预定也许会用某些事件把学习慢、幼稚和无知分别去掉。还要有加歌唱能力的事件。
	;在获得苦痛刻印2和3的时候会逐渐把开朗转为阴郁……本来是这么想的，但好像就得写三倍差分了，那我不是暴毙，还是算了。
	;去除了对莉莉白、莉莉黑、莉格露、妹红和蕾蒂的特殊相性。
	RELATION:14:18 = 0
	RELATION:14:19 = 0
	RELATION:14:27 = 0
	RELATION:14:61 = 0
	RELATION:14:74 = 0
	;将对三月精的相性调整为与三月精对⑨相同的120%。
	RELATION:14:5 = 120
	RELATION:14:6 = 120
	RELATION:14:7 = 120
	;增加对拉尔瓦的150%相性，以防单相思。
	RELATION:14:118 = 150
	;增加对文的150%相性。
	RELATION:14:29 = 150
ELSEIF ARG == 2
	;去除了莉莉白、莉莉黑、莉格露和蕾蒂对⑨的特殊相性。
	RELATION:18:14 = 0
	RELATION:19:14 = 0
	RELATION:27:14 = 0
	RELATION:74:14 = 0
	;增加文对⑨的150%相性。
	RELATION:29:14 = 150
	;增加隐崎奈对⑨的120%相性。
	RELATION:124:14 = 120
	;降低大妖精对琪露诺的150%相性。
	RELATION:13:14 = 150
ELSE
	;还原原本没有的坦率和开朗
	TALENT:14:11 = 0
	TALENT:14:24 = 0
	;还原与莉莉白、莉莉黑、莉格露、妹红和蕾蒂之间的特殊相性。
	RELATION:14:18 = 50
	RELATION:14:19 = 50
	RELATION:14:27 = 200
	RELATION:14:61 = 50
	RELATION:14:74 = 50
	RELATION:18:14 = 50
	RELATION:19:14 = 50
	RELATION:27:14 = 150
	RELATION:74:14 = 120
	;还原对三月精的特殊相性。
	RELATION:14:5 = 150
	RELATION:14:6 = 150
	RELATION:14:7 = 150
	;还原对拉尔瓦和文的普通相性。
	RELATION:14:118 = 0
	RELATION:14:29 = 0
	;还原文和隐岐奈对⑨的普通相性。
	RELATION:29:14 = 0
	RELATION:124:14 = 0
	;还原大妖精对琪露诺的300%相性。
	RELATION:13:14 = 300
ENDIF

;===========================================================
;冰之妖精的节日判断！
;用于作为节日相关差分的条件。因为没写完啊。
;ARG	0：问候	1：会话
;===========================================================
@CIRNO_FESTIVAL(ARG=0)
#FUNCTION
SELECTCASE FESTIVAL()
	CASE "元旦"
		RETURNF 1
	CASE "女兒節"
		SIF ARG
		RETURNF 1
	CASE "母親節"
	CASE "白色情人節前日"
	CASE "白色情人節"
	CASE "例大祭"
	CASE "兔子之日"
		SIF ARG
		RETURNF 1
	CASE "七夕"
		SIF !ARG
		RETURNF 1
	CASE "盂蘭盆節"
	CASE "風神「二百十日」"
	CASE "十五夜"
	CASE "収穫祭"
	CASE "萬聖節"
		SIF !ARG
		RETURNF 1
	CASE "節分"
	CASE "情人節"
		SIF !ARG
		RETURNF 1
	CASE "平安夜"
		SIF !ARG
		RETURNF 1
	CASE "聖誕節"
		SIF !ARG
		RETURNF 1
	CASE "大晦日"
		SIF !ARG
		RETURNF 1
ENDSELECT
RETURNF 0

;===========================================================
;冰之妖精的普通攻击！霜之哀伤吧大概。
;粗鲁的笨蛋就是会在因各种情况发火的时候会大打出手。但是，明明还是刮痧而已……
;到底是什么破口上会特地给攻击主角的行为写一个函数啊。
;ARG	0：初见	1：通常
;===========================================================
@CIRNO_ATTACK(ARG=1)
#DIM 伤害
#DIM DYNAMIC 威力 = 30
IF CFLAG:1001 == 1
	威力 = 1
ELSEIF MARK:反発刻印 >= 3
	威力 = 40
ENDIF

;战斗能力的等级差每级提供150的减伤。如果结果≤0，则视为闪避成功。如果结果≤-300，则视为轻松应对。
;以下的估算伤害值均为主角战力0级且⑨战力1级的初始情况。按照TW的能力提升机制，这个战力差应该不会再增加，只会减少。
SETCOLOR 0x51c4d3
IF ARG == 0
	SETCOLOR 0x51dede
	;150~270
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(4), 0))
	IF 伤害 <= 0
		PRINTFORMW	堪堪擦过了抛掷过来的冰块、姑且还是没有什么大碍……
		PRINTFORML	「呀、打歪啦！……唔、不攻过来吗？」
		PRINTFORMDL	似乎是想擅自挑起弹幕胜负的样子、但这种程度对付%CALLNAME:MASTER%只会被瞬间击溃吧。
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的冰锥")
		PRINTFORMDW	被抛掷过来的冰块打了个正着、虽然没有流血但还是很痛啊——
		PRINTFORML	「咦、你不会打架的吗？真没趣——」
		PRINTFORMDL	似乎被以一种非常丢脸的方式放过了……
	ENDIF
ELSEIF TALENT:愛欲
	;90~300
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(10), 3))
	IF 伤害 <= -300
		PRINTFORMW	轻松地躲开了整片寒雾……
	ELSEIF 伤害 <= 0
		PRINTFORMW	勉强躲开了寒雾的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的寒雾")
	ENDIF
ELSEIF CFLAG:好感度 >= 500 || BASE:10 >= 1000
	;270~600
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(20), 9))
	IF 伤害 <= -300
		PRINTFORMW	轻松地躲开了所有碎冰……
	ELSEIF 伤害 <= 0
		PRINTFORMW	勉强躲开了碎冰的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的碎冰")
	ENDIF
ELSE
	;450~900
	伤害 = 威力 * (5*(ABL:戦闘能力 - ABL:MASTER:戦闘能力) + MAX(RAND:(30), 15))
	IF 伤害 <= 0
		PRINTFORMW	勉强躲开了冰锥的攻击……
	ELSE
		CALL RECOVER(MASTER, -伤害, "体力", "琪露诺的冰锥")
	ENDIF
ENDIF
SETCOLOR 0x51dede
	
;===========================================================
;冰之妖精的入梦！
;和冰冰凉凉的小家伙睡在一块，就跟开满空调睡一样。夏天很爽，冬天就遭殃了……
;===========================================================
@CIRNO_SLEEP
SETCOLOR 0x51c4d3
PRINTL
SELECTCASE DAY:2
	CASE 4
		IF TALENT:恋慕
			PRINTFORMW 虽然琪露诺稍微收敛了冷气、但还是有些不太适应……
			CALL RECOVER(MASTER,-900,"体力")
			CALL RECOVER(MASTER,-900,"気力")
		ELSE
			PRINTFORMW 身边冷气萦绕、感觉实在是睡不着觉……
			CFLAG:MASTER:徹夜 = 1
		ENDIF
	CASE 2
		PRINTFORMW 在琪露诺身旁睡着、感觉很是凉爽……
		CALL RECOVER(MASTER,900,"体力")
		CALL RECOVER(MASTER,900,"気力")
	CASEELSE
		IF !TALENT:恋慕 && RAND:2 == 0
			PRINTFORMW 身边冷气萦绕、感觉有点难以入睡……
			CALL RECOVER(MASTER,-900,"体力")
			CALL RECOVER(MASTER,-900,"気力")
		ENDIF
ENDSELECT
SETCOLOR 0x51dede
		
;===========================================================
;冰之妖精的冷气！
;真正的招牌技能，恒定施放的冰冻光环。
;在各种会引发肢体碰撞的指令调用。
;ARG:0表示触发层数。自然是越激烈的接触越多。
;开空调，可以不可以？
;===========================================================
@CIRNO_FREEZE(ARG:0)
#DIM 冷气

IF FIRSTTIME("冷气")
	SETCOLOR 0x51c4d3
	PRINTL 
	PRINTFORMDL	就如同刚见面时的感觉一般、这个冰之妖精会在身边散发出强烈的冷气。
	PRINTFORMDL	尽管她自己大概只是无意识的举动、但对于人类而言就……
	PRINTFORMDL	恐怕会需要一些时间来适应。或许、如果足够亲近、也能让她稍微注意一点……
	SETCOLOR 0x51dede
ENDIF
SIF TCVAR:351 == 0
	TCVAR:351 += CFLAG:1008
	
;陷落决定基础伤害
IF TALENT:恋慕 && CFLAG:1119
	冷气 = 0
ELSEIF TALENT:恋慕
	冷气 = -30
ELSEIF TALENT:思慕
	冷气 = -60
ELSE
	冷气 = -90
ENDIF
;坏心情加伤
SIF TALENT:15 == -1
	冷气 -= 30
;反发加伤
IF MARK:反発刻印 >= 3 || CFLAG:1120
		冷气 -= 90
ELSEIF MARK:反発刻印 >= 2
		冷气 -= 60
ELSEIF MARK:反発刻印 >= 1
		冷气 -= 30
ENDIF
;好感度减伤
SELECTCASE CFLAG:2
	CASE IS >= 256000
		冷气 += 20
	CASE IS >= 128000
		冷气 += 18
	CASE IS >= 64000
		冷气 += 16
	CASE IS >= 32000
		冷气 += 14
	CASE IS >= 16000
		冷气 += 12
	CASE IS >= 8000
		冷气 += 10
	CASE IS >= 4000
		冷气 += 8
	CASE IS >= 2000
		冷气 += 6
	CASE IS >= 1000
		冷气 += 4
	CASE IS >= 500
		冷气 += 2
ENDSELECT
;信赖度减伤
SELECTCASE CFLAG:4
	CASE IS >= 51200
		冷气 += 20
	CASE IS >= 25600
		冷气 += 18
	CASE IS >= 12800
		冷气 += 16
	CASE IS >= 6400
		冷气 += 14
	CASE IS >= 3200
		冷气 += 12
	CASE IS >= 1600
		冷气 += 10
	CASE IS >= 800
		冷气 += 8
	CASE IS >= 400
		冷气 += 6
	CASE IS >= 200
		冷气 += 4
	CASE IS >= 100
		冷气 += 2
ENDSELECT
;亲密减伤
SELECTCASE ABL:親密
	CASE IS >= 60
		冷气 += 30
	CASE IS >= 50
		冷气 += 27
	CASE IS >= 40
		冷气 += 24
	CASE IS >= 30
		冷气 += 21
	CASE IS >= 25
		冷气 += 18
	CASE IS >= 20
		冷气 += 15
	CASE IS >= 15
		冷气 += 12
	CASE IS >= 9
		冷气 += 9
	CASE IS >= 6
		冷气 += 6
	CASE IS >= 3
		冷气 += 3
ENDSELECT
;爱情经验减伤
SELECTCASE EXP:愛情経験
	CASE IS >= 1000
		冷气 += 30
	CASE IS >= 600
		冷气 += 27
	CASE IS >= 400
		冷气 += 24
	CASE IS >= 250
		冷气 += 21
	CASE IS >= 150
		冷气 += 18
	CASE IS >= 100
		冷气 += 15
	CASE IS >= 70
		冷气 += 12
	CASE IS >= 45
		冷气 += 9
	CASE IS >= 25
		冷气 += 6
	CASE IS >= 10
		冷气 += 3
ENDSELECT
;同日耐受度减伤
SELECTCASE TCVAR:350
	CASE IS >= 900
		冷气 += 50
	CASE IS >= 660
		冷气 += 45
	CASE IS >= 490
		冷气 += 40
	CASE IS >= 340
		冷气 += 35
	CASE IS >= 220
		冷气 += 30
	CASE IS >= 120
		冷气 += 25
	CASE IS >= 70
		冷气 += 20
	CASE IS >= 35
		冷气 += 15
	CASE IS >= 15
		冷气 += 10
	CASE IS >= 3
		冷气 += 5
ENDSELECT
;夏天减伤，冬天加伤
IF DAY:2 == 2
	冷气 += 30
ELSEIF DAY:2 == 4
	冷气 -= 30
ENDIF
;当前指令为泡澡时减伤
SIF SELECTCOM == 431
	冷气 += 30
;当前地图为地底时减伤
SIF GET_MAPID(CFLAG:MASTER:現在位置) == 9
	冷气 += 30
;御寒心法（？
IF FIRSTTIME("御寒第一重") && TCVAR:350 >= 10
	CFLAG:1008 += 2
ELSEIF FIRSTTIME("御寒第二重") && TCVAR:350 >= 25
	CFLAG:1008 += 4
ELSEIF FIRSTTIME("御寒第三重") && TCVAR:350 >= 50
	CFLAG:1008 += 6
ELSEIF FIRSTTIME("御寒第四重") && TCVAR:350 >= 100
	CFLAG:1008 += 8
ELSEIF FIRSTTIME("御寒第五重") && TCVAR:350 >= 150
	CFLAG:1008 += 10
ELSEIF FIRSTTIME("御寒第六重") && TCVAR:350 >= 200
	CFLAG:1008 += 12
ELSEIF FIRSTTIME("御寒第七重") && TCVAR:350 >= 300
	CFLAG:1008 += 14
ELSEIF FIRSTTIME("御寒第八重") && TCVAR:350 >= 400
	CFLAG:1008 += 16
ELSEIF FIRSTTIME("御寒第九重") && TCVAR:350 >= 550
	CFLAG:1008 += 18
ENDIF

;结果处理
SETCOLOR 0x51c4d3
IF TALENT:10 == -1
	PRINTFORML	琪露诺似乎因为恐惧被报复而压抑着冷气……
ELSEIF CFLAG:1001 != 1 	;反暴力开关关闭
	REPEAT ARG:0
		IF TALENT:恋慕 && TALENT:恋人 && 冷气 >= 0
			冷气 /= 9
			CALL BUFF_BASE(MASTER,BASE_気力,冷气)
		ELSEIF 冷气 < 0
			SIF TCVAR:発情
				冷气 /= 2
			CALL BUFF_BASE(MASTER,BASE_気力,冷气)
			TCVAR:350 += ARG:0
			TCVAR:351 -= 冷气
		ELSE
			冷气 = 0
		ENDIF
	REND
	PRINTL 
	SELECTCASE TCVAR:351
		CASE IS >= 4000
			PRINTFORML	呼吸好艰难、眼前的东西也变得模糊了……
		CASE IS >= 3000
			PRINTFORML	明明很冷、但身体却感觉开始发热了、而且好痛……
		CASE IS >= 2000
			PRINTFORML	皮肤冷得发麻、关节好像都要冻僵了……
		CASE IS >= 500
			PRINTFORML	和琪露诺接触的时候、感觉冷意在往体内蔓延……
		CASE IS >= 0
			PRINTFORML	冰之妖精的身边似乎时刻充满冷气……
		CASEELSE
			PRINTFORML	从琪露诺的方向有着清爽的凉风吹来……
	ENDSELECT
ENDIF
SETCOLOR 0x51dede

;===========================================================
;冰之妖精的住处记录！
;在会话、来访时遭遇或带回主地图的时候，记录玩家角色与傻子互动时住过的地方。
;ARG	0：记录	1：随机提取
;===========================================================
@CIRNO_RESIDENCE(ARG = 0)
#DIM PLACE
#DIM LPCOUNT
PLACE = MAIN_MAP

SIF CFLAG:1016 == 0
	CFLAG:1016 = 1
IF !ARG
	SETBIT CFLAG:1016, PLACE
ELSE
	RESULT = 0
	WHILE !RESULT
		LPCOUNT ++
		PLACE = RAND:11
		SIF !GETBIT(CFLAG:1016, PLACE)
			CONTINUE
		SIF LPCOUNT >= 9
			BREAK
	WEND	;结果为1或0
	IF !RESULT
		RESULT = MAIN_MAP
	ELSE
		RESULT = PLACE
	ENDIF
ENDIF

;===========================================================
;冰之妖精的茶道！
;决定一下要泡什么茶的样子。
;===========================================================
@CIRNO_TEABAG()
#FUNCTIONS
#DIMS TEA
#DIM WILL
WILL = TFLAG:193 + TALENT:恋人

;基础
IF MAIN_MAP == 3 || MAIN_MAP == 5 || ((CFLAG:15:4 >= 1000 || CFLAG:16:4 >= 1000 || CFLAG:17:4 >= 1000) && RAND:3 == 0)
	TEA '= "红茶/红茶"	;红魔馆或人偶魔女的西式
ELSE
	TEA '= "绿茶/绿茶"
ENDIF
;多人在场的场合到这里就可以了。
SIF GET_TARGETNUM() > 1
	RETURNF CIRNO_RS(TEA)
IF TALENT:恋慕 && CFLAG:1119
	TEA '= "菊花茶/茉莉花茶"
	IF CFLAG:13:4 >= 1000 && RAND:5 == 0
		TEA += "洋甘菊茶"
	ELSEIF DAY:2 == 3 || DAY:2 == 4
		TEA += "菊花茶"
	ELSE
		TEA += "茉莉花茶"
	ENDIF
ENDIF
;指令判定结果，更复杂的款式随教养和结识角色提升。
;咖啡根据判定结果转化为黑咖啡或拿铁，花茶根据季节和大妖精关系转化为茉莉花茶或洋甘菊茶。
SELECTCASE WILL
	CASE 2
		TEA += "/大麦茶/绿豆沙/雪梨汁/抹茶牛奶/西瓜汁"
		SIF ABL:教養 >= 2
			TEA += "/淡盐冰茶/冰巧克力"
		SIF ABL:教養 >= 2 && (CFLAG:58:4 >= 1000 || CFLAG:90:4 >= 1000 || CFLAG:112:4 >= 1000 ||CFLAG:143:4 >= 1000)
			TEA += "/豆浆"
		SIF ABL:教養 >= 2 || CFLAG:5:4 >= 1000 || CFLAG:6:4 >= 1000 || CFLAG:7:4 >= 1000
			TEA += "/咖啡"
		SIF DAY:2 == 2
			TEA += "/西瓜汁"
	CASE 1
		TEA += "/玄米茶/红糖水/酸梅汤"
		SIF ABL:教養 >= 2 || CFLAG:5:4 >= 1000 || CFLAG:6:4 >= 1000 || CFLAG:7:4 >= 1000
			TEA += "/咖啡"
	CASE 0
		TEA += "/薄荷茶/糖水"
		SIF ABL:教養 >= 2 || CFLAG:5:4 >= 1000 || CFLAG:6:4 >= 1000 || CFLAG:7:4 >= 1000
			TEA += "/咖啡"
	CASE -1
		TEA += "/青汁/柠檬水"
ENDSELECT
	
RETURNF CIRNO_RS(TEA)

;===========================================================
;冰之妖精的嫉妒！
;大概是随机会抽取一个恋人。目前仅用于猜猜爷是谁。
;在使用时DIM一个变量ENVYTOY然后赋值为此函数，再%CALLNAME:ENVYTOY%就行了。
;===========================================================
@CIRNO_ENVY()
#FUNCTION
#LOCALSSIZE 10000
VARSET LOCALS

FOR LOCAL, 0, CHARANUM
    SIF CFLAG:LOCAL:2 < 1000 || NO:LOCAL == 0 || NO:LOCAL == 14
		CONTINUE
    IF TALENT:LOCAL:恋人
        SIF LOCALS != ""
            LOCALS += " "
		LOCALS += TOSTR(LOCAL)
    ENDIF
NEXT

SPLIT LOCALS, " ", LOCALS

SIF RESULT
	RETURNF TOINT(LOCALS:(RAND:RESULT))
RETURNF -1
	
;===========================================================
;冰之妖精的绝顶！
;计算当前事件的合计绝顶数。
;用于推倒结束时。
;===========================================================
@CIRNO_CLIMAX()
#FUNCTION
#DIM DYNAMIC 绝顶合计次数 = 0
#DIM 本日历史次数
绝顶合计次数 += EX:14:Ｃ絶頂
绝顶合计次数 += EX:14:Ｖ絶頂
绝顶合计次数 += EX:14:Ａ絶頂
绝顶合计次数 += EX:14:Ｂ絶頂
绝顶合计次数 += EX:14:Ｍ絶頂
IF ONCE("推倒结束")
	本日历史次数 = 绝顶合计次数
ELSE
	绝顶合计次数 -= 本日历史次数
	本日历史次数 += 绝顶合计次数
ENDIF
RETURNF 绝顶合计次数
	
;===========================================================
;冰之妖精的突击检查！
;没收胖次时检测是否有自己的胖次
;在原事件里声明变量：
;#DIM DYNAMIC iPants_Num
;iPants_Num = 0
;之后用以下语句分别赋值和判断。
;	iPants_Num = CIRNO_CHECKPANTS(TARGET)
;	IF iPants_Num
;===========================================================
@CIRNO_CHECKPANTS(iID=14)
#FUNCTION
#DIM iID
#DIM iPants_Type
#DIM iPants_Num

;パンツ輸送
SIF !FLAG:853
	RETURNF 0

iPants_Num = 0
FOR iPants_Type,0,MAXPANTS
	SIF PANTS_TEMP:iID:iPants_Type
		iPants_Num += PANTS_TEMP:iID:iPants_Type
NEXT
RETURNF iPants_Num

;===========================================================
;冰之妖精的在场观众！
;来历大概是典口上，往上追溯至四季之类。就是从同一位置的其他人里选取一个，优先度从关联角色到全随机。
;原本来说是优先度高的情况会直接决定结果，但为了自家想要的效果，改成了优先度仅影响概率。
;默认的时候是这样。如果给ARG另外赋值，则会只选取拥有对应编号素质的角色。恋慕是3，恋人是7。
;输出结果是角色编号，如果没有人的话就是0了。
;===========================================================
@CIRNO_FIND_AROUND(ARG = -1)
#FUNCTION
#DIM 关联角色,14 = 1, 5, 6, 7, 11, 13, 29, 30, 33, 49, 50, 68, 118, 124
#DIM 总人数
#DIM 抽选
#LOCALSIZE 200
;关联角色包括：灵梦、桑尼、露娜、斯塔、魔理沙、大妖精、文、映姬、诹访子、觉、芙兰朵露、幽香、拉尔瓦、隐岐奈。
VARSET LOCAL
抽选 = 0
总人数 = GET_TARGETNUM() 
;如果没人的话就是0。如果是1的场合，就是只有傻子一个了。
SIF 总人数 == 1
	RETURNF 0
;首先将所有在场的角色总结成一个权重池。
FOR COUNT, 1, 总人数 + 1
	;屏蔽抽中口上主的情况。
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;确认目标是否与主角和口上主在同一位置。
	SIF (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:MASTER:現在位置) || (CFLAG:(TARGET:COUNT):現在位置 != CFLAG:14:現在位置)
		CONTINUE
	;确认目标是否符合拥有对应素质的额外要求。
	IF ARG > 0
		SIF !TALENT:(TARGET:COUNT):ARG
			CONTINUE
	ENDIF
	;如果上述条件都是满足的，那么将此角色编号加入权重池。
	LOCAL:抽选 = TARGET:COUNT
	抽选 ++
NEXT
;然后将熟人额外加入一次权重池，也就是概率翻倍了。
FOR COUNT, 0, 14
	IF CFLAG:(关联角色:COUNT):現在位置 == CFLAG:MASTER:現在位置
		LOCAL:抽选 = 关联角色:COUNT
		抽选 ++
	ENDIF
NEXT
;不知道为什么，但总之有这么一个结尾。
SIF 抽选 < 1
	RETURNF 0
;最后在池子里随机抽一个啦。
RETURNF LOCAL:(RAND(抽选))

;===========================================================
;冰之妖精的好朋友卡片！
;判断在场的是否只有特别要好的家伙。
;===========================================================
@CIRNO_GETFRIEND()
#FUNCTION
#DIM 好友,4 = 1, 11, 13, 29
;好友包括：灵梦、魔理沙、大妖精、文。
#DIM 朋友数

FOR COUNT, 0, 4
	;确认目标是否与主角和口上主在同一位置。
	SIF (CFLAG:(好友:COUNT):現在位置 == CFLAG:MASTER:現在位置) || (CFLAG:(好友:COUNT):現在位置 == CFLAG:14:現在位置)
		朋友数 ++
NEXT
SIF 朋友数 == GET_TARGETNUM() - 1
	RETURNF 朋友数
RETURNF 0

;===========================================================
;功能性之量子坍缩。
;条件满足越多概率越高吧大概。
;使用时ARG默认输入1，后面的ARG:1到ARG:9都是条件（包括且不仅限于TALENT，CFLAG，TCVAR，ABL，EXP等）
;===========================================================
@CIRNO_RAND(ARG = 0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9)
#FUNCTION
#DIM LCOUNT

IF ARG == 1
	LOCAL = 11
	FOR LCOUNT, 1, 10
		SIF ARG:LCOUNT > 0
			LOCAL -= 1
	NEXT
ENDIF
SIF RAND:LOCAL == 0
	RETURNF 1
RETURNF 0

;===========================================================
;功能性之对齐。
;就是……对齐。
;ARG:0	总字节长度
;ARG:0	需要对齐的一个数值
;===========================================================
@CIRNO_ALIGN(ARG:0,ARG:1)
STRLENFORM {ARG:1}
REPEAT (ARG:0 - RESULT:0)
	PRINTFORMD	 
REND

;===========================================================
;功能性之储存字符串。
;并附带一个左对齐的部分。会在右边垫半角空格。
;ARG	0：储存	1：取出	more:需要对齐的字节数
;ARGS	需要储存的字符串，在取出时忽略
;===========================================================
@CIRNO_STORAGE(ARG, ARGS)
#DIMS STORE
IF ARG == 0
	STORE = %ARGS%
ELSE
	PRINTFORM %STORE%
	STRLENFORM %STORE%
	IF ARG > RESULT:0
		REPEAT (ARG - RESULT:0)
			PRINTFORMD	 
		REND
	ENDIF
ENDIF

;===========================================================
;功能性之掷骰特效。
;就是轮番显示几个数字并删掉，来模仿骰子摇一下的那种感觉。
;ARG:0	骰子面数
;ARG:1	等待时间
;ARG:2	掷骰次数
;===========================================================
@CIRNO_DICESHOW(ARG:0 = 10, ARG:1 = 2, ARG:2 = 4)
REPEAT ARG:2
	PRINTFORM {RAND:(ARG:0)}
	TWAIT ARG:1,1
	CLEARLINE 1
REND

;===========================================================
;功能性之随机字符串。
;取自爱丽丝口上，将一条字符串分割并取出随机片段。
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG    返す数、2個以降はRESULTS:1以降に返していく
;===========================================================
@CIRNO_RS(ARGS, ARGS:1 = "/", ARG = 1)
#FUNCTIONS
#LOCALSSIZE 1000
#LOCALSIZE 1
#DIM TEMP
#DIM NUM

VARSET LOCALS

SPLIT ARGS, ARGS:1 , LOCALS, NUM

FOR LOCAL, 0, ARG
	IF NUM > 0
		TEMP = RAND:NUM
		NUM--
		RESULTS:LOCAL = %LOCALS:TEMP%
		ARRAYREMOVE LOCALS, TEMP, 1
	ENDIF
NEXT
RETURNF RESULTS

;===========================================================
;功能性之条件差分。
;用于随机条件，但是仅在满足对应条件时产生该结果。
;使用时ARG默认输入1，后面的ARG:1到ARG:9都是条件（包括且不仅限于TALENT，CFLAG，TCVAR，ABL，EXP等）
;若输出为0则是默认情况。
;===========================================================
@CIRNO_SELECT(ARG = 0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9)
#FUNCTION
#DIM DYNAMIC LCOUNT = 0
VARSET LOCAL
IF ARG == 1
	FOR COUNT, 1, 10
		IF ARG:COUNT > 0
			LCOUNT ++
			LOCAL:LCOUNT = COUNT
		ENDIF
	NEXT
	SIF LCOUNT < 1
		RETURNF 0
	RETURNF LOCAL:(RAND(LCOUNT))
ENDIF

;===========================================================
;各类废稿和记事。原本是放在事件系开头的但越来越碍事了所以挪个位置。
;===========================================================
[SKIPSTART]
其实说来活泼好动和冰系角色一般都是不配对的啊。鳟先生真是独到。

突然想起来一个点。是关于记忆力的问题。
⑨的健忘虽然不如某只鸟，但是也是很差的（在则里完全忘记了一路上见过的都是谁）。理所当然地无陷落时见面会不记得叫什么，每次都要重复强调。
但是随着交往的深入，理应会记得越来越清楚，而玩家毕竟要靠料理和学习之类去刷好感，导致知识和才能也在增长。这属于是偏离了妖精所属自然的范畴吧，这样想。
「回忆是人生的食粮，还是枷锁——至少，太过执着的话，看来的确是不会有好事发生。就算这样，为了否定这个将重要的东西一起抛弃，只有这种事情我不想做——」
突然又想到一个点。恋慕线可以是这样，但爱欲线可能正好相反——沉溺在快乐之中，无论失去了什么记忆，只需要再次体验就可以了。没有什么是无比珍贵的。

预定的笔记：能否控制借宿？逆告白后借宿显然很符合气氛。能否检测角色已知的肉体关系？可以考虑给留宿后的当天加特殊对话。
能否检测已知的肉体关系？能否检测拥有的内裤数量？将主角运回的晚安文本可以加上在做的情况。
能否检测本日是否已经问候？想在会话的时候检测以进行一个对时停出现立刻聊天的主角的惊讶。
之后要考虑一下对主动告白各情况的再次细分。之后要给一个主动解除无知的选项，可能在读书或深层会话？
找个时间给所有指令阻绝一下时停……
记得加上从爱欲转到恋慕的特殊差分。
记得加上觉得困在陪睡时的差分。
可以来那种小孩学会催眠试图往主角身上使的怪剧情。
强化一下遭遇里的吓唬主角。
可以强化一下时停外面的身体接触类，以做到时停喂酒之类的效果。
以后记得给破瓜加上先爱欲解无知再V破处的差分。
喝茶的前后应有拓展，而非仅限饮料种类的区别。增加傻子的主动性？可以期待？可以点单？可以喝完之后要做什么？可以跟喝之前做过的什么有关系？

	我是覺得並不是描述細節的問題 你多加幾行琪露諾怎麼感謝你 其實也不會太有效果
	從寫作角度上來講 閒聊、泡茶應該是給你引發互動的 因為這兩件事本身很無趣
	要考慮的是做了指令之後能引發什麼有趣的小劇場、互動
	琪露諾挑起一根小指握起茶杯 輕抿了一口
	「嗯～嗯... 伯爵茶很有風味呢」
	接著茶盤的手在發抖... 明明就苦得不得了，在做大小姐的白日夢嗎？
	“咦！大叔大叔，这个是什么？”
	“乌-笼-茶？是什么啊？”
	“咦咦咦？真的可以让我喝吗？”
	“------呜，好苦，大叔坏，骗人QAQ”
	感覺會為了裝很強 FIRSTTIME之後會強迫自己喝下去
	可以考慮失敗時選擇熱飲或其他會噴出來的
	其實主要都是在互動感上加強 比如上次你大失敗了這次看到你再泡會縮一旁之類的

会话关系进展对话应有一分支，一周目是说之前还没跟人类玩过这么多，转生后是恢复记忆。
「有时候咱会忘记……被做过的事情了。」「……忘掉也……没关系吗？」残虐的结局。

TIMES SOURCE:CHARA:征服,0.50
DOWNBASE:MASTER:体力 += 100
CALL 抱き寄せ(ARG)
GROUPMATCH(CFLAG:MASTER:現在位置,xxx,xxx,xxx)
RELATION:CIRNO_ID:TARGET
ESTRUS_CYCLE(TARGET) == ?			15安全期，70危险期前日，100排卵期，150危险期
repeat x（次数）		下一行		rend		循环
!TCVAR:26:今天有否见面
CLEARLINE 1			清空上一行内容（但仍会出现再消失）
CFLAG:身に覚え_母 == 1 && !CFLAG:無自覚妊娠		为判断自觉怀孕（在判断TALENT:153为非零值之后）
因为越来越摸了，所以写上预定计划！——更多亲吻——抱枕——爱抚系——足交

■地域設定				茶种
-1 設定無し				清水
0　博麗神社				绿茶
1　命蓮寺				青汁
2　人間の里				
3　霧の湖～紅魔館		红茶
4　迷いの竹林			青汁
5　魔法の森				红茶
6　三途の川～冥界		
7　妖怪の山 (山麓)		果汁
8　妖怪の山 (山頂)		果汁
9　地底					
10 夢の世界～月			
[SKIPEND]


