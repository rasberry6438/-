
@K31_サブイベント1_表示条件(ARGS)
;IFの条件がストーリ表示条件
;ARGSは指令の名前（口上の各指令部分で引数として手動で持ってきてるので注意）（←ガバガバコーディング）
;RETURN1で表示フラグON
	IF ARGS == "会話" && CFLAG:31:1012 >= 5 && CFLAG:31:1001 > 1
	;ストーリー2話以降で発生、発生後5日以上経つと再発生
		CFLAG:31:1012 = 0
		;CFLAG:31:1012は口上イベントERBで毎日1増える
		RETURN 1
	ENDIF
RETURN 0


@K31_サブイベント1_出力
;ストーリーはここにゴリゴリ書く
;LOCALに今の所持金を退避
	FONTSTYLE 1
	PRINTFORMDW 　　◆サブイベント◆「酒定期納入」
	PRINTL 
	FONTREGULAR
	
	IF CFLAG:31:1011 == 0
		PRINTFORMW 「啊，%CALLNAME:MASTER%先生」
		PRINTFORMW 「听说%CALLNAME:MASTER%先生的酿酒技术相当不错」
		PRINTFORMW 「如果有多余的酒的话，我会定期收购的」
		PRINTFORMW 「当然，如果您没有多余的话，也请告诉我，没关系的」
		PRINTFORMW 「那么，请多关照了」
		CFLAG:31:1011 = 1
	ELSEIF CFLAG:31:1011 == 1
		PRINTFORML 「啊，%CALLNAME:MASTER%先生」
		PRINTFORMW 「有多余的酒要卖给我吗？」
		PRINTFORML
		PRINTFORMDL [0]-好的
		PRINTFORMDL [1]-下次再说吧
		INPUT	
		PRINTFORML
		IF RESULT == 0
			PRINT 要卖些什么呢...
			LOCAL = MONEY
			PRINTFORML 所持金:%金額表示(MONEY)%
			CALL 酒一覧表示(0, "売却", 70)
			IF RESULT == 0
				PRINTFORML 「还是决定不卖了吗？」
				PRINTFORML 「明白了」
				PRINTFORMW 「下次再问您吧」
				RETURN -1
			ENDIF
			IF ITEM:RESULT == 0
				PRINTW 没有物品可卖
				PRINTFORML 「好像没有东西呢...」
				PRINTFORMW 「没有着急的事，下次再问您吧」
				RETURN -1
			ENDIF
			SIF ALC_NAME(RESULT) != ""
				PRINTFORMW 「哦，是%ALC_NAME(RESULT)%吗」
			SELECTCASE RESULT
				CASE 51,56;米酒・甜酒
					PRINTFORMW 「这个我也能喝呢」
				CASE 52,55;清酒・神酒
					PRINTFORMW 「这个很适合作为供品，谢谢」
				CASE 59;药酒
					PRINTFORMW 「看起来对健康有好处」
				CASE 53,58;鬼杀・蒸馏酒
					PRINTFORMW 「对我来说有点烈，但很多人喜欢呢」
				CASEELSE
					PRINTFORMW 「哦哦...？这可是相当珍贵的东西啊...」
			ENDSELECT
			ITEM:RESULT -= 1
			;CALL GET_MONEY_YEN(1000, 2)
			MONEY += ITEMPRICE:RESULT * 70 / 100
			PRINTL
			PRINTFORML 所持金:%金額表示(LOCAL)%　→　%金額表示(MONEY)%
			PRINTL
			PRINTFORMW 「非常感谢您，以后还请多多关照」
			CALL CHANGE_CFLAG(2, 31, 50)
			CALL CHANGE_CFLAG(4, 31, 30)
			CALL KIGEN_CHANGE(31,100,1)
		ELSEIF RESULT == 1
			PRINTFORML 「明白了」
			PRINTFORMW 「下次再向您请教」
		ENDIF
	ENDIF
	

RETURN 1
