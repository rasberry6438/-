@AUTO_SWEEP_BEFORE_SLEEP_ASK

;气力过低或TPS不足跳过，参考内职需求。
SIF BASE:MASTER:気力 < 300 || BASE:MASTER:TSP < 50
	RETURN

IF AUTO_SWEEP_BEFORE_SLEEP_DIM == 1
	PRINTFORML 是否要进行睡前自动扫除？
	PRINTL
	PRINTFORML [1]是
	PRINTFORML [0]否
	INPUT
	SIF RESULT == 1
		CALL AUTO_SWEEP_BEFORE_SLEEP
ELSEIF AUTO_SWEEP_BEFORE_SLEEP_DIM == 2
	CALL AUTO_SWEEP_BEFORE_SLEEP
ENDIF

@AUTO_SWEEP_BEFORE_SLEEP
#DIM AUTOSWEEP_ROOM_ID
#DIM AUTOSWEEP_ENDROOM_ID
#DIM SWEEP_POWER
#DIM SWEEP_YOGORE

;赋值房间ID
AUTOSWEEP_ROOM_ID = MINROOM()
AUTOSWEEP_ENDROOM_ID = MAXROOM

;清扫力
SWEEP_POWER = (2 + ABL:MASTER:清掃技能 + 3 * TALENT:MASTER:清掃中毒)

WHILE AUTOSWEEP_ROOM_ID <= AUTOSWEEP_ENDROOM_ID && BASE:MASTER:TSP >= 50
	WHILE YOGORE:AUTOSWEEP_ROOM_ID > 100 && BASE:MASTER:TSP >= 50
	;先赋值当前房间垃圾总量，然后进行清扫结算。
	SWEEP_YOGORE = YOGORE:AUTOSWEEP_ROOM_ID
	YOGORE:AUTOSWEEP_ROOM_ID = YOGORE:AUTOSWEEP_ROOM_ID / SWEEP_POWER
	EX:MASTER:今日的掃除回数 = EX:MASTER:今日的掃除回数 + SWEEP_YOGORE - YOGORE:AUTOSWEEP_ROOM_ID
	EXP:MASTER:清掃経験 ++
	BASE:MASTER:TSP -= 50
	WEND
AUTOSWEEP_ROOM_ID ++
WEND

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%在睡覺之前使用剩餘的TSP进行了清扫。