;患者昏迷
@PATIENT_COMA

CALL Madical_interface_top
CALL Medical_Assistant
PRINTL
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%接受%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的治疗后倒地昏迷不起！要进行救援吗？
PRINTL
PRINTL [1]紧急救援！！！
PRINTFORML [2]支付{BASE:M_C_ID:医疗费 * 2}现金作为赔礼，放弃这次治疗。
PRINTL [0]弃之不顾（救不了，等死吧，抬出去！！！扁鹊三连）

INPUT

IF RESULT == 1
	CALL CARDIO_PULMONARY_RESUSCITATION
ELSEIF RESULT == 2
	PRINTL
	PRINTFORML 将昏迷的%NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%抬出去了，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%带着歉意将{BASE:M_C_ID:医疗费 * 2}现金塞进%NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%的钱包里。
	PRINTL
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% 现金 - {BASE:M_C_ID:医疗费 * 2}
	PRINTL
	MONEY -= BASE:M_C_ID:医疗费 * 2
ELSE
	PRINTL
	PRINTFORML 将昏迷的%NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%抬了出去，任其自生自灭。
	PRINTL
	;修改遗弃患者变量，1 = 遗弃，影响后续循环判定。
	ABANDONED_PATIENTS = 1
ENDIF


;心肺复苏
@CARDIO_PULMONARY_RESUSCITATION
WHILE 1
	IF BASE:M_C_ID:患者体力 > 0
		PRINTFORMW  %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%醒过来了！
		PRINTL
		RETURN 0
	ELSEIF BASE:MASTER:体力 < 500
		PRINTFORMW  %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%没有足够体力执行！
		PRINTL
		RETURN 0
	ENDIF
	
	CALL Madical_interface_top
	CALL Medical_Assistant
	PRINTL
	FOR LOCAL, 800, 807
		PRINTFORML [{LOCAL}]%STR:(LOCAL + 18000), 20,LEFT% 成功率：{CPR_SUCCESS_RATE(LOCAL)}％  恢复患者体力：{DRUG_REJUVENATION(LOCAL)} / EP {DRUG_PRICE(LOCAL), 4, RIGHT}    
	NEXT
	PRINTL

	INPUT

	;添加急救耗时
	TIME += 10
	IF CPR_SUCCESS_RATE(RESULT) > (RAND:100 + 1)
		CALL DRUG_CALCULATION(RESULT)
	ELSE
		PRINTL 没能成功！！！
		PRINTL
		PRINTFORML 医生：%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%  ↓体力-100↓  ↓气力-100↓
		BASE:MASTER:体力 -= 100
		BASE:MASTER:気力 -= 100
		PRINTW
	ENDIF
	
WEND

@CPR_SUCCESS_RATE(ARG)
#FUNCTION

SELECTCASE ARG

	CASE 800
		RETURNF 50
	CASE 801
		RETURNF 65
	CASE 802
		RETURNF 60
	CASE 803
		RETURNF 75
	CASE 804
		RETURNF 75
	CASE 805
		RETURNF 55
	CASE 806
		RETURNF 99

	CASEELSE
		RETURNF 0

ENDSELECT