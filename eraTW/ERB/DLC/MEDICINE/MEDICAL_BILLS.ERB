;结算医疗费用
@MEDICAL_BILLS

;医疗满意度
#DIM DYNAMIC SATISFACTION

;仁义值额外奖励
#DIM DYNAMIC BONUS

;按照治疗百分比计算医疗费和满意度
IF BASE:M_C_ID:病症残余 <= 0
	LOCAL = BASE:M_C_ID:医疗费
	SATISFACTION = 100
ELSE
	;求百分比，（历史记录-当前剩余数值）乘100，最后再除以历史记录。ERA只能接整数，只有小数点后的情况直接视为0。
	LOCAL = (BASE:M_C_ID:医疗费 / 100 ) * ((DISEASE_HISTORY - BASE:M_C_ID:病症残余) * 100 / DISEASE_HISTORY)
	SATISFACTION = (DISEASE_HISTORY - BASE:M_C_ID:病症残余) * 100 / DISEASE_HISTORY
ENDIF

;计算仁义值修正奖励，分别对应仁义的正负两种情况。
IF BENEVOLENCE >= 0
	BONUS = LOCAL * BENEVOLENCE_BONUS(BENEVOLENCE) / 100
	ELSE
	BONUS -= LOCAL * BENEVOLENCE_BONUS(BENEVOLENCE) / 100
ENDIF

;生成付款事件，正常付款或别的支付方式。
IF (RAND:10) == 0 && SATISFACTION > 20
	PRINTL
	PRINTFORML 医疗满意度：{SATISFACTION}％
	PRINTL
	CALL MEDICAL_SATISFACTION(SATISFACTION, LOCAL, BONUS)
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%好像忘记带钱包了。
ELSE
	PRINTL
	PRINTFORML 医疗满意度：{SATISFACTION}％
	PRINTL
	CALL MEDICAL_SATISFACTION(SATISFACTION, LOCAL, BONUS)
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%支付了医疗费，共计：{LOCAL} 和 {BONUS} / 仁义修正。
	BASE:M_C_ID:医疗费 -= LOCAL
	MONEY += (LOCAL + BONUS)
	PRINTL
	RETURN
ENDIF
	
PRINTL
PRINTFORML [1] 这次就不用付钱了！要好好保重身体啊，%NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%！（仁义）
PRINTL [2] 能帮忙采集些制药素材吗？（EP）

INPUT

IF RESULT == 1
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%感激地向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%道别。
	PRINTL
	LOCAL:1 = (LOCAL + BONUS) / 1000
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%  仁义+{LOCAL:1}
	BASE:M_C_ID:医疗费 -= LOCAL
	BENEVOLENCE += LOCAL:1
ELSEIF RESULT == 2
	PRINTFORML 为了报答%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的恩情，%NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%前去采集药材。
	PRINTL
	PRINTFORML 获得{LOCAL / 2}EP 和 {BONUS / 2}EP / 仁义修正。
	BASE:M_C_ID:医疗费 -= LOCAL
	EIRIN_POINT += (LOCAL / 2) + (BONUS / 2)
ENDIF


;医疗满意度，依据满意度获得奖励。ARG:0 = 满意度 ARG:1 = 医疗费 ARG:2 = 仁义修正
@MEDICAL_SATISFACTION(ARG:0, ARG:1, ARG:2)

IF ARG:0 >= 80
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%对本次治疗非常满意。
	PRINTL
	LOCAL:0 = (ARG:1 + ARG:2) / 500
	LOCAL:1 = (ARG:1 + ARG:2) / 1000
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)% 好感度 + {LOCAL:0}  信賴度 + {LOCAL:1}
	PRINTL
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% 名望 + 1 仁义 + 1
	PRINTL
	POPULARITY ++
	BENEVOLENCE ++
	CFLAG:M_C_ID:好感度 += LOCAL:0
	CFLAG:M_C_ID:信賴度 += LOCAL:1
	TALENT:M_C_ID:心情 = 1
	BASE:M_C_ID:憤怒 = 0
	PALAM:M_C_ID:好意 += BASE:M_C_ID:医疗费 * 10
ELSEIF ARG:0 <= 20
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)%对本次治疗特别失望。
	PRINTL
	LOCAL:0 = RAND:21 + 20
	LOCAL:1 = RAND:11 + 10
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", M_C_ID)% 好感度 - {LOCAL:0}  信賴度 - {LOCAL:1}
	PRINTL
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% 名望 - 5 仁义 - 5
	PRINTL
	POPULARITY -= 5
	BENEVOLENCE -= 5
	CFLAG:M_C_ID:好感度 -= LOCAL:0
	CFLAG:M_C_ID:信賴度 -= LOCAL:1
ELSE
	;暂时没有，以后可能会进行补充。
	RETURN
ENDIF

;仁义值奖励加成，干坏事有惩罚。
@BENEVOLENCE_BONUS(ARG)
#FUNCTION

SELECTCASE ARG

	CASE 900 TO 1000
		RETURNF 50
	CASE 800 TO 899
		RETURNF 40
	CASE 700 TO 799
		RETURNF 35
	CASE 600 TO 699
		RETURNF 30
	CASE 500 TO 599
		RETURNF 25
	CASE 400 TO 499
		RETURNF 20
	CASE 300 TO 399
		RETURNF 15
	CASE 200 TO 299
		RETURNF 10
	CASE 100 TO 199
		RETURNF 5
	CASE -1 TO -99
		RETURNF 5
	CASE -100 TO -199
		RETURNF 10
	CASE -200 TO -299
		RETURNF 15
	CASE -300 TO -399
		RETURNF 20
	CASE -400 TO -500
		RETURNF 25

	CASEELSE
		RETURNF 0

ENDSELECT