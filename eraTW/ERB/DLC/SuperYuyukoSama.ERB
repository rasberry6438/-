;魔改函数，超级幽幽子大人
@superYuyukoSama
#DIM DISH_RATE = 0
#DIM CHIPS = 0

;依据反発状态使用不同状态
CALL Message_Box("超级幽幽子大人出现了！！！")
IF MARK:66:反発刻印 != 0
	CALL PRINT_IMAGE("SuperYuyukoSama_2")
ELSE
	CALL PRINT_IMAGE("SuperYuyukoSama_1")
ENDIF
CALL Message_Box_mk2("我开动了！", "PINK", 20)
PRINTFORML 超级幽幽子双眼发光扑向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%刚做好的%EX_DISH_NAME%。
PRINTFORML [1] 阻止她！（战斗）
TINPUT 1500, 0, 0
;如果选择阻止就依据你先生/小姐的战斗力选择使用的能力。
IF RESULT == 1
	;阻止会降低超级幽幽子对食物的评分
	DISH_RATE -= 2
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%拿出珍藏已久的那把‘好折凳’...
	PRINTL
	SELECTCASE ABL:MASTER:戦闘能力
		CASE 1
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『折凳横扫』
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%抄起折凳劈头盖脸砸向超级幽幽子大人。
		CASE 2
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『折凳投掷』
			PRINTFORML 将折凳像飞镖一样朝超级幽幽子大人投掷出去。
		CASE 3
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『折凳飞盘』
			PRINTFORML 折凳像飞盘一样旋转，并飞向超级幽幽子大人。
		CASE 4
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『折反』！
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%轮起折凳，转瞬间三道凌厉的攻势分别从左右和上方向超级幽幽子大人袭去。
		CASE 5
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『时停折凳』！！
			PRINTFORML 时间为%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%停下脚步，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不紧不慢的走到超级幽幽子大人身后，挥舞折凳。
		CASE 6
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%使出『折凳弹幕』！！！
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%同时使用『折凳横扫』、『折凳投掷』、『时停折凳』等技巧，当时间再次流逝超级幽幽子大人淹没在折凳的海洋之中。
	ENDSELECT
ELSE
	;不抵抗在倒计时结束后擦除抵抗选项的文本，并跳转到食物评分环节。
	CLEARLINE 3
	PRINTL
	GOTO DISH_RATE
ENDIF

PRINTL

IF EXP:MASTER:戦闘経験 >= EXP:66:戦闘経験 && RAND:8
	CALL PRINT_IMAGE("SuperYuyukoSama_3")
	CALL Message_Box_mk2("呜~呜~呜~~~为什么要欺负我，好饿呀！！！", "PINK", 20)
	PRINTFORML 超级幽幽子被砸的满头包，可怜兮兮的跑掉了。
	RETURN
ENDIF

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的折凳对超级幽幽子毫无效果。

$DISH_RATE
;使用原版幽幽子的口味对食物评分
DISH_RATE = REVIEW_DISH_FOR_MENU(66,DISH_TASTE)

PRINTFORML %EX_DISH_NAME%被超级幽幽子吃掉了。
PRINTFORML 她对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的【%EX_DISH_NAME%】评分为{DISH_RATE}分。
PRINTFORML （负分会受到惩罚的哦！）
PRINTL
;依据评分采取不同行动
IF DISH_RATE >= 3
	;评分大于等于3生成报酬
	CHIPS += (DISH_RATE * (RAND:3 + 1))
	CALL Message_Box_mk2("真不错！", "PINK", 20)
	PRINTFORML 超级幽幽子大人扔下{CHIPS}个筹码，满意离开。
	MONEY:2 += CHIPS
ELSEIF DISH_RATE <= 0
	CALL PRINT_IMAGE("SuperYuyukoSama_2")
	CALL Message_Box_mk2("啊~啊~啊~好难吃！！！", "PINK", 20)
	PRINTFORML 超级幽幽子大人发脾气把房间弄得乱糟糟的，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%疲于应付。
	BASE:MASTER:体力 -= MAXBASE:MASTER:体力 / 4
	BASE:MASTER:気力 -= MAXBASE:MASTER:気力 / 4
	YOGORE:(CFLAG:MASTER:現在位置) += 10000
ELSE
	CALL Message_Box_mk2("呼~马马虎虎吧。", "PINK", 20)
	PRINTFORML 超级幽幽子大人吃完%EX_DISH_NAME%后，什么都没留下直接跑掉了。
ENDIF
PRINTL
PRINTFORML 任何时间，任何地点，超级幽幽子，顶风作案！！！
;结束，重置数据
CALL RESET_DISH
superYuyukoSamaIsHungry = 0

@Paper_recorded_an_urban_legend
#DIM DYNAMIC SuperYutuko_linecount
SuperYutuko_linecount = LINECOUNT
PRINTFORML
PRINTFORML ┌%"─" * 21%┐
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"最近", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"幻想乡出现新怪谈————", 34, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"长相酷似幽幽子的怪盗", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"会忽然出现把刚做好的食物吃掉", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"这位怪盗自称超级幽幽子", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"并且从不和真正幽幽子同时出现", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"有吃完食物后留下筹码的传言", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 6, LEFT%%"但也有吃完后大闹一场的报告", 30, LEFT%%" ", 6, LEFT%｜
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML ｜%" ", 42, LEFT%｜
PRINTFORML └%"─" * 21%┘
PRINTFORML
TWAIT 10000, 0
PRINTBUTTON "[“无稽之谈”]", 0
PRINTL
PRINTBUTTON "[“应该是吧”]", 1
PRINTL
PRINTBUTTON "[“我想见见她”]", 2
PRINTL
PRINTBUTTON "[“……这个背面似乎什么东西？”]", 3
PRINTL
PRINTBUTTON "[不去在意]", 4
INPUT
SELECTCASE RESULT
	CASE 0
		PRINTFORMW 这种东西不可能是真的啦——
		superYuyukoSamaMode = 1
		superYuyukoSamaIsHungry = 100
	CASE 1
		PRINTFORMW 幻想乡里发生这种事也不是不可能对吧……对，对吗？
		superYuyukoSamaMode = 1
	CASE 2
		PRINTFORMW “超级幽幽子”吗，想亲眼见识一下
		superYuyukoSamaMode = 2
		superYuyukoSamaIsHungry = 100
	CASE 3
		CLEARLINE LINECOUNT - SuperYutuko_linecount
		PRINTFORML
		PRINTFORML ┌%"─" * 21%┐
		PRINTFORML ｜%" ", 6, LEFT%%"最近听到", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"有关幽幽子和紫苑", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"会在【任何】情况下", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"都出现在做饭现场的传言", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"但经过维护人查证", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"此情况并不存在", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"但此说法十分有趣", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"于是将其作为彩蛋实装到游戏中", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"附上各选项效果：", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"“无稽之谈”：首次必出现", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"“应该是吧”：出现率每日增加1%", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"“我想见见她”：每日必出现", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 6, LEFT%%"可在此选择彻底关闭本彩蛋", 30, LEFT%%" ", 6, LEFT%｜
		PRINTFORML ｜%" ", 42, LEFT%｜
		PRINTFORML └%"─" * 21%┘
		PRINTFORML
		WAIT
		PRINTBUTTON "[来来来来]", 0
		PRINTL
		PRINTBUTTON "[别再来了]", 1
		PRINTL
		INPUT
		IF RESULT == 1
			PRINTFORMW 超级幽幽子大人不会再出现了
			superYuyukoSamaMode = 0
			superYuyukoSamaIsHungry = 0
		ELSEIF RESULT == 0
			PRINTFORMW 虽然这对于心脏有些不好就是了
			SIF superYuyukoSamaMode == 0
				superYuyukoSamaMode = 1
			CLEARLINE LINECOUNT - SuperYutuko_linecount
			RESTART
		ENDIF
ENDSELECT
CLEARLINE LINECOUNT - SuperYutuko_linecount
RETURN