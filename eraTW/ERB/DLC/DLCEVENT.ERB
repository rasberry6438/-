@DLCEVENT
#DIM MEETMP
#DIM RELATIONTAG
#DIM SHARE
#DIM TMP
#DIM WATERED, 2
#DIM NUM
VARSET WATERED
    IF AUTOWATER
        NUM = 1 + FLAG:畑拡張
        FOR TMP, 0, NUM
            IF PLANT_GROW:TMP == 10
                WATERED:0 = 1
                CONTINUE
            ENDIF
            IF ITEM:簡易泳池 && GETBIT (FLAG:泳池, 2)
                PLANT_DRY:TMP = 0
                WATERED:1 = 1
            ELSE
                WATERED:1 = 2
            ENDIF
        NEXT
        IF WATERED:0 == 1
            PRINTFORMW 田里有些果实成熟了……
        ENDIF
        IF WATERED:1 == 1
            PRINTFORMW 在河童的谜之科技的作用下，自动灌溉系统把农田灌溉好了……
        ELSEIF WATERED:1 == 2
            PRINTFORMW 田里需要浇水了……
        ENDIF
    ENDIF
    IF PANGCI
        SETCOLOR C_PINK
        PRINTFORML 幻想乡的居民们，在无意识中吸收了一些来自胖次的力量……
        FOR LOCAL,0,CHARANUM
            SIF CFLAG:LOCAL:出禁
                CONTINUE
            CALL PANNOQIKALA_ABL(LOCAL)
        NEXT
        RESETCOLOR
    ENDIF
    IF MEETCHECKDLC
        SETCOLOR C_YELLOW
        FOR LOCAL,1,CHARANUM
            RELATIONTAG = 1
            SHARE = 0
            SIF CFLAG:LOCAL:出禁
                CONTINUE
            SIF CFLAG:LOCAL:18 == 0
                CONTINUE
            IF DAY == MEETTIME:0:LOCAL || DAY == MEETTIME:0:LOCAL + 1
                IF (MEETTIME:1:LOCAL != CFLAG:LOCAL:2) || (MEETTIME:2:LOCAL != CFLAG:LOCAL:4)
                    FOR MEETMP, 0, CHARANUM
                        SIF RELATION:LOCAL:MEETMP < 100
                            CONTINUE
                        SIF CFLAG:MEETMP:出禁
                            CONTINUE
                        IF RELATION:LOCAL:MEETMP >= 100 && !(TALENT:LOCAL:恋人 || TALENT:LOCAL:恋慕) && CFLAG:MEETMP:18 != 0
                            CALL ADDFRINEDSLOVE(LOCAL,MEETMP)
                            SHARE = 1
                        ENDIF
                    NEXT
                    SIF SHARE
                        PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%\@ RAND:7 ? %TEXTR("迫不及待地/自然而然地/绘声绘色地/兴高采烈地/轻描淡写地/普普通通地")% # \@和伙伴们分享了与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%相处的一些事情……
                    MEETTIME:1:LOCAL = CFLAG:LOCAL:2
                    MEETTIME:2:LOCAL = CFLAG:LOCAL:4
                ENDIF
            ENDIF
            ;魔改内容，原版限时为10天，修改为15天
            IF ((DAY - MEETTIME:0:LOCAL) > 15) && (CFLAG:LOCAL:2 > 0 || CFLAG:LOCAL:4 > 0)
            ;魔改完毕
                FOR MEETMP, 0, CHARANUM
                    SIF RELATION:LOCAL:MEETMP < 100
                        CONTINUE
                    SIF CFLAG:MEETMP:出禁
                        CONTINUE
                    ;魔改内容，原版限时为10天，修改为15天
                    IF RELATION:LOCAL:MEETMP >= 100 && ((DAY - MEETTIME:0:MEETMP) <= 15) && (TALENT:LOCAL:恋慕 || TALENT:LOCAL:愛欲) && RELATIONTAG
                    ;魔改完毕
                        ;魔改内容，真正进行好感信赖改变时候才出现提示语
                        SIF GROUPMATCH(COUNT_FOR_FAVOR_AND_TRUST, 3, 7)
                        ;魔改完毕
                            PRINTFORMW 虽然太久没有和%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%联系，但是%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%在和朋友的相处中，回忆起了部分逐渐淡忘了的，和%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%相处的记忆……
                        CALL CHANGEMEET(LOCAL, 1)
                        RELATIONTAG = 0
                    ENDIF
                NEXT
                IF RELATIONTAG
                    ;魔改内容，真正进行好感信赖改变时候才出现提示语
                    SIF GROUPMATCH(COUNT_FOR_FAVOR_AND_TRUST, 3, 7)
                    ;魔改完毕
                        PRINTFORMW 太久没有和%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%联系，%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%逐渐淡忘了和%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%相处的部分记忆……
                    CALL CHANGEMEET(LOCAL, 0)
                ENDIF
            ;魔改内容，差三天开始进行好感信赖改变时进行提醒
            ELSEIF ((DAY - MEETTIME:0:LOCAL) == 13) && (CFLAG:LOCAL:2 > 0 || CFLAG:LOCAL:4 > 0)
                PRINTFORMW 有段时间没有和%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%联系了，也许应该去见一面
            ;魔改完毕
            ENDIF
        NEXT
        ;魔改内容，不过这个大概算修复，处理结束后好感信赖重记录
        FOR LOCAL,1,CHARANUM
            MEETTIME:1:LOCAL = CFLAG:LOCAL:2
            MEETTIME:2:LOCAL = CFLAG:LOCAL:4
        NEXT
        RESETCOLOR
    ENDIF
    SIF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 0)
        CALL ACCELERATE_PREGANCY_AND_RAISING_CHILD_FUNCTION
@ADDFRINEDSLOVE(ACTION,ACTARGET)
    #DIM ACTION
    #DIM ACTARGET
    #DIM LOVETMP
    #DIM FRITMP
    FRITMP = (CFLAG:ACTION:2 - MEETTIME:1:ACTION) * RELATION:ACTION:ACTARGET / 1000
    LOVETMP = (CFLAG:ACTION:4 - MEETTIME:2:ACTION) * RELATION:ACTION:ACTARGET / 1000

    CFLAG:ACTARGET:2 += FRITMP
    MEETTIME:1:ACTARGET += FRITMP

    CFLAG:ACTARGET:4 += LOVETMP
    MEETTIME:2:ACTARGET += LOVETMP


@CHANGEMEET(MEETTAR, STAUS)
    #DIM MEETTAR
    #DIM STAUS
    IF TALENT:MEETTAR:恋人 || TALENT:MEETTAR:風騷
        CALL TRUSTCHANGE(3, MEETTAR, STAUS)
    ELSEIF TALENT:MEETTAR:恋慕 || TALENT:MEETTAR:愛欲
        CALL TRUSTCHANGE(4, MEETTAR, STAUS)
    ELSEIF TALENT:MEETTAR:思慕 || TALENT:MEETTAR:炮友
        CALL TRUSTCHANGE(5, MEETTAR, STAUS)
    ELSE
        CALL TRUSTCHANGE(6, MEETTAR, STAUS)
    ENDIF
;魔改内容，对生动记忆的好感信赖削减力度修改以及处理整合
@TRUSTCHANGE(PERCENT, MEETTAR, STAUS)
    #DIM PERCENT
    #DIM MEETTAR
    #DIM STAUS
    ;IF !STAUS
        ;IF CFLAG:MEETTAR:2 * PERCENT >= 100
        ;    CFLAG:MEETTAR:2 = CFLAG:MEETTAR:2 - ( (CFLAG:MEETTAR:2 * PERCENT) / 100)
        ;ELSEIF CFLAG:MEETTAR:2 >= 1
        ;    CFLAG:MEETTAR:2--
        ;ENDIF
        ;IF CFLAG:MEETTAR:4 * PERCENT >= 100
        ;    CFLAG:MEETTAR:4 = CFLAG:MEETTAR:4 - ( (CFLAG:MEETTAR:4 * PERCENT) / 100)
        ;ELSEIF CFLAG:MEETTAR:4 >= 1
        ;    CFLAG:MEETTAR:4--
        ;ENDIF
    ;ELSE
        ;PERCENT -= 2
        ;IF CFLAG:MEETTAR:2 * PERCENT >= 100
        ;    CFLAG:MEETTAR:2 = CFLAG:MEETTAR:2 - ( (CFLAG:MEETTAR:2 * PERCENT) / 100)
        ;ELSEIF CFLAG:MEETTAR:2 >= 1
        ;    CFLAG:MEETTAR:2--
        ;ENDIF
        ;IF CFLAG:MEETTAR:4 * PERCENT >= 100
        ;    CFLAG:MEETTAR:4 = CFLAG:MEETTAR:4 - ( (CFLAG:MEETTAR:4 * PERCENT) / 100)
        ;ELSEIF CFLAG:MEETTAR:4 >= 1
        ;    CFLAG:MEETTAR:4--
        ;ENDIF
    ;ENDIF
    IF INRANGE(COUNT_FOR_FAVOR_AND_TRUST, 0, 7)
        IF COUNT_FOR_STAUS >= 0
            IF !GROUPMATCH(COUNT_FOR_FAVOR_AND_TRUST, 3, 7) && STAUS
                COUNT_FOR_STAUS ++
            ELSEIF GROUPMATCH(COUNT_FOR_FAVOR_AND_TRUST, 3, 7) && (STAUS || COUNT_FOR_STAUS)
                PERCENT -= 2
                COUNT_FOR_STAUS = 0
            ENDIF
        ELSE
            PRINTFORMW 计数器COUNT_FOR_STAUS记录错误，现已重置计数器
            COUNT_FOR_STAUS = 0
        ENDIF
    ELSE
        PRINTFORMW 计数器COUNT_FOR_FAVOR_AND_TRUST记录错误，现已重置计数器
        COUNT_FOR_FAVOR_AND_TRUST = 0
    ENDIF
    CALL TRUSTCHANGE_OVERALL(PERCENT, MEETTAR)

@TRUSTCHANGE_OVERALL(PERCENT, MEETTAR)
    #DIM PERCENT
    #DIM MEETTAR
    SELECTCASE COUNT_FOR_FAVOR_AND_TRUST
        CASE 3
            CALL TRUSTCHANGE_FAVOR(PERCENT, MEETTAR)
            COUNT_FOR_FAVOR_AND_TRUST ++
        CASE 7
            CALL TRUSTCHANGE_FAVOR(PERCENT, MEETTAR)
            CALL TRUSTCHANGE_TRUST(PERCENT, MEETTAR)
            COUNT_FOR_FAVOR_AND_TRUST = 0
        CASEELSE
            IF INRANGE(COUNT_FOR_FAVOR_AND_TRUST, 0, 7)
                COUNT_FOR_FAVOR_AND_TRUST ++
            ELSE
                PRINTFORMW 计数器COUNT_FOR_FAVOR_AND_TRUST记录错误，现已重置计数器
                COUNT_FOR_FAVOR_AND_TRUST = 0
            ENDIF
    ENDSELECT

@TRUSTCHANGE_FAVOR(PERCENT, MEETTAR)
    #DIM PERCENT
    #DIM MEETTAR
    #DIM DYNAMIC TEMPORARY_RECORD
    TEMPORARY_RECORD = CFLAG:MEETTAR:2
    IF CFLAG:MEETTAR:2 * PERCENT / 500 > 1000
        CFLAG:MEETTAR:2 -= 1000
    ELSEIF CFLAG:MEETTAR:2 * PERCENT / 500 >= 1
        CFLAG:MEETTAR:2 = CFLAG:MEETTAR:2 - ( (CFLAG:MEETTAR:2 * PERCENT) / 500)
    ELSEIF CFLAG:MEETTAR:2 >= 1
        CFLAG:MEETTAR:2--
    ENDIF
    SIF TALENT:MEETTAR:恋慕 && TEMPORARY_RECORD >= REQUIRED_FAVOR_恋慕 && CFLAG:MEETTAR:2 < REQUIRED_FAVOR_恋慕
        CFLAG:MEETTAR:2 = REQUIRED_FAVOR_恋慕
    SIF TALENT:MEETTAR:思慕 && TEMPORARY_RECORD >= REQUIRED_FAVOR_思慕 && CFLAG:MEETTAR:2 < REQUIRED_FAVOR_思慕
        CFLAG:MEETTAR:2 = REQUIRED_FAVOR_思慕

@TRUSTCHANGE_TRUST(PERCENT, MEETTAR)
    #DIM PERCENT
    #DIM MEETTAR
    #DIM DYNAMIC TEMPORARY_RECORD
    TEMPORARY_RECORD = CFLAG:MEETTAR:4
    IF CFLAG:MEETTAR:4 * PERCENT / 500 >= 1000
        CFLAG:MEETTAR:4 -= 1000
    ELSEIF CFLAG:MEETTAR:4 * PERCENT / 500 >= 1
        CFLAG:MEETTAR:4 = CFLAG:MEETTAR:4 - ( (CFLAG:MEETTAR:4 * PERCENT) / 500)
    ELSEIF CFLAG:MEETTAR:4 >= 1
        CFLAG:MEETTAR:4--
    ENDIF
    SIF TALENT:MEETTAR:恋慕 && TEMPORARY_RECORD >= REQUIRED_TRUST_恋慕 && CFLAG:MEETTAR:4 < REQUIRED_TRUST_恋慕
        CFLAG:MEETTAR:4 = REQUIRED_TRUST_恋慕
    SIF TALENT:MEETTAR:思慕 && TEMPORARY_RECORD >= REQUIRED_TRUST_思慕 && CFLAG:MEETTAR:4 < REQUIRED_TRUST_思慕
        CFLAG:MEETTAR:4 = REQUIRED_TRUST_思慕
;魔改完毕
;加速怀孕和育儿补丁
@ACCELERATE_PREGANCY_AND_RAISING_CHILD_FUNCTION
#DIM DYNAMIC TEMPORARY_SAVE_0
#DIM DYNAMIC TEMPORARY_SAVE_1
#DIM DYNAMIC TEMPORARY_SAVE_BIT1
#DIM DYNAMIC TEMPORARY_SAVE_BIT2
#DIM DYNAMIC TEMPORARY_SAVE_BIT3
#DIM DYNAMIC TEMPORARY_SAVE_BIT4

FOR LOCAL, 0, CHARANUM
    TEMPORARY_SAVE_0 = 999
    TEMPORARY_SAVE_1 = 999
    TEMPORARY_SAVE_BIT1 = 999
    TEMPORARY_SAVE_BIT2 = 999
    TEMPORARY_SAVE_BIT3 = 999
    IF CFLAG:LOCAL:妊娠経過日数 > 0 && RATE_OF_PREGNANCY_ACCELERATION > 0
        TEMPORARY_SAVE_0 = CFLAG:LOCAL:妊娠経過日数 + RATE_OF_PREGNANCY_ACCELERATION
        IF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 1)
            IF INRANGE(CFLAG:LOCAL:妊娠経過日数 % 10, 0, 8)
                TEMPORARY_SAVE_BIT1 = CFLAG:LOCAL:妊娠経過日数 + 9 - (CFLAG:LOCAL:妊娠経過日数 % 10)
            ELSE
                TEMPORARY_SAVE_BIT1 = CFLAG:LOCAL:妊娠経過日数
            ENDIF
        ENDIF
        SIF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 2)
            TEMPORARY_SAVE_BIT2 = CFLAG:LOCAL:妊娠経過日数 + 10 - (CFLAG:LOCAL:妊娠経過日数 % 10)
        IF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 3)
            IF INRANGE(CFLAG:LOCAL:妊娠経過日数 % 10, 5, 9)
                TEMPORARY_SAVE_BIT3 = CFLAG:LOCAL:妊娠経過日数 + 14 - (CFLAG:LOCAL:妊娠経過日数 % 10)
            ELSEIF INRANGE(CFLAG:LOCAL:妊娠経過日数 % 10, 0, 4)
                TEMPORARY_SAVE_BIT3 = CFLAG:LOCAL:妊娠経過日数 + 4 - (CFLAG:LOCAL:妊娠経過日数 % 10)
            ENDIF
        ENDIF
        TEMPORARY_SAVE_1 = MIN(TEMPORARY_SAVE_0, TEMPORARY_SAVE_BIT1, TEMPORARY_SAVE_BIT2, TEMPORARY_SAVE_BIT3)
        SIF TEMPORARY_SAVE_1 < 999
            CFLAG:LOCAL:妊娠経過日数 = TEMPORARY_SAVE_1
    ELSEIF CFLAG:LOCAL:妊娠経過日数 < 0 && !GETBIT(DEBUG_INGNOREANCE_CHARACTER_VALUE_SAVE:LOCAL:0, 0)
        DRAWLINE
        SETCOLOR C_RED
        PRINTFORML 检测到%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{妊娠経過日数\}变量为{CFLAG:LOCAL:妊娠経過日数}，可能是系统bug或者某些口上文件的特殊处理
        PRINTFORMW 您希望将数据归零吗？
        RESETCOLOR
        CALL ASK_YN
        SELECTCASE RESULT
            CASE 0
                CFLAG:LOCAL:妊娠経過日数 = 0
                PRINTFORMW %NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{妊娠経過日数\}变量已归零
            CASE 1
                PRINTFORMW 您希望以后不再提醒对此角色的\{妊娠経過日数\}的报告吗（可以通过重启本补丁取消不再提醒）
                CALL ASK_YN
                SELECTCASE RESULT
                    CASE 0
                        SETBIT DEBUG_INGNOREANCE_CHARACTER_VALUE_SAVE:LOCAL:0, 0
                        PRINTFORMW 不再检测%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{妊娠経過日数\}变量（可以通过重启本补丁取消本设定）
                    CASE 1
                        PRINTFORMW 持续检测%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{妊娠経過日数\}变量
                ENDSELECT
        ENDSELECT
        DRAWLINE
    ENDIF
    TEMPORARY_SAVE_0 = 999
    TEMPORARY_SAVE_1 = 999
    TEMPORARY_SAVE_BIT4 = 999
    IF CFLAG:LOCAL:出産経過日 > 0 && RATE_OF_CHILD_RAISING_ACCELERATION > 0
        TEMPORARY_SAVE_0 = CFLAG:LOCAL:出産経過日 + RATE_OF_CHILD_RAISING_ACCELERATION
        IF GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 4)
            IF CFLAG:LOCAL:出産経過日 < CHILD_離乳 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_離乳 - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_玩具 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_玩具 - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_つかまり立ち - 1
                TEMPORARY_SAVE_BIT4 = CHILD_つかまり立ち - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_よちよち - 1
                TEMPORARY_SAVE_BIT4 = CHILD_よちよち - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_喋る - 1
                TEMPORARY_SAVE_BIT4 = CHILD_喋る - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_喋る - 2
                TEMPORARY_SAVE_BIT4 = CHILD_喋る - 2
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_喋る - 3
                TEMPORARY_SAVE_BIT4 = CHILD_喋る - 3
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_喋る - 4
                TEMPORARY_SAVE_BIT4 = CHILD_喋る - 4
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_語彙 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_語彙 - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_しつけ - 1
                TEMPORARY_SAVE_BIT4 = CHILD_しつけ - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_就学 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_就学 - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_自立前 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_自立前 - 1
            ELSEIF CFLAG:LOCAL:出産経過日 < CHILD_自立 - 1
                TEMPORARY_SAVE_BIT4 = CHILD_自立 - 1
            ENDIF
        ENDIF
        TEMPORARY_SAVE_1 = MIN(TEMPORARY_SAVE_0, TEMPORARY_SAVE_BIT4)
        SIF TEMPORARY_SAVE_1 < 999
            CFLAG:LOCAL:出産経過日 = TEMPORARY_SAVE_1
    ELSEIF CFLAG:LOCAL:出産経過日 < 0 && !GETBIT(DEBUG_INGNOREANCE_CHARACTER_VALUE_SAVE:LOCAL:0, 1)
        DRAWLINE
        SETCOLOR C_RED
        PRINTFORML 检测到%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{出産経過日\}变量为{CFLAG:LOCAL:出産経過日}，可能是系统bug或者某些口上文件的特殊处理
        PRINTFORMW 您希望将数据归零吗？
        RESETCOLOR
        CALL ASK_YN
        SELECTCASE RESULT
            CASE 0
                CFLAG:LOCAL:出産経過日 = 0
                PRINTFORMW %NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{出産経過日\}变量已归零
            CASE 1
                PRINTFORMW 您希望以后不再提醒对此角色的\{出産経過日\}的报告吗（可以通过重启本补丁取消不再提醒）
                CALL ASK_YN
                SELECTCASE RESULT
                    CASE 0
                        SETBIT DEBUG_INGNOREANCE_CHARACTER_VALUE_SAVE:LOCAL:0, 1
                        PRINTFORMW 不再检测%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{出産経過日\}变量（可以通过重启本补丁取消本设定）
                    CASE 1
                        PRINTFORMW 持续检测%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%的\{出産経過日\}变量
                ENDSELECT
        ENDSELECT
        DRAWLINE
    ENDIF
NEXT

@ACCELERATE_PREGANCY_AND_RAISING_CHILD_FUNCTION_SETTING
WHILE 1
    LOCAL = LINECOUNT
    DRAWLINE
    FONTSTYLE 1
    PRINTL 加速怀孕和育儿补丁设定界面
    FONTREGULAR
    SETCOLOR C_YELLOW
    PRINTFORML 温馨提示：原版怀孕进程在第20、30、40、60、71天有特殊触发事件（发现怀孕、产休、母乳体质等）
    PRINTFORML 强烈建议将怀孕加速保留个位数为0和1的天数功能开启，否则可能会发生意料之外的运行问题
    RESETCOLOR
    DRAWLINE
    HTML_PRINT @"<button value='1'>[1]怀孕加速速率设定-当前加速速率：{RATE_OF_PREGNANCY_ACCELERATION + 1}天／1天</button>"
    HTML_PRINT @"<button value='11'>　\@GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 1)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@不跳过个位数为0的天数</button>"
    HTML_PRINT @"<button value='12'>　\@GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 2)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@不跳过个位数为1的天数</button>"
    HTML_PRINT @"<button value='13'>　\@GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 3)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@不跳过个位数为5的天数</button>"
    HTML_PRINT @"<button value='2'>[2]育儿加速速率设定-当前加速速率：{RATE_OF_CHILD_RAISING_ACCELERATION + 1}天／1天</button>"
    HTML_PRINT @"<button value='21'>　\@GETBIT(ACCELERATE_PREGANCY_AND_RAISING_CHILD, 4)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@不跳过具体时间节点</button>"
    HTML_PRINT @"<button value='0'>[0]退出</button>"
    INPUT
    SELECTCASE RESULT
        CASE 0
            CLEARLINE LINECOUNT - LOCAL
            BREAK
        CASE 1
            PRINTFORML 　请输入怀孕加速速率（单位：天／1天,范围1-999）
            INPUT
            IF RESULT < 1 || RESULT > 999
                PRINTFORMW 　　数据无效，请填入范围在1-999的整数
            ELSE
                RATE_OF_PREGNANCY_ACCELERATION = RESULT - 1
                PRINTFORMW 　　设定完成！当前怀孕加速速率：{RATE_OF_PREGNANCY_ACCELERATION + 1}天／1天
            ENDIF
        CASE 11
            INVERTBIT ACCELERATE_PREGANCY_AND_RAISING_CHILD, 1
        CASE 12
            INVERTBIT ACCELERATE_PREGANCY_AND_RAISING_CHILD, 2
        CASE 13
            INVERTBIT ACCELERATE_PREGANCY_AND_RAISING_CHILD, 3
        CASE 2
            PRINTFORML 　请输入育儿加速速率（单位：天／1天,范围1-999）
            INPUT
            IF RESULT < 1 || RESULT >= 999
                PRINTFORMW 　　数据无效，请填入范围在1-999的整数
            ELSE
                RATE_OF_CHILD_RAISING_ACCELERATION = RESULT - 1
                PRINTFORMW 　　设定完成！当前育儿加速速率：{RATE_OF_CHILD_RAISING_ACCELERATION + 1}天／1天
            ENDIF
        CASE 21
            INVERTBIT ACCELERATE_PREGANCY_AND_RAISING_CHILD, 4
    ENDSELECT
    DRAWLINE
    CLEARLINE LINECOUNT - LOCAL
WEND
;补丁结束