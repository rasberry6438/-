@attackSnatchFoodChara()
	;when the plugin isn't open
	SIF !ASFCConfig
		RETURN 0
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%似乎想要抢夺食物\n要阻止吗?
	;yes is false, no is ture……tw is very interesting
	CALL ASK_YN()
	;yes
	IF !RESULT
		JUMP attackSnatchFoodCharaFight()
	;no
	ELSE
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把%EX_DISH_NAME%递给了两眼放光的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
		RETURN 0
	ENDIF

;fight
@attackSnatchFoodCharaFight()
	#DIM gradeOfMaster	;fight grade of master
	#DIM gradeOfTarget	;fight grade of target
	gradeOfTarget = ndRandom(ABL:戦闘能力)
	IF ASFCConfig == 1
		gradeOfMaster = ndRandom(ABL:戦闘能力)
	ELSE
		gradeOfMaster = 7
	ENDIF
	;scuess
	IF gradeOfMaster > gradeOfTarget
		PRINTFORMW 经过一番纠缠下、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%吧%EX_DISH_NAME%藏了起来
		RETURN 1
	;fail
	ELSE
		PRINTFORMW 最终%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%还是抢走了%EX_DISH_NAME%
		RETURN 0
	ENDIF

;normal distribution random
;once I use normally rand
;I be scold by player because 100 grade ,the result is 10……
;so I think I should use normal distribution random
@ndRandom(num)
	#FUNCTION
	#DIM num
	#DIM index
	#DIM DYNAMIC out
	FOR index, 0, num
		out += RAND:2
	NEXT
	RETURNF out