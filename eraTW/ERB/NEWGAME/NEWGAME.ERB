;-----------------------------------------------------------
;初次遊玩
;-----------------------------------------------------------
@NEWGAME
ゲーム開始処理中 = 1
;本体バージョンを記録
SAVESTR:本体バージョン = %eraTW_Version%
;クラス関数初期化
CALL MAKE_ALL_CLASS_LIST
;默认オプション
CALL DEFAULT_OPTION
;筹码初期化
CALL SET_CHARISMA("NEW")
PBAND = 12

;日期１日目
DAY = 1
DAY:2 = 1
DAY:3 = 1
;FLAG:酒虫 = 1
VAR_FERMENT = 普通的水
;(天候パッチ)冷害発生回数・風害発生回数・水害発生回数・連続降水Counterをリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
CONTINUOUS_RAINFALL = 0
;(天候パッチ)降雪・雪崩判定を解除
CONTINUOUS_SNOWFALL = 0
SNOW_COVERAGE = 0
POSSIBLITY_AVALANCHE = 0
;(天候パッチ)時間帯をリセット
WEATHER_TIMEZOME = 0
;(天候パッチ)長期天候傾向をリセット
YEAR_WEATHER_CONTROL = 0
;(天候パッチ)短期予報を行う
CALL WEATHER_FORECAST_1TERM
;(天候パッチ)1日予報を行う
CALL WEATHER_FORECAST_1DAY

;角色生成
CALL ADD_ALL_CHARACTERS
;MASTER生成
CALL NEWGAME_MAKE
;recolored map or default
CALL DEFAULT_MAP_COLOR
;住居設定
CALL SET_MAINHOME
;オプション
CALL NEWGAME_OPTION

;角色設定
FOR LOCAL,0,CHARANUM
	;開始位置
	CFLAG:LOCAL:現在位置 = CFLAG:LOCAL:初期位置
	;服装
	CALL SELECT_CLOTHES(LOCAL, "今日の衣装")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_便装")
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	CALL 内衣確認リセット(LOCAL)
	CVARSET BASE, GETNUM(BASE, "酒気"), 0
NEXT
;MASTER起床時間セット（7時）
TIME:3 = 420
;MASTER睡眠中フラグON
CALL SET_SLEEP(1,MASTER,0)
ゲーム開始処理中 = 0
RETURN 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;角色を追加したときにいちいちここをいじる必要の無いように
;人物数量上限はERHファイルで定義
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS
FOR LOCAL,CHARANUM,人物数量上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	CALL SET_MINIMUM_EXP_ALL(LOCAL)
	CALL KOJO_MULTIPLE(LOCAL, "NO_SELECT")
	IF !EXP:LOCAL:接吻経験
		TALENT:LOCAL:無接吻経験 = 1
	ENDIF
NEXT
CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING("SELECTION")

;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_OPTION
#DIMS choices, 100
;初期化
;甲斐性無、農民
SETBIT FLAG:1000, 1
SETBIT FLAG:1000, 2
;TINKO
SIF !TALENT:MASTER:形状
	TALENT:MASTER:形状 = 3
;服
SIF !CFLAG:MASTER:基本服装セット
	CALL MASTER_CLOTHES_INIT
;
SIF !ゲーム開始処理中
	新游戏特典 '= "無"
;選択肢
VARSET choices
PRINTL
PRINTL
PRINTL 在游戏開始前你可以変更游戏各種的設定。
DRAWLINE
;你設定
SIF !FLAG:扮演
	choices:0 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%进行自定义角色"
SIF HAS_PENIS(MASTER)
	choices:1 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的男性器为：%TINKO_SIMPLE(MASTER)%"
choices:2 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的服装为：%MASTER_CLOTHES_NAME(CFLAG:MASTER:基本服装セット)%"
IF FLAG:甲斐性無
	choices:3 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%没有告白的胆量"
ELSE
	choices:3 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%有告白的胆量"
ENDIF
IF FLAG:地主
	choices:4 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%是为%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:地主))%劳动的農民"
ELSE
	choices:4 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%是无债一身轻的自由人"
ENDIF
choices:5 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%居住在%GET_MAPNAME(MAIN_MAP)%的%STR:(默认初期位置 + (MAIN_MAP ? 8000 # 0) )%"
;幻想郷
choices:10 '= @"可以进行各角色的処女・非処女変更（＋futa化与否＆形状）"
choices:11 '= @"可以进行各角色的体型変更"
;特典
IF ゲーム開始処理中
	choices:20 '= NEWGAMEBONUS_TEXT()
ELSEIF ゲーム继承処理中
	choices:20 '= 周回特典_表示()
ENDIF

CALL ASK_CHOICES(choices, 100, "完成自定义阶段并開始游戏")
SELECTCASE RESULT
	CASE 0
		IF !FLAG:扮演
			CALL CUSTOM_TERMINAL(MASTER)
			CALL SET_MAINHOME
		ENDIF
	CASE 100
		CALL VIRGIN_SET_APPLY
		CALL SET_BODY_APPLY
		CALL NEWGAME_BONUS_APPLY
		CALL SET_APPLY_NOUMIN
		;CALL 周回特典_適用
		ITEM:平价食材 = 10
		RETURN
	CASE 1
		SIF HAS_PENIS(MASTER)
			CALL TINKO_DECIDE(MASTER)
	CASE 2
		CALL MASTER_CLOTHES
	CASE 3
		CALL KAISYONASI
	CASE 4
		CALL RESET_NOUMIN
		CALL SET_NOUMIN
	CASE 5
		CALL SET_MAINHOME
	CASE 10
		CALL VIRGIN_SET
	CASE 11
		CALL BODY_SET
	CASE 20
		IF ゲーム開始処理中
			CALL NEWGAME_BONUS_SELECT
		ELSEIF ゲーム继承処理中
			CALL 周回特典
		ENDIF
ENDSELECT
RESTART

;-----------------------------------------------------------
;設定変更
;-----------------------------------------------------------
@NEWGAME_VERUP
;恋人修正済フラグを立てる
SETBIT FLAG:1000, 3
SETBIT FLAG:1000, 4
modding_version = 20250501
CALL VERSION_UP
PRINTW 游戏開始処理完成
CALL saveManageSaveGame(1000,, "初次遊玩")
PRINTL 已自動保存
PRINTW 要进行游戏的UPDATE処理吗？
CALL ASK_YN()
IF !RESULT
	CALL UPDATE
ELSE
	PRINTL 已跳过UPDATE
	PRINTW 请在起床前的SHOP画面进行UPDATE
ENDIF