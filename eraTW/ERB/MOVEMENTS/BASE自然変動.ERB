@BASE自然変動(ARG)
#DIM 体力回復量
#DIM 気力回復量
#DIM 欲求不満
#DIM 傘ダメージ
#DIM Lapse
#DIM 地底
#DIMS 低気圧位置
低気圧位置 = %GET_LOCATION_LOW()%
Lapse = TIME_PROGRESS(TIME:1)

;MASTERと同じ場所にいないとき
IF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	BASE:ARG:情緒 = BASE:ARG:情緒 - Lapse * 4 * (BASE:ARG:情緒 + 500) / 500
	BASE:ARG:情緒 = MAX(0,BASE:ARG:情緒)
	BASE:ARG:理性 = BASE:ARG:理性 + Lapse * (MAXBASE:ARG:理性 + 500) / (BASE:ARG:理性 + 500)
ELSE
	;仙香玉兎による補正　情緒↑理性↓憤怒↓
	IF IsInsenceValid(ARG)
		BASE:ARG:情緒 += Lapse * (SQRT(TFLAG:仙香玉兎) / 2)
		BASE:ARG:情緒 = MIN(BASE:ARG:情緒 , MAXBASE:ARG:情緒)
		BASE:ARG:理性 -= Lapse * (SQRT(TFLAG:仙香玉兎) / 2)
		BASE:ARG:憤怒 -= Lapse * SQRT(TFLAG:仙香玉兎) / (CFLAG:ARG:ブチギレ + 1)
	ENDIF
	;劇毒身軀の媚毒付与による補正　理性↓ 発情付与・無差別だがそれなり注ぐ必要あり
	;計算式が複雑すぎる
	IF TCVAR:ARG:ポイズンゲージ
		BASE:ARG:理性 -= Lapse * (MAX(SQRT(TCVAR:ARG:ポイズンゲージ) / 2, 1))
		IF BASE:ARG:理性 <= 0 && PALAM:ARG:欲情 >= PALAMLV:7 && TCVAR:ARG:発情 == 0 && !TALENT:ARG:妊娠 && !TALENT:ARG:初潮 && TALENT:ARG:幼稚
			;1000ほどゲージを稼ぐと3割程度で発情付与　欲求不満度が上がるほど確率向上
			IF RAND:(TCVAR:ARG:ポイズンゲージ + SQRT(TCVAR:ARG:ポイズンゲージ * CFLAG:ARG:积攒度 / 2)) > SQRT(TCVAR:ARG:ポイズンゲージ) * 20
				TCVAR:ARG:発情 = 1
				IF Lapse && !CFLAG:ARG:睡眠
					SETCOLOR C_RED
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的理性隨著\@ TCVAR:ARG:ポイズンゲージ > 1000? 完全# \@ 滲透全身的媚毒，被藥性完全溶解……
					PRINTFORML 被媚毒完全浸润的子宫先于幼小的身体做好了准备…%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%迎来了初潮……
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%安抚着看到下体一片鲜红而有些害怕的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%…%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的身體變得可以懷孕了…
					RESETCOLOR
					TALENT:ARG:初潮 = 1
				ENDIF
			ENDIF
		ELSEIF BASE:ARG:理性 <= 0 && PALAM:ARG:欲情 >= PALAMLV:7 && TCVAR:ARG:発情 == 0 && !TALENT:ARG:妊娠
			;1000ほどゲージを稼ぐと3割程度で発情付与　欲求不満度が上がるほど確率向上
			IF RAND:(TCVAR:ARG:ポイズンゲージ + SQRT(TCVAR:ARG:ポイズンゲージ * CFLAG:ARG:积攒度 / 2)) > SQRT(TCVAR:ARG:ポイズンゲージ) * 20
				TCVAR:ARG:発情 = 1
				IF Lapse && !CFLAG:ARG:睡眠
					SETCOLOR C_PINK
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的理性隨著\@ TCVAR:ARG:ポイズンゲージ > 1000? 完全# \@ 滲透全身的媚毒，被藥性完全溶解……
					PRINTFORML 浸润媚毒的卵巢开心的排出一枚娇嫩的卵子❤%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%感受着小腹深处莫名的颤抖…一阵幸福感油然而生……
					PRINTFORMW 在%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%享受奇妙感觉的时候，身体已然悄悄做好了受孕的准备……
					RESETCOLOR
					TCVAR:強制排卵 = 180
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
;毒气花园　理性↓积攒度↑ 発情付与 薬毒耐性・非生物には効かない
IF IsGardenValid(ARG)
	BASE:ARG:理性 -= Lapse * MAX(RAND:(ABL:ARG:欲望 + 1), 4)
	欲求不満 = 欲求不満素質補正(ARG, (2 * Lapse))
	;自宅Mapでないと上昇量が半減
	SIF !IN_HOME(TFLAG:GG設置位置)
		欲求不満 /= 2
	CFLAG:ARG:积攒度 = LIMIT(CFLAG:ARG:积攒度 + 欲求不満, 0, 1000)
	SIF BASE:ARG:理性 <= 0 && CFLAG:ARG:积攒度 >= 1000 && PALAM:ARG:欲情 >= PALAMLV:5 && !TALENT:ARG:妊娠
		TCVAR:ARG:発情 = 1
ENDIF
BASE:ARG:理性 = LIMIT(BASE:ARG:理性, 0, MAXBASE:ARG:理性)

;仕事中の場合、工作量が減少する
IF CFLAG:ARG:行動 == 5
	BASE:ARG:工作量 = MAX(BASE:ARG:工作量 - Lapse * 10, 0)
	;宴会で呑んでる場合酒気が上昇
	SIF CFLAG:ARG:職種 == 49 && !(TALENT:ARG:酒耐性 == -2 || CFLAG:ARG:妊娠自覚状態 == 1)
		BASE:ARG:酒気 += Lapse * (RAND:3 * (4 - TALENT:ARG:酒耐性))
ENDIF
;憤怒は常に減少する
BASE:ARG:憤怒 = BASE:ARG:憤怒 - Lapse * 5 / (CFLAG:ARG:ブチギレ + 1)
;合意誤認
SIF TALENT:ARG:合意誤認
	BASE:ARG:憤怒 -= Lapse * 10 / (CFLAG:ARG:ブチギレ + 1)
;宴会の席ならさらに減少する
SIF CFLAG:ARG:職種 == 49 && CFLAG:ARG:ブチギレ > 0
	BASE:ARG:憤怒 -= Lapse * 5 / (CFLAG:ARG:ブチギレ + 1)
SIF BASE:ARG:憤怒 <= 0
	CFLAG:ARG:ブチギレ = 0
BASE:ARG:憤怒 = MAX(0,BASE:ARG:憤怒)
;酒気は常に減少する
BASE:ARG:酒気 = BASE:ARG:酒気 - Lapse
;烂醉時は減少量が増える
SIF TCVAR:ARG:烂醉
	BASE:ARG:酒気 -= Lapse * (RAND:4) 
BASE:ARG:酒気 = MAX(0,BASE:ARG:酒気)
;睡眠深度の変動
IF CFLAG:ARG:睡眠
	;寝てる場合
	TCVAR:ARG:睡眠深度 += Lapse * 10 * MAX(TCVAR:ARG:睡眠薬強度, 1)
	TCVAR:ARG:睡眠深度 += (BASE:ARG:酒気 / 5 + TCVAR:ARG:疲労度 / 3)
	TCVAR:ARG:疲労度    = TCVAR:ARG:疲労度 * 2 / 3
	SIF TCVAR:ARG:疲労度 < 100
		TCVAR:ARG:疲労度 = 0
ELSE
	;寝てない
	IF TCVAR:ARG:睡眠深度 > 0
		TCVAR:ARG:睡眠深度 -= Lapse * 10
	ELSE
		TCVAR:ARG:睡眠深度 = 0
	ENDIF
ENDIF


;心情不快
SIF CFLAG:ARG:心情不快
	BASE:ARG:憤怒 = MAXBASE:ARG:憤怒
SIF FLAG:不会生气
	BASE:ARG:憤怒 = 0

;勃起度の自然増減
IF CFLAG:ARG:诶嘿嘿
	IF BASE:ARG:勃起 <= 900 && (BASE:ARG:精力 >= 500 || TCVAR:ARG:国士無双の薬)
		BASE:ARG:勃起 += Lapse * 2
		SIF BASE:ARG:精力 >= 1000 || TCVAR:ARG:国士無双の薬
			BASE:ARG:勃起 += Lapse * 5
	ENDIF
ELSE
	SIF CFLAG:ARG:勃起度２ <= CFLAG:ARG:勃起度 && !TCVAR:ARG:要去了
		BASE:ARG:勃起 = MAX(0,BASE:ARG:勃起 - Lapse * 20)
ENDIF
IF ARG == MASTER
	IF BASE:ARG:勃起 <= 900 && (BASE:ARG:精力 >= 500 || TCVAR:ARG:国士無双の薬)
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF !CFLAG:(TARGET:LOCAL):诶嘿嘿
				CONTINUE
			;リアリティはともかくとしてとりあえず
			BASE:MASTER:勃起 += GET_TARGETNUM() * 5
			BREAK
		NEXT
		SIF TARGET
			BASE:MASTER:勃起 += MIN(10, 1 + ABL:TARGET:欲望 / 5) * Lapse
	ENDIF
	;仕様が正しいのかわからないが睡眠姦BONUS
	SIF CFLAG:MASTER:恶作剧 == 2 || FLAG:70
		BASE:MASTER:勃起 += 5
ENDIF
BASE:ARG:勃起 = MIN(BASE:ARG:勃起, MAXBASE:ARG:勃起)

;媚薬フラグ
TCVAR:ARG:媚薬 = MAX(TCVAR:ARG:媚薬 - Lapse,0)
;利尿剤
TCVAR:ARG:利尿剤 = MAX(TCVAR:ARG:利尿剤 - Lapse,0)
;催情薬フラグ
TCVAR:ARG:催情薬 = MAX(TCVAR:ARG:催情薬 - Lapse,0)
;新鲜精液
TCVAR:ARG:新鲜精液 = MAX(TCVAR:ARG:新鲜精液 - Lapse,0)
;排卵誘発剤
TCVAR:ARG:強制排卵 = MAX(TCVAR:ARG:強制排卵 - Lapse,0)
;仙香玉兎,劇毒身軀
IF ARG == MASTER
	TFLAG:仙香玉兎 = MAX(TFLAG:仙香玉兎 - Lapse,0)
	TFLAG:劇毒身軀 = MAX(TFLAG:劇毒身軀 - Lapse, 0)
ENDIF
;ポイズンゲージ
TCVAR:ARG:ポイズンゲージ = MAX(TCVAR:ARG:ポイズンゲージ - (Lapse * MAX(TCVAR:ARG:ポイズンゲージ / 100, 1)), 0)

欲求不満 = 0
IF (TCVAR:ARG:媚薬 || TCVAR:ARG:発情 || IsInsenceValid(ARG)) && ARG != MASTER
	欲求不満 = 欲求不満素質補正(ARG,Lapse)
	CFLAG:ARG:积攒度 = LIMIT(CFLAG:ARG:积攒度 + 欲求不満, 0, 1000)
ENDIF
;延迟減算処理
CFLAG:ARG:延迟 = MAX(CFLAG:ARG:延迟 - Lapse,0)
;汚れを付ける
TCVAR:ARG:肮脏度 = 0
IF AT_HOME(ARG) || CFLAG:ARG:現在位置 == OMANEKIBEYA()
	TCVAR:ARG:肮脏度 = CFLAG:ARG:睡眠 ? 0 # 3 - ABL:ARG:清掃技能 / (1 + TALENT:ARG:懒散)
	SIF TALENT:ARG:幼稚
		TCVAR:ARG:肮脏度 += 2
	SIF CFLAG:ARG:初期位置 == CFLAG:ARG:現在位置 && TALENT:ARG:懒散
		TCVAR:ARG:肮脏度 += 3
	SIF CFLAG:ARG:大掃除 > 0
		TCVAR:ARG:肮脏度 = CFLAG:ARG:睡眠 ? 0 # 1 + TALENT:ARG:幼稚 - 3 * (ABL:ARG:清掃技能 + 1)
	;掃除をしてる場合、汚れが減少
	IF CFLAG:ARG:職種 == 40 && CFLAG:ARG:行動 == 5
		TCVAR:ARG:肮脏度 = -2 * (ABL:ARG:清掃技能 * 20 + 100)
	;掃除係は12～18時の間掃除,你の技能が高いと掃除させてもらえない
	ELSEIF TALENT:ARG:掃除係 && CFLAG:ARG:神社在住 && BETWEENTIME(720,1080)
		TCVAR:ARG:肮脏度 = -1 * MAX(ABL:ARG:清掃技能 + 2 - ABL:MASTER:清掃技能, 1)
	ENDIF
	YOGORE:(CFLAG:ARG:現在位置) += TCVAR:ARG:肮脏度 * Lapse / 5
	YOGORE:(CFLAG:ARG:現在位置) = LIMIT(YOGORE:(CFLAG:ARG:現在位置), 0, 6000)
ENDIF
IF !CFLAG:ARG:诶嘿嘿 && CFLAG:ARG:行動 != 5 && Lapse >= 10 && ARG
	体力回復量 = (10 + TALENT:ARG:回復速度) * Lapse / 20
	気力回復量 = (10 + TALENT:ARG:回復速度) * Lapse / 20
	;回復量が100未満の場合は非表示
	CALL RECOVER(ARG,体力回復量,"体力", 体力回復量 < 100 ? "" # "時間経過による自然回復" )
	CALL RECOVER(ARG,気力回復量,"気力", 気力回復量 < 100 ? "" # "時間経過による自然回復" )
ENDIF
;衰弱の処理
IF CFLAG:ARG:衰弱
	体力回復量 = (3 + TALENT:ARG:回復速度) * MAXBASE:ARG:体力/2000
	気力回復量 = (3 + TALENT:ARG:回復速度) * MAXBASE:ARG:気力/2000
	;添い寝状態だと相手の回復量が上昇
	IF CFLAG:ARG:陪睡中
		体力回復量 *= 2
		気力回復量 *= 2
	ENDIF
	CALL RECOVER(ARG,Lapse * 体力回復量,"体力")
	CALL RECOVER(ARG,Lapse * 気力回復量,"気力")
	CFLAG:ARG:衰弱 = MAX(CFLAG:ARG:衰弱 - Lapse,0)
	IF BASE:ARG:体力 > MAXBASE:ARG:体力 / 2
		CFLAG:ARG:衰弱 = 0
		TCVAR:ARG:休憩中 = 0
	ENDIF
ENDIF
;雨、雪が降っている場合、屋根がないなら体力が稍微ずつ減る
;神社外にいる角色は減らないが你と约会中の相手は減る
;雨で衰弱状態にはならない
IF OUTROOF(CFLAG:ARG:現在位置) && (AT_HOME(ARG) || ARG == MASTER || FLAG:约会的对象 == ARG)
	;你が折りたたみ傘を持っていない場合
	IF WEATHERDLC
		CALL WEATHERCHECK(TARGET, ARG, Lapse)
	ENDIF
	IF ARG == MASTER && !ITEM:折畳傘
		SELECTCASE TIME:5
			;雨・霧雨
			CASE 4, 6
				;余計なタイミングでメッセージが出ないように
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 被雨淋的皮肤发寒
			;大雨
			CASE 5
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 冰冷的雨刺激着皮肤
			;雪・細雪・霧雪・雨雪交加
			CASE 8, 10, 12
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 被雪弄得皮肤发寒
			;大雪
			CASE 9
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					;PRINTW 大雪で身体が凍てつくようだ
					PRINTW 大雪冻得身体发冷
			;(天候パッチ)豪雨・ひょうを追加、冰雹を変更
			;冰雹
			CASE 13
				IF GET_HEAT_THUNDERSTORM() == 1
					SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
						;PRINTW 冰雹が勢いよく当たって痛い
						PRINTW 被冰雹砸得生疼
				ELSE
					SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
						PRINTW 被雪弄得皮肤发寒
				ENDIF
			;豪雨
			CASE 14
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					;PRINTW 滝のような雨が身体を打ち付ける
					PRINTW 瀑布般的雨打在身上
			;ひょう
			CASE 15
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					;PRINTW 大粒のひょうが身体を痛めつける
					PRINTW 大颗的冰雹摧残着身体
		ENDSELECT
	;你が折りたたみ傘を持っていると体力の代わりに傘耐久度が減る
	ELSEIF ARG == MASTER
		SELECTCASE TIME:5
			;霧雨、霧雪
			CASE 6, 12
				傘ダメージ = 1
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 20
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 50
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 100
				ENDIF
			;雨雪交加
			CASE 13
				傘ダメージ = 5
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 50
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 200
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 400
				ENDIF
			;雨・雪・細雪
			CASE 4, 6, 8, 10, 12
				傘ダメージ = 2
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 30
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 100
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 200
				ENDIF
			;大雨・大雪
			CASE 5, 9
				傘ダメージ = 20
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 50
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 200
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 400
				ENDIF
			;(天候パッチ)豪雨・ひょうを追加
			;豪雨
			CASE 14
				傘ダメージ = 50
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 50
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 150
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 300
				ENDIF
			;ひょう
			CASE 15
				傘ダメージ = 100
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					傘ダメージ += 50
				ELSEIF WIND_VELOCITY == 2
					傘ダメージ += 200
				ELSEIF WIND_VELOCITY >= 3
					傘ダメージ += 400
				ENDIF
			CASEELSE
				傘ダメージ = 0
		ENDSELECT
		FLAG:傘耐久度 += 傘ダメージ * Lapse
		IF FLAG:傘耐久度 > 10000
			IF MONEY >= 1200 && Automatic_umbrella_repair == 1;1==花钱自动维修
				FLAG:傘耐久度 = 0
				MONEY -= 1200
				CALL COLORMESSAGE(@"傘壊了！%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%花费1200¥从间隙商店重新购买一把新的。",C_YELLOW,2)
			ELSEIF BASE:MASTER:TSP >= 600 &&Automatic_umbrella_repair == 2;2==花TSP自动维修
				FLAG:傘耐久度 = 0
				BASE:MASTER:TSP -= 600
				CALL COLORMESSAGE(@"傘壊了！%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%暂停时间将其修好。（TSP -600）",C_YELLOW,2)
			ELSEIF Automatic_umbrella_repair == 3;3==作弊自动维修
				FLAG:傘耐久度 = 0
				CALL COLORMESSAGE(@"傘壊了！%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%瞬间将其重新修好。",C_YELLOW,2)
			ELSE
				ITEM:折畳傘 = 0
				ITEMSALES:36 ++
				FLAG:傘耐久度 = 0
				ITEM:壊掉的傘 = 1
				SIF EQUIP:TARGET:飾品 != 6
					TEQUIP:TARGET:共用雨伞 = 0
				CALL COLORMESSAGE(@"傘壊了！",C_YELLOW,2)
			ENDIF
		ELSEIF FLAG:傘耐久度 > 8000 && 傘ダメージ
			SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
				CALL COLORMESSAGE(@"傘快要壊了…",C_YELLOW,1)
		ENDIF
	ENDIF
	SIF (CFLAG:ARG:诶嘿嘿 == 2 && BASE:ARG:体力 > 500) || CFLAG:ARG:诶嘿嘿 != 2
		BASE:ARG:体力 = MAX(BASE:ARG:体力 + 天候造成的影響(ARG, CFLAG:ARG:現在位置) * Lapse, 500)
ENDIF

;(天候パッチ)雷・竜巻遭遇判定
地底 = 0
IF CFLAG:MASTER:约会中 == 9
	地底 = 1
ELSEIF CFLAG:MASTER:現在位置 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:招待, 36, 37, 38, 49, 60, 64, 83, 84)
	地底 = 1
ENDIF
IF OUTROOF(CFLAG:ARG:現在位置) && ARG == MASTER && Lapse && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠 && CFLAG:MASTER:現在位置 != 天界 && !地底
	IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && GET_WEATHER_THUNDERSTORM() == 1
		;CALL COLORMESSAGE(@"雷が激烈地轟いている",C_YELLOW,2)
		CALL COLORMESSAGE(@"雷声轰轰烈烈",C_YELLOW,2)
	ELSEIF (GET_FRONTAL_THUNDERSTORM() == 1 || GET_HEAT_THUNDERSTORM() == 1) && GET_WEATHER_THUNDERSTORM() == 1
		;CALL COLORMESSAGE(@"雷鳴が聞こえてくる",C_YELLOW,2)
		CALL COLORMESSAGE(@"听到了雷鸣",C_YELLOW,2)
	ENDIF
ENDIF

IF OUTROOF(CFLAG:ARG:現在位置) && ARG == MASTER && WIND_VELOCITY >= 2 && Lapse && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠 && CFLAG:MASTER:現在位置 != 天界 && !地底
	IF CAN_CUMULUS:2 == 2 && RAND:10 == 0
		SETCOLOR C_RED
		PRINTW 龙卷风！！
		RESETCOLOR
		PRINTL 
		PRINTW 被龙卷风卷入其中吹跑了……
		PRINTL 
		CALL ASK_DIARY("竜巻に遭遇した")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)
	ELSEIF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -2 && TIME:5 >= 13 && RAND:30 == 0
		SETCOLOR C_RED
		PRINTW 龙卷风！！
		RESETCOLOR
		PRINTL 
		PRINTW 被龙卷风卷入其中吹跑了……
		PRINTL 
		CALL ASK_DIARY("竜巻に遭遇した")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)
	ENDIF
ELSEIF OUTROOF(CFLAG:ARG:現在位置) && ARG == MASTER && Lapse && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠 && CFLAG:MASTER:現在位置 != 天界 && !地底
	IF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && WIND_VELOCITY >= 3 && RAND:10 == 0
		PRINTFORML …正前进的时候有什么东西飞了过来
		SETCOLOR C_RED
		PRINTW 哪来的树！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 被暴風吹飞的树直接命中了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		PRINTL 
		;CALL ASK_DIARY("飛来物が直撃した")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 3)
	ELSEIF WIND_VELOCITY >= 3 && RAND:20 == 0
		PRINTFORML …正前进的时候有什么东西飞了过来
		SETCOLOR C_RED
		PRINTW 大货车！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 被暴風吹飞的货车直接命中了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		PRINTL 
		;CALL ASK_DIARY("飛来物が直撃した")
		BASE:MASTER:体力 -= (MAXBASE:MASTER:体力 * 2)
	ELSEIF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && WIND_VELOCITY >= 2 && RAND:40 == 0
		PRINTFORML …正前进的时候有什么东西飞了过来
		SETCOLOR C_RED
		PRINTW 瓦片！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 被暴風吹飞的瓦片直接命中了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		PRINTL 
		;CALL ASK_DIARY("飛来物が直撃した")
		BASE:MASTER:体力 -= MAXBASE:MASTER:体力
	ELSEIF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && WIND_VELOCITY >= 2 && RAND:80 == 0
		PRINTFORML …正前进的时候有什么东西飞了过来
		SETCOLOR C_RED
		PRINTW 瓦片！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 被暴風吹飞的瓦片直接命中了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		PRINTL 
		;CALL ASK_DIARY("飛来物が直撃した")
		BASE:MASTER:体力 -= MAXBASE:MASTER:体力
	ENDIF
ENDIF

;------------------------------------------
;角色(ARG)が地点(ARG:1)に居る時に接受体力への増減値
;移動傾向でも使用ので関数に分離
;------------------------------------------
@天候造成的影響(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
;屋外、傘装備無、共用雨伞してない
IF OUTROOF(ARG:1) && EQUIP:ARG:飾品 != 6 && !TEQUIP:ARG:共用雨伞
	;你は傘を持っていれば除外
	SIF ARG == MASTER && ITEM:折畳傘
		RETURNF 0
	SELECTCASE TIME:5
		;夏の晴天
		CASE 0
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:氷精 == 2)
				LOCAL = -2
		;夏の晴天
		CASE 1
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:氷精 == 2)
				LOCAL = -3
		;雨・霧雨
		CASE 4, 6
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;雪・細雪・霧雪・雨雪交加
		CASE 8, 10, 11, 12
			IF TALENT:ARG:氷精
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;大雨
		CASE 5
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;吹雪(天候パッチ)冰雹
		CASE 9, 13
			IF TALENT:ARG:氷精
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;(天候パッチ)豪雨・ひょうを追加
		;豪雨
		CASE 14
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -6
			ENDIF
		;ひょう
		CASE 15
			LOCAL = -10
	ENDSELECT
	;(天候パッチ)風速の影響
	IF WIND_VELOCITY == 1
		LOCAL -= 1
	ELSEIF WIND_VELOCITY == 2
		LOCAL -= 3
	ELSEIF WIND_VELOCITY >= 3
		LOCAL -= 6
	ENDIF
ENDIF

;吸血鬼と氷精はダメージ2倍,你も防衛できるので影響を大きく
SIF LOCAL < 0 && (TALENT:ARG:吸血鬼 || TALENT:ARG:氷精 || ARG == MASTER)
	TIMES LOCAL, 2.0
RETURNF LOCAL

@IsGardenValid(ARG)
#FUNCTION
SIF CFLAG:ARG:現在位置 != TFLAG:GG設置位置
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF TALENT:ARG:人形
	RETURNF 0
SIF TALENT:ARG:薬毒耐性
	RETURNF 0

RETURNF 1

@IsInsenceValid(ARG)
#FUNCTION
SIF !TFLAG:仙香玉兎
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF CFLAG:ARG:現在位置 != CFLAG:MASTER:現在位置
	RETURNF 0
SIF TALENT:ARG:人形
	RETURNF 0
SIF TALENT:ARG:薬毒耐性
	RETURNF 0
RETURNF TFLAG:仙香玉兎

@IsHeat(ARG)
#FUNCTION
SIF TCVAR:ARG:媚薬 || TCVAR:ARG:発情 || IsGardenValid(ARG)
	RETURNF 1
RETURNF 0
