[SKIPSTART]
◆衣装設定の仕様
・基本的には記法を変えて可読性を上げただけ
・GET_INT/GET_STRを使用することで情報を取得することができる
・1つの衣装につき2関数を使用する（うち1つは存否関数）
・各衣装は衣装カテゴリ（≒EQUIPNAMEと似ているが少し変えた）ごとに管理する
・同じ衣装カテゴリのなかで同一名称の装備品は作れない
　逆に言えば、違う衣装カテゴリであるならば名称は重複しても構わない
　例）「頭装備」の蝴蝶結と「其他衣装」の蝴蝶結
　ただし、「外搭」の白衣（はくい）と「上身衣服_はだけ不可」の白衣（はくえ、びゃくえ）の場合は
　「同一表記で指し示すものが全く違う」ために上身衣服１の側を変更した（まぎらわしいよ）
・衣装カテゴリと装備部位は基本的に一対一の対応関係にある
　たとえば「頭装備」はEQUIP:帽子にのみ装備可能
　ただし、例外として「其他衣装」はEQUIP:其他１、EQUIP:其他２、EQUIP:其他３の全てに装備できる
　この仕様は変えようと思えば変えられるけど@CLOTHES_IDや全衣装IDの再設定が必要になるはず（つまりめんどい）
・衣装の参照の際にはこのようにする
　GET_INT(登録番号等, 衣装カテゴリ, 衣装のカテゴリ内ID, 参照したい情報の種類)
・特定の名前の衣装のIDを取得したい時はこのようにする
　CLOTHES_ID(装備部位名, 装備名)
・装備部位から該当する衣装カテゴリを取得する時はこのようにする
　CLOTHES_PARTS_TO_CATEGORY(装備部位名)
・以上の関数から、装備部位と装備名のみで（番号を使用せずに）装備を参照することができる
　GET_INT(登録番号等, CLOTHES_PARTS_TO_CATEGORY(装備部位名), CLOTHES_ID(装備部位名, 装備名), 参照したい情報の種類)
　まー、現在装備中の装備の参照ならCLOTHES_IDは不要なんで
　GET_INT(登録番号等, CLOTHES_PARTS_TO_CATEGORY(装備部位名), EQUIP:登録番号:装備部位名, 参照したい情報の種類)
　こうなる

・自分用の覚書
　（下半身内衣系はまた別）
　おそらくこういう考え方でいいと思う
　上身衣服１（はだけ不可）が外側、上身衣服２（はだけ可能）は内側に着用する衣装


・衣装名取得関数@CLOTHNAME
各装備の設定から引っ張ってこられるようになったので大幅に簡略化

・服装名取得関数（@SHOW_BOTTOM等）
単に最も上の装備の名前を返すように変更（簡略化）しています
名前とは別にしたいならSTR用で"描写"なりを全衣装で設定すれば簡単に参照できます

・換装関連についても類似の仕様で実装した
・衣装セット関数群
　この関数群は換装セットの内容を記述する
・キャラデータ関数群
　この関数群は各角色の個別のデータを一元管理させる
　便装の設定は各角色で独立しているためにここに記述した
　また、愛麗絲の衣装に存在していた年越し和装処理の実現のために
　便装の切替制御の機能も追加した
　曜日によって切り替えるとかもやりたければできる
　目指せ七色の魔法使
・衣装設定について
　きせかえによって「この服装を毎日着るように」という指定が可能
　0　　　默认のまま
　1～99　COORDINATE1～99を指定
　100　　手動で設定する（CFLAG:600～630）
　101～　特定角色の真似（特定角色の便装を参照する、101なら霊夢）
・睡衣設定について
　睡衣はそんなにこだわってもしかたねーなと思ったので
　COORDINATE関数のなかからどれを着させるのかを選ぶ型式にした
　・睡衣（襦袢）
　・睡衣（睡衣）
　・睡衣（背带裙衣）
　・睡衣（連衣裙）
　・睡衣（裤子上下）
　・睡衣（裙子上下）
　・睡衣（裤子＋兜帽）
　・睡衣（裙子＋兜帽）
　ただし、例外として"パンイチ"で胖次一丁、"全裸"で赤裸的んぼで寝させることができる
　（そういう処理が実装されてた娘がいたので）



◆個々の装備品まわりの変更
・名称変更
　睡衣→長睡衣
　画像検索の結果が過激過ぎるので名称変更
　背带裙衣との差別化のためにおと無めな衣装を想定する
・カテゴリ変更
　「上身衣服_はだけ不可」の「白衣（はくえ、びゃくえ）」の名称を「白小袖」に変更
　衣装セット系では「一套神官服」のみが使用しており「保健室的女教师套」でも「白衣天使套」でも使っていない
　また、便装使用角色もいなかったので変更
　
　さらに、この「白小袖」は内に着こむ衣装であることが考えられるため、
　衣装カテゴリを「上身衣服_はだけ可能」に移動した
・追加衣装
　頭装備「兜帽」、睡衣用に新設
　スレでの議論で出てきたアイデアを採用させてもらいました
・追加衣装
　腕部装束「臂套」、帕露西用に新設
・追加衣装
　上半身衣装_はだけ不可「水干」、布都ちゃん用に新設
　巫女でもつかうところあるみたいね
・追加衣装
　上半身衣装_はだけ不可「狩衣」
　和っぽい方向を拡充したかった
　白だととても神職っぽいですよ
・追加衣装
　靴「沓」
　神職やら貴族的な装束ということで「水干」「狩衣」とのセットを想定して追加
・追加衣装
　其他「胸针」
　影狼ちゃんの首元のとても印象的な胸针が無かったので増設した
　蕾蒂さんにも装備



◆衣装カテゴリの名前について
仕様がややこしくなってしまうのは承知の上ですが
なぜ一部の衣装カテゴリの名前をEQUIPNAMEと変えてあるかといえば
それは以下の理由からです
　理由１：関数名で@下半身内衣１2もしくは@下半身内衣１_2とかやんのが余りにも気色悪すぎる
　理由２：直感的でないEQUIPNAMEがある
　　　　　・蝴蝶結や髮箍は「帽子」
　　　　　・下半身内衣１と下半身内衣２の区分の違いがわかりにくい（この番号もマジックナンバー）
　　　　　・とりあえず「はだけ可能・はだけ不可」「ずらし可能・ずらし不可」の名前にした
　　　　　・どうやらこの分類も不明確であるらしいが番号よりはましか？
　　　　　・とりあえず暫定の仕様です
　理由３：EQUIPNAME側を弄ろうとすると修正箇所が膨大
　　　　　追加部分だけ変えたほうがラク

以下、EQUIPNAMEと衣装カテゴリの対応

飾品
→飾品
　其他は違い、何らかの効果をもつことがある
　傘は天候依存で行動時のBASE消費軽減に使われることがある

帽子
→頭装備
　帽子だけでなく白色女僕髮箍や蝴蝶結などといった頭部装飾品を指す

靴
→靴
　室内では脱掉

襪子
→襪子
　一部の角色はコンフィグによって便装の衣装がニーソになる

下半身内衣１
→下半身内衣_ずらし不可

下半身内衣２
→下半身内衣_ずらし可能
　下半身内衣（ずらし可能）
　いわゆる胖次（＋比基尼の下半身だけ）
　独特な処理が多いため、ほかの衣装とは別扱いで管理する

上半身内衣１
→上半身内衣_はだけ不可

上半身内衣２
→上半身内衣_はだけ可能

緊身内衣
→緊身内衣

連体内衣
→連体内衣

裤子
→裤子
　ずらし不可

下身衣服
→該当装備が少なすぎるので廃止、復活させたい場合は其他なりで追加して下さい

裙子
→裙子
　めくれる下半身装備

上身衣服１
→上身衣服_はだけ不可

上身衣服２
→上身衣服_はだけ可能

連衣裙
→連衣裙

和服
→和服

外搭
→外搭
　衣服とは別に上から羽织ってる衣装

其他１、其他２、其他３
→１～３を全部まとめて「其他衣装」に統合

腕部装束
→腕部装束
　主に手套系



◆胖次
特殊な処理が多そうなので別で処理する
・CFLAG:きょうの胖次
　一日の最初にきょうのぱんつを設定してCFLAGで保持
・CFLAG:睡衣胖次
　一日の最初に睡衣用の胖次を設定してCFLAGで保持
　睡衣に着替えた時に没穿内裤で那个ば睡衣胖次を着用する
　睡衣胖次を着用した時点で睡衣胖次に0が代入されるため
　無限睡衣胖次は不可能です



◆プロパティについて

装備品系クラスのプロパティ
CASE "名前"
	名前、その衣装の名称（STR）
	衣装カテゴリ内で重複させることはできない
	CALLF MAKE_STR(V_NAME, "眼鏡")
CASE "装備部位"
	その衣装を装備するEQUIP（STR）
	CALLF MAKE_STR(V_NAME, "「飾品」")
CASE "換装可能"
	胖次限定、換装指令で設定できるかどうか
	比基尼内袴、微型内裤限定
	CALLF MAKE_INT(V_NAME, 1)
	つくったけど手動きせかえ機能が出来てないので実質未実装である
CASE "能穿"
	今天穿的内裤での判定用（INT、というか真偽値1/0）
	この項目を作らないぱんつは角色が自発的に履くことはない



衣装セットクラスのプロパティ
CASE "名前"
	名前、その衣装セットの名称（STR）
	衣装セットクラス内で重複させることはできない
CASE "各種装備部位"
	その装備部位に着せる衣装の内容（INT）
	項目を作らなかった部位は0とみなされ何も着用しない
CASE "基本套装"
CASE "睡衣セット"
CASE "cosplay服装"
CASE "角色特殊服装集"
	これらは全て衣装セットの種類を表す（INT）
	まー、現状でそれっぽくやっただけです
	1にするとその属性の衣装セットだとみなされる
	列表表示用の設定です



キャラデータクラスのプロパティ
CASE "稱呼"
	必須、STR
	呼び名、その角色の名称
	キャラデータクラス内で重複させることはできない
CASE "今天的睡衣"
	必須、STR
	着用する睡衣の種類を記述する
	睡衣の種類の判定としては
	１．まず、例外を処理する（恋を参照）
	"パンイチ"を指定した場合、睡衣は着用せずに内衣のみを着用する
	"全裸"を指定した場合、服は何も着ない
	２．次に、STRをもとに衣装セットを参照する
	一致する衣装セットが存在する場合はその衣装セットを着用する
	白衬衫だけとかも可能（胖次の有無は別個で指定のこと）
	３．最後に、STRをもとにキャラデータを参照する
	STR == "便装"ならその角色の便装が参照される

CASE "便装：各種装備部位"
	必須、INT
	便装の衣装設定
	その装備部位に着せる衣装の内容
	項目を作らなかった部位は0とみなされ何も着用しない
	「便装」以外の衣装の角色別指定も可能
	例）"年越し：各種装備部位"で年越し用の衣装セットを作成できる
	ただし、この場合は"今日の衣装", "明日の衣装"を設定する必要がある
CASE "今日の衣装", "明日の衣装"
	省略可、STR
	便装以外の衣装を設定しない場合には必要ない
	特定条件に合わせて衣装を変更させたい時に使用する
	SIF (DAY:2 == 1 && DAY:3 == 1) || (DAY:2 == 4 && DAY:3 == 31)
		CALLF MAKE_STR(V_NAME, "年越し")
	このように記述すると条件に合致する時に「年越し」の衣装を着用する（愛麗絲の処理を参照）
	
	"今日の衣装", "明日の衣装"の何が違うかというと
	衣装の更新処理はMASTERが起床した際に行うので
	その時点で睡眠中（＝睡衣姿）なら明日の衣装を設定し
	その時点で活動中なら現在着用中の便装を設定することになる
	
	注）機能追加
	STRで衣装セットに存在する名前を返した際にはその衣装をセットする機能を追加


	衣装指定用の文字列として"推奨", "固定"は使用不可能（別の機能で使用中）
	
CASE "推奨：各種装備部位", "固定：各種装備部位"
	みじっそう
	やろうと思えばできる、作ろうか迷ってる
	
	省略可、INT
	角色ごとの「これは外しちゃダメだろ」という装備用の処理
	指定した部位の装備を推奨・強要する
	Emueraのconfigにおけるdefault/fixみたいなもん
	この情報は換装関連処理の最後に適用される
	例）華扇
	・束髪簪は「推奨」
	・绷帯と拘束具は「固定」
	推奨を指定した場合、その部位に装備しているものが何もなければ装備を行う
	固定を指定した場合、その部位の装備を上書きする
	ハダカになる時は
	EQUIP:0 = 0した上で固定装備だけ着用する
[SKIPEND]


;EQUIP:X:ARGに装備した
;ARG:1の衣装の名前を返す
@CLOTHNAME(ARG,ARG:1)
#FUNCTIONS
RETURNF GET_STR(, CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:(ARG:0)), ARG:1, "名前")



;装備部位から該当する衣装カテゴリを返す
@CLOTHES_PARTS_TO_CATEGORY(ARGS)
#FUNCTIONS
SELECTCASE ARGS
[SKIPSTART]
;衣装の統合化
;途中まで考えたがめんどくてやめた
CASE "下半身内衣２"
	RETURNF "胖次"
CASE "和服", "緊身内衣", "連体内衣", "連衣裙"
	RETURNF "全身衣装"
CASE "帽子", "外搭", "腕部装束", "上身衣服１", "上身衣服２", "上半身内衣１", "上半身内衣２"
	RETURNF "上半身衣装"
CASE "裙子", "裤子", "靴", "襪子", "下半身内衣１"
	RETURNF "下半身衣装"
CASE "飾品", "其他１", "其他２", "其他３"
	RETURNF "其他衣装"
[SKIPEND]

CASE "飾品", "靴", "襪子", "緊身内衣", "連体内衣", "裤子", "裙子", "連衣裙", "和服", "外搭", "腕部装束"
	RETURNF ARGS
CASE "帽子"
	RETURNF "頭装備"
CASE "下半身内衣１"
	RETURNF "下半身内衣_ずらし不可"
CASE "下半身内衣２"
	RETURNF "下半身内衣_ずらし可能"
CASE "上半身内衣１"
	RETURNF "上半身内衣_はだけ不可"
CASE "上半身内衣２"
	RETURNF "上半身内衣_はだけ可能"
CASE "上身衣服１"
	RETURNF "上身衣服_はだけ不可"
CASE "上身衣服２"
	RETURNF "上身衣服_はだけ可能"
CASE "其他１", "其他２", "其他３"
	RETURNF "其他衣装"
ENDSELECT
THROW 不明な部位%ARGS%


;装備部位と衣装名から各衣装のカテゴリ内IDを返す
@CLOTHES_ID(PARTS, C_NAME)
#FUNCTION
#DIMS PARTS
#DIMS C_NAME
#DIM  C_ID
C_ID = OBJNAME_TO_ID(, "GET", CLOTHES_PARTS_TO_CATEGORY(PARTS), C_NAME)
;SIF C_ID == -1
;	THROW %C_NAME%は%CLOTHES_PARTS_TO_CATEGORY(PARTS)%には存在しません
RETURNF C_ID



;-------------------------------------------------
;関数名:SHOW_BOTTOM
;概　要:下半身服装名取得関数
;戻り値:服装名(文字列)
;引数:0 参照する角色番号
;引数:1裙子、2ずらし可、3ずらし不可、4下身衣服ずらし不可の種別(省略した場合は最も露出を妨げているもの)
;備　考:文中関数
;TEQUIP:0に対応して一番上に着ている衣装の種類を返す
;
;引数:1に複数のビットを指定すればその中から優先されるものを表示させることができる
;引数:1が9なら裙子と下身衣服ずらし不可のどちらか着ているものを表示、など
;COUNTER_MESSAGE.ERB内の@DATUI_MESSAGEが使用例になってるのでピンとこない人は見てね
;-------------------------------------------------
@SHOW_BOTTOM(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 5 = "", "裙子", "ずらし可", "ずらし不可", "下身衣服ずらし不可"
#DIMS PARTS
#DIM EQUIP_TARGET

;引数1とTEQUIP:0を照らし合わせて表示するものを選ぶ
IF ARG:1
	EQUIP_TARGET = ARG:1 & TEQUIP:ARG:0
ELSE
	EQUIP_TARGET = TEQUIP:ARG:0
ENDIF

IF EQUIP_TARGET & 8
	LOCAL = 4
	;4下半身服装描写_下身衣服ずらし不可
ELSEIF EQUIP_TARGET & 4
	;3下半身服装描写_ずらし不可
	LOCAL = 3
ELSEIF EQUIP_TARGET & 2
	;2下半身服装描写_ずらし可
	LOCAL = 2
ELSEIF EQUIP_TARGET & 1
	;1下半身服装描写_裙子
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"下半身服装描写_%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "名前")



;-------------------------------------------------
;関数名:SHOW_下半身内衣
;概　要:下半身内衣名取得関数
;戻り値:内衣名(文字列)
;引数:0 参照する角色番号
;引数:1裙子、2ずらし可、3ずらし不可、4下身衣服ずらし不可の種別(省略した場合は最も露出を妨げているもの)
;備　考:文中関数
;TEQUIP:0に対応して一番上に着ている衣装の種類を返す
;
;引数:1に複数のビットを指定すればその中から優先されるものを表示させることができる
;引数:1が9なら裙子と下身衣服ずらし不可のどちらか着ているものを表示、など
;COUNTER_MESSAGE.ERB内の@DATUI_MESSAGEが使用例になってるのでピンとこない人は見てね
;-------------------------------------------------
@SHOW_下半身内衣(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 5 = "", "裙子", "ずらし可", "ずらし不可", "下身衣服ずらし不可"
#DIMS PARTS
#DIM EQUIP_TARGET

;引数1とTEQUIP:0を照らし合わせて表示するものを選ぶ
IF ARG:1
	EQUIP_TARGET = ARG:1 & TEQUIP:ARG:0
ELSE
	EQUIP_TARGET = TEQUIP:ARG:0
ENDIF

IF EQUIP_TARGET & 8
	LOCAL = 4
	;4下半身服装描写_下身衣服ずらし不可
ELSEIF EQUIP_TARGET & 4
	;3下半身服装描写_ずらし不可
	LOCAL = 3
ELSEIF EQUIP_TARGET & 2
	;2下半身服装描写_ずらし可
	LOCAL = 2
ELSEIF EQUIP_TARGET & 1
	;1下半身服装描写_裙子
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"下半身内衣描写_%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "名前")



;-------------------------------------------------
;関数名:SHOW_TOP
;概　要:上半身服装名取得関数
;戻り値:服装名(文字列)
;引数:0 参照する角色番号
;引数:1はだけ可、2はだけ不可(省略した場合は最も露出を妨げているもの)
;備　考:文中関数
;TEQUIP:1に対応して一番上に着ている衣装の種類を返す
;-------------------------------------------------
@SHOW_TOP(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 3 = "", "はだけ可", "はだけ不可"
#DIMS PARTS
IF ARG:1
	LOCAL = ARG:1
ELSEIF TEQUIP:ARG:1 & 2
	LOCAL = 2
ELSEIF TEQUIP:ARG:1 & 1
	LOCAL = 1
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"上半身服装描写_%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "名前")



;-------------------------------------------------
;関数名:SHOW_上半身内衣
;概　要:上半身内衣名取得関数
;戻り値:上半身内衣名(文字列)
;引数:0 参照する角色番号
;引数:1はだけ可、2はだけ不可(省略した場合は最も露出を妨げているもの)
;備　考:文中関数
;TEQUIP:1に対応して一番上に着ている衣装の種類を返す
;-------------------------------------------------
@SHOW_上半身内衣(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 3 = "", "はだけ可", "はだけ不可"
#DIMS PARTS
LOCAL = 0
IF ARG:1
	LOCAL = ARG:1
ELSEIF TEQUIP:ARG:1 & 2
	LOCAL = 2
ELSEIF TEQUIP:ARG:1 & 1
	LOCAL = 1
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"上半身内衣描写_%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "名前")




;最も露出を妨げている衣装の装備部位を返す
;@SHOW_装備系の判定が煩わしかったので簡略化用に
@COVER_CLOTHES(ARG, TYPE)
#FUNCTIONS
#DIMS TYPE
SELECTCASE TYPE
CASE "上半身服装描写_はだけ不可"
	SIF EQUIP:ARG:上身衣服１
		RETURNF "上身衣服１"
	SIF EQUIP:ARG:連衣裙
		RETURNF "連衣裙"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	; SIF EQUIP:ARG:連体内衣
	; 	RETURNF "連体内衣"
	SIF EQUIP:ARG:上半身内衣１
		RETURNF "上半身内衣１"
	RETURNF
CASE "上半身服装描写_はだけ可"
	SIF EQUIP:ARG:上身衣服２
		RETURNF "上身衣服２"
	SIF EQUIP:ARG:和服
		RETURNF "和服"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
	SIF EQUIP:ARG:上半身内衣２
		RETURNF "上半身内衣２"
	RETURNF


CASE "上半身内衣描写_はだけ不可"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
	SIF EQUIP:ARG:上半身内衣１
		RETURNF "上半身内衣１"
	RETURNF
CASE "上半身内衣描写_はだけ可"
	SIF EQUIP:ARG:上半身内衣２
		RETURNF "上半身内衣２"
	RETURNF


CASE "下半身内衣描写_下身衣服ずらし不可"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
CASE "下半身内衣描写_ずらし不可"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
	SIF EQUIP:ARG:下半身内衣１
		RETURNF "下半身内衣１"
CASE "下半身内衣描写_ずらし可"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	SIF EQUIP:ARG:下半身内衣２
		RETURNF "下半身内衣２"
CASE "下半身内衣描写_裙子"
	SIF EQUIP:ARG:裙子
		RETURNF "裙子"
	SIF EQUIP:ARG:和服
		RETURNF "和服"
	RETURNF


CASE "下半身服装描写_下身衣服ずらし不可"
	SIF EQUIP:ARG:裤子
		RETURNF "裤子"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
	RETURNF
CASE "下半身服装描写_ずらし不可"
	SIF EQUIP:ARG:裤子
		RETURNF "裤子"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	SIF EQUIP:ARG:連体内衣
		RETURNF "連体内衣"
	SIF EQUIP:ARG:下半身内衣１
		RETURNF "下半身内衣１"
	SIF EQUIP:ARG:下半身内衣２
		RETURNF "下半身内衣２"
	RETURNF
CASE "下半身服装描写_ずらし可"
	SIF EQUIP:ARG:緊身内衣
		RETURNF "緊身内衣"
	SIF EQUIP:ARG:下身衣服
		RETURNF "下身衣服"
	SIF EQUIP:ARG:下半身内衣２
		RETURNF "下半身内衣２"
	RETURNF
CASE "下半身服装描写_裙子"
	SIF EQUIP:ARG:裙子
		RETURNF "裙子"
	SIF EQUIP:ARG:連衣裙
		RETURNF "連衣裙"
	SIF EQUIP:ARG:和服
		RETURNF "和服"
	SIF EQUIP:ARG:連衣裙
		RETURNF "連衣裙"
	RETURNF
ENDSELECT



;全キャラの衣装を默认に戻す
@RESET_ALL_CLOTHES
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:基本服装セット = 0
	CALL SELECT_CLOTHES(LOCAL, "今日の衣装")
	CALL CTRL_CLOTHES_SET(LOCAL, "現在衣装の変更_便装")
	CALL CTRL_CLOTHES_SET(LOCAL, "套装のリセット")
	CFLAG:LOCAL:指定胖次 = 0
	CFLAG:LOCAL:睡衣胖次 = 0
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT



;EQUIPからTEQUIPに変換
;このタイミングで靴脱ぎと全裸チェック
@CLOTHES_SETTING_TRAIN(ARG)
TEQUIP:ARG:0 = 0
TEQUIP:ARG:1 = 0

;ぶっちゃけここの処理は適当
IF INROOM(CFLAG:ARG:現在位置)
	SIF EQUIP:ARG:飾品 == 6
		EQUIP:ARG:飾品 = 0
	SIF INROOM(CFLAG:ARG:現在位置) == 1 && !CFLAG:ARG:うふふ
		EQUIP:ARG:靴 = 0
ELSE
	IF CFLAG:ARG:203
		EQUIP:ARG:靴 = CFLAG:ARG:203
	ELSEIF CFLAG:ARG:603
		EQUIP:ARG:靴 = CFLAG:ARG:603
	ENDIF
	IF !EQUIP:ARG:飾品 && CFLAG:ARG:201 == 6 && !FLAG:時間停止
		EQUIP:ARG:飾品 = CFLAG:ARG:201
	ELSEIF !EQUIP:ARG:飾品 && CFLAG:ARG:601 == 6 && !FLAG:時間停止
		EQUIP:ARG:飾品 = CFLAG:ARG:601
	ENDIF
ENDIF

IF CHECK_CHARA(ARG, "全裸")
	EQUIP:ARG:0 = 0
	EQUIP:ARG:靴 = 0
ELSE
	EQUIP:ARG:0 = 1
ENDIF

;換装が有効でない
SIF !EQUIP:ARG:0
	RETURN 0
;下半身
;TEQUIP:0 下半身着衣状況(ビット0=裙子 1=ずらし可 2=ずらし不可 3=ずらせない下身衣服)
;	(ビットを使いたくない人用 0=無 1=没穿内裤裙子 2=內褲のみ 3=裙子+內褲 
;	4=安全褲(内衣)もしくは没穿内裤裤子 5=裙子+安全褲 6=裤子+內褲 7=フル装備(このバリアントでは無理)）
;EQUIP:5,6 下半身内衣
SIF EQUIP:ARG:下半身内衣１
	TEQUIP:ARG:0 |= 4
SIF EQUIP:ARG:下半身内衣２
	TEQUIP:ARG:0 |= 2
;EQUIP:7～9 下身衣服
IF EQUIP:ARG:裤子
	TEQUIP:ARG:0 |= 4
	TEQUIP:ARG:0 |= 8
ENDIF
SIF EQUIP:ARG:下身衣服
	TEQUIP:ARG:0 |= 2
SIF EQUIP:ARG:裙子
	TEQUIP:ARG:0 |= 1
;EQUIP:14～16 全身外套
IF EQUIP:ARG:連体内衣
	TEQUIP:ARG:0 |= 4
	TEQUIP:ARG:0 |= 8
	TEQUIP:ARG:0 |= 16	
ENDIF
SIF EQUIP:ARG:連衣裙
	TEQUIP:ARG:0 |= 1
SIF EQUIP:ARG:和服
	TEQUIP:ARG:0 |= 1
IF EQUIP:ARG:緊身内衣
	TEQUIP:ARG:0 |= 2
	TEQUIP:ARG:0 |= 16
ENDIF
;上半身
;TEQUIP:1 上半身着衣状況(0=無 1=はだけ可 2=はだけ不可 3=突っ込み不可)
;	(ビットを使いたくない人用 0=無 1=ノーブラ和服 2=ブラのみ 3=和服+ブラ）
;EQUIP:10,11 上半身内衣
IF EQUIP:ARG:上半身内衣１
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:上半身内衣２
	TEQUIP:ARG:1 |= 1
;EQUIP:12,13 上身衣服
IF EQUIP:ARG:上身衣服１
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:上身衣服２
	TEQUIP:ARG:1 |= 1
;EQUIP:14～16 全身外套
IF EQUIP:ARG:連体内衣
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:連衣裙
	TEQUIP:ARG:1 |= 2
SIF EQUIP:ARG:和服
	TEQUIP:ARG:1 |= 1
IF EQUIP:ARG:緊身内衣
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF

;ずらし処理
SIF TEQUIP:ARG:上半身裸露状態
	CLEARBIT TEQUIP:ARG:1, 2
SIF TEQUIP:ARG:4
	CLEARBIT TEQUIP:ARG:1, 1



;特殊衣装への一時きせかえ
;とりあえず衣装セットにあるやつ・もしくは他角色の便装だけ
;着替えたらRESULT==1
@EX_COSTUME(C_ID, TYPE)
#DIM  C_ID
#DIM  EX_COS
#DIMS TYPE
EX_COS = GET_INT(C_ID, "キャラデータ", NO:C_ID, @"%TYPE%衣装")
;#; PRINTFORML {C_ID}特殊衣装変更%TYPE% {EX_COS}
SIF !EX_COS
	RETURN
CALL OKIGAE(C_ID, EX_COS)
;#; CALL SHOW_CLOTHES(C_ID, "現在的衣装")
RETURN 1


;一時お着替え関数
@OKIGAE(C_ID, COS_ID, COS_TYPE)
#DIM C_ID      ;着せ替えを行うキャラクター
#DIM COS_ID    ;衣装セットのID、101以上だとキャラ衣装だとみなされる（キャラ衣装を指定したい時はNO + 100にすること）
#DIMS COS_TYPE ;参照衣装文字列、キャラデータに記載があるのが前提なので、未実装だと真っ裸になる
SIF !TCVAR:C_ID:本来的服装
	TCVAR:C_ID:本来的服装 = CFLAG:C_ID:基本服装セット
CFLAG:C_ID:基本服装セット = COS_ID

IF STRLENS(COS_TYPE)
;文字列指定で便装以外も指定できるように
	;#; PRINTFORML {COS_ID}特殊衣装変更%COS_TYPE%
	CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_角色衣装", COS_ID, COS_TYPE)
	CALL CLOTHES_SETTING_TRAIN(C_ID)
	RETURN
ENDIF

SELECTCASE COS_ID
CASE 100
	CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_おきにいり")
CASE 1 TO 99
	CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_衣装セット", COS_ID)
CASE 101 TO 100 + 人物数量上限
	CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_角色衣装", COS_ID - 100, "便装")
CASEELSE
	THROW 错误的衣装ID {COS_ID}
ENDSELECT
CALL CHANGE_CLOTHES(C_ID, "复原衣服")
CALL CLOTHES_SETTING_TRAIN(C_ID)




@SET_UNDERWEAR(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM DYNAMIC TOP_OVERRIDDEN
#DIM DYNAMIC BOTTOM_OVERRIDDEN



IF TIME_TYPE != "今天的睡衣"
	LOCAL:10 = CFLAG:C_ID:基本服装セット

	IF CHECK_CLO("bra_overridden", C_ID)
		TOP_OVERRIDDEN = 1
		IF LOCAL:10 == 100
			CFLAG:C_ID:207 = CFLAG:C_ID:607
			CFLAG:C_ID:208 = CFLAG:C_ID:608
		ELSEIF LOCAL:10 > 100 ;shouldn't be possible, but covered regardless
			CFLAG:C_ID:207 = GET_INT(C_ID, "キャラデータ", LOCAL:10 - 100, @"便装：上半身内衣１")
			CFLAG:C_ID:208 = GET_INT(C_ID, "キャラデータ", LOCAL:10 - 100, @"便装：上半身内衣２")
		ELSE
			CFLAG:C_ID:207 = GET_INT(C_ID, "衣装セット", LOCAL:10, @"上半身内衣１")
			CFLAG:C_ID:208 = GET_INT(C_ID, "衣装セット", LOCAL:10, @"上半身内衣２")
		ENDIF
	ENDIF
	IF CHECK_CLO("panties_overridden", C_ID)
		TOP_OVERRIDDEN = 1
		IF LOCAL:10 == 100
			CFLAG:C_ID:205 = CFLAG:C_ID:605
			CFLAG:C_ID:206 = CFLAG:C_ID:606
		ELSEIF LOCAL:10 > 100 ;shouldn't be possible, but covered regardless
			CFLAG:C_ID:205 = GET_INT(C_ID, "キャラデータ", LOCAL:10 - 100, @"便装：下半身内衣１")
			CFLAG:C_ID:206 = GET_INT(C_ID, "キャラデータ", LOCAL:10 - 100, @"便装：下半身内衣２")
		ELSE
			CFLAG:C_ID:205 = GET_INT(C_ID, "衣装セット", LOCAL:10, @"下半身内衣１")
			CFLAG:C_ID:206 = GET_INT(C_ID, "衣装セット", LOCAL:10, @"下半身内衣２")
		ENDIF
	ENDIF
ENDIF

CALL SET_PANTS(C_ID, TIME_TYPE, BOTTOM_OVERRIDDEN)
CALL SET_BRA(C_ID, TIME_TYPE, TOP_OVERRIDDEN)

;パンツセッティング関数
;CFLAGに設定された衣装を上書きする
;パンツ指定がある場合には下半身内衣１を解除し
;下半身内衣１が指定されている場合にはパンツよりもそれを優先する
@SET_PANTS(C_ID, TIME_TYPE, OVERRIDDEN)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  P_ID
#DIM  OVERRIDDEN
IF TIME_TYPE == "今天的睡衣"
	IF CFLAG:C_ID:睡衣胖次 == 100
		P_ID =  0
		CFLAG:C_ID:245 = 0
	ELSEIF CFLAG:C_ID:睡衣胖次
		P_ID = CFLAG:C_ID:睡衣胖次
		CFLAG:C_ID:245 = 0
	ELSEIF CFLAG:C_ID:245
		CFLAG:C_ID:246 = 0
		RETURN
	ELSEIF CFLAG:C_ID:246
		CFLAG:C_ID:245 = 0
		; CFLAG:C_ID:睡衣胖次 = CFLAG:C_ID:246
		RETURN
	ELSEIF !GETBIT(TALENT:C_ID:2, 0)
		P_ID = CLOTHES_ID("下半身内衣２", "四角裤")
	ELSEIF !CFLAG:C_ID:245
		P_ID = 今天穿的内裤(C_ID)
	ENDIF
	;PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%のぱじゃまぱんつ：%GET_STR(C_ID, "下半身内衣_ずらし可能", P_ID, "名前")%
	; CFLAG:C_ID:睡衣胖次 = P_ID
	CFLAG:C_ID:246 = P_ID
	RETURN
ENDIF

IF OVERRIDDEN
	IF CFLAG:C_ID:205
		CFLAG:C_ID:206 = 0
		RETURN
	ELSE
		CFLAG:C_ID:205 = 0
		CFLAG:C_ID:きょうのぱんつ = CFLAG:C_ID:206
		RETURN
	ENDIF
ELSEIF CFLAG:C_ID:指定胖次 == 100
;没穿内裤指定
	P_ID =  0
	CFLAG:C_ID:205 = 0
ELSEIF CFLAG:C_ID:指定胖次
;指定胖次があるなら下半身内衣１を剥ぐ
	CFLAG:C_ID:205 = 0
	P_ID = CFLAG:C_ID:指定胖次
ELSEIF CFLAG:C_ID:205
;下半身内衣１が指定なら胖次は無し
	CFLAG:C_ID:206 = 0
	RETURN
ELSEIF CFLAG:C_ID:206
;下半身内衣２が指定なら胖次抽選は無し
	CFLAG:C_ID:205 = 0
	CFLAG:C_ID:きょうのぱんつ = CFLAG:C_ID:206
	RETURN
ELSEIF !GETBIT(TALENT:C_ID:2, 0)
;男人ならとりあえず四角裤で
	P_ID = CLOTHES_ID("下半身内衣２", "四角裤")
ELSEIF !CFLAG:C_ID:205
;上記以外で胖次抽選
	P_ID = 今天穿的内裤(C_ID)
ENDIF
CFLAG:C_ID:きょうのぱんつ = P_ID
CFLAG:C_ID:206 = P_ID

@SET_BRA(C_ID, TIME_TYPE, OVERRIDDEN)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  DYNAMIC B_ID
#DIM  OVERRIDDEN
IF TIME_TYPE == "今天的睡衣"
	IF CFLAG:C_ID:247
		CFLAG:C_ID:248 = 0
		B_ID = CFLAG:C_ID:247
	ELSEIF CFLAG:C_ID:248
	ELSEIF !GETBIT(TALENT:C_ID:2, 0)
		B_ID = CLOTHES_ID("上半身内衣１", "Ｔ恤衫")
	ELSE
		B_ID = 今日の文胸(C_ID, CFLAG:C_ID:246)
	ENDIF
	CFLAG:C_ID:睡衣ブラ = B_ID
	CFLAG:C_ID:247 = B_ID
	RETURN
ENDIF

IF OVERRIDDEN
	IF CFLAG:C_ID:207
		CFLAG:C_ID:208 = 0
		B_ID = CFLAG:C_ID:207
	ENDIF
ELSEIF CFLAG:C_ID:207
	CFLAG:C_ID:208 = 0
	B_ID = CFLAG:C_ID:207
ELSEIF CFLAG:C_ID:208
ELSEIF !GETBIT(TALENT:C_ID:2, 0)
	B_ID = CLOTHES_ID("上半身内衣１", "Ｔ恤衫")
ELSE
	B_ID = 今日の文胸(C_ID, CFLAG:C_ID:きょうのぱんつ)
ENDIF
CFLAG:C_ID:きょうのブラ = B_ID
CFLAG:C_ID:207 = B_ID


;今日の衣装決定関数
;必ず衣装→睡衣でセットで使用こと
@SELECT_CLOTHES(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  衣装C_ID
#DIMS 衣装TYPE

VARSET 衣装C_ID
VARSET 衣装TYPE
;先に睡衣の設定
IF TIME_TYPE == "今天的睡衣"
	SELECTCASE CFLAG:C_ID:睡衣指定
	CASE 1000
		CFLAG:C_ID:246 = 0
		CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
		RETURN
	CASE 300
	;個別設定
		CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_套装")
		CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
		RETURN
	CASE 301
	;パンイチ
		衣装TYPE '= "パンイチ"
	CASE 302
	;全裸
		衣装TYPE '= "全裸"
	CASE 1 TO 99
	;衣装セットの指定
		衣装TYPE '= GET_STR(C_ID, "衣装セット", CFLAG:C_ID:睡衣指定, "名前")
	CASE 0
	;默认服装による設定
		IF !(C_ID == MASTER)
			衣装C_ID = NO:C_ID
			衣装TYPE '= GET_STR(C_ID, "キャラデータ", 衣装C_ID, TIME_TYPE)
		ELSEIF FLAG:扮演
			衣装C_ID = FLAG:扮演
			衣装TYPE '= GET_STR(C_ID, "キャラデータ", 衣装C_ID, TIME_TYPE)
		ENDIF
	ENDSELECT
	;未設定なら睡裤
	SIF !STRLENS(衣装TYPE)
		衣装TYPE '= "睡衣（裤子）套"
	CALL CHANGE_CLOTHES(C_ID, TIME_TYPE, 衣装C_ID, 衣装TYPE)
	SIF 衣装TYPE == "パンイチ"
		JUMP SET_PANTS(C_ID, TIME_TYPE)
	SIF !(衣装TYPE == "全裸")
		CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
	RETURN
ENDIF

;今日の衣装、明日の衣装
SELECTCASE CFLAG:C_ID:基本服装セット
CASE 1000
	CFLAG:C_ID:206 = 0
	CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
	RETURN
CASE 100
;個別設定
	CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_套装")
	CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
CASE 1 TO 99
;衣装セットの指定
	衣装TYPE '= GET_STR(C_ID, "衣装セット", CFLAG:C_ID:基本服装セット, "名前")
	;念のため
	SIF !STRLENS(衣装TYPE)
		GOTO DEFAULT
CASE 101 TO 100 + 人物数量上限
;特定角色の衣装を参照するとき
	衣装C_ID = CFLAG:C_ID:基本服装セット - 100
	衣装TYPE '= "便装"
	;念のため
	SIF !EXISTCSV(衣装C_ID)
		GOTO DEFAULT
CASE 0
;默认服装による設定
	$DEFAULT
	IF !(C_ID == MASTER)
		衣装C_ID = NO:C_ID
		衣装TYPE '= GET_STR(C_ID, "キャラデータ", 衣装C_ID, TIME_TYPE)
	ELSEIF FLAG:扮演
		衣装C_ID = FLAG:扮演
		衣装TYPE '= GET_STR(C_ID, "キャラデータ", 衣装C_ID, TIME_TYPE)
	ELSEIF GETBIT(TALENT:C_ID:2, 0)
	;まんまんがあるときは女装を基本とする
		衣装TYPE '= "一套女装"
	ELSE
	;♂は男装を基本とする
		衣装TYPE '= "一套男装"
	ENDIF
	;未設定なら便装
	SIF !STRLENS(衣装TYPE)
		衣装TYPE '= "便装"
ENDSELECT
CALL CHANGE_CLOTHES(C_ID, TIME_TYPE, 衣装C_ID, 衣装TYPE)
CALL SET_UNDERWEAR(C_ID, TIME_TYPE)



;衣装更新関数
@CHANGE_CLOTHES(C_ID, TYPE, 衣装C_ID, 衣装TYPE)
#DIM  C_ID
#DIMS TYPE
#DIM  衣装C_ID
#DIMS 衣装TYPE
#DIM  衣装SET_ID
衣装SET_ID = 0
SIF STRLENS(衣装TYPE)
	衣装SET_ID = OBJNAME_TO_ID(C_ID, "GET", "衣装セット", 衣装TYPE)
SELECTCASE TYPE
CASE "今天的睡衣"
	SIF 衣装TYPE == "パンイチ"
		JUMP CTRL_CLOTHES_SET(C_ID, "睡衣的变更_パンイチ", 衣装SET_ID)
	SIF 衣装TYPE == "全裸"
		JUMP CTRL_CLOTHES_SET(C_ID, "睡衣的变更_全裸", 衣装SET_ID)
	
	IF 衣装SET_ID > 0
		CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_衣装セット", 衣装SET_ID)
	ELSE
		CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_角色衣装", 衣装C_ID, 衣装TYPE)
	ENDIF
	
CASE "今日の衣装", "明日の衣装"
	IF 衣装SET_ID > 0
		CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_衣装セット", 衣装SET_ID)
	ELSE
		CALL CTRL_CLOTHES_SET(C_ID, "便装の変更_角色衣装", 衣装C_ID, 衣装TYPE)
	ENDIF
CASE "复原衣服"
	;#;SIF C_ID == TARGET
	;#;	PRINTFORML TARGET复原衣服
	IF 睡眠時間(C_ID)
		CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_睡衣")
		CFLAG:C_ID:睡衣 = 1
	ELSE
		IF TCVAR:C_ID:EVENT_CLOTHING_SET
			SELECTCASE TCVAR:C_ID:EVENT_CLOTHING_SET
				CASE IS < 100
					FOR LOCAL, 1, 23
						EQUIP:C_ID:LOCAL = GET_INT(C_ID, "衣装セット", TCVAR:C_ID:EVENT_CLOTHING_SET, EQUIPNAME:LOCAL)
					NEXT
				CASE 101 TO 100 + 人物数量上限
					FOR LOCAL, 1, 23
						EQUIP:C_ID:LOCAL = GET_INT(C_ID, "キャラデータ", TCVAR:C_ID:EVENT_CLOTHING_SET - 100, @"便装：%EQUIPNAME:LOCAL%")
					NEXT
			ENDSELECT
		ELSE
			SELECTCASE CFLAG:C_ID:衣装一時変更
				CASE 0
					SELECTCASE CFLAG:C_ID:基本服装セット
						CASE IS < 100
							FOR LOCAL, 1, 23
								SIF !GROUPMATCH(LOCAL, 6, 7)							
									CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "衣装セット", CFLAG:C_ID:基本服装セット, EQUIPNAME:LOCAL)
							NEXT
						CASE 100
							FOR LOCAL, 1, 23
								SIF !GROUPMATCH(LOCAL, 6, 7)		
									CFLAG:C_ID:(200 + LOCAL) = CFLAG:C_ID:(600 + LOCAL)
							NEXT
						CASE 101 TO 100 + 人物数量上限
							FOR LOCAL, 1, 23
								SIF !GROUPMATCH(LOCAL, 6, 7)		
									CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "キャラデータ", CFLAG:C_ID:基本服装セット - 100, @"便装：%EQUIPNAME:LOCAL%")
							NEXT
					ENDSELECT

					CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_便装")
				CASE 100
					CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_おきにいり")
				CASE 1 TO 99
					CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_衣装セット", CFLAG:C_ID:衣装一時変更)
				CASE 101 TO 100 + 人物数量上限
					CALL CTRL_CLOTHES_SET(C_ID, "現在衣装の変更_キャラ衣装", CFLAG:C_ID:衣装一時変更 - 100, "便装")
			ENDSELECT
		ENDIF
		CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")
		CFLAG:C_ID:睡衣 = 0
	ENDIF
ENDSELECT



[SKIPSTART]
衣装セットのコントロール関数
CFLAGとEQUIPのやりとりを行う
EQUIP:0の扱いとしては
・@CLOTHES_SETTING_TRAINの冒頭に更新処理を仕込んだ
・SAVE/LOADの範囲を1～22までにする（EQUIP:0は保存されない）
これでだいたいいいと思う
[SKIPEND]
@CTRL_CLOTHES_SET(C_ID, TYPE, ARG, ARGS)
#DIM  C_ID
#DIMS TYPE
SELECTCASE TYPE
; ;リセット用に記録する
;暂时封闭
; CASE "現在衣装の保存_便装"
; 	FOR LOCAL, 1, 23
; 		CFLAG:C_ID:(200 + LOCAL) = EQUIP:C_ID:LOCAL
; 	NEXT
; CASE "現在衣装の保存_睡衣"
; 	FOR LOCAL, 1, 23
; 		CFLAG:C_ID:(240 + LOCAL) = EQUIP:C_ID:LOCAL
; 	NEXT
CASE "現在衣装の保存_おきにいり"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = EQUIP:C_ID:LOCAL
	NEXT
	;CFLAG:ARG:套装 = 1

	LOCAL = CURRENT_CLOTHES_SET(C_ID)
	IF INRANGE(LOCAL, 0, 99)
		CFLAG:C_ID:FAVORITE_OVERRIDE_UNDERWEAR = GET_INT(C_ID, "衣装セット", LOCAL, "OVERRIDE_UNDERWEAR_TOP") || GET_INT(C_ID, "衣装セット", LOCAL, "OVERRIDE_UNDERWEAR_BOTTOM")
	ELSE
		CFLAG:C_ID:FAVORITE_OVERRIDE_UNDERWEAR = 0
	ENDIF
;リセット
CASE "套装のリセット"
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(600 + LOCAL) = 0
	NEXT
	; SIF CFLAG:C_ID:基本服装セット == 100
		CFLAG:C_ID:基本服装セット = 100 + C_ID
	; SIF CFLAG:C_ID:睡衣指定 == 300
		CFLAG:C_ID:睡衣指定 = 0
	;CFLAG:C_ID:套装 = 0

	CFLAG:C_ID:FAVORITE_OVERRIDE_UNDERWEAR = 0

;セットした衣装をロード
;ここらへんあくまでロード処理であって睡衣フラグはいじらないので注意ね
CASE "現在衣装の変更_便装"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(200 + LOCAL)
	NEXT
	CFLAG:C_ID:衣装一時変更 = 0
	CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")


CASE "現在衣装の変更_睡衣"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(240 + LOCAL)
	NEXT
	CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")
	; IF CFLAG:C_ID:睡衣胖次 == 100
	; 	EQUIP:C_ID:下半身内衣１ = 0
	; 	EQUIP:C_ID:下半身内衣２ = 0
	; 	CFLAG:C_ID:245 = 0
	; 	CFLAG:C_ID:246 = 0
	; 	;CFLAG:C_ID:睡衣胖次 = 0
	; 	SIF CFLAG:C_ID:没穿内裤 != 2
	; 		CFLAG:C_ID:没穿内裤 = 1
	; ELSEIF CFLAG:C_ID:没穿内裤 && CFLAG:C_ID:睡衣胖次
	; 	EQUIP:C_ID:下半身内衣１ = 0
	; 	EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:睡衣胖次
	; 	SIF !CFLAG:C_ID:245
	; 		CFLAG:C_ID:246 = CFLAG:C_ID:睡衣胖次
	; 	;CFLAG:C_ID:睡衣胖次 = 0
	; 	SIF CFLAG:C_ID:没穿内裤 != 2
	; 		CFLAG:C_ID:没穿内裤 = 0
	; ELSEIF CFLAG:C_ID:没穿内裤 && (EQUIP:C_ID:下半身内衣１ || EQUIP:C_ID:下半身内衣２)
	; 	EQUIP:C_ID:下半身内衣１ = 0
	; 	EQUIP:C_ID:下半身内衣２ = 0
	; 	CFLAG:C_ID:245 = 0
	; 	CFLAG:C_ID:246 = 0
	; ENDIF
	CFLAG:C_ID:衣装一時変更 = 0

CASE "現在衣装の変更_おきにいり"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(600 + LOCAL)
	NEXT
	; IF CFLAG:C_ID:FAVORITE_OVERRIDE_UNDERWEAR
	; ELSEIF CFLAG:C_ID:没穿内裤
	; 	EQUIP:C_ID:下半身内衣１ = 0
	; 	EQUIP:C_ID:下半身内衣２ = 0
	; ELSEIF EQUIP:C_ID:下半身内衣１
	; 	EQUIP:C_ID:下半身内衣２ = 0
	; ELSEIF EQUIP:C_ID:下半身内衣２
	; 	EQUIP:C_ID:下半身内衣１ = 0
	; ELSEIF CFLAG:C_ID:睡衣
	; 	EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:246
	; ELSE
	; 	EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:206
	; ENDIF
	CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")
	; CFLAG:C_ID:衣装一時変更 = 100
	
CASE "現在衣装の変更_角色衣装"
;ARGで指定された角色の衣装を参照する
	CFLAG:C_ID:衣装一時変更 = 100 + ARG
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = GET_INT(C_ID, "キャラデータ", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
	CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")

CASE "現在衣装の変更_衣装セット"
;ARGで指定されたIDの衣装セットを参照する
    ;as this should never happen, show a warning if a nonexisting clothing set is called
    SIF !GET_EXIST("衣装セット", ARG)
        PRINTFORML ＊WARNING＊: Clothing set {ARG} does not exist. %CALLNAME:C_ID% will be nude.
	CFLAG:C_ID:衣装一時変更 = ARG
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = GET_INT(C_ID, "衣装セット", ARG, EQUIPNAME:LOCAL)
	NEXT
	CALL CTRL_CLOTHES_SET(C_ID, "内衣一括設定")
CASE "内衣一括設定"
	SIF !GET_INT(C_ID, "衣装セット", CURRENT_CLOTHES_SET(C_ID), "OVERRIDE_UNDERWEAR_BOTTOM")
	CALL CTRL_CLOTHES_SET(C_ID, "胖次一括設定")
	SIF !GET_INT(C_ID, "衣装セット", CURRENT_CLOTHES_SET(C_ID), "OVERRIDE_UNDERWEAR_TOP")
	CALL CTRL_CLOTHES_SET(C_ID, "ブラ一括設定")
CASE "胖次一括設定"
	IF !CFLAG:C_ID:睡衣
		IF CFLAG:C_ID:没穿内裤
			EQUIP:C_ID:下半身内衣１ = 0
			EQUIP:C_ID:下半身内衣２ = 0
		ELSEIF EQUIP:C_ID:下半身内衣１
			EQUIP:C_ID:下半身内衣２ = 0
		ELSE
			EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:きょうのぱんつ
		ENDIF
	ELSE
		IF CFLAG:C_ID:没穿内裤
			EQUIP:C_ID:下半身内衣１ = 0
			EQUIP:C_ID:下半身内衣２ = 0
		ELSEIF EQUIP:C_ID:下半身内衣１
			EQUIP:C_ID:下半身内衣２ = 0
		ELSEIF CFLAG:C_ID:睡衣胖次 == 100
			EQUIP:C_ID:下半身内衣２ = 0
		ELSEIF CFLAG:C_ID:睡衣胖次 > 0
			EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:睡衣胖次
		ELSE
			EQUIP:C_ID:下半身内衣２ = CFLAG:C_ID:246
		ENDIF
	ENDIF
CASE "ブラ一括設定"
	IF !CFLAG:C_ID:睡衣
		EQUIP:C_ID:上半身内衣１ = CFLAG:C_ID:きょうのブラ
	ELSE
		EQUIP:C_ID:上半身内衣１ = CFLAG:C_ID:247
	ENDIF

;脱衣処理
CASE "現在衣装の変更_全裸"
	FOR LOCAL, 1, 23
		SIF LOCAL == 3 && FLAG:時間停止
			CONTINUE
		SIF LOCAL == 4 && EQUIP:C_ID:靴
			CONTINUE
		EQUIP:C_ID:LOCAL = 0
	NEXT

CASE "現在衣装の変更_内衣姿"
;このコードなんか気持ち悪いんだけどとりあえずこのままで
	FOR LOCAL, 18, 10, -1
		EQUIP:C_ID:LOCAL = 0
	NEXT
	EQUIP:C_ID:帽子 = 0
	TEQUIP:C_ID:上半身着衣状況 = 2
	TEQUIP:C_ID:上衣脱衣完毕 = 1
	
CASE "現在衣装の変更_半脱"
	EQUIP:C_ID:上半身内衣１ = 0
	TEQUIP:C_ID:上半身裸露状態 = 1
	SIF EQUIP:C_ID:裤子
		EQUIP:C_ID:裤子 = 0



CASE "便装の変更_角色衣装"
;ARGで指定された角色のARGSで指定された衣装を参照する
	CFLAG:C_ID:基本服装セット = 100 + ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "キャラデータ", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT

CASE "便装の変更_衣装セット"
;ARGで指定されたIDの衣装セットを参照する
	CFLAG:C_ID:基本服装セット = ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "衣装セット", ARG, EQUIPNAME:LOCAL)
	NEXT
CASE "便装の変更_套装"
	CFLAG:C_ID:基本服装セット = 100
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = CFLAG:C_ID:(600 + LOCAL)
	NEXT

CASE "睡衣的变更_角色衣装"
;ARGで指定された角色のARGSで指定された衣装を参照する
	CFLAG:C_ID:睡衣指定 = 100 + ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = GET_INT(C_ID, "キャラデータ", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
CASE "睡衣的变更_衣装セット"
	CFLAG:C_ID:睡衣指定 = ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = GET_INT(C_ID, "衣装セット", ARG, EQUIPNAME:LOCAL)
	NEXT
CASE "睡衣的变更_套装"
	CFLAG:C_ID:睡衣指定 = 300
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = CFLAG:C_ID:(600 + LOCAL)
	NEXT
CASE "睡衣的变更_パンイチ"
	CFLAG:C_ID:睡衣指定 = 301
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(240 + LOCAL) = 0
	NEXT
	CFLAG:C_ID:246 = CFLAG:C_ID:睡衣胖次
	SIF CFLAG:C_ID:246 == 100
		CFLAG:C_ID:246 = 0
	
CASE "睡衣的变更_全裸"
	CFLAG:C_ID:睡衣指定 = 302
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(240 + LOCAL) = 0
	NEXT
	CFLAG:C_ID:睡衣胖次 = 100

CASE "套装の変更_角色衣装"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = GET_INT(C_ID, "キャラデータ", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
	;CFLAG:C_ID:套装 = 1

CASE "套装の変更_衣装セット"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = GET_INT(C_ID, "衣装セット", ARG, EQUIPNAME:LOCAL)
	NEXT
	;CFLAG:C_ID:套装 = 1
ENDSELECT


TRYCCALLFORM KOJO_ACTIVE_INFO(C_ID)
	TRYCALLFORM CTRL_CLOTHES_SET_K{C_ID}%RESULTS%
CATCH
ENDCATCH


;特定角色の衣装設定の内容を表示する
@SHOW_CHARA_CLOTHES(C_ID, PAGE = 0)
#DIM  C_ID
#DIMS CONST 表示衣装, 4 = "現在的衣装", "今日の衣装", "今天的睡衣", "套装"
#DIM  PAGE
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%表示衣装:PAGE%
SELECTCASE CFLAG:C_ID:指定胖次
CASE 0
	PRINTFORML 　現在的胖次設定：無指定
CASE 100
	PRINTFORML 　現在的胖次設定：指定不穿内裤
CASEELSE
	PRINTFORML 　現在的胖次設定：%GET_STR(C_ID, "下半身内衣_ずらし可能", CFLAG:C_ID:指定胖次, "名前")%
ENDSELECT
PRINTFORML 
CALL SHOW_CLOTHES(C_ID, 表示衣装:PAGE)
PRINTFORML 
PRINTBUTTON @"[1] - %表示衣装:((PAGE + 4 - 1) % 4), 14%", 1
PRINTFORM 　　　　　　　　　　
PRINTBUTTON @"[3] - %表示衣装:((PAGE + 4 + 1) % 4), 14%", 3
PRINTFORML 
PRINTBUTTON "[0] - 返回", 0
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		PAGE = (PAGE + 4 - 1) % 4
	CASE 3
		PAGE = (PAGE + 4 + 1) % 4
	CASEELSE
		REUSELASTLINE 無效輸入
		CONTINUE
	ENDSELECT
	RESTART
LOOP 1


;衣装系の表示関数
@SHOW_CLOTHES(C_ID, TYPE, ID)
#DIM  C_ID
#DIMS TYPE
#DIM  ID
;#DIM  装備部位
#DIM  DYNAMIC CLOTHES, 23
#DIMS 表示内容
{
#DIMS CONST 表示部位, 23 = "", 
	"帽子", "飾品", "腕部装束", 
	"外搭",
	"上身衣服１","上身衣服２", 
	"和服", "連衣裙",
	"裙子", "下身衣服", "裤子", 
	"緊身内衣", "連体内衣", 
	"上半身内衣１", "上半身内衣２", 
	"下半身内衣１", "下半身内衣２", 
	"其他１", "其他２", "其他３", 
	"襪子", "靴"
}
{
#DIMS CONST 表示ラベル, 23 = "", 
	"　装備:頭　　　", "　装備:装飾　　", "　装備:腕　　　", 
	"　装備:外搭　　",
	"　装備:套头衫　", "　装備:衬衫　　", 
	"　装備:和服　　", "　装備:全身服　",
	"　装備:裙子　　", "　装備:下身衣物", "　装備:裤子　　", 
	"　装備:緊身内衣", "　装備:連體内衣", 
	"　装備:内衣(上)", "　装備:内衣(上)", 
	"　装備:内衣(下)", "　装備:内衣(下)", 
	"　装備:付属　　", "　装備:付属　　", "　装備:付属　　", 
	"　装備:襪子　　", "　装備:鞋　　　"
}

SELECTCASE TYPE
CASE "現在的衣装"
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = EQUIP:C_ID:(FINDELEMENT(EQUIPNAME, 表示部位:LOCAL))
		;ノーブラ、没穿内裤用の例外処理
		;ここから
		; IF 装備部位 == "上半身内衣１" && TALENT:C_ID:性別 != 2 && EQUIP:C_ID:上半身内衣２ == 0 && EQUIP:C_ID:上半身内衣１ == 0 && EQUIP:C_ID:連体内衣 == 0 && EQUIP:C_ID:緊身内衣 == 0 && EQUIP:C_ID:0
		; 	IF 上半身内衣表示(C_ID) == 1
		; 		PRINTFORML 　装備:内衣(上)[沒穿]
		; 	ELSEIF 下半身内衣表示(C_ID) == 1
		; 		PRINTFORML 　装備:内衣(上)[？？？？？]
		; 	ENDIF
		; ENDIF
		; IF 装備部位 == "下半身内衣１" && EQUIP:C_ID:下半身内衣２ == 0 && EQUIP:C_ID:下半身内衣１ == 0 && EQUIP:C_ID:連体内衣 == 0 && EQUIP:C_ID:緊身内衣 == 0 && EQUIP:C_ID:0
		; 	IF !上半身内衣表示(C_ID) && !下半身内衣表示(C_ID)
		; 		PRINTFORML 　装備:内衣　　[？？？？？]
		; 		CONTINUE
		; 	ELSEIF !下半身内衣表示(C_ID)
		; 		PRINTFORML 　装備:内衣(下)[？？？？？]
		; 	ELSE
		; 		PRINTFORML 　装備:内衣(下)[沒穿]
		; 	ENDIF
		; ENDIF
		IF STRFIND(表示部位:LOCAL, "上半身内衣") != -1
			表示内容 '= BRANAME(CLOTHES:LOCAL, C_ID)
		ELSEIF STRFIND(表示部位:LOCAL, "下半身内衣") != -1
			表示内容 '= PANTSNAME(CLOTHES:LOCAL, C_ID)
		ELSE
			表示内容 '= CLOTHNAME(FINDELEMENT(EQUIPNAME, 表示部位:LOCAL), CLOTHES:LOCAL)
		ENDIF
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE
									; IF STRLENS(CSTR:C_ID:(50 + 装備部位)) && !CFLAG:C_ID:睡衣
									; 	表示内容 '= CSTR:C_ID:(50 + 装備部位)
		;胖次は専用処理が必要
		; ELSEIF 装備部位 == "上半身内衣１" && !(C_ID == MASTER) && !上半身内衣表示(C_ID)
		; 	SIF !下半身内衣表示(C_ID)
		; 	 	CONTINUE
		; 	表示内容 '= "？？？？？"
		; ELSEIF 装備部位 == "下半身内衣２" && !(C_ID == MASTER) && !下半身内衣表示(C_ID)
		; 	IF !上半身内衣表示(C_ID)
		; 		PRINTFORML 　装備:内衣　　[？？？？？]
		; 		CONTINUE
		; 	ELSE
		; 		表示内容 '= "？？？？？"
		; 	ENDIF
		; ELSE
		; 	IF 表示部位:LOCAL == "上半身内衣１"
		; 		表示内容 '= BRANAME(EQUIP:C_ID:上半身内衣１, C_ID)
		; 	ELSEIF 表示部位:LOCAL == "下半身内衣２"
		; 		表示内容 '= PANTSNAME(EQUIP:C_ID:下半身内衣２, C_ID)
		; 	ELSE
		; 		表示内容 '= CLOTHNAME(装備部位, EQUIP:C_ID:装備部位)
		; 	ENDIF
		; ENDIF
		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT


CASE "今日の衣装", "便装"
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = CFLAG:C_ID:(200 + FINDELEMENT(EQUIPNAME, 表示部位:LOCAL))
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT


CASE "今天的睡衣"
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = CFLAG:C_ID:(240 + FINDELEMENT(EQUIPNAME, 表示部位:LOCAL))
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT


CASE "套装"
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = CFLAG:C_ID:(600 + FINDELEMENT(EQUIPNAME, 表示部位:LOCAL))
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT


CASE "角色便装"
;角色番号IDの便装の内容を表示する
	IF !EXISTCSV(ID)
		PRINTFORML 不存在可以参照的角色
		RETURN
	ENDIF
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ID)%的便装
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = GET_INT(C_ID, "キャラデータ", ID, @"便装：%表示部位:LOCAL%")
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT


CASE "衣装セット"
;衣装セットIDの内容を表示する
	IF GET_STR(C_ID, "衣装セット" , ID, "名前") == ""
		PRINTFORML 不存在可以参照的衣装
		RETURN
	ENDIF
	PRINTFORML %GET_STR(C_ID, "衣装セット" , ID, "名前")%
	FOR LOCAL, 1, 23
		;装備部位のID取得
		;装備部位 = FINDELEMENT(EQUIPNAME, 表示部位:LOCAL)
		SIF 表示部位:LOCAL == "下身衣服"
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = GET_INT(C_ID, "衣装セット", ID, 表示部位:LOCAL)
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		IF (表示部位:LOCAL == "上半身内衣１"  && C_ID != MASTER && !上半身内衣表示(C_ID)) || (表示部位:LOCAL == "下半身内衣１" && C_ID != MASTER && !下半身内衣表示(C_ID))
			PRINTFORML %表示ラベル:LOCAL%[？？？？？]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣１" || 表示部位:LOCAL == "下半身内衣１") && CLOTHES:LOCAL > 0
			PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
			LOCAL += 1
			CONTINUE
		ELSEIF (表示部位:LOCAL == "上半身内衣２" || 表示部位:LOCAL == "下半身内衣２") && CLOTHES:LOCAL == 0
			PRINTFORML %表示ラベル:LOCAL%[没穿]
		ENDIF
		SIF 表示部位:LOCAL == "連衣裙" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全上半身[没穿]
		SIF 表示部位:LOCAL == "裤子" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1), CLOTHES:(LOCAL - 2), CLOTHES:(LOCAL - 3), CLOTHES:(LOCAL - 4))
			PRINTFORML 　装備:全下半身[没穿]
		SIF 表示部位:LOCAL == "靴" && !MAX(CLOTHES:LOCAL, CLOTHES:(LOCAL - 1))
			PRINTFORML 　装備:足部　　[裸足]
		SIF !CLOTHES:LOCAL
			CONTINUE

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT
CASE "暂存状态"
	FOR LOCAL, 1, 23
		SIF GROUPMATCH(表示部位:LOCAL, "下身衣服", "上半身内衣２", "下半身内衣１")
			CONTINUE
		SIF FINDELEMENT(EQUIPNAME, 表示部位:LOCAL) < 0
			THROW 不存在的装備部位%表示部位:LOCAL%
		CLOTHES:LOCAL = TEMP_CLOTHES:C_ID:(FINDELEMENT(EQUIPNAME, 表示部位:LOCAL))
		表示内容 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(表示部位:LOCAL), CLOTHES:LOCAL, "名前")
		SIF !CLOTHES:LOCAL
			表示内容 = 无

		PRINTFORML %表示ラベル:LOCAL%[%表示内容%]
	NEXT
ENDSELECT

@SPECIAL_OUTFIT(CHARA)
#FUNCTION
#DIM CHARA
IF FESTIVAL() == "兔子之日"
	RETURNF 55
ELSEIF FLAG:ファッション
	RETURNF FLAG:ファッション
ENDIF
RETURNF GET_INT(0, "キャラデータ", CHARA, "特殊衣装着用")


@UNIQUE_UNDERWARE_TOP(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "キャラデータ", CHARA, "固有上半身内衣名")

@UNIQUE_UNDERWARE_BOTTOM(CHARA)
#FUNCTIONS
#DIM CHARA
SIF GET_STR(0, "キャラデータ", CHARA, "固有下半身内衣名") == ""
	RETURNF "新品の胖次"
RETURNF GET_STR(0, "キャラデータ", CHARA, "固有下半身内衣名")

@UNIQUE_UNDERWARE_ADJ(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "キャラデータ", CHARA, "固有内衣形容詞")

@UNIQUE_UNDERWARE_ABLE(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "キャラデータ", CHARA, "固有内衣着用条件")

@UNIQUE_UNDERWARE_TRANSPARENCY_FRONT(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "キャラデータ", CHARA, "固有内衣半透明(前)")

@UNIQUE_UNDERWARE_TRANSPARENCY_BACK(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "キャラデータ", CHARA, "固有内衣半透明(後)")

@UNIQUE_UNDERWARE_TOP_THICK(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "キャラデータ", CHARA, "固有内衣布料厚度")

@上半身内衣表示(C_ID)
#FUNCTION
#DIM C_ID

SIF !CFLAG:C_ID:睡眠 && CFLAG:C_ID:上半身内衣確認
	RETURNF 1
SIF CFLAG:C_ID:睡眠 && CFLAG:C_ID:睡衣上半身内衣確認
	RETURNF 1
RETURNF 0

@下半身内衣表示(C_ID)
#FUNCTION
#DIM C_ID

SIF !CFLAG:C_ID:睡眠 && CFLAG:C_ID:下半身内衣確認
	RETURNF 1
SIF CFLAG:C_ID:睡眠 && CFLAG:C_ID:睡衣下半身内衣確認
	RETURNF 1
RETURNF 0

@上半身内衣チェック(C_ID)
#FUNCTION
#DIM C_ID

IF !CFLAG:C_ID:睡眠
	CFLAG:C_ID:上半身内衣確認 = 1
ELSE
	CFLAG:C_ID:睡衣上半身内衣確認 = 1
ENDIF

@下半身内衣チェック(C_ID)
#FUNCTION
#DIM C_ID

IF !CFLAG:C_ID:睡眠
	CFLAG:C_ID:下半身内衣確認 = 1
ELSE
	CFLAG:C_ID:睡衣下半身内衣確認 = 1
ENDIF

@内衣確認リセット(C_ID)
#DIM C_ID

CFLAG:C_ID:上半身内衣確認 = 0
CFLAG:C_ID:下半身内衣確認 = 0
CFLAG:C_ID:睡衣上半身内衣確認 = 0
CFLAG:C_ID:睡衣下半身内衣確認 = 0

RETURN

;蛇版魔改重做函数
@衣服选择列表(换装选择, C_ID, PAGE, 部位选择屏蔽)
#DIM DYNAMIC 换装选择
#DIM C_ID
#DIM REF PAGE
#DIM REF 部位选择屏蔽
SIF PAGE < 0
	PAGE = 0
IF VARSIZE("部位选择屏蔽") == 1
	IF 换装选择 == 部位选择屏蔽
		FOR LOCAL, 1, 25
			IF LOCAL != 部位选择屏蔽
				换装选择 = LOCAL
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSEIF VARSIZE("部位选择屏蔽") > 1
	IF 部位选择屏蔽:换装选择 == 1
		FOR LOCAL, 1, 25
			IF !部位选择屏蔽:LOCAL
				换装选择 = LOCAL
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	THROW WARNING:UNEXPECTED ERROR DETECTED!
ENDIF
IF 换装选择 == 23
	FOR LOCAL, 20 * PAGE + 1, 20 * (PAGE + 1) + 1
		IF PAGE != 0 && LOCAL == 20 * PAGE + 1 && !STRLENS(GET_STR(C_ID, "衣装セット", LOCAL, "名前"))
			PAGE -= 1
			LOCAL = 20 * PAGE + 1
		ENDIF
		SIF GET_EXIST("衣装セット", LOCAL) && STRLENS(GET_STR(C_ID, "衣装セット", LOCAL, "名前")) != 1
			PRINTFORML [{LOCAL, 2}] - %GET_STR(C_ID, "衣装セット", LOCAL, "名前")%
	NEXT
ELSEIF 换装选择 == 24
	FOR LOCAL, 20 * PAGE + 1, 20 * (PAGE + 1) + 1
		IF PAGE > 4
			PAGE -= 1
			LOCAL = 20 * PAGE + 1
		ENDIF
		IF 自设套装:LOCAL:0 == 1
			PRINTFORML [{LOCAL, 2}] - %自设套装名称:LOCAL%
		ELSE
			PRINTFORML [{LOCAL, 2}] - ----
		ENDIF
	NEXT
ELSE
	FOR LOCAL, 20 * PAGE + 1, 20 * (PAGE + 1) + 1
		IF PAGE != 0 && LOCAL == 20 * PAGE + 1 && !STRLENS(GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:换装选择), LOCAL, "名前"))
			PAGE -= 1
			LOCAL = 20 * PAGE + 1
		ENDIF
		SIF GET_EXIST(CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:换装选择), LOCAL) && STRLENS(GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:换装选择), LOCAL, "名前")) != 1
			PRINTFORML [{LOCAL, 2}] - %GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:换装选择), LOCAL, "名前")%
	NEXT
ENDIF
PRINTFORML [100] - 不穿
HTML_PRINT "<button value = '1001' title = '上一页'>　［＜］　</button><button value = '1002' title = '下一页'>　［＞］　</button>"