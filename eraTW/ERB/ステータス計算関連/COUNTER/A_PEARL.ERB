@PEARL_CHK_MASTER
#DIM INSERTER
#DIM CONST INTERVAL = 30
#DIM KAKUNIN
#DIM LAPSE

SIF !TFLAG:パール期限
	RETURN

INSERTER = TFLAG:パール入れた人

;流石に他人がいるとチェックしない
KAKUNIN = 1
SIF CFLAG:INSERTER:現在位置 != CFLAG:MASTER:現在位置
	KAKUNIN = 0
SIF GET_TARGETNUM() > 1
	KAKUNIN = 0
SIF !SHIRAHU(INSERTER)
	KAKUNIN = 0

IF TFLAG:パール挿入個数
	SOURCE:MASTER:快Ａ += TIME_PROGRESS(TIME:1) * TFLAG:パール挿入個数
	SOURCE:MASTER:苦痛 += TIME_PROGRESS(TIME:1) * TFLAG:パール挿入個数 * 2
	DOWNBASE:MASTER:体力 += TIME_PROGRESS(TIME:1)
ENDIF

IF TFLAG:パール期限 < TIME && TFLAG:パール期限 > 0 && TFLAG:パール挿入個数
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%坚持到了約定的時間
	PRINTFORML 把%NAME_OUTPUT_TRANSLATION("CALLNAME", INSERTER)%抜了出来
	TFLAG:パール期限 = -1
ENDIF
IF KAKUNIN
	IF TFLAG:パール期限 == -1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", INSERTER)%在抚摸着已经忍耐到极限的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的头的时候，将肛门拉珠一个一个的慢慢地拔了出来
		CALL PEARL_EJECT("引っこ抜き")
		RETURN
	ELSEIF TFLAG:パール期限 == -2
		PRINTFORML 注意到肛门拉珠脱落了的%NAME_OUTPUT_TRANSLATION("CALLNAME", INSERTER)%，“重做。”，这么说着，又一个一个地将珠子往%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的直肠内部塞了进去！
		CALL PEARL_INSERT(INSERTER,5)
	ENDIF
ENDIF

LAPSE = (TIME - TFLAG:パール排出チェック) / INTERVAL

IF LAPSE && TFLAG:パール期限 > 0
	TFLAG:パール排出チェック = TIME
	IF KAKUNIN
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", INSERTER)%将手放在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的臀部之上，确认着肛门拉珠的状态…
		PRINTFORML 确认了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的尻穴下垂着的珠子的情况后，%NAME_OUTPUT_TRANSLATION("CALLNAME", INSERTER)%微笑着轻轻拉动着拉绳末端的戒指，对着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%说道再稍微忍耐一会…
		SOURCE:MASTER:快Ａ += 50
	ENDIF
	FOR LOCAL, 0, LAPSE
		CALL PEARL_PATIENCE
	NEXT
ENDIF

@PEARL_EJECT(ARGS)
#DIM INSERTER
#DIM OTOSIDAMA
INSERTER = TFLAG:パール入れた人

IF ARGS == "自然排出"
	SOURCE:MASTER:快Ａ += 100
	EXP:MASTER:Ａ経験 += 1
	TFLAG:パール挿入個数 -= 1
	PRINTFORMW 排出了1个肛珠（剩余{TFLAG:パール挿入個数}个）
	IF !TFLAG:パール挿入個数
		PRINTFORMW 把肛珠都排出来了…
		TFLAG:パール期限 = -2
	ENDIF
ELSEIF ARGS == "引っこ抜き"
	SOURCE:MASTER:快Ａ += TFLAG:パール挿入個数 * 200
	EXP:MASTER:Ａ経験 += TFLAG:パール挿入個数
	EXP:MASTER:苦痛快楽経験 += TFLAG:パール挿入個数 * 3
	EXP:INSERTER:嗜虐快楽経験 += TFLAG:パール挿入個数 * 3
	SOURCE:INSERTER:加虐 += TFLAG:パール挿入個数 * 100
	TFLAG:パール挿入個数 = 0
	TFLAG:パール期限 = 0
	TFLAG:パール入れた人 = 0
	TFLAG:パール排出チェック = 0
	IF FESTIVAL() == "元旦"
		SELECTCASE RAND:3
			CASE 0
				OTOSIDAMA = 10000
			CASE 1
				OTOSIDAMA = 30000
			CASE 2
				OTOSIDAMA = 50000
		ENDSELECT
		PRINTFORMW …？
		PRINTFORMW 肛珠的末端、有着不知道什么时候被放进去的黄金玉
		CALL COLORMESSAGE(@"红包奖励！获得了%金額表示(OTOSIDAMA)%！",C_YELLOW,2)
		MONEY += OTOSIDAMA
	ENDIF
ENDIF

@PEARL_INSERT(ARG,ARG:1)
TFLAG:パール挿入個数 = ARG:1
TFLAG:パール期限 = TIME + TFLAG:パール挿入個数 * 30 + RAND:30 + RAND:30
TFLAG:パール入れた人 = ARG
SOURCE:MASTER:快Ａ += TFLAG:パール挿入個数 * 10
TFLAG:パール排出チェック = TIME

@PEARL_PATIENCE
PRINTFORML 肛珠快要从肛門里出来了…
CALL ASK_M("忍住（気力50）",BASE:MASTER:気力 >= 50, "全力忍住（気力200）",BASE:MASTER:気力 >= 100, "放松力气",1)
SELECTCASE RESULT
	CASE 0
		DOWNBASE:MASTER:気力 += 50
		IF !RAND:2
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%忍住了排泄感
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%没能忍住排泄感
			CALL PEARL_EJECT("自然排出")
		ENDIF
	CASE 1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%拼命忍住了排泄感
		DOWNBASE:MASTER:気力 += 200
	CASE 2
		CALL PEARL_EJECT("自然排出")
ENDSELECT