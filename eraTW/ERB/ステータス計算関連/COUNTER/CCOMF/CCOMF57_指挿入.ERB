;*********************************************************************************************
;Counter指令：指挿入
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOMF57(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_YUBI,6,ARG)
	TFLAG:MASTERのＶ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	IF BEDROOM(CFLAG:MASTER:現在位置)
		SIF TEQUIP:PLAYER:0
			CALL DATUI_BOTTOM(ARG,2)
	ELSE
		SIF TEQUIP:PLAYER:0 & 14
			CALL DATUI_BOTTOM(ARG,1)
	ENDIF
	SOURCE:ARG:性行動 += 150
	SOURCE:ARG:侮辱 += 200
	SOURCE:ARG:挑発 += 150
	SOURCE:ARG:征服 += 400
	SOURCE:ARG:加虐 = 100
	SOURCE:PLAYER:快Ｖ += 300
	EXP:ARG:手淫経験 ++
	TCVAR:ARG:嗜虐フラグ = 1
	;性交中は69にならない
	IF MASTER_POSE(SET_V,SET_P) == ARG || MASTER_POSE(SET_A,SET_P) == ARG || MASTER_POSE(SET_P,SET_V) == ARG || MASTER_POSE(SET_P,SET_A) == ARG
		LOCAL = 0
	ELSE
		LOCAL = 2
	ENDIF
	CALL EVENT_COUNTER_POSE_69(ARG,LOCAL)

	;マ斯塔Counter制御
	;(ビット立ちで不可 0=摸屁股 1=擁抱 2=接吻 3=胸愛撫 4=肛門愛撫 5=陰蒂愛撫 6=指挿入れ 7=掀裙子)
	;=============================
	SIF EQUIP:PLAYER:50 < 0
		CALL Counter挿入され継続中処理(ARG)
	IF TEQUIP:MASTER:体位 == 2
		SETBIT TCVAR:ARG:26 ,0
		SETBIT TCVAR:ARG:26 ,1
		SETBIT TCVAR:ARG:26 ,2
		SETBIT TCVAR:ARG:26 ,3
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 5
		SETBIT TCVAR:ARG:26 ,0
		SETBIT TCVAR:ARG:26 ,1
		SETBIT TCVAR:ARG:26 ,3
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 3 || TEQUIP:MASTER:体位 == 1
		SETBIT TCVAR:ARG:26 ,4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ELSEIF TEQUIP:MASTER:体位 == 4
		SETBIT TCVAR:ARG:26 ,5
		SETBIT TCVAR:ARG:26 ,6
	ENDIF
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;Counter実行可否出力関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ABLE57(ARG)

	SIF TFLAG:63
		RETURN 0
	SIF !TALENT:ARG:施虐狂 && TALENT:PLAYER:処女 && TALENT:PLAYER:処女 != -1
		RETURN 0
	SIF !(TALENT:PLAYER:2 & 1)
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0
		RETURN 0
	SIF !TALENT:ARG:施虐狂 && PALAM:MASTER:潤滑 < PALAMLV:2
		RETURN 0

RETURN 1

;*********************************************************************************************
;Counter実行判定値算出関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ORDER57(指令番号, ARG, 基本値)
#DIM 指令番号
#DIM 基本値
#DIMS Counterタイプ

	;Counterタイプ設定
	;=============================
	Counterタイプ = 脱衣愛撫

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + 2 * TALENT:ARG:靈活手指 + ABL:ARG:指

	;Counterタイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:指令番号, 1)
	LOCAL *= COUNTER_TYPE(Counterタイプ, ARG)

RETURN LOCAL
