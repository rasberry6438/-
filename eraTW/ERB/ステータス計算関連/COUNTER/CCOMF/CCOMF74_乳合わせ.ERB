;*********************************************************************************************
;Counter指令：乳合わせ
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOMF74(ARG)

	;体勢の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_B,5,ARG)
	TFLAG:MASTERのＢ使用中 ++
	
	;ソース計算および内部処理系統
	;=============================
	SIF TEQUIP:PLAYER:1 > 1
		CALL DATUI_TOP(ARG,0)
	SIF TEQUIP:ARG:1 > 1
		CALL DATUI_TOP_T(ARG,0)
	SOURCE:ARG:快Ｂ += 80
	SIF GETBIT(TALENT:PLAYER:2,0)
		SOURCE:PLAYER:快Ｂ += 100
	SOURCE:ARG:性行動 += 300
	SOURCE:ARG:誘惑 += 600
	SOURCE:ARG:強要 += 100
	;奉仕FLAG
	TCVAR:ARG:108 = 1

	;マ斯塔Counter制御
	;(ビット立ちで不可 0=摸屁股 1=擁抱 2=接吻 3=胸愛撫 4=肛門愛撫 5=陰蒂愛撫 6=指挿入れ 7=掀裙子)
	;=============================
	SIF GROUPMATCH(TEQUIP:MASTER:体位, 1, 3, 4)
		CALL Counter挿入され継続中処理(ARG)
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;Counter実行可否出力関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ABLE74(ARG)

	SIF !(TALENT:ARG:2 & 2)
		RETURN 0
	SIF TFLAG:65
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0
	SIF TEQUIP:ARG:体位
		RETURN 0

RETURN 1

;*********************************************************************************************
;Counter実行判定値算出関数
;ARG = 対象の角色番号
;*********************************************************************************************
@CCOM_ORDER74(指令番号, ARG, 基本値)
#DIM 指令番号
#DIM 基本値
#DIMS Counterタイプ

	;Counterタイプ設定
	;=============================
	Counterタイプ = 脱衣強要

	;実行判定値算出式
	;=============================
	LOCAL = 基本値 + ABL:ARG:Ｂ感覚

	;Counterタイプ別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:指令番号, 1)
	LOCAL *= COUNTER_TYPE(Counterタイプ, ARG)

RETURN LOCAL
