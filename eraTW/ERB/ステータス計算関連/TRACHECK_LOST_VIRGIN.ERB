;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;処女喪失時の処理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@LOST_VIRGIN(ARG)
#DIM DYNAMIC 喪失者No
#DIMS DYNAMIC 喪失者名
#DIM DYNAMIC 取得者No
#DIMS DYNAMIC 取得者名
#DIMS 修飾語
;-------------------------------------------------
;処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == 1 && EXP_UP(1,ARG) > 0 && !TFLAG:2 && GETBIT(TALENT:ARG:性別,0) ;まずＶがないと！みんな有るけど…念の為。

	修飾語 = ""
	IF ARG == MASTER
		CFLAG:ARG:処女喪失对手 = TARGET
		喪失者No = MASTER
		喪失者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		取得者No = MASTER_POSE(SET_P,SET_V)
		;Ｐ以外で喪失した場合、MASTER_POSE(SET_P,SET_V)は0となり、結果的に取得者名まで你になってしまう
		;よって取得者No=0ならTARGETを代入
		SIF !取得者No
			取得者No = TARGET
		取得者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 取得者No)%
	ELSE
		CFLAG:ARG:処女喪失对手 = PLAYER
		喪失者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%
		喪失者No = ARG
		取得者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		取得者No = MASTER
	ENDIF
	IF ARG != MASTER
		;贞操重视
		IF TALENT:ARG:貞操 > 0
			IF !TALENT:ARG:恋慕
				TIMES SOURCE:ARG:情愛 , 0.60
				TIMES SOURCE:ARG:反感 , 10.00
			ENDIF
		;不在乎貞操
		ELSEIF TALENT:ARG:貞操 < 0
			TIMES SOURCE:ARG:情愛 , 0.60
			TIMES SOURCE:ARG:反感 , 0.50
		ELSEIF TALENT:ARG:無知
			TIMES SOURCE:ARG:情愛 , 0.30
			TIMES SOURCE:ARG:反感 , 0.75
			SOURCE:ARG:恐怖 += 3000
		ENDIF
		IF CFLAG:ARG:诶嘿嘿 == 2
			TIMES SOURCE:ARG:情愛 , 1.50
			TIMES SOURCE:ARG:反感 , 0.25
		ENDIF
	ENDIF
	SETCOLOR 247,171,166
	PRINTFORMW 【処女喪失】
	IF TEQUIP:ARG:50 == MASTER && GROUPMATCH(TALENT:MASTER:形状,5,6)
		PRINTFORMW %取得者名%那凶悪的肉棒、毫不留情地撕裂了%喪失者名%的純潔……
		TIMES SOURCE:ARG:苦痛, 3.00
		TIMES SOURCE:ARG:恐怖, 3.00
	ENDIF
	IF FLAG:70
		IF ARG == MASTER
			PRINTFORMW %取得者名%在对方无意識的情况下、奪走了\@ TALENT:ARG:路人 == 1 ? 刚遇到的# \@%喪失者名%的処女……
		ELSE
			PRINTFORMW %喪失者名%在无意識的情况下、被\@ TALENT:ARG:路人 == 1 ? 刚遇到的# \@%取得者名%奪走了処女……
		ENDIF
	ELSE
		;陥落素質による処理
		;MASTER喪失
		IF ARG == MASTER
			IF TCVAR:TARGET:烂醉
				PRINTFORMW 意识模糊的%取得者名%、把%喪失者名%的処女奪走了……
			;睡眠姦、お前がパパになるんだよ！
			ELSEIF CFLAG:TARGET:睡眠
				PRINTFORMW 熟睡中的%取得者名%、被迫奪走了%喪失者名%的処女
			;無知ふたろり（ショタ）で処女喪失とか最高……いや、再考
			ELSEIF TALENT:TARGET:無知
				;IF GROUPMATCH(TALENT:MASTER:形状,5,6)
				;無知な相手のちんちんが入ってる場合
				IF TEQUIP:MASTER:50 == 取得者No
					PRINTFORMW 缺乏性知識的%取得者名%不知道自己被做了什么、对\@ TALENT:取得者No:2 & 2 ?強烈的快感和 # \@%喪失者名%流出的鲜血而感到害怕……
				;ちんちん以外
				ELSE
					PRINTFORMW 看到破瓜血的%取得者名%、以为自己弄伤了%喪失者名%而慌了手脚
				ENDIF
				PRINTFORMW %喪失者名%一边强忍着破瓜的疼痛、一边让%取得者名%冷静下来
			;素質恋人
			ELSEIF TALENT:TARGET:恋人  || TALENT:TARGET:恋慕 > 1
				PRINTFORMW %取得者名%入迷地看着从%喪失者名%秘処流出的鮮血……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情愛 , 3.00
				TIMES SOURCE:TARGET:反感 , 0.10
			;恋慕
			ELSEIF TALENT:TARGET:恋慕
				PRINTFORMW %取得者名%露出了開心的笑容、凝视着献出処女的%喪失者名%……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情愛 , 2.00
				TIMES SOURCE:TARGET:反感 , 0.30
				EXP:TARGET:愛情経験 += 30
				CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			ELSEIF TALENT:TARGET:思慕
				IF TCVAR:TARGET:强行 == 1
					PRINTFORMW 在%取得者名%对快感不知所措的时候、%喪失者名%含着泪看了过来……
					TIMES SOURCE:TARGET:情愛 , 0.80
					TIMES SOURCE:TARGET:反感 , 1.50
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSEIF TALENT:TARGET:貞操 >= 0
					PRINTFORMW %取得者名%虽然很困惑、但总觉得很開心似的看着%喪失者名%……
					TIMES SOURCE:TARGET:情愛 , 1.30
					TIMES SOURCE:TARGET:反感 , 0.50
				ENDIF
				CFLAG:ARG:処女喪失対象 |= 喪失_思慕
			ELSE
				IF TCVAR:TARGET:强行 == 1
					TIMES SOURCE:TARGET:情愛 , 0.10
					TIMES SOURCE:TARGET:反感 , 3.00
					PRINTFORMW %取得者名%用混雜了憤怒和困惑的表情、瞪著%喪失者名%……
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSE
					PRINTFORMW %取得者名%露出想说什么的表情、盯着和%喪失者名%的結合处……
				ENDIF
			ENDIF
		;MASTERがTARGETの膣に挿入している
		ELSEIF TEQUIP:ARG:50 == MASTER
			IF TCVAR:ARG:烂醉
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在意識混沌的時候被奪走了処女……
			;睡眠姦　良い子は沉浸在夢鄉中間にママにしちゃおうね～
			ELSEIF CFLAG:ARG:睡眠
				PRINTFORMW %喪失者名%的第一次在睡著的時候被奪走了……
			ELSEIF TALENT:ARG:無知
				PRINTFORMW 性知识匮乏的%喪失者名%不明白\@ TCVAR:ARG:誤爆挿入フラグ ? 发生了什么 # 被做了什么\@，在处女失去的痛苦中感到困惑和害怕...
				PRINTFORMW %取得者名%让%喪失者名%平静了下来
			;親愛かつ相手が主人(現在親愛がないので、恋人で代用
			ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕 > 1
				SIF TCVAR:ARG:誤爆挿入フラグ
					PRINT 事故虽然是导火索，但是
				PRINTFORMW %喪失者名%因为能让%取得者名%成为第一次的对象而感到无上的幸福感的様子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情愛 , 3.00
				TIMES SOURCE:ARG:反感 , 0.10
				 CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			;恋慕かつ相手が主人
			ELSEIF TALENT:ARG:恋慕
				SIF TCVAR:ARG:誤爆挿入フラグ
					PRINT 事故虽然是导火索，但是
				PRINTFORMW %喪失者名%因为把处女献给了%取得者名%而感到幸福的様子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情愛 , 2.00
				TIMES SOURCE:ARG:反感 , 0.30
				EXP:ARG:愛情経験 += 30
				CFLAG:ARG:処女喪失対象 |= 喪失_恋慕
			ELSEIF TALENT:ARG:思慕
				IF TCVAR:ARG:强行 == 1
					PRINTFORMW 被怀有好感的对象硬上了的%喪失者名%、眼泪如雨点般開始洒落下来……
					TIMES SOURCE:ARG:情愛 , 0.80
					TIMES SOURCE:ARG:反感 , 1.50
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
				ELSEIF TCVAR:ARG:誤爆挿入フラグ
					PRINTFORMW 虽然怀有好意，但%喪失者名%稍微后悔在事故中将贞操奉献给了%取得者名%......
					TIMES SOURCE:ARG:情愛 , 1.30
					TIMES SOURCE:ARG:反感 , 0.80
				ELSEIF TALENT:ARG:貞操 >= 0
					PRINTFORMW 尽管怀有好感、%喪失者名%还是对被%取得者名%夺去貞操感到不知如何是好……
					TIMES SOURCE:ARG:情愛 , 1.30
					TIMES SOURCE:ARG:反感 , 0.80
				ENDIF
				CFLAG:ARG:処女喪失対象 |= 喪失_思慕
			ELSE
				IF TCVAR:ARG:强行 == 1
					TIMES SOURCE:ARG:情愛 , 0.10
					TIMES SOURCE:ARG:反感 , 3.00
					IF CFLAG:ARG:败犬
						PRINTFORMW %喪失者名%作为敗北的代价、被%取得者名%奪走了処女……
					ELSE
						PRINTFORMW %喪失者名%的反抗徒劳无功、被%取得者名%奪走了処女……
					ENDIF
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
					IF TALENT:ARG:路人 == 1
						CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","现在已经老实了","、処女被夺走了")%
						CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","肛門処女被夺走了","処女被夺走了")%
					ENDIF
				ELSEIF TALENT:ARG:路人 == 1
					PRINTFORMW 看到秘処流下的鲜血、%喪失者名%意识到自己把处女献给了刚认识不久的%取得者名%、有些後悔……
				ELSEIF TCVAR:ARG:誤爆挿入フラグ
					PRINTFORMW %喪失者名%不小心将处女之身献给了%取得者名%......
				ELSE
					PRINTFORMW %喪失者名%半推半就地被%取得者名%奪走了処女……
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	IF CSTR:ARG:処女喪失履歴 == ""
		IF GROUPMATCH(TCVAR:喪失者No:Counter行動, 80, 83, 85)
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "阴茎")
		ELSEIF TCVAR:取得者No:Counter行動 == 57
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "指")
		ELSEIF SELECTCOM == 44
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "振動棒")
		ELSE
			CALL SET_HISTORY_LOST_V(喪失者No, 取得者No, "阴茎")
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
;-------------------------------------------------
;再生処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == 2 && EXP_UP(1,ARG)
	TCVAR:ARG:破瓜 = 2
	;视频拍摄
	IF TEQUIP:28
		;性交
		IF TEQUIP:ARG:50
			LOCALS = LV1/
		ELSEIF TEQUIP:ARG:13
			LOCALS = LV3/
		;其他
		ELSE
			LOCALS = LV4/
		ENDIF
		TSTR:1 += LOCALS
	ENDIF
	PRINTW 再生処女喪失
ENDIF
;-------------------------------------------------
;無自覚処女喪失のチェック
;-------------------------------------------------
IF TALENT:ARG:処女 == -1 && EXP_UP(1,ARG) && CFLAG:ARG:292 == 1 && !FLAG:70 && !TCVAR:ARG:烂醉 && !CFLAG:MASTER:恶作剧
	TCVAR:ARG:破瓜 = -1
	CFLAG:ARG:292 = 0
	PRINTW 処女喪失？
	SETCOLOR 247,171,166
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%对自己没有感受到痛苦而感到困惑…
	RESETCOLOR
ENDIF

@童貞無接吻経験喪失(ARG)
#DIM シチュエーション
;シチュエーション	 0:通常
;					 1:约会帰り
;					 2:オナバレ
;					 4:阴茎に
;					 8:自発的に
;					16:無自覚
;					32:買春
シチュエーション = 0
VARSET LOCALS
FONTBOLD
SETCOLOR 255,100,255
IF !TALENT:ARG:非童貞 && TALENT:ARG:性別 & 2
	IF TCVAR:ARG:童貞喪失 == 1
		PRINTFORMW 童貞喪失(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
		TALENT:ARG:非童貞 = 1
	ENDIF
ENDIF

;無接吻経験喪失
IF ARG == MASTER && TALENT:ARG:無接吻経験 && GROUPMATCH(SELECTCOM,2,20,312)
	シチュエーション |= 8
	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,2) && TALENT:TARGET:性別 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(ARG, TARGET, シチュエーション)
	ENDIF
ELSEIF TALENT:MASTER:無接吻経験 && GROUPMATCH(TCVAR:ARG:Counter行動,30,39,60,71,91)
	IF GROUPMATCH(TCVAR:ARG:Counter行動,30,39,60)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(TCVAR:ARG:Counter行動,71,91) && TALENT:ARG:性別 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ENDIF
ENDIF
IF TALENT:ARG:無接吻経験 && (TCVAR:ARG:ＦＫ喪失 || GROUPMATCH(TCVAR:ARG:Counter行動,30,39,60,51))
	;無自覚
	SIF CFLAG:ARG:睡眠 || FLAG:70
		シチュエーション |= 16
	;自発的に
	SIF GROUPMATCH(TCVAR:ARG:Counter行動,30,39,60,51)
		シチュエーション |= 8
	;阴茎に
	SIF TCVAR:ARG:ＦＫ喪失 == 2 || GROUPMATCH(TCVAR:ARG:Counter行動,51)
		シチュエーション |= 4

	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		;デコチューを除外する
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,81,88,140,337) || GROUPMATCH(TCVAR:ARG:Counter行動,30,60,51)
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF TCVAR:ARG:Counter行動 == 39
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション, "糖と一緒に")
	ENDIF
ENDIF

;尻穴処女喪失
IF CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止 || !CFLAG:ARG:尻穴処女喪失記念日
	IF TCVAR:ARG:Ａ喪失発生 
		;時間停止中に経験済み
		IF !FLAG:70 && ARG && CFLAG:ARG:尻穴処女喪失対象 & 喪失_時間停止
			SIF FIRSTTIME("Ａ破瓜",0)
				PRINTFORMW 肛門処女喪失？(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
			TCVAR:ARG:Ａ破瓜 = -1
		;完全に初めて
		ELSEIF !CFLAG:ARG:尻穴処女喪失記念日
			PRINTFORMW 肛門処女喪失(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
			IF TEQUIP:ARG:51 == MASTER && GROUPMATCH(TALENT:MASTER:形状,5,6,10,11)
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%那凶悪的肉棒、毫不留情地開通了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%緊閉的後庭……
				TIMES SOURCE:ARG:苦痛, 3.00
				TIMES SOURCE:ARG:恐怖, 3.00
				CFLAG:ARG:尻穴処女喪失対象 |= 喪失_凶悪
			ENDIF
			IF ARG != MASTER && !GROUPMATCH(SELECTCOM,45,46)
				IF CFLAG:ARG:睡眠
					PRINTFORMW 趁%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%睡著正香的時候，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪走了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的肛門處女……
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_睡眠
				ELSEIF FLAG:時間停止
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在無意識的情況下，被\@ TALENT:ARG:路人 == 1 ? 剛遇到的# \@%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪走了肛門處女……
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_時間停止
				ELSEIF TALENT:ARG:無知
					PRINTFORMW 性知識匱乏的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%完全不知道自己在被做什麼、只是對排洩的后穴被插入的事感到了困惑與慌張……
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%安慰起了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%。
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_無知
				ELSEIF TALENT:ARG:思慕
					IF TCVAR:ARG:强行 == 1
					PRINTFORMW 被怀有好感的对象硬上了的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%，強忍著被插入的不適感流下了淚水……
					TIMES SOURCE:ARG:情愛 , 0.80
					TIMES SOURCE:ARG:反感 , 1.50
					CFLAG:ARG:処女喪失対象 |= 喪失_强行
					ELSEIF TALENT:ARG:貞操 >= 0
						PRINTFORMW 雖然懷有好感、但%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%還是對被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪去第一次而感到些許無措……
						TIMES SOURCE:ARG:情愛 , 1.30
						TIMES SOURCE:ARG:反感 , 0.80
					ENDIF
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_思慕
				ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%因為能讓%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%成為第一次的對象而感到十分幸福……
					TIMES SOURCE:ARG:情愛 , 3.00
					TIMES SOURCE:ARG:反感 , 0.10
					EXP:ARG:愛情経験 += 20
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_恋慕
				ELSEIF TCVAR:ARG:烂醉
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在意識混沌之中被奪去了肛門處女……
					CFLAG:ARG:尻穴処女喪失対象 |= 喪失_烂醉
				ELSEIF TCVAR:ARG:强行
				IF CFLAG:ARG:败犬
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%作为敗北的代價、被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪走了肛門處女……
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的抵抗毫無作用、被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪走了肛門處女……
				ENDIF
				CFLAG:ARG:尻穴処女喪失対象 |= 喪失_强行
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%半推半就地被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%奪走了肛門處女……
				ENDIF
			ENDIF
			IF CSTR:ARG:路人子プロフィール２ != ""
				CSTR:ARG:路人子プロフィール２ = %REPLACE(@"%CSTR:ARG:路人子プロフィール２%","现在已经老实了","、肛門処女被夺走了")%
			ENDIF
			IF SELECTCOM == 45
				LOCALS = 肛用振動棒
				CFLAG:ARG:尻穴処女喪失対象 |= 喪失_振動棒
			ELSEIF SELECTCOM == 46
				LOCALS = 後庭拉珠
				CFLAG:ARG:尻穴処女喪失対象 |= 喪失_振動棒
			ELSE
				LOCALS = 阴茎
				CFLAG:ARG:尻穴処女喪失対象 |= 喪失_阴茎
			ENDIF
			IF !FLAG:70
				TCVAR:ARG:Ａ破瓜 = 1
			ELSE
				TCVAR:ARG:Ａ破瓜 = -2
			ENDIF
			IF ARG == TARGET || (CFLAG:MASTER:诶嘿嘿 == 2 && ARG != TARGET && ARG != MASTER)
				CALL SET_HISTORY_LOST_A(ARG, PLAYER, @"%LOCALS%")
			ELSE
				CALL SET_HISTORY_LOST_A(ARG, TARGET, @"%LOCALS%")
			ENDIF
			
		ENDIF
	ENDIF
ENDIF
FONTREGULAR
RESETCOLOR

@LOST_VIRGIN_STOP(ARG)
SIF !SHIRAHU(ARG)
	RETURN
IF CFLAG:ARG:破瓜中止口上有
	;口上側から干渉可能に
	CALL KOJO_MESSAGE_SEND("LOST_VIRGIN_STOP", 0, ARG)
	IF RESULT == 1
		RETURN -1
	ELSE
		RETURN 0
	ENDIF
	RESETCOLOR
ELSEIF !TALENT:ARG:無知 && !TALENT:ARG:恋慕 && !TALENT:ARG:愛欲 && !TALENT:ARG:恋人 && !TCVAR:ARG:不耐烦 && !CFLAG:恶作剧
;贞操重视持ちor貞操普通かつ思慕無
	IF TALENT:ARG:貞操 > 0 || (!TALENT:ARG:貞操 && !TALENT:ARG:思慕)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%表示这种事还是等一段时间再考虑吧
		IF (EXP:ARG:60 + EXP:ARG:61 + EXP:ARG:62 + EXP:ARG:63 + EXP:ARG:64) == 0
			IF TALENT:ARG:貞操 > 0
				PRINTFORMW 果然持身甚紧呢……
			ELSE
				PRINTFORMW 意外坚固的防线呢……
			ENDIF
		ELSE
			PRINTFORMW 麻烦的臭女人……
		ENDIF
		CALL ASK_YN("放棄","不收自己靠过来的女人是男人的恥辱")
		IF RESULT == 1
			IF TALENT:ARG:貞操 > 0 && ABL:ARG:戦闘能力 > ABL:MASTER:戦闘能力 && !TCVAR:ARG:强行
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%虽然想霸王硬上弓、但因为%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%相当激烈的抵抗而放弃了
				RETURN 1
			ELSEIF TALENT:ARG:貞操 > 0 && FLAG:催眠強度 <= 75
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%流着眼泪拒绝了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
				PRINTL [0] 真的那么讨厌做就算了
				PRINTL [1] 上吧
				INPUT
				IF !RESULT
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%松了一口气…
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%有点受伤
					RETURN -1
				ELSE
					IF FLAG:催眠強度 >=75
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%发动催眠告诉她这只是体内按摩，并不会失去处女…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%虽然有些犹豫，但还是认同了
						RETURN 0
					ELSE
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%压在了不情愿的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%身上…
						TCVAR:ARG:不耐烦 = 1
						TCVAR:ARG:强行 = 1
						RETURN 0
					ENDIF
				ENDIF
			ELSE
				IF FLAG:催眠強度 >=75
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%发动催眠告诉她这只是体内按摩，并不会失去处女…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%虽然有些犹豫，但还是认同了
						RETURN 0
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%强行压了上去…
					TCVAR:ARG:不耐烦 = 1
					RETURN 0
				ENDIF
			ENDIF
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%松了一口气…
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SET_FIRSTKISS(ARG, 接吻相手, シチュエーション, 修飾語)
#DIM 接吻相手
;シチュエーション	 0:通常
;					 1:约会帰り
;					 2:オナバレ
;					 4:阴茎に
;					 8:自発的に
;					16:無自覚
;					32:買春
#DIM 强行シチュ
#DIM シチュエーション
#DIMS 修飾語
#DIM 自己

VARSET LOCALS
自己 = 0
强行シチュ = (TCVAR:ARG:强行 && !(TALENT:ARG:愛欲 || TALENT:ARG:炮友 || TALENT:ARG:恋慕 || TALENT:ARG:思慕))
;强行状態で、接吻合意はつかない。强行だから！！！唇を許すわけ無いでしょ！！　ただし、陥落素質が那个ばギリギリセーフ。

;接吻経験済み
IF TALENT:ARG:無接吻経験 == 0
	RETURN
;無自覚接吻経験済み
ELSEIF TALENT:ARG:無接吻経験 < 0
	;無自覚接吻同士では重複しない
	SIF シチュエーション & 16
		RETURN
ENDIF

;FLAG:ＦＫ判定によっては返回
SIF !(FLAG:ＦＫ判定 & 1) && シチュエーション & 16
	RETURN
SIF !(FLAG:ＦＫ判定 & 2) && シチュエーション & 4
	RETURN


IF ARG == 接吻相手
	IF ARG == MASTER
		接吻相手 = TARGET
	ELSE
		接吻相手 = MASTER
	ENDIF
ENDIF

SETCOLOR 247,171,166
PRINTFORM 無接吻経験喪失
SIF シチュエーション & 16
	PRINT ？
PRINTFORMW (%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
RESETCOLOR

CFLAG:ARG:初吻喪失記念日 = DAY
TALENT:ARG:無接吻経験 = 0
CFLAG:ARG:初吻喪失对手 = 接吻相手

;対象と修飾語決定
IF TALENT:ARG:思慕
	CFLAG:ARG:初吻喪失対象 |= 喪失_思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF ARG == MASTER && TALENT:(接吻相手):恋人
	CFLAG:ARG:初吻喪失対象 |= 喪失_恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	CFLAG:ARG:初吻喪失対象 |= 喪失_恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF TALENT:ARG:精愛味覚
	LOCALS:0 = 让自己饮精成瘾的	
ELSEIF TALENT:ARG:合意誤認
	LOCALS:0 = 灌输了虚假记忆的
ELSEIF TALENT:ARG:反発抑制
	LOCALS:0 = 操控自己感情的
ELSEIF FLAG:催眠強度 >= 5
	LOCALS:0 = 偷偷催眠了自己的
ENDIF
IF シチュエーション & 2
	IF ARG != MASTER
		LOCALS:2 = 自慰中闯入的
	ELSE
		LOCALS:2 = 自慰着
		自己 = 1
	ENDIF
ENDIF
IF シチュエーション & 4
	LOCALS:1 = 的阴茎
	CFLAG:ARG:初吻喪失対象 = 1
ENDIF
SIF シチュエーション & 8
	自己 = 1
IF シチュエーション & 16
	IF FLAG:70
		CFLAG:ARG:初吻喪失対象 |= 喪失_時間停止
		LOCALS:2 = 在時間停止中
	ELSEIF CFLAG:ARG:睡眠
		CFLAG:ARG:初吻喪失対象 |= 喪失_睡眠
		LOCALS:2 = 在沉睡中
	ELSE
		LOCALS:2 = 在自己不知道的时候
	ENDIF
	CFLAG:ARG:初吻喪失対象 |= 喪失_强行
	TALENT:ARG:無接吻経験 = -1
ENDIF
IF TALENT:ARG:無知 && シチュエーション & 4
	CFLAG:ARG:初吻喪失対象 |= 喪失_無知
	LOCALS:2 = %LOCALS:2%连这代表什么都不知道
ENDIF
IF 强行シチュ
	CFLAG:ARG:初吻喪失対象 |= 喪失_强行
	LOCALS:2 = %LOCALS:2%强行
ENDIF
;場所決定
IF シチュエーション & 1
	CSTR:ARG:初吻位置 = 約會後回去的路上
	SIF ARG != MASTER
		自己 = 1
ELSEIF シチュエーション & 2
	IF ARG != MASTER
		CSTR:ARG:初吻位置 = 自室
	ELSE
		CSTR:ARG:初吻位置 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%宅
	ENDIF
ELSE
	IF CHK_DATENOW(CFLAG:ARG:约会中) && CFLAG:ARG:現在位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_SPOT()%
	ELSEIF CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:現在位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_PLACE()%の%DATENAME_SPOT()%
	ELSE
		CSTR:ARG:初吻位置 = %NAME_FROM_PLACE(CFLAG:ARG:現在位置)%
	ENDIF
ENDIF

;動詞決定
IF 修飾語 == "糖と一緒に"
	LOCALS:1 += "和"
	LOCALS:3 = 交换了
ELSEIF 自己
	CFLAG:ARG:初吻喪失対象 |= 喪失_自己
	LOCALS:1 += "❤"
	LOCALS:3 = 献上了
ELSEIF シチュエーション & 32
	LOCALS:1 += "❤"
	LOCALS:3 = 奪走了
ELSE
	LOCALS:1 += "❤"
	LOCALS:3 = 被奪走了
ENDIF
IF CSTR:ARG:初吻喪失履歴 == ""
	CSTR:ARG:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%で%LOCALS:2%%LOCALS:0%%NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%%LOCALS:1%%修飾語%初吻を%LOCALS:3%
ELSE
	;無自覚接吻がある場合のみ重複する
	CSTR:ARG:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%で%LOCALS:2%%LOCALS:0%%NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%%LOCALS:1%%修飾語%初吻を%LOCALS:3%￥ＢＲ%CSTR:ARG:初吻喪失履歴%
ENDIF
;既成事實もつけておく
SIF ARG != MASTER && !(シチュエーション & 16) && !强行シチュ
	SETBIT CFLAG:ARG:既成事實, 0
;愛情経験もここで入れることにする
SIF ARG != MASTER && !(シチュエーション & 16) && !强行シチュ
	EXP:ARG:愛情経験 += 5

;===================================================================================
;履歴文字列設定用関数群
;対象が未記入だと"阴茎"が代入されます。
;またその場合含め対象が阴茎"だと相手が生えてない場合自動で穿戴式假陰茎になります。
;場所が未記入だと自動で位置を取得します。
;時間停止中だとか酔いつぶれてるだとかは自動で代入されます。
;各種フラグ処理も関数側で設定します。
;つまり呼び出し側で分岐入れる必要がほとんどなくなりました。
;ARGが你の阴茎で処女喪失したなら極端な話CALL SET_HISTORY_LOST_V(ARG)だけでOK。
;やったね。凄いね。
;===================================================================================

@SET_HISTORY_LOST_A(ARG,相手,対象,場所,修飾語)
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語

CFLAG:ARG:尻穴処女喪失記念日 = DAY
CFLAG:ARG:尻穴処女喪失对手 = 相手

CSTR:ARG:尻穴処女喪失履歴 = %HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,"尻穴処女")%

@SET_HISTORY_LOST_V(ARG,相手,対象,場所,修飾語)
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語

;非処女化、享用処女フラグ、喪失記念日なども関数側で設定
SIF ARG != MASTER
	FLAG:享用処女 ++
CFLAG:ARG:処女喪失記念日 = DAY
TALENT:ARG:処女 = 0
TCVAR:ARG:破瓜 = 1
CFLAG:ARG:処女喪失对手 = 相手
CSTR:ARG:処女喪失履歴 = %HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,"処女")%

@HISTORY_LOST_TEXT(ARG,相手,対象,場所,修飾語,喪失したもの)
#FUNCTIONS
#DIM 相手
#DIMS 対象
#DIMS 場所
#DIMS 修飾語
#DIMS 喪失したもの
#DIMS 状況
#DIM 自己

VARSET LOCALS
LOCALS:4 = 
自己 = 0
IF ARG == MASTER || (CFLAG:MASTER:诶嘿嘿 != 2 && ARG != MASTER && ARG != TARGET)
	LOCALS:0 = 
ELSEIF TALENT:ARG:思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF !CFLAG:ARG:面識 && ARG != MASTER
	LOCALS:0 = 在这之前面都没有见过的
ELSEIF TALENT:ARG:路人 == 1
	LOCALS:0 = 初見面的
ELSEIF TALENT:ARG:精愛味覚
	LOCALS:0 = 让自己饮精成瘾的	
ELSEIF TALENT:ARG:合意誤認
	LOCALS:0 = 灌输了虚假记忆的
ELSEIF TALENT:ARG:反発抑制
	LOCALS:0 = 操控自己感情的
ELSEIF FLAG:催眠強度 >= 5
	LOCALS:0 = 改变了自己常识的
ENDIF

SIF !SHIRAHU(ARG)
	自己 = 0
SIF 対象 == ""
	対象 = 阴茎

SIF 場所 == ""
	場所 = %PLACE_NAME(ARG)%
LOCALS:4 = %場所%

IF FLAG:70
	LOCALS:4 = 時間停止中的%LOCALS:4%
ELSEIF TCVAR:ARG:烂醉
	LOCALS:4 = %LOCALS:4%喝得酩酊大醉的时候
ELSEIF CFLAG:ARG:睡眠
	LOCALS:4 = %LOCALS:4%沉睡的时候
ELSEIF FLAG:催眠強度 >= 5
	LOCALS:4 = 常识明显不正常的%LOCALS:4%
ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:诶嘿嘿 != 2
	LOCALS:4 = %LOCALS:4%、就像%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%期望的一様
ENDIF

IF 対象 == "阴茎"
	IF GETBIT(TALENT:相手:2,1)
		LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的%TINKO_SIMPLE(相手)%
	ELSE
		LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的穿戴式假陰茎
	ENDIF
ELSEIF 対象 == "指"
	LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的手指
ELSE
	LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的%対象%
ENDIF

SIF ARG == MASTER && TALENT:相手:無知
	LOCALS:1 = 什么都不知道的%LOCALS:1%

IF 修飾語 == ""  && SHIRAHU(ARG)
	IF TCVAR:ARG:强行 && CFLAG:ARG:败犬
		修飾語 = 作为敗北的代价
		自己 = 0
	ELSEIF TCVAR:ARG:强行
		修飾語 = 霸王硬上弓
		自己 = 0
	ELSEIF TALENT:ARG:合意誤認 || TALENT:ARG:反発抑制 || FLAG:催眠強度 >= 5
		修飾語 = 不清楚自己被催眠地
		自己 = 0
	ELSEIF TALENT:ARG:無知
		修飾語 = 什么都不知道的
		自己 = 0
	ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:诶嘿嘿 != 2
		修飾語 = 在不満中
	ELSEIF TALENT:ARG:恋人
		修飾語 = 在幸福感中
	ELSEIF CFLAG:ARG:诶嘿嘿 == 2
		修飾語 = 亲自
		自己 = 1
	ENDIF
ENDIF

IF 自己
	LOCALS:4 = 在%LOCALS:4%%修飾語%将%喪失したもの%献给了%LOCALS:0%%LOCALS:1%
ELSEIF ARG == MASTER || 修飾語 == "治療と称して"
	修飾語 = 以治療的名义
	LOCALS:4 = 在%LOCALS:4%%修飾語%将%喪失したもの%给与了%LOCALS:0%%LOCALS:1%
ELSEIF 修飾語 == "お金を払って"
	修飾語 = 付了钱后
	LOCALS:4 = 在%LOCALS:4%%修飾語%将%喪失したもの%让%LOCALS:0%%LOCALS:1%奪走了
ELSE
	LOCALS:4 = 在%LOCALS:4%%修飾語%%喪失したもの%被%LOCALS:0%%LOCALS:1%奪走了
ENDIF

LOCALS:10 = %PRINT_DATE_F(DAY)%、%LOCALS:4%

RETURNF LOCALS:10

;你の場合イベントでCSVで定義されていない相手と致すこともありうるのでより自由度の高いものを
@SET_FIRSTKISS_MASTER(ARGS)
IF TALENT:MASTER:無接吻経験
	CALL COLORMESSAGE(@"無接吻経験喪失(%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%)", C_D_PINK, 2)
	CFLAG:MASTER:初吻喪失記念日 = DAY
	TALENT:MASTER:無接吻経験 = 0
	CSTR:MASTER:初吻喪失履歴 = %PRINT_DATE_F(DAY)%、%ARGS%
ENDIF
