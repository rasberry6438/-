;-------------------------------------------------
;ARGSが色を表す文字列かを判定する。色の種類を増やしたらここにも追加する事
;各色についての名称はここに書いたものに統一する。紺色、緑色には注意
;-------------------------------------------------
@TEXT_ALLCOLOR()
#FUNCTIONS

LOCALS = 草莓紋/小熊紋/鱼紋/猫紋/小鸡紋/蝴蝶结紋/水玉模様/水玉粉紅/水玉黒/水玉黄色/水玉水色/水玉白黒/水玉白/
LOCALS += "紅白/緑白/青白/黄白/黒白/粉紅/橘/白黒/花柄/"
LOCALS += "黄色/橙色/黒/濃紫/紫/薄青色/青/紺色/真紅/赤/薄桃色/桃色/純白/白/緑色/薄緑/灰色/水色/焦茶色/茶色/米色/"
LOCALS += "條紋/水玉色/豹纹/奶牛/虎紋/儿童胖次颜色/淡色/Ｈ的颜色/隨機顏色/比基尼色/辣妹/男式色/柄/"
;色のある无がバグの温床なので全てＯＫとして、後で本体側が使いやすい方を自動選択させる
LOCALS += "橘色/黒色/紫色/青色/赤色/桃/白色/薄緑色/條紋色/水玉色/辣妹色/"
LOCALS += "黄/橙/紺/薄青/薄桃/桃色/緑/灰/焦茶/茶/"
;特殊
LOCALS += "藤の花/"
RETURNF LOCALS

;-------------------------------------------------
;ARGSが色を表す文字列かを判定する関数
;-------------------------------------------------
@IS_CLOCOLOR(ARGS)
#FUNCTION
SIF STRCOUNT(@"\/%TEXT_ALLCOLOR()%", @"\/%ARGS%\/")
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;衣装文字列ARGSにARGS:1を、以下のルールを守りながら混ぜる
;・色に関してはARGS:1の要素で上書きさせる
;・正面蝴蝶結属性は"正面蝴蝶結"を含む文字列と入れ替え
;・共通属性追加済/は末尾に付け直す
;-------------------------------------------------
@MERGE_STR(ARGS, ARGS:1)
#FUNCTIONS
#DIM LCOUNT
#DIMS STR_ADD

SIF ARGS:1 == "" || ARGS == ""
	RETURNF ARGS

STR_ADD = 
;ARGS:1の色文字列を使える状態に変換し的同时、ARGSに混ぜる文字列STR_ADDに作り変える
VARSET LOCALS
SPLIT ARGS:1, "/", LOCALS
FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		;色文字列は使える状態に変換
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			LOCALS:LCOUNT = %SET_CLOCOLOR(LOCALS:LCOUNT)%
		STR_ADD += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT

;STR_ADDが色に関する場合は、ARGSから色文字列を拔出
SIF STR_ADD != RESET_CLOCOLOR(STR_ADD)
	ARGS = %RESET_CLOCOLOR(ARGS)%
;STR_ADDにインナー属性がある場合には、ARGSからインナー属性を消す
IF STRCOUNT(STR_ADD, "インナー：")
	VARSET LOCALS
	SPLIT ARGS, "/", LOCALS
	ARGS = 
	FOR LCOUNT, 0, 100
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASEELSE
			SIF STRCOUNT(LOCALS:LCOUNT, "インナー：")
				CONTINUE
			ARGS += @"%LOCALS:LCOUNT%\/"
		ENDSELECT
	NEXT
ENDIF
;STR_ADDが正面蝴蝶結に関する場合はARGSから正面蝴蝶結属性を拔出
IF STRCOUNT(STR_ADD, "正面蝴蝶結") && STRCOUNT(ARGS, "正面蝴蝶結")
	VARSET LOCALS
	SPLIT ARGS, "/", LOCALS
	ARGS = 
	FOR LCOUNT, 0, 100
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASEELSE
			SIF STRCOUNT(LOCALS:LCOUNT, "正面蝴蝶結")
				CONTINUE
			ARGS += @"%LOCALS:LCOUNT%\/"
		ENDSELECT
	NEXT
ENDIF



;共通属性追加済みを末尾に付け直す処理
IF STRCOUNT(ARGS, "共通属性追加済")
	ARGS = %EXTSTR(ARGS, "", "共通属性追加済/")%
	STR_ADD += "共通属性追加済/"
ENDIF

ARGS += @"%STR_ADD%"

RETURNF ARGS

;-------------------------------------------------
;衣装文字列ARGSから色に関するものを消し去る
;色に関わる文字列かどうかは@IS_CLOCOLORで判定する
;-------------------------------------------------
@RESET_CLOCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

ARGS = 
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			CONTINUE
		ARGS += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT
RETURNF ARGS

;-------------------------------------------------
;衣装文字列ARGSから色に関するもの＆正面蝴蝶結属性のみを /色/正面蝴蝶結属性 の形で抜き出す
;色に関わる文字列かどうかは@IS_CLOCOLORで判定する
;-------------------------------------------------
@CHECK_ADDCLO(ARGS)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

ARGS = 
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF IS_CLOCOLOR(LOCALS:LCOUNT) || STRCOUNT(LOCALS:LCOUNT, "正面蝴蝶結")
			ARGS += @"\/%LOCALS:LCOUNT%"
	ENDSELECT
NEXT
RETURNF ARGS

;-------------------------------------------------
;衣装文字列ARGSから色に関するものを返す
;色に関わる文字列かどうかは@IS_CLOCOLORで判定する
;注意：これは@CLOCOLOR内部での計算が主な用途
;-------------------------------------------------
@CHECK_CLOCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

;文字列に / を含まない場合は飾品なので特殊な判定をする
SIF STRCOUNT(ARGS, "/") == 0
	RETURNF CHECK_OTHERCOLOR(ARGS)

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			RETURNF LOCALS:LCOUNT
	ENDSELECT
NEXT
RETURNF ""

;-------------------------------------------------
;衣装文字列ARGSが色に関するものを含んでいるだけでそれを返す。主に飾品用
;画像表示される色を求められる饰に関しては、色が指定されていない場合には默认色を返す
;-------------------------------------------------
@CHECK_OTHERCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT TEXT_ALLCOLOR(), "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF STRCOUNT(ARGS, LOCALS:LCOUNT)
			RETURNF LOCALS:LCOUNT
	ENDSELECT
NEXT
;特に色指定がされていない饰は、もしあるならば默认カラーを返す
IF CUSEABLE(ARGS) != ""
	VARSET LOCALS
	SPLIT CUSEABLE(ARGS), "/", LOCALS
	RETURNF LOCALS
ENDIF

RETURNF ""

;-------------------------------------------------
;衣装文字列ARGSから色を返させる。ARGS:1に"形容詞"等を入れる事で変化を出せる
;-------------------------------------------------
@CLOCOLOR(ARGS, ARGS:1)
#FUNCTIONS
#DIMS COLOR_ABOUT
#DIMS COLOR_PATTERN

COLOR_PATTERN = 
;柄物格子
IF STRCOUNT(ARGS, "條紋")
	COLOR_PATTERN = 條紋
ELSEIF STRCOUNT(ARGS, "水玉")
	COLOR_PATTERN = 水玉模様
ELSEIF STRCOUNT(ARGS, "豹纹")
	COLOR_PATTERN = 豹纹
ENDIF

COLOR_ABOUT = %CHECK_CLOCOLOR(ARGS)%

VARSET LOCALS
SELECTCASE ARGS:1
CASE "色のみ"
	LOCALS = %COLOR_ABOUT%
CASE "形容詞"
	SIF COLOR_PATTERN != ""
		COLOR_ABOUT = %COLOR_PATTERN%

	SELECTCASE COLOR_ABOUT
	CASE ""
	CASE "赤", "青", "黒", "白"
		LOCALS = %COLOR_ABOUT%い
	CASE "柄"
		LOCALS = %COLOR_ABOUT%有些の
	CASEELSE
		LOCALS = %COLOR_ABOUT%の
	ENDSELECT
CASEELSE
	SIF COLOR_PATTERN != ""
		COLOR_ABOUT = %COLOR_PATTERN%
	LOCALS = %COLOR_ABOUT%
ENDSELECT
RETURNF LOCALS


;-------------------------------------------------
;衣装ARGS:1に、色ARGSを適用する関数。もしもARGSが使えない場合は、使用可能な別の色がARGSに代入される
;ARGS:1には衣装の文字列が入っている事が有参照できる（水玉模様の蕾丝的胖次、等が起きないように参照するため）
;-------------------------------------------------
@SET_CLOCOLOR(ARGS, ARGS:1)
#FUNCTIONS
#DIMS COLOR_USABLE

SIF SAVESTR:内衣の指定色 != ""
	RETURNF SAVESTR:内衣の指定色

;まずは間違いが起きやすい色(緑色or緑、など)の変換
SELECTCASE ARGS
CASE "橘色", "黒色", "紫色", "青色", "赤色", "白色", "薄緑色", "條紋色", "水玉色", "辣妹色"
	ARGS = %SUBSTRINGU(ARGS, 0, STRLENSU(ARGS) - 1)%
CASE "黄", "橙", "紺", "薄青", "薄桃", "緑", "灰", "水", "茶"
	ARGS = %ARGS%色
ENDSELECT

SELECTCASE ARGS
;絶対に変更が起きないもの
CASE "水玉模様"
	RETURNF ARGS
CASE "條紋"
	LOCALS = %TEXTR("紅白/緑白/青白/黄白/黒白")%
CASE "花柄"
	LOCALS = %TEXTR("桃色/水色/薄緑/青")%
CASE "水玉"
	LOCALS = 水玉%TEXTR("粉紅/黄色/黒/水色/白/白黒")%
CASE "淡色"
	LOCALS = %TEXTR("白/桃色/緑色/黄色/青")%
CASE "辣妹"
	LOCALS = %TEXTR("豹纹/黒/紫/赤")%
CASE "Ｈ的颜色"
	LOCALS = %TEXTR("黒/紫/真紅/赤/純白")%
CASE "男式色"
	LOCALS = %TEXTR("黒/紺色")%
CASE "儿童胖次颜色"
	LOCALS = %TEXTR("白/草莓紋/小熊紋/鱼紋/猫紋/小鸡紋/蝴蝶结紋/水玉模様/緑色")%
CASE "隨機顏色"
	LOCALS = %TEXTR("白/黒/赤/桃色/紫/青/緑色/黄色")%
CASE "比基尼色"
	LOCALS = %TEXTR("白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色")%
CASEELSE
	LOCALS = %ARGS%
ENDSELECT

COLOR_USABLE = 
;大人の事情により色制限がかかるものの管理。ARGS:1 == ""ならLOCALSが返る
;まずは後面要素
IF STRCOUNT(ARGS:1, "Ｔ字褲")
	IF STRCOUNT(ARGS:1, "丁字褲")
		COLOR_USABLE = %CUSEABLE("丁字褲")%
	ELSE
		COLOR_USABLE = %CUSEABLE("汎用Ｔ字褲")%
	ENDIF
ENDIF
;ついで前面
IF STRCOUNTS(ARGS:1, "吊帯背心")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("吊帯背心"), COLOR_USABLE)%
ELSEIF STRCOUNTS(ARGS:1, "開放型開洞")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("開放型開洞胖次"), COLOR_USABLE)%
ELSEIF STRCOUNTS(ARGS:1, "Micro內褲")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("Micro內褲"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "丁字褲")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("丁字褲"), COLOR_USABLE)%
ELSEIF STRCOUNTS(ARGS:1, "Micro比基尼", "淚珠泳衣")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("汎用Ｔ字褲"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "兜襠布")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("兜襠布"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "四角内褲")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("四角内褲"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "超低腰")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("超低腰胖次"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "蕾丝的胖次")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("蕾丝的胖次"), COLOR_USABLE)%
ELSEIF STRCOUNTS(ARGS:1, "內褲", "三角胖次", "高開衩", "高開衩胖次", "低腰胖次", "褶边")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("內褲"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "Ｃ胖次")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("Ｃ胖次"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "心形前貼")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("心形前貼"), COLOR_USABLE)%
ELSEIF STRCOUNT(ARGS:1, "前貼")
	COLOR_USABLE = %EXTRACT_COMMON_COLOR(CUSEABLE("前貼"), COLOR_USABLE)%
ENDIF
IF EXTRACT_COMMON_COLOR(COLOR_USABLE, LOCALS) != ""
	RETURNF LOCALS
ELSE
	RETURNF TEXTR(COLOR_USABLE)
ENDIF


;-------------------------------------------------
;角色ARGの胖次の色をARGSにし、その後胸罩の色を胖次に合わせる
;-------------------------------------------------
@EQ_CLOCOLOR, ARG, ARGS
#DIM LCOUNT
SIF ARG <= 0 && TARGET > 0
	ARG = TARGET
SELECTCASE ARGS
CASE ""
	;胸罩の色を胖次に合わせる
	SIF EQUIP:ARG:胸罩 && EQUIP:ARG:胖次 && STRCOUNT(CUSEABLE(BRA(ARG, "種類") ), CLOCOLOR(CSTR:ARG:胖次) )
		CSTR:ARG:胸罩 = %MERGE_STR(CSTR:ARG:胸罩, CHECK_CLOCOLOR(CSTR:ARG:胖次) )%
CASEELSE
	;胖次と胸罩の色をARGSに変える
	SIF EQUIP:ARG:胖次
		CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, ARGS)%
	SIF EQUIP:ARG:胸罩
		CSTR:ARG:胸罩 = %MERGE_STR(CSTR:ARG:胸罩, ARGS)%
ENDSELECT

EQUIP:ARG:胖次 = STRLENS(CSTR:ARG:胖次)
EQUIP:ARG:胸罩 = STRLENS(CSTR:ARG:胸罩)

;-------------------------------------------------
;ARGが服装ARGSを着る際にどの色を着るかを決める
;ARGS:1に色が入っている場合にはそれを優先する
;-------------------------------------------------
@CLOCOLOR_MAIDFACE(ARGS, ARGS:1, ARG)
#FUNCTIONS
#DIM LCOUNT
;その衣装で使うことが出来る色を/区切りで記述して、CSTR:喜欢的颜色 に対応できるようにする
#DIMS COLOR_USABLE
#DIMS COLOR_CHOICES
#DIMS COLOR_DEFO

SIF ARGS == ""
	RETURNF ""

COLOR_DEFO = 
COLOR_CHOICES = 
COLOR_USABLE = %CUSEABLE(ARGS, ARG)%

SIF ARGS:1 != "" && (STRCOUNT(COLOR_USABLE, ARGS:1) || COLOR_USABLE == "")
	RETURNF ARGS:1

VARSET LOCALS
SPLIT COLOR_USABLE, "/", LOCALS
;各衣裳の默认カラーをここで決めておく
COLOR_DEFO = %LOCALS:0%

;喜欢的颜色に変更する？
FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF STRCOUNT(CSTR:ARG:喜欢的颜色, LOCALS:LCOUNT) == 0
			CONTINUE
		COLOR_CHOICES = %LOCALS:LCOUNT%
		BREAK
	ENDSELECT
NEXT

SIF COLOR_CHOICES != ""
	RETURNF COLOR_CHOICES

;学年や立場などでの自動決定があるならここに書く
SELECTCASE ARGS
CASE "水手"
	;SIF CHECK_SHIRT("水手夏服", ARG)
	;	RETURNF "白"
	SELECTCASE CSTR:ARG:学年
	CASE "初等部", "中等部"
		RETURNF "紺色"
	CASEELSE
		RETURNF "青"
	ENDSELECT
CASE "男子背心"
	RETURNF "黒"
CASE "背心"
	RETURNF "橙色"
CASE "西装外衣", "領帶西装", "男子西装"
	IF CHECK_SHIRT("男物", ARG)
		IF COND("教員", ARG)
			RETURNF "黒"
		ELSE
			RETURNF "茶色"
		ENDIF
	ELSEIF COND("教員", ARG)
		RETURNF "赤"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "青"
		CASEELSE
			RETURNF "茶色"
		ENDSELECT
	ENDIF
CASE "領帯女式衬衫"
	IF COND("教員", ARG)
		RETURNF "青"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黒"
	ELSE
		RETURNF "赤"
	ENDIF
CASE "蝴蝶結女式衬衫"
	IF COND("教員", ARG)
		RETURNF "青"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黒"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "緑色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "运动衫", "运动衫休闲褲", "五分褲"
	IF COND("教員", ARG)
		IF STRCOUNT(CSTR:ARG:学年, "体育")
			RETURNF "緑色"
		ELSE
			RETURNF "黒"
		ENDIF
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部"
			RETURNF "青"
		CASE "中等部"
			RETURNF "青"
		CASE "１年生"
			RETURNF "緑色"
		CASE "２年生"
			RETURNF "赤"
		CASE "３年生"
			RETURNF "黒"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "詰襟运动衫"
	IF COND("教員", ARG)
		RETURNF "橙色"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黄色"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "緑色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "布鲁马", "体操服", "高開衩布鲁马", "体操服(短)"
	IF COND("教員", ARG)
		RETURNF "赤"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部"
			RETURNF "水色"
		CASE "中等部"
			RETURNF "緑色"
		CASE "１年生"
			RETURNF "紺色"
		CASE "２年生"
			RETURNF "紺色"
		CASE "３年生"
			RETURNF "紺色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "紧身短褲"
	SELECTCASE CSTR:ARG:学年
	CASE "初等部", "中等部", "１年生", "２年生", "３年生"
		RETURNF "黒"
	CASEELSE
		RETURNF "柄"
	ENDSELECT
CASE "競泳泳装"
	IF COND("教員", ARG)
		IF STRCOUNT(CSTR:ARG:学年, "体育")
			RETURNF "赤"
		ELSE
			RETURNF "黒"
		ENDIF
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "赤"
		CASE "１年生", "２年生", "３年生"
			RETURNF "青"
		CASEELSE
			RETURNF "黒"
		ENDSELECT
	ENDIF
CASE "旗袍"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		RETURNF "赤"
	CASEELSE
		IF COND("教員", ARG)
			RETURNF "黒"
		ELSE
			RETURNF "赤"
		ENDIF
	ENDSELECT
CASE "兔女郎制服"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		RETURNF "赤"
	CASE "金"
		RETURNF "青"
	CASEELSE
		IF COND("教員", ARG)
			RETURNF "黒"
		ELSE
			RETURNF "赤"
		ENDIF
	ENDSELECT
CASE "振袖"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		SELECTCASE NORM(1315, ARG)%4
		CASE 0
			RETURNF "紫"
		CASE 1
			RETURNF "青"
		CASE 2
			RETURNF "赤"
		CASE 3
			RETURNF "緑色"
		ENDSELECT
	CASE "赤"
		SELECTCASE NORM(1315, ARG)%4
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "紫"
		CASE 2
			RETURNF "青"
		CASE 3
			RETURNF "緑色"
		ENDSELECT
	CASEELSE
		SELECTCASE NORM(1315, ARG)%5
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "紫"
		CASE 2
			RETURNF "青"
		CASE 3
			RETURNF "赤"
		CASE 4
			RETURNF "緑色"
		ENDSELECT
	ENDSELECT
CASE "浴衣"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "金"
		SELECTCASE NORM(1316, ARG)%4
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "赤"
		CASE 2
			RETURNF "緑色"
		CASE 3
			RETURNF "青"
		ENDSELECT
	CASEELSE
		SELECTCASE NORM(1316, ARG)%5
		CASE 0
			RETURNF "黄色"
		CASE 1
			RETURNF "黒"
		CASE 2
			RETURNF "赤"
		CASE 3
			RETURNF "緑色"
		CASE 4
			RETURNF "青"
		ENDSELECT
	ENDSELECT
CASE "和服"
	SELECTCASE NORM(1317, ARG)%5
	CASE 0
		RETURNF "黒"
	CASE 1
		RETURNF "青"
	CASE 2
		RETURNF "赤"
	CASE 3
		RETURNF "白"
	CASE 4
		RETURNF "緑色"
	ENDSELECT
CASE "眼鏡"
	IF STRCOUNT(CSTR:ARG:喜欢的颜色, "黒")
		RETURNF "黒"
	ELSEIF STRCOUNT(CSTR:ARG:喜欢的颜色, "赤")
		RETURNF "赤"
	ELSEIF STRCOUNT(CSTR:ARG:喜欢的颜色, "青")
		RETURNF "青"
	ELSE
		SELECTCASE CONDS("肤色", ARG)
		CASE "普通"
			RETURNF "黒"
		CASE "褐色肌", "色黒"
			SELECTCASE CONDS("右瞳色", ARG)
			CASE "赤", "紅"
				RETURNF "青"
			CASEELSE
				RETURNF "赤"
			ENDSELECT
		CASEELSE
			SELECTCASE CONDS("右瞳色", ARG)
			CASE "赤", "紅"
				RETURNF "黒"
			CASEELSE
				RETURNF "赤"
			ENDSELECT
		ENDSELECT
	ENDIF
CASE "贝雷帽"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "青"
		RETURNF "茶色"
	CASE "黒", "銀", "緑"
		RETURNF "赤"
	CASE "金", "赤", "茶", "桃"
		RETURNF "緑色"
	CASEELSE
		RETURNF "白"
	ENDSELECT
CASE "両側蝴蝶結", "单側蝴蝶結"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "桃", "紫"
		RETURNF "白"
	CASE "青", "緑"
		RETURNF "黄"
	CASE "金"
		RETURNF "青"
	CASE "赤"
		RETURNF "緑"
	CASEELSE
		RETURNF "赤"
	ENDSELECT
CASE "両側蝴蝶結団子頭", "单側蝴蝶結団子頭"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "赤", "桃", "紫", "黒", "茶"
		RETURNF "白"
	CASEELSE
		RETURNF "黒"
	ENDSELECT
ENDSELECT



;まだ決まっていない場合は默认カラーを採用
RETURNF COLOR_DEFO

;角色ARGの身に着けている衣装ARGSが、色ARGS:1を使ってよいか見る
@CHECK_CUSEABLE(ARGS, ARGS:1, ARG)
#FUNCTION
SIF STRCOUNT(@"/%CUSEABLE(ARGS, ARG)%/", @"/%ARGS:1%/")
	RETURNF 1
RETURNF 0


;現状だと水手服と围巾以外はARG省略しても良い
@CUSEABLE(ARGS, ARG)
#FUNCTIONS
#DIMS COLOR_USABLE

SIF ARG == 0 && TARGET
	ARG = TARGET

COLOR_USABLE = 

SELECTCASE ARGS
CASE ""
	RETURNF ""
CASE "荷葉邊"
	COLOR_USABLE = 白/黒/青/桃色/水色/緑色/黄色
CASE "花柄荷葉邊"
	COLOR_USABLE = 桃色/水色/薄緑/青
CASE "百褶裙", "長百褶裙", "迷你百褶裙", "超短迷你百褶裙", "Micro百褶裙"
	COLOR_USABLE = 紺色/黒/灰色/白/赤
CASE "格子裙", "長格子裙", "迷你格子裙", "超短迷你格子裙", "Micro格子裙"
	COLOR_USABLE = 赤/灰色/青/黄色
CASE "紧身裙", "長紧身裙", "迷你紧身裙", "Micro紧身裙"
	COLOR_USABLE = 黒/灰色/青/赤

CASE "工装裤"
	COLOR_USABLE = 青/白/桃色

;萌え袖グループ
CASE "V领毛衣", "前開V领毛衣", "露肩夹克", "露脐装超短裙V领毛衣", "裙V领毛衣", "露脐装超短裙前開V领毛衣", "裙前開V领毛衣", "萌え袖V领毛衣"
	COLOR_USABLE = 灰色/橙色/赤/青/緑色/濃紫/桃色/白/黒
CASE "萌袖毛衣"
	COLOR_USABLE = 白/赤/緑色/灰色/黒

CASE "皮革夹克", "恤皮革夹克"
	COLOR_USABLE = 黒
CASE "圆领衬衫"
	COLOR_USABLE = 白/赤/青/緑色
CASE "連衣裙"
	COLOR_USABLE = 白/黒
CASE "褶边連衣裙"
	COLOR_USABLE = 赤/青/緑色/黒
CASE "露肩連衣裙"
	COLOR_USABLE = 白/桃色/水色/黒
CASE "哥特連衣裙"
	COLOR_USABLE = 黒/赤/青/緑色
CASE "洛丽塔連衣裙"
	COLOR_USABLE = 赤/桃色/水色/焦茶色
CASE "格子連衣裙", "格子連衣裙黒蝴蝶結", "格子連衣裙Ｂ", "格子連衣裙Ｂ黒蝴蝶結"
	COLOR_USABLE = 橙色/青/緑色
CASE "Ｔ恤", "怪图案Ｔ恤", "宽大的Ｔ恤"
	COLOR_USABLE = 黄色/白/黒/赤/緑色/青/紫/桃色/橙色
CASE "変な柄"
	COLOR_USABLE = 漢字１/英字１/英字２/ＡＡ１
CASE "睡衣"
	COLOR_USABLE = 薄緑/白/紺色/桃色/水色
CASE "連帽風衣", "長連帽風衣", "无袖連帽風衣", "前開連帽風衣", "前開无袖連帽風衣"
	COLOR_USABLE = 黒/灰色/白/青/赤/桃色/米色/緑色/紺色
CASE "哥特式披风"
	COLOR_USABLE = 赤/青/緑色
CASE "哥特蘿莉礼服"
	COLOR_USABLE = 黒/赤/白
CASE "夏日礼服"
	COLOR_USABLE = 黒/緑色/赤/白
CASE "紧身長袍"
	COLOR_USABLE = 黄色/紺色
CASE "中世紧身長袍"
	COLOR_USABLE = 赤/緑色/青
CASE "執事服"
	COLOR_USABLE = 黒/灰色/緑色
CASE "女仆装", "潇洒的女仆装", "露脐装超短裙女仆", "法式女仆装"
	COLOR_USABLE = 青/黒/赤/緑色/桃色/水色
CASE "聖誕服"
	COLOR_USABLE = 赤
CASE "開胸旗袍"
	COLOR_USABLE = 黒/赤/青/緑色/白
CASE "旗袍"
	COLOR_USABLE = 赤/青/黒/緑色
CASE "露脐女式衬衫"
	COLOR_USABLE = 白
CASE "女式衬衫", "宽松女式衬衫", "美式校服"
	COLOR_USABLE = 白
CASE "无袖", "无袖女式衬衫", "无袖Ｂ", "无袖女式衬衫Ｂ", "露脐无袖", "露脐无袖女式衬衫"
	COLOR_USABLE = 白
CASE "領帯女式衬衫"
	COLOR_USABLE = 赤/青/黒
CASE "蝴蝶結女式衬衫", "蝴蝶結領帯丝巾女式衬衫", "女教師"
	COLOR_USABLE = 赤/青/黒/緑色
CASE "褶边女式衬衫"
	COLOR_USABLE = 黒/白
CASE "蕾丝女式衬衫"
	COLOR_USABLE = 黒/白/米色
CASE "領帯无袖女式衬衫", "露脐領帯无袖女式衬衫"
	COLOR_USABLE = 赤/黒/青/紫
CASE "彼恤"
	COLOR_USABLE = 白/黒
CASE "前開的衬衫"
	COLOR_USABLE = 白/黒/緑色/紺色
CASE "水手服", "水手夏服", "无袖水手服", "露脐装超短裙水手服", "超短水手服"
	COLOR_USABLE = 青/紺色/白/黒/赤/灰色
CASE "西装外衣", "領帶西装", "蝴蝶結領帯丝巾西装外衣", "露脐装超短裙西装外衣", "露脐装超短裙領帶西装", "裙西装外衣", "男子西装"
	COLOR_USABLE = 茶色/灰色/青/赤/緑色/黒
CASE "背心", "露脐装超短裙背心", "男子背心"
	COLOR_USABLE = 橙色/灰色/赤/青/緑色/濃紫/桃色/白/黒/紺色

CASE "校服"
	COLOR_USABLE = 黒
CASE "体操服", "露脐体操服", "体操服布鲁马", "体操服高開衩布鲁马", "前開运动衫布鲁马", "体操服五分褲", "体操服紧身短褲", "体操服运动衫"
	COLOR_USABLE = 紺色/水色/赤/緑色

CASE "运动衫", "露脐装超短裙运动衫", "裙运动衫", "前開运动衫", "运动衫布鲁马", "运动衫高開衩布鲁马", "运动衫五分褲", "运动衫紧身短褲", "上下运动衫", "运动衫休闲褲"
	COLOR_USABLE = 黒/青/赤/緑色
CASE "詰襟运动衫"
	COLOR_USABLE = 黄色/青/赤/白/緑色/橙色
CASE "吊帯背心", "露脐吊帯背心"
	COLOR_USABLE = 白/黒/青/緑色
CASE "吊帯衫", "露脐吊帯衫"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色
CASE "露肩外衣"
	COLOR_USABLE = 米色/黒/白/灰色

CASE "针织毛衣", "縦毛衣", "无袖毛衣", "露背针织毛衣", "高領毛衣", "開胸高領毛衣"
	COLOR_USABLE = 茶色/白/紺色/赤/緑色/水色/米色/灰色/茶色/黒/紫

CASE "无袖高領毛衣"
	COLOR_USABLE = 白/灰色/赤/青/米色/緑色/濃紫/薄桃色/黒/焦茶色

CASE "振袖"
	COLOR_USABLE = 赤/黒/紫/青/緑色
CASE "浴衣"
	COLOR_USABLE = 黄色/黒/青/赤/緑色
CASE "和服"
	COLOR_USABLE = 黒/青/赤/白/緑色/橙色

CASE "内衣胸罩", "３／４杯胸罩", "半杯胸罩", "Micro胸罩", "１／４杯胸罩"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色/豹纹
CASE "杯罩", "简约胸罩", "满杯胸罩", "開放型胸罩", "抹胸"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色/豹纹
CASE "蕾丝胸罩"
	COLOR_USABLE = 純白/白/黒/赤/青/緑色/黄色/紫/桃色
CASE "運動胸罩"
	COLOR_USABLE = 灰色/黒/赤/桃色/紺色/青

CASE "褶边胸罩"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色/豹纹
CASE "褶边吊罩"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色

CASE "比基尼胸罩"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/豹纹
CASE "褶边比基尼胸罩"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色/豹纹
CASE "Micro比基尼胸罩"
	COLOR_USABLE = 黒/紫/赤/青/白/豹纹

CASE "拘束衣胸罩", "紐拘束衣胸罩", "拘束衣", "紐拘束衣", "拘束衣胖次", "開放型拘束衣胖次"
	COLOR_USABLE = 黒/赤/真紅

CASE "猫型胸罩"
	COLOR_USABLE = 白/桃色/黒
CASE "裹胸布"
	COLOR_USABLE = 白

CASE "彈弓泳衣"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/豹纹
CASE "競泳泳装", "高開衩競泳泳装"
	COLOR_USABLE = 青/黒/赤/緑色
CASE "新死库水", "紧身短褲型死库水"
	COLOR_USABLE = 紺色
CASE "死库水"
	COLOR_USABLE = 紺色/白
CASE "情趣内衣"
	COLOR_USABLE = 黒/赤/緑色
CASE "晨衣"
	COLOR_USABLE = 白/黒/桃色/水色/米色/薄緑/紫

CASE "両側蝴蝶結", "单側蝴蝶結"
	COLOR_USABLE = 赤/白/黄/青/緑
CASE "両側蝴蝶結団子頭", "单側蝴蝶結団子頭"
	COLOR_USABLE = 白/黒
CASE "布鲁马"
	COLOR_USABLE = 赤/紺色/水色/緑色
CASE "学生休闲褲"
	COLOR_USABLE = 黒
CASE "西褲"
	COLOR_USABLE = 黒/青/灰色/緑色/桃色/白
CASE "五分褲", "五分褲泳装"
	COLOR_USABLE = 黒/青/赤/緑色/白/灰色/米色/薄桃色/桃色
CASE "热褲", "牛仔热褲", "迷你热褲", "前開热褲", "長褲", "前開長褲", "休闲褲", "前開休闲褲", "西褲"
	COLOR_USABLE = 青/黒/灰色/緑色/桃色/白
CASE "短褲", "ダメージ短褲", "迷你短褲", "前開短褲"
	COLOR_USABLE = 青/黒/灰色/緑色/桃色/白
CASE "牛仔褲", "前開牛仔褲", "低腰牛仔褲"
	COLOR_USABLE = 青/黒/灰色/緑色/桃色/白
CASE "開裆皮裤"
	COLOR_USABLE = 黒
CASE "猫耳"
	COLOR_USABLE = 黒/白/茶色
CASE "兔女郎制服", "兔耳", "蝶形領帯", "生足兔女郎制服", "连褲襪兔女郎制服", "裤襪兔女郎制服", "開放型连褲襪兔女郎制服", "開放型裤襪兔女郎制服", "開放型网纹裤襪兔女郎制服", "高開衩兔女郎制服"
	COLOR_USABLE = 赤/黒/青/紫/白
CASE "逆兔女郎套装"
	COLOR_USABLE = 赤
CASE "紧身短褲"
	COLOR_USABLE = 黒/柄
CASE "连褲襪", "開放型连褲襪"
	COLOR_USABLE = 黒/赤/白
CASE "裤襪", "開放型裤襪"
	COLOR_USABLE = 黒/赤
CASE "网纹裤襪", "開放型网纹裤襪", "渔网连褲襪", "開放型渔网连褲襪"
	COLOR_USABLE = 黒
CASE "吊帯内衣", "吊帯襪", "吊帯長筒襪"
	COLOR_USABLE = 純白/白/黒/赤/真紅/桃色/青/紺色/薄緑

;以下は胖次
CASE "胖次花绳"
	COLOR_USABLE = 純白/白/黒/灰色/真紅/赤/桃色/紫/青/水色/薄緑/緑色/黄色/橙色/米色/豹纹/奶牛/虎紋/紺色
	COLOR_USABLE += "水玉粉紅/水玉黒/水玉黄色/水玉水色/水玉白黒/水玉白"
	COLOR_USABLE += "紅白/緑白/青白/黄白/黒白"

CASE "比基尼", "紐比基尼", "Ｔ字褲比基尼", "紐Ｔ字褲比基尼"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/豹纹
CASE "內褲", "简约內褲", "三角胖次", "高開衩", "高開衩胖次", "低腰胖次", "超低腰胖次", "褶边の胖次", "褶边高開衩", "四角内褲", "四角褲", "四角内褲贴阴内裤", "四角褲贴阴内裤"
	COLOR_USABLE = 純白/白/黒/赤/水色/緑色/黄色/紫/桃色
CASE "蕾丝的胖次", "総蕾丝的胖次"
	COLOR_USABLE = 純白/白/黒/赤/水色/緑色/紫/桃色
CASE "透明"
	COLOR_USABLE = 純白/白/黒/赤/水色/緑色/紫/桃色/豹纹
CASE "正面蝴蝶結內褲", "正面蝴蝶結三角胖次"
	COLOR_USABLE = 三角黄/三角黒/三角青/三角赤/三角白/紐黄/紐黒/紐青/紐赤/紐白
CASE "正面蝴蝶結褶边の胖次", "正面蝴蝶結褶边高開衩"
	COLOR_USABLE = 紐黄/紐黒/紐青/紐赤/紐白

CASE "高開衩布鲁马"
	COLOR_USABLE = 紺色/赤/水色/緑色
CASE "丁字褲", "紐丁字褲"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/豹纹/奶牛/虎紋/
	COLOR_USABLE += "水玉粉紅/水玉黒/水玉黄色/水玉水色/水玉白黒/水玉白"
CASE "丁字褲比基尼", "紐丁字褲比基尼"
	COLOR_USABLE = 白/黒/灰色/赤/桃色/紫/青/水色/薄緑/緑色/黄色/豹纹
CASE "兜襠布"
	COLOR_USABLE = 黒/赤/桃色/白
CASE "儿童胖次"
	COLOR_USABLE = 白/草莓紋/小熊紋/鱼紋/猫紋/小鸡紋/蝴蝶结紋/水玉模様/緑色
CASE "Micro比基尼", "淚珠泳衣比基尼", "汎用Ｔ字褲"
	COLOR_USABLE = 黒/紫/赤/青/白/豹纹
CASE "Micro內褲"
	COLOR_USABLE = 黒/紫/赤/青/白
CASE "衝浪胖次"
	COLOR_USABLE = 黒
CASE "條紋胖次", "縞胖次", "條紋胸罩"
	COLOR_USABLE = 紅白/緑白/青白/黄白/黒白
CASE "襯褲"
	COLOR_USABLE = 黒/白
CASE "開放型開洞胖次", "贴身開放型開洞胖次"
	COLOR_USABLE = 純白/白/黒/紫/真紅/赤
CASE "前貼"
	COLOR_USABLE = 白
CASE "心形前貼", "Ｃ胖次", "乳贴", "心形乳貼", "圆形乳贴", "×型乳贴", "星型乳贴"
	COLOR_USABLE = 黒/桃色/青
CASE "叶子胖次"
	COLOR_USABLE = 緑色

CASE "手套", "絹手套", "革手套", "指抜きグローブ", "絲綢手套", "礼服手套", "皮革袖套", "礼服袖套", "手臂束缚器"
	COLOR_USABLE = 白/黒

;以下は饰枠
CASE "眼鏡", "专家眼鏡", "黑框眼鏡", "金丝眼鏡", "玳瑁眼鏡"
	COLOR_USABLE = 黒/赤/青
CASE "眼罩"
	COLOR_USABLE = 白/黒
CASE "口罩", "顎口罩"
	COLOR_USABLE = 白/黒
CASE "围巾", "围巾Ｂ", "围巾Ｃ"
	IF CEVENT("猩々木に祝福を祈る", ARG)
		COLOR_USABLE = 橙色/緑色/紫/赤/黄色/青/白/藤の花
	ELSE
		COLOR_USABLE = 橙色/緑色/紫/赤/黄色/青/白
	ENDIF
CASE "铃铛项圈"
	COLOR_USABLE = 黒/茶色/赤
CASE "项圈"
	COLOR_USABLE = 赤/黒/青
CASE "牛铃"
	COLOR_USABLE = 赤/青/緑色/紫/茶色/桃色/白/黒

CASE "礼服頭花"
	COLOR_USABLE = 白/黒
CASE "贝雷帽"
	COLOR_USABLE = 赤/白/緑色/茶色
CASE "鸭舌帽"
	COLOR_USABLE = 茶色/黒/灰色/白/赤/青/緑色
CASE "水兵帽"
	COLOR_USABLE = 白/青/赤/桃色/茶色/黒
CASE "胖次帽"
	COLOR_USABLE = 白/黒/純白/灰色/赤/桃色/紫/青/紺色/水色/薄緑/緑色/黄色/橙色/米色/水玉白/水玉黒/水玉白黒/水玉粉紅/水玉水色/水玉黄色/青白/紅白/緑白/黄白


;衣领颜色
CASE "褞袍"
	COLOR_USABLE = 茶色/灰色/赤/緑色/紺色
CASE "粗呢外套"
	COLOR_USABLE = 茶色/白/青/黒/焦茶色
CASE "外套"
	COLOR_USABLE = 茶色/赤/白
CASE "工作围裙"
	COLOR_USABLE = 橙色/灰色/赤/青/緑色/濃紫/桃色/白/黒
CASE "裤吊帯"
	COLOR_USABLE = 黒/白/茶色/赤/青/焦茶色
CASE "褶边裙吊帯"
	COLOR_USABLE = 白/黒/桃色

CASEELSE
	COLOR_USABLE = 
ENDSELECT

RETURNF COLOR_USABLE


;-------------------------------------------------
;色文字列ARGSとARGS:1の共通の色文字列のみを取り出す
;もしも片方が空文字ならもう片方を返す
;-------------------------------------------------
@EXTRACT_COMMON_COLOR(ARGS, ARGS:1)
#FUNCTIONS
#DIM LCOUNT
#DIMS TEXT_EXTRACT

SIF ARGS:1 == ""
	RETURNF ARGS
SIF ARGS == ""
	RETURNF ARGS:1

TEXT_EXTRACT = 
VARSET LOCALS
SPLIT ARGS:1, "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF STRCOUNT(@"\/%ARGS%\/", @"\/%LOCALS:LCOUNT%\/") == 0
			CONTINUE
		SIF TEXT_EXTRACT != ""
			TEXT_EXTRACT += "/"
		TEXT_EXTRACT += @"%LOCALS:LCOUNT%"
	ENDSELECT
NEXT
RETURNF TEXT_EXTRACT
