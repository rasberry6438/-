;-------------------------------------------------
;拘束されて弄ばれるイベント
;欲求不満イベントに変更してみる
;-------------------------------------------------
@BIND_AT_MORNING
#DIM LCOUNT
#DIM MEMO_ABL_MASTER
;随机選択に用いる
#DIM NUM_FIND
;行為の回数
#DIM NUM_PLAY, 3
;乱交参加者の人数
#DIM CHARANUM_RANKOU
;乱交PLAY参加者の名前の羅列。%CALLNAME:TARGET%と%CALLNAME:(TARGET:1)% など
#DIMS CNAMES_RANKOU, 2
#DIMS MEMO_EVENT

;周日朝だけ＆约会の日は起きない
SIF TEXTS("曜日") != "日" || CHARA_NUM("约会的约定")
	RETURN 0

;そもそもこの種類のイベントが起きるかの格子
CALL CHECK_BIND_AT_MORNING, TARGET, ""

;TARGETがPLAYの条件を満たしているか？
SIF CSTR:朝袭 == ""
	RETURN 0

;複数角色を使うイベント用の対象フラグ重置
VARSET TARGET, 0, 1, 4

;一時的にイベント名を記録
MEMO_EVENT = %CSTR:朝袭%

NUM_FIND = 1
;仲間を呼ぶ
FOR LCOUNT, 2, CHARANUM
	SIF LCOUNT == TARGET || LCOUNT == MASTER
		CONTINUE

	CALL CHECK_BIND_AT_MORNING, LCOUNT, CSTR:朝袭
	;選択されているPLAYを許容できるなら欲求不満値を記録して、参加候補入り（ＭＡＸ二人）
	SIF CSTR:朝袭 != MEMO_EVENT
		CONTINUE

	IF TARGET:1 == 0
		TARGET:1 = LCOUNT
	ELSEIF TARGET:2 == 0
		TARGET:2 = LCOUNT
	ELSEIF RAND:NUM_FIND <= 1
		TARGET:(RAND:2 + 1) = LCOUNT
	ENDIF
	NUM_FIND += 1
NEXT

CSTR:朝袭 = %MEMO_EVENT%

CNAMES_RANKOU = %CALLNAME:TARGET%
CNAMES_RANKOU:1 = %CALLNAME:TARGET%
NUM_PLAY = MIN(5, ABL:欲望/2) + RAND:(MIN(5, ABL:施虐属性) + 5) + TALENT:淫乱*2 + TALENT:恋慕

IF TARGET:1 > 0
	CNAMES_RANKOU = %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
	CNAMES_RANKOU:1 = %CALLNAME:TARGET%她们
	NUM_PLAY:1 = MIN(5, ABL:(TARGET:1):欲望/2) + RAND:(MIN(5, ABL:(TARGET:1):施虐属性) + 1) + TALENT:(TARGET:1):淫乱*2 + TALENT:(TARGET:1):恋慕
ENDIF
IF TARGET:2 > 0
	CNAMES_RANKOU = %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%还有%CALLNAME:(TARGET:2)%
	CNAMES_RANKOU:1 = %CALLNAME:TARGET%她们
	NUM_PLAY:2 = MIN(5, ABL:(TARGET:2):欲望/2) + RAND:(MIN(5, ABL:(TARGET:2):施虐属性) + 1) + TALENT:(TARGET:2):淫乱*2 + TALENT:(TARGET:2):恋慕
ENDIF

CHARANUM_RANKOU = 1 + (TARGET:1 > 0) + (TARGET:2 > 0)
DRAWLINE

CVARSET TEQUIP, GETNUM(TEQUIP, "推倒"), 1
;拘束されているということで技巧を下げる
MEMO_ABL_MASTER = ABL:MASTER:技巧
ABL:MASTER:技巧 = 1

CALL SIMULATE_TRAIN_FIRST, CHARANUM_RANKOU

SELECTCASE CSTR:朝袭
CASE "朝ＳＭ"
	PRINTFORMW 早上、一觉醒来发现身体動不了……
	PRINTFORMW 不知道什么时候被裸体地绑在了床上
	PRINTFORMW 俯视着这边的%CNAMES_RANKOU%、用充满嗜虐和情欲的眼神打着招呼……
	CALL KOJO_EVENT, CSTR:朝袭
	PRINTL 
	PRINTFORMW 就这様一直到夜里、被%CNAMES_RANKOU:1%持续地责弄着……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 1, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, MAX(NUM_PLAY:LCOUNT/(3 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		IF RAND:2 && COND("女性主導生性交")
			CALL COM_ABLE65
			SIF RESULT && TALENT:処女 + TALENT:MASTER:童貞 == 0 && ABL:信頼+ABL:欲望+ABL:Ｖ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 65, MAX(NUM_PLAY:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		IF RAND:3 == 0
			CALL COM_ABLE75
			SIF RESULT && TALENT:Ａ処女 == 0 && EXP:PLAYER:Ａ挿入経験 && ABL:Ａ感覚 >= MAX(3, ABL:Ｖ感覚) && ABL:信頼+ABL:欲望+ABL:Ａ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 75, MAX(NUM_PLAY:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 90, NUM_PLAY:LCOUNT/(3 + RAND:3)
		CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, MAX(NUM_PLAY:LCOUNT/(4 + RAND:3), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束騎乗位"
	PRINTFORMW 早上、一觉醒来感觉身体好重……\@ TARGET:1 > 0 ? 不仅如此、呼吸也有些困难… # \@
	IF TARGET:1
		PRINTFORM …认清状况花了些時間、好像是被%CALLNAME:(TARGET:1)%用脸
		SIF TARGET:2
			PRINTFORM 、和%CALLNAME:(TARGET:2)%用手臂、
		PRINTFORMW 入迷地摩擦着股間的样子
		PRINTFORMW 不止如此、还有%CALLNAME:TARGET%正在以騎乗位享受着、股間各种液体都有、真是够厉害的…
		PRINTL 
		PRINTFORMW 刚刚醒来的%CALLNAME:MASTER%注意到了%CNAMES_RANKOU:1%被嗜虐和情欲充满的眼神、看来短时间内是不会被放開了…
	ELSE
		PRINTFORMW 好像是、不知道什么时候被裸体绑在了床上的样子…
		PRINTFORMW 看向感到重量的地方时、目光和骑在腰上的%CALLNAME:TARGET%对上了
		PRINTFORMW 就像是享受了长时间的结合一様、結合部起泡的液体吧嗒吧嗒地往下滴着、变得黏黏糊糊的…
		PRINTL 
		PRINTFORMW 看着%CALLNAME:TARGET%被嗜虐和情欲充满的眼神、看来短时间内是不会被放開了…
	ENDIF

	CALL KOJO_EVENT, CSTR:朝袭
	PRINTL 
	PRINTFORMW 就这様一直到夜里、被%CNAMES_RANKOU:1%持续地榨取着、责弄着……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, NUM_PLAY:LCOUNT/(2 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, NUM_PLAY:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, NUM_PLAY:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束Ａ騎乗位"
	PRINTFORMW 早上、一觉醒来感觉身体好重……\@ TARGET:1 > 0 ? 不仅如此、呼吸也有些困难… # \@
	IF TARGET:1
		PRINTFORM …认清状况花了些時間、好像是被%CALLNAME:(TARGET:1)%用脸
		SIF TARGET:2
			PRINTFORM 、和%CALLNAME:(TARGET:2)%用手臂、
		PRINTFORMW 入迷地摩擦着股間的样子
		PRINTFORMW 不止如此、还有%CALLNAME:TARGET%正在用尻穴享受着、股間各种液体都有、真是够厉害的…
		PRINTL 
		PRINTFORMW 刚刚醒来的%CALLNAME:MASTER%注意到了%CNAMES_RANKOU:1%被嗜虐和情欲充满的眼神、看来短时间内是不会被放開了…
	ELSE
		PRINTFORMW 好像是、不知道什么时候被裸体绑在了床上的样子…
		PRINTFORMW 看向感到重量的地方时、目光和骑在腰上肛門里含着阴茎的%CALLNAME:TARGET%对上了
		PRINTFORMW 就像是享受了长时间的结合一様、結合部起泡的液体吧嗒吧嗒地往下滴着、变得黏黏糊糊的…
		PRINTL 
		PRINTFORMW 看着%CALLNAME:TARGET%被嗜虐和情欲充满的眼神、看来短时间内是不会被放開了…
	ENDIF

	CALL KOJO_EVENT, CSTR:朝袭
	PRINTL 
	PRINTFORMW 就这様一直到夜里、被%CNAMES_RANKOU:1%持续地榨取着、责弄着……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, NUM_PLAY:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, NUM_PLAY:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
;今のところこの下は没有
CASE "朝拘束逆強姦"
	PRINTFORMW 早上、一觉醒来发现身体動不了……
	PRINTFORMW 不知道什么时候被裸体地绑在了床上
	PRINTFORMW 俯视着这边的%CNAMES_RANKOU%、用充满嗜虐和情欲的眼神打着招呼……
	CALL KOJO_EVENT, CSTR:朝袭
	PRINTFORMW 就这様一直到夜里、被%CNAMES_RANKOU:1%持续地责弄着……
CASE "朝拘束逆肛交"
	PRINTFORMW 早上、一觉醒来发现身体動不了……
	PRINTFORMW 不知道什么时候被裸体地绑在了床上
	PRINTFORMW 俯视着这边的%CNAMES_RANKOU%、用充满嗜虐和情欲的眼神打着招呼……
	CALL KOJO_EVENT, CSTR:朝袭
	PRINTFORMW 就这様一直到夜里、被%CNAMES_RANKOU:1%持续地责弄着……
ENDSELECT

CALL SIMULATE_TRAIN_CALC
CALL SIMULATE_TRAIN_SHOW
WAIT

CALL KOJO_GREETING, "朝拘束", TARGET

CALL CALC, "欲求不満解消", TARGET, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*15
IF TARGET:1 > 0
	CALL CALC, "欲求不満解消", TARGET:1, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*10
	;仲良くなる
	CALL CHANGE_RELATION, 5, TARGET, TARGET:1, TARGET:2
	CSTR:(TARGET:1):朝袭 = CSTR:朝袭
ENDIF
IF TARGET:2 > 0
	CALL CALC, "欲求不満解消", TARGET:2, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*10
	CSTR:(TARGET:2):朝袭 = CSTR:朝袭
ENDIF

CVARSET TEQUIP, GETNUM(TEQUIP, "推倒")

ABL:MASTER:技巧 = MEMO_ABL_MASTER
RETURN 1


@CHECK_BIND_AT_MORNING, ARG, ARGS
#DIM LCOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

;欲求不満度が限界までは行ってないor欲望が5LV未満、好感度が500未満
SIF COND("欲求不満", ARG) < 3 || ABL:ARG:欲望 < 5 || CFLAG:ARG:好感度 < 500	|| COND("只和你Ｈ")
	RETURN 0

VARSET EVENT_TABLE

;施虐属性5LVならＳＭコース
IF ABL:ARG:施虐属性 >= 5
	IF ARGS == "朝ＳＭ"
		CSTR:朝袭 = 朝ＳＭ
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝ＳＭ/"
ENDIF

CALL COM_ABLE65
IF RESULT && PENIS(MASTER) && TALENT:ARG:処女 + TALENT:MASTER:童貞 == 0 && ABL:ARG:Ｖ感覚 >= 3 && EXP:ARG:Ｖ性交経験 >= 10 && EXP:ARG:腰靈活経験 >= 20 && COND("女性主導生性交", ARG)
	IF ARGS == "朝拘束騎乗位"
		CSTR:朝袭 = 朝拘束騎乗位
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝拘束騎乗位/"
ENDIF

CALL COM_ABLE75
IF RESULT && PENIS(MASTER) && EXP:MASTER:Ａ挿入経験 && COND("Ａ熟達", ARG)
	IF ARGS == "朝拘束Ａ騎乗位"
		CSTR:朝袭 = 朝拘束Ａ騎乗位
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝拘束Ａ騎乗位/"
ENDIF

CSTR:朝袭 = %TEXTR(EVENT_TABLE)%

RETURN 0

