;文化祭についての話題があったので、できるとこまでやってやろうじゃねえか！　という試み
;製作者  道端の菠萝包
;-------------------------------------------------
;文化祭出し物の解説文
;-------------------------------------------------
@MESSAGE_BUNKASAI, ARGS

SELECTCASE ARGS
CASE "乐队演奏"
	PRINTFORMW %CALLNAME:TARGET%在体育馆和其他乐队成员一起演奏音乐。
CASE "演劇"
	PRINTFORMW %CALLNAME:TARGET%在体育馆中表演%ARGS%。
CASE "占卜館"
	PRINTFORMW 看起来%CALLNAME:TARGET%的班级展出的项目是让%CALLNAME:TARGET%进行占卜的样子。
CASE "恐怖屋敷"
	PRINTFORMW 看起来%CALLNAME:TARGET%的班级展出的项目是%ARGS%的样子。
;以下はまだ本文が無い物
CASE "烦悩相談室"
	PRINTFORMW 看起来%CALLNAME:TARGET%借了教室的一角正在做烦恼相谈的样子。
CASE "PaFuPaFu屋"
	PRINTFORMW 看起来%CALLNAME:TARGET%的班级正在做%ARGS%的样子
CASE "女仆咖啡厅", "執事咖啡厅", "西式料理店"
	PRINTFORMW 看起来%CALLNAME:TARGET%的班级%TEXTR(@"的展示项目是%ARGS%的样子/正在进行%ARGS%的样子/正在做%ARGS%的样子/的展示项目是%ARGS%的样子")%。
ENDSELECT
;台詞
CALL KOJO_DATE, ARGS

;-------------------------------------------------
;文化祭出し物の本文
;-------------------------------------------------
@DAILY_MESSAGE_BUNKASAI, ARGS

SELECTCASE ARGS
CASE "乐队演奏"
	PRINTFORM %CALLNAME:TARGET%
	SELECTCASE RAND:5
	CASE 0
		PRINTFORM 作为主唱、
		IF TALENT:内気
			PRINTFORM 和平常比简直换了个人一样热情地
		ELSEIF TALENT:喜歡引人注目
			PRINTFORM 摆出生动的表情活灵活现地
		ELSEIF TALENT:淫乱
			PRINTFORM 蠱惑地摆动腰肢的同时
		ELSE
			PRINTFORM 青春洋溢地
		ENDIF
		PRINTFORMW 进行着表演……
	CASE 1
		PRINTFORM 作为吉他手、
		IF ABL:技巧 >= 5
			PRINTFORMW 展现着无法用眼捕捉的速弾技术令観客叹为观止……
		ELSE
			PRINTFORMW 狂热地搅动着琴弦……
		ENDIF
	CASE 2
		PRINTFORM 作为贝斯手、
		IF TALENT:喜歡引人注目
			PRINTFORMW 展示着自己精彩的贝斯技术……
		ELSE
			PRINTFORMW 用堅実的演奏支撑起了乐队的基础……
		ENDIF
	CASE 3
		PRINTFORMW 作为鼓手、华丽表演的同时为乐队带来节奏的動感……
	CASE 4
		PRINTFORMW 担当了电子琴手、被围在各类电子琴中间弹奏着……
	ENDSELECT
CASE "演劇"
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMW 看起来在进行以漫画为原作的改编劇。
	CASE 1
		PRINTFORMW 看起来在进行古典戏劇。
	CASE 2
		PRINTFORMW 看起来在进行原创演劇。
	ENDSELECT
	PRINTFORM %CALLNAME:TARGET%扮演了%TEXTR("主人公/女主人公/配角/悪役角色")%、用
	IF TALENT:喜歡引人注目
		PRINTFORM 活灵活现的
	ELSEIF TALENT:内気
		PRINTFORM 緊張が隠しきれない
	ELSE
		PRINTFORM 堂々とした
	ENDIF
	PRINTFORM 演技
	IF TALENT:謎之魅力 || TALENT:魅力
		PRINTFORMW 吸引住了観客……
	ELSE
		PRINTFORMW 留住了観客……
	ENDIF
CASE "占卜館"
	PRINTFORMW 桌子上放置着一个水晶球、即使是不知道对方是魔法使的学生们、在这种气氛下也会確信对方是真正的占卜師。
	PRINTFORMW ……终于轮到%CALLNAME:MASTER%了。
	PRINTFORMW 对面前的%CALLNAME:MASTER%的出现感到惊讶后、%CALLNAME:TARGET%熟练地開始了占卜。
	PRINTFORMW 不一会儿后、便将脸贴近了%CALLNAME:MASTER%的耳朵、告知了%CALLNAME:MASTER%的運勢。
	PRINTFORMW ……可以期待今夜了。

CASE "恐怖屋敷"
	PRINTFORMW 里面的构造似乎十分精密、在入口处就已经感受到一股惊悚的气氛。
	PRINTFORMW 就在%CALLNAME:MASTER%和%CALLNAME:PARTNER()%期待着会出现什么而进入正門后、完全的黑暗蔓延開来。
	PRINTFORMW 在什么也看不到的迷宫中摸索着前进的时候、眼前突然有了亮光。
	PRINTFORM 就像夜虫一样过去的时候、从侧面
	SELECTCASE RAND:3
	CASE 0
		PRINTFORM 血淋淋的
	CASE 1
		PRINTFORM 幽霊化的
	CASEELSE
		PRINTFORM 因为特殊的化妆没有被察觉到、
	ENDSELECT
	PRINTFORMW %CALLNAME:TARGET%飛了出来！
CASEELSE
	PRINTFORMW 展出的项目……%TEXTR("还行、如果一定要给评价的话。/有点粗糙、但因为很热闹所以观感没有太糟糕。/像是有懂得専业知識的学生存在的样子、优秀到令人惊讶。真是厉害呀……")%
ENDSELECT

;-------------------------------------------------
;文化祭の出し物を細かく設定する関数
;素質や種族で内容が変わったりする……といいな
;-------------------------------------------------
@SHOP_BUNKASAI()
#FUNCTIONS
#DIM LCOUNT
#DIMS SHOP_TABLE

;まずは固定の出し物
SHOP_TABLE = 女仆咖啡厅/執事咖啡厅/恐怖屋敷/乐队演奏/演劇/

;次に種族によって出る出し物を加える
SIF FIND_COND("烦悩相談室")
	SHOP_TABLE += "烦悩相談室/"
SIF FIND_COND("占卜館")
	SHOP_TABLE += "占卜館/"

;素質によって出る出し物を加える
SIF FIND_COND("西式料理店")
	SHOP_TABLE += "西式料理店/"
SIF FIND_COND("PaFuPaFu屋")
	SHOP_TABLE += "PaFuPaFu屋/"

;もう巡った場所は除外
VARSET LOCALS
SPLIT SHOP_TABLE, "/", LOCALS
SHOP_TABLE = 
FOR LCOUNT, 0, 10
	SIF CEVENT(@"学園祭：%LOCALS:LCOUNT%", MASTER)
		CONTINUE
	SHOP_TABLE += @"%LOCALS:LCOUNT%/"
NEXT

RETURNF SHOP_TABLE

;-------------------------------------------------
;出し物ARGSをやっているARGをふさわしい格好に着替えさせる
;-------------------------------------------------
@SET_COORDINATE_BUNKASAI, ARGS, ARG
;外套は脱掉
CALL RESET_OTHER, "外套", ARG

SELECTCASE ARGS
CASE "西式料理店"
	CALL SET_SHIRT, "哥特連衣裙", ARG
CASE "女仆咖啡厅"
	CALL SET_COORDINATE, "女仆装", ARG
CASE "執事咖啡厅"
	CALL SET_SHIRT, "領帯女式衬衫/黒", ARG
CASE "演劇"
	IF COND("池面", ARG)
		CALL SET_SHIRT, "中世紧身長袍", ARG
	ELSE
		CALL SET_SHIRT, "洛丽塔連衣裙", ARG
	ENDIF
CASE "PaFuPaFu屋"
	CALL SET_SHIRT, "情趣内衣", ARG
ENDSELECT


