;----------
;青空鈴口上を書くにいたって処理を別ファイルにしたほうがいいなって思って作った関数置き場
;era悪魔で女仆で使う分にはご自由にお使いください
;----------
;
;----------
;連続指令時に口上が被らないようにする関数
;
;SELECTCASE KABURAND(3)のように表記する
;----------
@KABURAND(ARG, ARG:1)
#FUNCTION
#DIM RVALUE
#DIM ACTOR
SIF ARG <= 1
	RETURNF 0

ACTOR = ARG:1
SIF ACTOR == 0 && TARGET > 0
	ACTOR = TARGET

RVALUE = RAND:ARG
IF COND("KOJO_DAILY台詞中")
	SIF CFLAG:ACTOR:カ胸罩ンド_DAILY == RVALUE
		RESTART
	CFLAG:ACTOR:カ胸罩ンド_DAILY = RVALUE
ELSE
	SIF CFLAG:ACTOR:カ胸罩ンド == RVALUE
		RESTART
	CFLAG:ACTOR:カ胸罩ンド = RVALUE
ENDIF
RETURNF RVALUE

;----------
;口上独自の写真撮影用関数
;
;@KOJO_MESSAGE_BEFORE_COMに
;IF SELECTCOM == 158作って
;CALL KOJOPIC_KX1を書いてここを呼び出す
;----------
@KOJOPIC_K33
#DIM LCOUNT
TFLAG:地の文スキップ = 1

;COMF158_写真撮影.ERBを参考にしながら
;条件を書いて地の文と写真の标题を書く
;ついでに口上も書けばなお良し
IF PREVEX("Ｖ中出し") && INSERT("挿入") && PREVEX("Luxurious") && ( PREVPLAY("屈曲位") || PREVPLAY("正常位") )
	PRINTFORMW %CALLNAME:MASTER%一边对%CALLNAME:TARGET%%TEXTR("播种/中出")%一边把手机摆好了…
	PRINTFORML 咔嚓（攝像聲）
	STR:写真 = %TEXTR("おまんこ/膣内/子宮")%に%TEXTR("精液を注がれる/射精される")%感覚で%TEXTR("仰け反って絶頂している/惚けた表情をしている/トロ顔になっている")%%CALLNAME:TARGET%
	PRINTFORMW %CALLNAME:MASTER%拍下了『%STR:写真%』的照片收了起来。
;書いてないパターンは汎用文を出す
ELSE
	TFLAG:地の文スキップ = 0
	RETURN 1
ENDIF

STR:補足 += @"_%CALLNAME:TARGET%"
STR:補足 += "_事後"

SIF CHECK_CLO("泳装姿")
	STR:補足 += "_泳装"
SIF CHECK_CLO("体操服姿")
	STR:補足 += "_体操服"
SIF CHECK_CLO("学生服姿")
	STR:補足 += "_学生服"

;日期の記録
STR:補足 += @"_日期%TOSTR(DAY*10 + TIME)%"

SAVESTR:写真 = %STR:写真%

CALL SAVE_PHOTO, @"%STR:写真%%STR:補足%"

;履歴を残す
VARSET LOCALS
SPLIT SAVESTR:Ｈ中の撮影履歴, "/", LOCALS

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK
	;すでに似た写真があるかを格子
	SIF CHECK_HIGHMATCH(SAVESTR:写真, LOCALS:LCOUNT) == 0
		CONTINUE
	SAVESTR:写真 = 
NEXT
SIF SAVESTR:写真 != ""
	SAVESTR:Ｈ中の撮影履歴 += @"%SAVESTR:写真%\/"

STR:青写真 = 
