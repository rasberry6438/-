;-------------------------------------------------
;口上で使う乱数やテ接吻トを詰め込むための場所
;-------------------------------------------------
;CNAMEの詳細を書く場所
;ARGに呼びたい相手の番号を入れておく
;細かく設定しようとすると全体的にややこしく、かつ複雑になりがちなので、ここで一括で設定できるようにする
;もちろん個別でも対応できるように
@C_DETAIL, ARG
#FUNCTIONS

SELECTCASE CALLNAME:TARGET
CASE "乃々"
	IF ARG == MASTER
		IF NAME:ARG == "你"
			LOCALS = 前辈_学长
		ELSE
			LOCALS = %NAME:ARG%前輩_%CSTR:ARG:ふりがな%前輩
		ENDIF
	ELSEIF COND("诺伊", ARG)
		IF (COND("夜") || DAYEV("周日", "游戏内")) && CEVENT("家族関係")
			LOCALS = %CALLNAME:ARG%伯父桑_%CSTR:ARG:ふりがな%おじ桑
		ELSE
			LOCALS = %CALLNAME:ARG%老师_%CSTR:ARG:ふりがな%老师
		ENDIF
	ELSEIF COND("白色死神", ARG) && CEVENT("名前のある死神", ARG) == 0
		LOCALS = 死神小姐_死神桑
	ELSEIF COND("学生", ARG)
		SELECTCASE SOME_CLASS(TARGET, ARG)
		CASE "年下"
			IF TALENT:ARG:男性
				IF TALENT:ARG:性反転 || TALENT:ARG:伪娘 || TRUE_SEX(ARG) == "扶她"
					LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
				ELSE
					LOCALS = %CALLNAME:ARG%君_%CSTR:ARG:ふりがな%君
				ENDIF
			ELSE
				IF TALENT:ARG:性反転 && COND("性転換前的素質：伪娘", ARG) == 0
					LOCALS = %CALLNAME:ARG%君_%CSTR:ARG:ふりがな%君
				ELSE
					LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
				ENDIF
			ENDIF
		CASEELSE
			LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
		ENDSELECT
	ELSEIF COND("教員", ARG)
		LOCALS = %CALLNAME:ARG%老师_%CSTR:ARG:ふりがな%老师
	ELSE
		LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
	ENDIF
CASE "诺伊"
	IF ARG == MASTER
		IF NAME:ARG == "你"
			LOCALS = 你_你
		ELSE
			SELECTCASE TONE_K46()
			CASE "穏やか"
				LOCALS = %NAME:ARG%桑_%CSTR:ARG:ふりがな%桑
			CASE "丁寧"
				LOCALS = %NAME:ARG%様_%CSTR:ARG:ふりがな%大人
			CASEELSE
				LOCALS = %NAME:ARG%_%CSTR:ARG:ふりがな%
			ENDSELECT
		ENDIF
	ELSEIF TEQUIP:新妻PLAY
		IF TRUE_SEX(ARG) == "男性"
			LOCALS = 亲爱的_达令
		ELSE
			LOCALS = 亲爱的_甜心
		ENDIF
	ELSEIF COND("白色死神", ARG) && CEVENT("名前のある死神", ARG) == 0
		LOCALS = 『死神』_もり
	ELSEIF COND("乃々", ARG)
		LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
	ELSE
		SELECTCASE TONE_K46(TARGET)
		CASE "穏やか"
			LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
		CASE "丁寧"
			LOCALS = %CALLNAME:ARG%様_%CSTR:ARG:ふりがな%大人
		CASE "甘えんぼ"
			IF TRUE_SEX(ARG) == "男性" && TALENT:ARG:伪娘 == 0
				LOCALS = %CALLNAME:ARG%君_%CSTR:ARG:ふりがな%君
			ELSE
				LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
			ENDIF
		CASE "傲嬌"
			LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
		CASEELSE
			IF COND("学生", ARG)
				LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
			ELSEIF COND("教師", ARG)
				IF COND("相性◎", ARG)
					LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
				ELSE
					LOCALS = %CALLNAME:ARG%老师_%CSTR:ARG:ふりがな%老师
				ENDIF
			ELSE
				IF COND("相性◎", ARG)
					LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
				ELSE
					LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
				ENDIF
			ENDIF
		ENDSELECT
	ENDIF
;新角色予定
CASE "遥"
	IF ARG == MASTER
		IF NAME:MASTER == "你"
			IF CEVENT("師匠フラグ")
				LOCALS = 師匠_ししょー
			ELSE
				LOCALS = 前辈_学长
			ENDIF
		ELSE
			IF CEVENT("師匠フラグ")
				LOCALS = %NAME:MASTER%師匠_%CSTR:ARG:ふりがな%ししょー
			ELSE
				LOCALS = %NAME:MASTER%前輩_%CSTR:ARG:ふりがな%前輩
			ENDIF
		ENDIF
	ELSEIF COND("莉", ARG)
		LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
	ELSEIF COND("学生", ARG)
		SELECTCASE SOME_CLASS(TARGET, ARG)
		CASE "同級生"
			IF TALENT:ARG:伪娘 || TRUE_SEX(ARG) == "女性" || TRUE_SEX(ARG) == "扶她"
				IF COND("相性◎", ARG)
					LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
				ELSE
					LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
				ENDIF
			ELSE
				LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
			ENDIF
		CASE "年下"
			IF TALENT:ARG:伪娘 || TRUE_SEX(ARG) == "女性" || TRUE_SEX(ARG) == "扶她"
				IF COND("相性◎", ARG)
					LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
				ELSE
					LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
				ENDIF
			ELSE
				IF COND("相性◎", ARG)
					LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
				ELSE
					LOCALS = %CALLNAME:ARG%君_%CSTR:ARG:ふりがな%君
				ENDIF
			ENDIF
		CASE "年上"
			LOCALS = %CALLNAME:ARG%前輩_%CSTR:ARG:ふりがな%前輩
		ENDSELECT
	ELSEIF COND("教員", ARG)
		LOCALS = %CALLNAME:ARG%老师_%CSTR:ARG:ふりがな%老师
	ELSE
		LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
	ENDIF
CASEELSE
	IF COND("学生", ARG)
		SELECTCASE SOME_CLASS(TARGET, ARG)
		CASE "年下"
			IF TRUE_SEX(ARG) == "男性" && TALENT:ARG:伪娘 == 0
				LOCALS = %CALLNAME:ARG%君_%CSTR:ARG:ふりがな%君
			ELSE
				LOCALS = %CALLNAME:ARG%酱_%CSTR:ARG:ふりがな%酱
			ENDIF
		CASE "同級生"
			IF COND("相性◎", ARG)
				LOCALS = %CALLNAME:ARG%_%CSTR:ARG:ふりがな%
			ELSE
				LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
			ENDIF
		CASE "年上"
			LOCALS = %CALLNAME:ARG%前輩_%CSTR:ARG:ふりがな%前輩
		ENDSELECT
	ELSEIF COND("教師", ARG)
		LOCALS = %CALLNAME:ARG%老师_%CSTR:ARG:ふりがな%老师
	ELSE
		LOCALS = %CALLNAME:ARG%桑_%CSTR:ARG:ふりがな%桑
	ENDIF
ENDSELECT

RETURNF LOCALS


;自分と相手の年齢が同じかどうかを判別する
;と是え厳密にはできないので、其他＝教師(6)＞研究生(5)＞３年生(4)＞２年生(3)＞１年生(2)＞中等部(1)＞初等部(0)の順で年を計算する
@SOME_CLASS, ARG, ARG:1
#FUNCTIONS
#DIM CLASS, 2
#DIM LCOUNT

FOR LCOUNT, 0, 2
	IF COND("教員", ARG:LCOUNT)
		CLASS:LCOUNT = 6
	ELSE
		SELECTCASE CSTR:(ARG:LCOUNT):学年
		CASE "初等部"
			CLASS:LCOUNT = 0
		CASE "中等部"
			CLASS:LCOUNT = 1
		CASE "１年生"
			CLASS:LCOUNT = 2
		CASE "２年生"
			CLASS:LCOUNT = 3
		CASE "３年生"
			CLASS:LCOUNT = 4
		CASE "研究生"
			CLASS:LCOUNT = 5
		CASEELSE
			CLASS:LCOUNT = 6
		ENDSELECT
	ENDIF
NEXT

IF CLASS > CLASS:1
	RETURNF "年下"
ELSEIF CLASS == CLASS:1
	RETURNF "同級生"
ELSE
	RETURNF "年上"
ENDIF


;精液や阴茎などについて随机で異なる名称をしゃべるようになる関数
;そのつど書くのも若干面倒なので追加
;2020/11/22から改装
;休托莉酱の性器の呼称変更をパク……参考にさせて頂いています
;2020/12/10再びの改装
;役割を大幅に変更し、前口上生成ジェネレータに変化しました
@ADANA, ARGS, ARG, ARG:1, ARGS:1
#FUNCTIONS
#DIMS 名称
#DIMS TABLE
#DIMS HURIGANA, 2

SIF ARG == 0 && TARGET > 0
	ARG = TARGET
SIF ARG:1 == 0 && PLAYER > 0
	ARG:1 = PLAYER

SELECTCASE ARGS
CASE "阴茎"
	TABLE = %CNAMET(ARG, "男性器")%
	HURIGANA:1 = %CNAMET(ARG, "男性器", "かな")%
CASE "ヴァギナ"
	TABLE = %CNAMET(ARG, "女性器")%
	HURIGANA:1 = %CNAMET(ARG, "女性器", "かな")%
CASEELSE
	TABLE = %CNAMET(ARG, ARGS)%
	HURIGANA:1 = %CNAMET(ARG, ARGS, "かな")%
ENDSELECT

名称 = 
IF RAND:3 == 0 && ARG:1 && MARK:ARG:屈服刻印 >= 2
	SELECTCASE ARGS
	CASE "阴茎"
		IF TALENT:(ARG:1):阴茎 == 0
			名称 = 可爱/子ども/赤酱/
		ELSEIF TALENT:(ARG:1):阴茎 >= 2
			名称 = 真っ黒/極太/粗大的/おまんこ特攻/
		ENDIF
		SIF 名称 != ""
			名称 = %TEXTR(@"%名称%")%
		IF TALENT:(ARG:1):包茎
			IF RAND:2
				名称 += "包茎"
			ELSE
				名称 += "包皮"
			ENDIF
		ENDIF
	CASE "精液"
		名称 = 黄ばんだ/濃厚/
		SIF TALENT:(ARG:1):小种马
			名称 += "百発百中/"
		名称 = %TEXTR(@"%名称%")%
	CASE "ヴァギナ"
		IF TALENT:(ARG:1):性器年齢 >= 4
			名称 = 淫水焼けした
		ELSEIF TALENT:(ARG:1):処女
			名称 = 処女
		ENDIF
	ENDSELECT
ENDIF

;前の段階がなく、かつtnkの呼び方が「マラ」なら、自動で凶悪を前に着けるようにする
SIF 名称 == "" && TABLE == "マラ"
	名称 = 凶悪

;一回目のフリガナ探索
;前口上（？）のフリガナを尋找
HURIGANA = %RUBY_GENERATE(名称)%%HURIGANA:1%

IF ARGS:1 == "ふりがな" || ARGS:1 == "かな" || PALAM:ARG:欲情 >= PALAMLV:5
	名称 = %HURIGANA%
ELSE
	名称 = %TABLE%
ENDIF

RETURNF 名称


@RUBY_GENERATE, ARGS
#FUNCTIONS

SELECTCASE ARGS
CASE "可爱"
	RETURNF "可爱"
CASE "子ども"
	RETURNF "こども"
CASE "赤酱"
	RETURNF "あか酱"
CASE "真っ黒"
	RETURNF "まっくろ"
CASE "極太"
	RETURNF "ごくぶと"
CASE "粗大的"
	RETURNF "ふとい"
CASE "おまんこ特攻"
	RETURNF "おまんことっこう"
CASE "包茎"
	RETURNF "ほうけい"
CASE "包皮"
	RETURNF "かわかぶり"
CASE "黄ばんだ"
	RETURNF "きばんだ"
CASE "濃厚"
	RETURNF "のうこう"
CASE "百発百中"
	RETURNF "ひゃっぱつひゃくちゅう"
CASE "淫水焼けした"
	RETURNF "いんすいやけした"
CASE "凶悪"
	RETURNF "きょうあく"
ENDSELECT


;欲望に正直になるかを判定する関数
;従来の『MARK:屈服刻印 == 3』に相当する
@YOKUBO, ARG
#FUNCTION
#DIM DYNAMIC 判定

;対象に何も入っていなかった場合はTARGETに変更する
SIF ARG == 0
	ARG = TARGET

IF MARK:ARG:屈服刻印 == 3 || TALENT:ARG:淫乱
	判定 = 1
	SIF COND("欲求不満", ARG) >= 2 || CFLAG:ARG:躁动难忍
		判定 += 1
ENDIF

RETURNF 判定

;-------------------------------------------------
;以降は喘ぎ声を生成するための関数
;六華口上や哈娜口上の汎用喘ぎ声を参考、また一部流用、改変させていただいて製作しています
;-------------------------------------------------

;喘ぎ声を随机で発声させる関数。挿入などの条件があればいくつかの喘ぎ声を混ぜ合わせる
;回数上限、ループ、爱心要不要は共通になる
;現状加える予定の喘ぎ声は、1.通常(製作済)、2.Ｍ異物（製作済）、3Ｖ異物（製作前、伪娘に膣がつけられるようにならない限り作らない予定）4Ａ異物（製作済）5挿入(製作前)
@MIX_AEGI, 回数上限, 対象, ARGS
#FUNCTIONS
#DIMS DYNAMIC 喘ぎ
#DIMS 爱心;ARGSを直接いじってしまうと爱心が不要になって以後出なくなるので、そうしないための一時的な変数
#DIM 回数上限
#DIM ループ
#DIM 対象
#DIMS 連続过滤器;あまり繰り返させたくない言葉に过滤器を掛ける

連続过滤器 = 無
爱心 = %ARGS%
FOR ループ, 0, 回数上限
	
	;肛門に挿入されていた場合
	IF (TEQUIP:対象:肛用振動棒 || TEQUIP:対象:浣腸 || INSERT("Ａ挿入", 対象) || GROUPMATCH(SELECTCOM, 2, 9, 14, 33, 70, 71, 72, 73, 74, 75, 136, 142, 143)) && RAND:7 == 0 && 連続过滤器 != "Ａ異物"
		喘ぎ += @"%AEGI(対象, 爱心, "Ａ異物")%"
		;肛門を連続して話さないために过滤器をかける
		連続过滤器 = Ａ異物
	;尿道に挿入されていた場合
	ELSEIF (INSERT("尿道挿入", 対象) || GROUPMATCH(SELECTCOM, 15, 83, 84, 85, 86, 87, 88, 149, 151)) && RAND:6 == 0 && 連続过滤器 != "尿道異物"
		喘ぎ += @"%AEGI(対象, 爱心, "尿道異物")%"
		;尿道を連続して話さないために过滤器をかける
		連続过滤器 = 尿道異物
	;口に何か入っていた場合
	ELSEIF (TEQUIP:対象:接吻 || TEQUIP:対象:深喉 || TEQUIP:対象:六九式 || GROUPMATCH(SELECTCOM, 6, 91, 96, 98, 99, 135, 148)) && RAND:5 == 0
		喘ぎ += @"%AEGI(対象, 爱心, "Ｍ異物")%"
	;Ｍ異物と条件が被るものの、接吻の時限定
	ELSEIF (TEQUIP:対象:接吻 || SELECTCOM == 6) && RAND:4 == 0
		喘ぎ += @"%AEGI(対象, 爱心, "接吻")%"
	;膣や肛門に挿入していた場合
	ELSEIF (INSERT("挿入", PLAYER, 対象) || (ASSI && INSERT("挿入", ASSI, 対象)) || GROUPMATCH(SELECTCOM, 120, 121, 122, 125, 126, 127)) && RAND:3 == 0 && 連続过滤器 != "挿入"
		喘ぎ += @"%AEGI(対象, 爱心, "挿入")%"
		;挿入を連続して話さないために过滤器をかける
		連続过滤器 = 挿入
	;飞机杯に挿入していた場合
	ELSEIF SELECTCOM == 37 && RAND:2 && 連続过滤器 != "飞机杯挿入"
		喘ぎ += @"%AEGI(対象, 爱心, "飞机杯挿入")%"
		;飞机杯挿入を連続して話さないために过滤器をかける
		連続过滤器 = 飞机杯挿入
	;条件に引っかからなければ汎用を呼び出す
	ELSE
		喘ぎ += @"%AEGI(対象, 爱心, "通常")%"
	ENDIF
	
	;連続して指定されなかった場合、过滤器を解除する
	IF 連続过滤器 != "無"
		IF LOCAL == 3
			連続过滤器 = 無
			LOCAL = 0
		ELSE
			LOCAL++
		ENDIF
	ENDIF
	
	;爱心が必要なときは随机でどちらにするのか指定する
	IF ARGS == "要" && (ループ == 0 || RAND:2)
		爱心 = %ARGS%
	;爱心が不要な場合
	;回数を繰り返したときは随机でこちらの方を選択する
	ELSEIF ARGS == "不要" || ループ >= 1
		爱心 = 不要
	;今のところは完全に不要だが後の拡張のため＆構文ミスに気付くために其他にはエラーをだす
	ELSE
		喘ぎ += "指定的范围外"
	ENDIF
NEXT

RETURNF 喘ぎ

;喘ぎ声を生成する関数。爱心を加えるか否かをARGSで指定する。
;喘ぎは使う度に空白にしていくので動的変数として指定
;それぞれのパターンを変数パターンに指定し、そのCASEにあわせて喘ぎ方を変えて行く(予定)
@AEGI, 対象, ARGS, パターン, 回数
#FUNCTIONS
#DIM ループ
#DIMS パターン
#DIM 対象
#DIMS DYNAMIC 喘ぎ
#DIM 回数

FOR ループ, 0, MAX(回数, 1)
	SELECTCASE パターン
	;汎用パターン
	CASE "通常"
		IF PALAM:対象:欲情 >= PALAMLV:5 && RAND:3
			喘ぎ += @"%TEXTR("啊/哦/啊、啊/噢嗬/啊嘿/哈啊/好喜翻这个！/啊ー")%"
		ELSEIF PALAM:対象:欲情 >= PALAMLV:3 && RAND:2
			喘ぎ += @"%TEXTR("啊哈/嗯哈/哈/嗯嗯/啊嗯/啊/哈嗯/哈/嗯哈/咿呀/啊啊/啊哈")%"
		ELSE
			喘ぎ += @"%TEXTR("啊……/咿嗯/嗯啊……/啊嗯/呀唔……/呼啊……/哈嗯……/嗯……/呀……/呼嗯……/っ……")%"
		ENDIF
	;口に何か入っているとき
	CASE "Ｍ異物"
		;長くなるのでいくつかに分割
		IF RAND:2
			喘ぎ += @"%TEXTR("呼唔……/嗯唔……/哈唔……/啾啪……/舔……/啾噜……/嗯……/嗯、吸溜…/吸溜…唔…嗯/嗯…、吸溜……/啾、吸溜…啾噗/嗯嗯……吸溜…/吸溜、哈唔……嗯")%"
		ELSE
			喘ぎ += @"%TEXTR("嗯、嗯咕…/嗯嗯…嗯……っ/嗯…、嗯嗯……/嗯咕、嗯…嗯/嗯唔……嗯/嗯唔、嗯嗯…っ/呼哇……")%"
		ENDIF
	;接吻しているとき
	CASE "接吻"
		喘ぎ += @"%TEXTR("啾……/啾噗……/嗯唔……/嗯……/啾、啾…/啾…啾……っ/啾…、啾……/啾、啾…啾/啾……啾/嗯啾、啾……っ")%"
	;肛門に何か入っているとき
	CASE "Ａ異物"
		喘ぎ += @"%TEXTR("屁股被/嗯哈/哈嗯っ")%"
	;尿道に何か入っているとき
	CASE "尿道異物"
		喘ぎ += @"%TEXTR("尿尿的孔被")%"
	;何かに挿入しているとき
	CASE "挿入"
		IF RAND:2
			喘ぎ += @"%CNAMET(対象, "二人称")%的"
			IF RAND:2
				IF GROUPMATCH(SELECTCOM, 120, 121, 122)
					喘ぎ += @"%ADANA("ヴァギナ")%"
				ELSEIF GROUPMATCH(SELECTCOM, 125, 126, 127)
					喘ぎ += @"%ADANA("肛門")%"
				ENDIF
			ELSE
				喘ぎ += "里面"
			ENDIF
			喘ぎ += "好舒服……"
		ELSE
			喘ぎ += "肉棒好舒服！"
		ENDIF
	;飞机杯に挿入しているとき
	CASE "飞机杯挿入"
		喘ぎ += @"%TEXTR("要被飞机杯啾噗啾噗地套弄了/啊……把里面做得黏黏糊糊的……")%"
	ENDSELECT
	;爱心が必要な場合
	IF ARGS == "要"
		喘ぎ += "_H_"
	;爱心が不要な場合
	ELSEIF ARGS == "不要"
	;今のところは完全に不要だが後の拡張のため＆構文ミスに気付くために其他にはエラーをだす
	ELSE
		喘ぎ = "AEGI出现了问题"
		RETURNF 喘ぎ
	ENDIF
	;読みやすいように半角スペースをくわえる
	喘ぎ += " "
NEXT

RETURNF 喘ぎ
