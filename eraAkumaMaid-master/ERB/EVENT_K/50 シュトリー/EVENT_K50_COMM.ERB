;-------------------------------------------------
;部員同士での交流。自分はTARGETとして呼ばれ、話相手の部員はPARTNER()として呼ばれる
;話相手への呼びかけはCNAME("partner")で出来る
;TFLAG:台詞スキップ = 1で話している相手のセリフのスキップ、TFLAG:地の文スキップ = 1で続く地の文のスキップをする事が出来る
;-------------------------------------------------
;基本的に驱魔人とは積極的に絡みにいかない
;老师、大人だとちょっと丁寧な言葉使いに

@KOJO_COMM_K50, ARGS
#DIM MEMO_LINECOUNT
#DIM LCOUNT
;対話相手にしたい角色の名前を/区切りでこれに突っ込んでFIND_TaiTa(EVENT_TABLE)とするとそのうち一人が選ばれる
#DIMS EVENT_TABLE

EVENT_TABLE = 
MEMO_LINECOUNT = LINECOUNT

SELECTCASE ARGS
;話しかける側のセリフ。COND("対話可能")を満たす相手を選んでPARTNER()にする事で、特定の相手に話しかけることもできる
CASE "お誘いする"
	;特定の相手に話しかける場合はEVENT_TABLEに対話相手の呼び名(CALLNAME)を / 区切りで並べて ; を消す
	;例１）弗理希艾尔or濡烏の君or杏珠から一人選ぶ場合
	;EVENT_TABLE = 弗理希艾尔/濡烏の君/杏珠
	;例２）部員全てから一人選ぶ場合
	;EVENT_TABLE = 全員

	;EVENT_TABLEに驱魔人を除く角色を入れていく
	FOR LCOUNT, 2, CHARANUM
		SIF LCOUNT == TARGET
			CONTINUE
		SIF COND("種族：驱魔人", LCOUNT)
			CONTINUE
		EVENT_TABLE = %EVENT_TABLE%%CSVCALLNAME(NO:LCOUNT)%\/
	NEXT

	IF FIND_TaiTa(EVENT_TABLE)
		;話し相手をpartnerにする
		CALL SET_PARTNER, FIND_TaiTa(EVENT_TABLE)

		CSTR:PARTNER():颜绘変化 = 对话框兴奋
		IF COND("種族：驱魔人", PARTNER() )
			CALL EMOTION_PLUS, "重置/〇〇/憤怒眉/对话框汗", TARGET, PARTNER()
		ELSEIF COND("教員", PARTNER() ) || TALENT:PARTNER():外見年齢 == 4
			CALL EMOTION_PLUS, "重置/对话框兴奋", TARGET, PARTNER()
		ELSE
			CALL EMOTION_PLUS, "重置/＞＜/对话框兴奋", TARGET, PARTNER()
		ENDIF
	ENDIF

	CALL CHANGE_OFC, "セット"
	;念のため
	IF COND("種族：驱魔人", PARTNER() )
		PRINTFORMW 「%CNAME("partner", "どもり")%、は、話しませんか？」
	ELSEIF COND("教員", PARTNER() ) || TALENT:PARTNER():外見年齢 == 4
		PRINTFORMW 「%CNAME("partner")%、お話しませんか？」
	ELSE
		PRINTFORMW 「%CNAME("partner")%、おしゃべりしよ！」
	ENDIF
	CALL CHANGE_OFC, "重置"
;話しかけられる側のセリフ。括弧は『』を使うのが望ましい
CASE "誘われた"
	CALL CHANGE_OFC, "セット"
	IF COND("種族：驱魔人", PARTNER() )
		PRINTFORMW 『は、是！』
	ELSEIF COND("教員", PARTNER() ) || TALENT:PARTNER():外見年齢 == 4
		PRINTFORMW 『はーい！』
	ELSE
		PRINTFORMW 『もちろん♪』
	ENDIF
	CALL CHANGE_OFC, "重置"
ENDSELECT
