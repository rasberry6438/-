;--------------------------------------------------
;角色を勧誘した時などの台詞
;--------------------------------------------------
@EVENT_PROFILE, ARGS, ARG
#DIM MEMO_LINECOUNT
#DIM MEMO_TARGET
SIF ARG <= 0 && TARGET > 0
	ARG = TARGET

MEMO_TARGET = TARGET
TARGET = ARG

;口上があるかどうかを行数で格子
MEMO_LINECOUNT = LINECOUNT
TRYCALLFORM EVENT_PROFILE_K{NO:TARGET}, ARGS

;何かしゃべったら１行開ける
SELECTCASE ARGS
CASE "紹介文", "自室描写"
CASEELSE
	SIF LINECOUNT > MEMO_LINECOUNT
		PRINTL 
ENDSELECT

TARGET = MEMO_TARGET

;--------------------------------------------------
;日常の台詞。ＳＨＯＰ画面の下に出力されるやつです
;--------------------------------------------------
@KOJO_DAILY
#DIM MEMO_LINECOUNT
#DIMS SHOW_ACT
#DIMS SHOW_SELECT
#DIM TEXT_LENGTH

IF TARGET <= 1
	PRINTFORML ……一人だ。
	RETURN 0
ENDIF

;KOJO_DAILY台詞中フラグON
SETBIT FLAG:状況判定, 33

;再描画をしても同じセリフが出るように乱数調整
CALL RESET_RANDOMIZE

;何をやるのか行動決定
CALL CHECK_MESSAGE_DAILY

;名前とだいたいの服装を表示
CALL PRINT_NAME_ICON

MEMO_LINECOUNT = LINECOUNT
;これは優先度が高いセリフ群
TRYCALLFORM KOJO_DAILY_K{NO:TARGET}, STR:イベント名

;特に台詞が無いなら表示した名前を消しておく
SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

;何かしたなら前回指令を表示する
SHOW_ACT = 
IF CSTR:PREV_DAILYCOM != ""
	SHOW_ACT = < \@ COND("直前行動") ? 直前行動 # 前回行動 \@：%CSTR:PREV_DAILYCOM% >
ELSEIF SAVESTR:PREV_直前行動 != ""
	SHOW_ACT = %EXTSTR(SAVESTR:PREV_直前行動, "", "/")%
ENDIF

TEXT_LENGTH += STRLENS("[111]pause解除")
IF CONFIG("自动调戏ＯＮ") || CONFIG("颜绘动画ＯＮ")
	IF FLAG:自動進行停止 == 0
		SHOW_SELECT = [111]pause　　
	ELSE
		SHOW_SELECT = [111]pause解除
	ENDIF
ENDIF

PRINTL 
CALL PRINT_STRL, @"NOBUTTON_%TEXT_LJ(SHOW_ACT, 24*4)%_%SHOW_SELECT%"

CFLAG:KOJO_DAILY済 = 1

;KOJO_DAILY台詞中フラグOFF
CLEARBIT FLAG:状況判定, 33

;--------------------------------------------------
;早朝の台詞。主に打招呼。あとでイベント増やしたい
;--------------------------------------------------
@KOJO_GREETING, ARGS, ARG
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
#DIM MEMO_TARGET
#DIM MEMO_CFLAG31

SIF STRCOUNT(ARGS, "地の文前")
	TFLAG:地の文スキップ = 0

MEMO_TARGET = TARGET
SIF ARG == 0 && TARGET > 0
	ARG = TARGET
TARGET = ARG

MEMO_CFLAG31 = CFLAG:睡眠
CFLAG:睡眠 = 0

MEMO_LINECOUNT2 = LINECOUNT
TFLAG:颜绘表示中 = 0

;改行が默认で付くかどうかなど
SELECTCASE ARGS
CASE "打招呼返し"
	TFLAG:交流中 = 1
CASE "既に入室"
	SIF COND("外套脱衣発生")
		CALL RESET_OTHER, "外套", TARGET
	TFLAG:交流中 = 1
	SIF TFLAG:地の文スキップ == 0
		PRINTL 
CASE "一番乗り", "入室打招呼", "簡易入室打招呼"
	TFLAG:交流中 = 1
	SIF TFLAG:地の文スキップ == 0
		PRINTL 
CASE "全裸化への反応", "他の人がＨ開始", "抜け出された", "朝袭"

CASE "登校"
	DRAWLINE
CASEELSE
	SIF TFLAG:地の文スキップ == 0
		PRINTL 
ENDSELECT

SELECTCASE ARGS
CASE "朝袭"
CASE "朝拘束"
	CALL EMOTION_PLUS, "躁动难忍", TARGET
CASE "打招呼"
	CALL EMOTION_PLUS, "甜言蜜语", TARGET
CASE "抜け出された"
	CALL EMOTION_PLUS, "重置", TARGET
CASEELSE
	CALL PRINT_MAIDFACE, TARGET
ENDSELECT

MEMO_LINECOUNT = LINECOUNT
TRYCALLFORM KOJO_GREETING_K{NO:TARGET}, ARGS

;フラグ変化
SELECTCASE ARGS
CASE "打招呼"
	CALL SETFLAG_DAILY, "打招呼", TARGET
CASE "打招呼返し", "全裸化への反応", "他の人がＨ開始"
CASEELSE
	CFLAG:打招呼した = 1
ENDSELECT

;	PRINTFORMW ARGS = %ARGS%, CSTR:現在位置 = %CSTR:現在位置%

;もしも特殊な事が起きていたら
SELECTCASE ARGS
CASE "既に入室"
	SIF LINECOUNT == MEMO_LINECOUNT
		PRINTFORMW %CALLNAME:MASTER%に気づいた%CALLNAME:TARGET%は轻轻地会釈してきた。
	CALL TEXT, "出会いざまの格好所感", TARGET
CASE "一番乗り", "入室打招呼", "簡易入室打招呼"
	SIF LINECOUNT == MEMO_LINECOUNT
		PRINTFORMW %CALLNAME:TARGET%が打招呼をしながら現れた。
	IF COND("外套脱衣発生")
		SIF OTHER(TARGET, "外套") != ""
			PRINTFORMW %CALLNAME:TARGET%は%OTHER(TARGET, "外套")%を脱いで外套ハンガーにかけた。
		CALL RESET_OTHER, "外套", TARGET
	ENDIF
	CALL TEXT, "出会いざまの格好所感", TARGET
CASE "登校"
	IF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT2
	ELSE
		DRAWLINE
	ENDIF
;特に台詞がない場合は空行や颜绘を消す
CASEELSE
	SIF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT2
ENDSELECT

TFLAG:交流中 = 0

CFLAG:睡眠 = MEMO_CFLAG31

TARGET = MEMO_TARGET

;--------------------------------------------------
;@KOJO_GREETINGの複数版
;--------------------------------------------------
@KOJO_GREETINGS, ARGS, ARG, ARG:1, ARG:2, ARG:3
#DIM LCOUNT
#DIM MEMO_TARGET

MEMO_TARGET = TARGET

FOR LCOUNT, 0, 4
	SIF ARG:LCOUNT == 0
		BREAK
	TARGET = ARG:LCOUNT
	CALL KOJO_GREETING, ARGS, ARG:LCOUNT
NEXT
TARGET = MEMO_TARGET

;--------------------------------------------------
;日常イベントの台詞。性騷擾したりした反応です
;--------------------------------------------------
@KOJO_DAILYCOM, ARGS, ARGS:1
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2

SIF TARGET <= 1
	RETURN 0
SIF STRCOUNT(ARGS, "地の文前")
	TFLAG:地の文スキップ = 0

MEMO_LINECOUNT2 = LINECOUNT

SIF ARGS:1 != "改行没有"
	CALL BLANKLINE

TFLAG:颜绘表示中 = 0

SELECTCASE STR:日常イベント名
CASE "Talk"
	IF FLAG:対複数
		TFLAG:交流中 = 1
		CALL PRINT_MAIDFACE, TARGET
	ENDIF
CASE "展示内褲"
	;複数の場合には胖次を表示
	IF FLAG:対複数
		CALL PRINT_NAME_PANTU
		CALL PRINT_CUTIN, "胖次正面", TARGET
	ENDIF
CASEELSE
	SELECTCASE ARGS
	CASE "性処理：前", "休憩", "特殊事件", "点心を用意した", "放課後Ｈ的邀请"
		CALL PRINT_MAIDFACE, TARGET
	CASE "連れ見抜き"
		CALL EMOTION_PLUS, "害羞", TARGET
	CASE "連れ見抜きに誘われた"
		CALL PRINT_MAIDFACE, TARGET
	CASE "撫摸股間"

	CASE "特殊事件"

	CASE "Ｈ目的で呼び出された"
		CUSTOMDRAWLINE ━
		;圓圈中
		SETBIT FLAG:状況判定, 49
		IF COND("颜绘表示中")
			CALL PRINT_MAIDFACE, TARGET
		ELSE
			CALL PRINT_NAME
		ENDIF
	CASE "お昼ご飯のお誘い"
		CUSTOMDRAWLINE ━
		PRINTL ﾎﾟﾍﾟｯ
		;圓圈中
		SETBIT FLAG:状況判定, 49
		CALL PRINT_MAIDFACE, TARGET
	CASEELSE
		IF FLAG:対複数
			TFLAG:交流中 = 1
			CALL PRINT_MAIDFACE, TARGET
		ENDIF
	ENDSELECT
ENDSELECT

MEMO_LINECOUNT = LINECOUNT
TRYCALLFORM KOJO_DAILYCOM_K{NO:TARGET}, ARGS

IF LINECOUNT > MEMO_LINECOUNT
	;喋った口上のフラグを記録
	IF CFLAG:睡眠 == 0
		IF STR:日常イベント名 != ""
			CALL SET_DEVENT, STR:日常イベント名
		ELSE
			CALL SET_DEVENT, ARGS
		ENDIF
	ENDIF
ENDIF

SELECTCASE ARGS
CASE "Ｈ目的で呼び出された", "お昼ご飯のお誘い"
	SIF LINECOUNT > MEMO_LINECOUNT
		CUSTOMDRAWLINE ━
ENDSELECT

IF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT2
	;SIF STRCOUNTS(ARGS, "地の文前", "特殊事件") == 0
	;	CALL BLANKLINE
ELSEIF ARGS:1 != "改行没有"
	CALL BLANKLINE
ENDIF

;圓圈中解除
CLEARBIT FLAG:状況判定, 49

TFLAG:交流中 = 0


;--------------------------------------------------
;@KOJO_DAILY で何について喋るかの判定。上の分岐が優先度が高い。
;一部イベントはフラグ変動有些（ここでフラグ自体弄ります）
;--------------------------------------------------
@CHECK_MESSAGE_DAILY
#DIM LCOUNT

STR:イベント名 = 

;-------------------------------------------------
;寝ている時は梦话とか寝こけてる描写
;-------------------------------------------------
IF CFLAG:睡眠
	STR:イベント名 = 睡眠
	RETURN 1
ENDIF
;-------------------------------------------------
;初回台詞。仲間变成了際に最初にＳＨＯＰ画面で喋ることです。打招呼も兼ねる
;-------------------------------------------------
IF DEVENT("初回台詞") == 0 && COND("朝") == 0
	STR:イベント名 = 初回台詞
	CALL SET_DEVENT, STR:イベント名
	RETURN 1
ENDIF

;其他
IF COND("浑身是汗")
	STR:イベント名 = 浑身是汗
	RETURN 1
ELSEIF CFLAG:刚睡醒
	STR:イベント名 = 刚睡醒
	RETURN 1
ELSEIF CFLAG:调戏数 == 0
	SIF CFLAG:Cosplay中
		STR:イベント名 = Cosplay中
ENDIF
SIF STR:イベント名 != ""
	RETURN 1

IF COND("点心有") && DEVENT("点心の感想") == 0 && (COND("亲手自制点心") || RANDC(5) == 0)
	STR:イベント名 = 点心の感想
	CALL SET_DEVENT, "点心の感想"
	SIF COND("亲手自制点心")
		CALL SET_DEVENT, @"%TEXTS("点心当番")%の亲手自制"
ENDIF

RETURN 1

;--------------------------------------------------
;约会の台詞
;--------------------------------------------------
@KOJO_DATE, ARGS
#DIM MEMO_LINECOUNT

TFLAG:地の文スキップ = 0

SELECTCASE ARGS
CASE "邀请去约会"
	IF COND("同居中") == 0
		CUSTOMDRAWLINE ━
		PRINTL ﾋﾟﾎﾟｯ
		;圓圈中
		SETBIT FLAG:状況判定, 49
	ENDIF
	CALL PRINT_MAIDFACE, TARGET
ENDSELECT

MEMO_LINECOUNT = LINECOUNT
TRYCALLFORM KOJO_DATE_K{NO:TARGET}, ARGS

SELECTCASE ARGS
CASE "邀请去约会"
	IF COND("同居中") == 0
		SIF LINECOUNT > MEMO_LINECOUNT
			CUSTOMDRAWLINE ━
	ENDIF
ENDSELECT

;地の文を飛ばす指定がない場合には汎用の反応を出す
SIF TFLAG:地の文スキップ == 0
	CALL DAILY_MESSAGE, ARGS
SIF LINECOUNT > MEMO_LINECOUNT
	CALL BLANKLINE

;圓圈中解除
CLEARBIT FLAG:状況判定, 49

;--------------------------------------------------
;取引での台詞。
;--------------------------------------------------
@KOJO_WISH, ARGS, ARGS:1
#DIM MEMO_LINECOUNT
SIF TARGET <= 1
	RETURN 0

SIF STRCOUNT(ARGS, "地の文前")
	TFLAG:地の文スキップ = 0

MEMO_LINECOUNT = LINECOUNT

TRYCALLFORM KOJO_WISH_K{NO:TARGET}, ARGS

;何かしゃべったら１行開ける
IF LINECOUNT > MEMO_LINECOUNT
	SIF ARGS:1 != "改行没有"
		PRINTL 
	;喋った口上のフラグを記録
	CALL SET_DEVENT, ARGS
ENDIF

;--------------------------------------------------
;购物での台詞。
;--------------------------------------------------
@KOJO_SHOP, ARGS
#DIM MEMO_LINECOUNT
SIF TARGET <= 1
	RETURN 0

SIF STRCOUNT(ARGS, "地の文前")
	TFLAG:地の文スキップ = 0

MEMO_LINECOUNT = LINECOUNT
CALL BLANKLINE

TRYCALLFORM KOJO_SHOP_K{NO:TARGET}, ARGS

;何かしゃべった
IF LINECOUNT > MEMO_LINECOUNT
	;喋った口上のフラグを記録
	CALL SET_SEVENT, ARGS
ENDIF
CALL BLANKLINE

;--------------------------------------------------
;ロード直後の台詞。不在関係なく、現在勧誘済みの全角色から一人選ばれて発言する。
;ARGにはTARGETが記録されている
;--------------------------------------------------
@KOJO_LOAD, ARG
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM ACTOR

;現在は封印
RETURN 0

SIF FLAG:ロード時台詞
	RETURN 0

DRAWLINE
MEMO_LINECOUNT = LINECOUNT

;万が一誰も喋る人がいない時のために１００回を試行上限とする
FOR LCOUNT, 0, 100
	ACTOR = RAND:(CHARANUM - 1) + 1
	TRYCALLFORM KOJO_LOAD_K{NO:ACTOR}, ACTOR

	SIF LINECOUNT > MEMO_LINECOUNT
		BREAK
NEXT

TARGET = ARG
FLAG:ロード時台詞 = 1
