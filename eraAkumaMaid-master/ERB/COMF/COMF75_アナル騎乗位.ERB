@COM75
;騎乗位肛門
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 75

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM75, ARG
IF INSERT("狂野摇摆")
	CALL FLAG_INSERTION, "Ａ挿入", ARG, 70
ELSE
	CALL FLAG_INSERTION, "Ａ挿入", ARG, 50
ENDIF

@SOURCE_COM75, ARG
IF INSERT("狂野摇摆")
	CALL SOURCE_INSERTION, "Ａ挿入", ARG, 70
ELSE
	CALL SOURCE_INSERTION, "Ａ挿入", ARG, 50
ENDIF

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 75

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:露出 = 1800
SOURCE:0:情愛 = 500

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0 TO 5
	SOURCE:0:性行動 = 300 + ABL:奉仕精神*200
	SOURCE:0:達成感 = 100 + ABL:奉仕精神*300
	SOURCE:0:逸脱 = MAX(300 - ABL:奉仕精神*100, 0)
CASE 6 TO 10
	SOURCE:0:性行動 = 1300 + (ABL:奉仕精神 - 5)*100
	SOURCE:0:達成感 = 1600 + (ABL:奉仕精神 - 5)*200
CASEELSE
	SOURCE:0:性行動 = 1800 + (ABL:奉仕精神 - 10)*50
	SOURCE:0:達成感 = 2600 + (ABL:奉仕精神 - 10)*100
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM75
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

SIF TEQUIP:電話中
	PRINTFORM %TEXTR(@"保持着和%TEXTS("彼氏")%的電話、/继続和%TEXTS("彼氏")%的電話的同时")%

PRINTFORM %CALLNAME:TARGET%
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;穴開きでない胖次を是ていて、ずらされていないなら、必ずずらす
ELSEIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
	CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
ENDIF
IF PREVCOM == 1 || PREVCOM == 9 || (PREVCOM >= 90 && PREVCOM <= 119)
	PRINTFORM %CALLNAME:PLAYER%の上にまたがると、
ELSEIF TALENT:好色 && RAND:2 == 0
	PRINTFORM 讓%CALLNAME:PLAYER%的陰莖與自己的肛門對準、
ELSE
	PRINTFORM 讓%CALLNAME:PLAYER%的陰莖%TEXTR("的位置準確的對上/與自己的肛門緊貼")%、
ENDIF

IF ABL:Ａ拡張 > SIZE("阴茎", PLAYER) && RAND:3 == 0
	IF SIZE("阴茎", PLAYER) >= 3 && EXP:結腸経験
		PRINTFORMW 慢慢地放下腰驱使着结肠将所有的阴茎吞入其中…
		CALL SETFLAG, "結腸責め", TARGET
	ELSE
		PRINTFORMW 慢慢地放下腰用菊穴整根吞入…
	ENDIF
	RETURN 1
ELSEIF TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 && COND("視力") && RAND:3 == 0
	PRINT 在用心醉的眼神注視著恍惚的面孔的同時、
ELSEIF TEQUIP:推倒 && ABL:施虐属性 >= 2 && RAND:2
	IF TALENT:高慢 + TALENT:強気 && RANDIF(2, TALENT:傲慢 + TALENT:小悪魔)
		PRINT 在浮現出大膽的笑容的同時、
	ELSEIF TALENT:傲慢 + TALENT:小悪魔 && RAND:2
		PRINT 在臉上露出戲謔的壞笑的同時、
	ELSE
		PRINT 在對痛苦的渴求與喜悅不禁滲透而出的同時、
	ENDIF
ELSEIF ABL:Ａ感覚 >= 3 && RAND:2
	PRINTFORM %TEXTR("在從唇角漏出甜美的呻吟/在露出恍惚而快樂的蕩漾表情")%的同時、
ELSE
	PRINT 在小口的吐出灼熱的喘息的同時、
ENDIF
IF ABL:Ａ拡張 >= SIZE("阴茎", PLAYER) && ABL:施虐属性 && RAND:2
	PRINT 一口気
ELSE
	PRINT 溫柔緩慢地
ENDIF
PRINTFORM 沉下了%TEXTR(@"腰/%TEXTS("お尻：視覚")%")%
IF PREVPLAY("清洁口交") && RAND:3 == 0
	PRINTFORMW 將%TEXTR("被漂亮的清潔乾淨的/清潔口交之後的/在口舌侍奉之後振作起來的")%陰莖\@ CHOSECOMS("Ａ挿入") ? 再一次的 # 毫不猶豫的 \@用尻穴深深含入體內……
ELSEIF CHECK("Ａ挿入") && RAND:3
	IF ABL:Ａ拡張 > SIZE("阴茎", PLAYER) && RAND:2
		PRINTW 用後側的淫穴毫無阻礙地將陰莖吞入體內、開始急促的扭動腰肢追逐著快樂……
	ELSE
		PRINTW 借助肛門柔肉的蠕動與本身的體重將挺立的陰莖慢慢吞入體內……
	ENDIF
ELSE
	PRINTW 用肛門擼動著陰莖……
ENDIF


@COM_ORDER_COM75, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;一定のＡ経験かＡ感覚が必要
IF EXP:Ａ経験 < 100 && ABL:Ａ感覚 < 3
	LOCAL -= 30
	IF EXP:Ａ経験 < 100
		LOCALS = %LOCALS% - Ａ経験不足(30)
	ELSEIF ABL:Ａ感覚 < 3
		LOCALS = %LOCALS% - Ａ感覚不足(30)
	ENDIF
ENDIF

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF

IF COND("調教中")
	;PALAM:Ａ潤（の不足）
	SELECTCASE PALAM:Ａ潤
	CASE IS < 3000
		LOCAL -= 20
		LOCALS = %LOCALS% - Ａ潤不足(20)
	CASE IS < 10000
		LOCAL -= 5
		LOCALS = %LOCALS% - Ａ潤不足(5)
	ENDSELECT
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 10
	LOCALS = %LOCALS% - 害羞(10)
ENDIF
;Ａ損傷
IF TALENT:Ａ損傷
	LOCAL -= 30 * TALENT:Ａ損傷
	LOCALS = %LOCALS%- Ａ損傷({30 * TALENT:Ａ損傷})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;Ａ性向
IF TALENT:Ａ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ａ性向(10)
ENDIF
;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;合計を表示(40以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
