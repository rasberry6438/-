;実際に指令選択をしたら、これを呼ぶ
@SHOW_COM_ORDER_COM, ARG, ARG:1
;ARG:1が1ならテ接吻トが出力されない
TRYCALLFORM COM_ORDER_COM{ARG}, ARG:1
TCVAR:干劲 = RESULT

@PRINT_COM_ORDER_COM, ARGS, ARG, ARG:1, ARG:2
SELECTCASE ARG
CASE 0
	PRINTFORM %ARGS% = {ARG:1} 
	SELECTCASE ARG:1
	CASE IS < ARG:2
		PRINT <
	CASE ARG:2
		PRINT =
	CASEELSE
		PRINT >
	ENDSELECT
	PRINTFORMW  実行値{ARG:2}
ENDSELECT

;RESULT > 0で実行値阴蒂ア
RETURN MAX(ARG:1 - ARG:2 + 1, 0)


@COM_ORDER, ARG
;少しは早くなるか…？
SIF TFLAG:実行値BOUNS
	RETURN TFLAG:実行値BOUNS
SIF TCVAR:基本実行値 && ARG
	RETURN TCVAR:基本実行値

STR:実行値 = 
TCVAR:基本実行値 = 0

;周回BOUNS
IF FLAG:周回数
	IF FLAG:陥落人数
		SELECTCASE FLAG:陥落人数
		CASE IS <= 4
			TCVAR:基本実行値 += 1
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(1)
		CASE IS <= 8
			TCVAR:基本実行値 += 2
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(2)
		CASE IS <= 16
			TCVAR:基本実行値 += 3
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(3)
		CASE IS <= 24
			TCVAR:基本実行値 += 4
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(4)
		CASE IS <= 32
			TCVAR:基本実行値 += 5
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(5)
		CASE IS <= 40
			TCVAR:基本実行値 += 6
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(6)
		CASEELSE
			TCVAR:基本実行値 += 7
			STR:実行値 = %STR:実行値% + 陥落人数BOUNS(7)
		ENDSELECT
	ENDIF
	TCVAR:基本実行値 += FLAG:周回数
	STR:実行値 = %STR:実行値% + 周回数({FLAG:周回数})
ENDIF
IF EQUIP:Ｈ的奖励
	TCVAR:基本実行値 += 15
	STR:実行値 = %STR:実行値% + Ｈ的奖励(15)
ENDIF
IF TEQUIP:MASTER:无用相談
	TCVAR:基本実行値 += 8
	STR:実行値 = %STR:実行値% + 无用相談(8)
ENDIF
IF TEQUIP:解放感 && PLACE("野外")
	TCVAR:基本実行値 += 15
	STR:実行値 = %STR:実行値% + 解放感(15)
ENDIF
IF TEQUIP:欺诈师
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 欺诈师(5)
ENDIF

IF TEQUIP:甜言蜜语 + CFLAG:甜言蜜语 && CFLAG:心情好
	TCVAR:基本実行値 += 10
	STR:実行値 = %STR:実行値% + 欣喜(10)
ELSE
	IF TEQUIP:甜言蜜语
		TCVAR:基本実行値 += 5
		STR:実行値 = %STR:実行値% + 心动不已(5)
	ENDIF
	IF CFLAG:心情好
		TCVAR:基本実行値 += 3
		STR:実行値 = %STR:実行値% + 心情好(3)
	ENDIF
ENDIF

IF CFLAG:躁动难忍
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 躁动难忍中(5)
ENDIF
IF ABL:信頼
	TCVAR:基本実行値 += MIN(ABL:信頼, 10)
	STR:実行値 = %STR:実行値% + 信頼LV{ABL:信頼}({MIN(ABL:信頼, 10)})
ENDIF
IF ABL:欲望
	TCVAR:基本実行値 += MIN(ABL:欲望, 10)
	STR:実行値 = %STR:実行値% + 欲望LV{ABL:欲望}({MIN(ABL:欲望, 10)})
ENDIF

;刻印格子
IF MARK:苦痛刻印
	TCVAR:基本実行値 += MARK:苦痛刻印 * 5
	STR:実行値 = %STR:実行値% + 苦痛刻印LV{MARK:苦痛刻印}({MARK:苦痛刻印 * 5})
ENDIF
IF MARK:快楽刻印
	TCVAR:基本実行値 += MARK:快楽刻印 * 2
	STR:実行値 = %STR:実行値% + 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 2})
ENDIF
IF MARK:淫紋 && TALENT:男性 == 0
	TCVAR:基本実行値 += MARK:淫紋 * 1
	STR:実行値 = %STR:実行値% + 淫紋LV{MARK:淫紋}({MARK:淫紋 * 1})
ENDIF

IF MARK:屈服刻印
	TCVAR:基本実行値 += MARK:屈服刻印 * (3 + TALENT:高慢*3 - TALENT:謙虚)
	STR:実行値 = %STR:実行値% + 屈服刻印LV{MARK:屈服刻印}({MARK:屈服刻印 * (3 + TALENT:高慢*3 - TALENT:謙虚)})
ENDIF

;好感度(補正のMAXは16)
IF PLAYER == MASTER && MIN(GET_EXP_LV(CFLAG:好感度)/5, 16)
	TCVAR:基本実行値 += MIN(GET_EXP_LV(CFLAG:好感度)/5, 16)
	STR:実行値 = %STR:実行値% + 好感度({MIN(GET_EXP_LV(CFLAG:好感度)/5, 16)})
ENDIF

IF GET_PALAMLV(PALAM:欲情)
	TCVAR:基本実行値 += GET_PALAMLV(PALAM:欲情)
	STR:実行値 = %STR:実行値% + 欲情LV{GET_PALAMLV(PALAM:欲情)}({GET_PALAMLV(PALAM:欲情)})
ENDIF
IF GET_PALAMLV(PALAM:恐怖)
	TCVAR:基本実行値 += GET_PALAMLV(PALAM:恐怖)
	STR:実行値 = %STR:実行値% + 恐怖LV{GET_PALAMLV(PALAM:恐怖)}({GET_PALAMLV(PALAM:恐怖)})
ENDIF

IF TALENT:叛逆
	TCVAR:基本実行値 -= 10
	STR:実行値 = %STR:実行値% - 叛逆(10)
ENDIF
IF TALENT:堅強
	TCVAR:基本実行値 -= 5
	STR:実行値 = %STR:実行値% - 堅強(5)
ENDIF
IF TALENT:坦率
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 坦率(5)
ENDIF
IF TALENT:高慢 && TEQUIP:推倒 == 0
	IF COND("陶酔")
		TCVAR:基本実行値 -= 5
		STR:実行値 = %STR:実行値% - 高慢(陶酔)(5)
	ELSEIF COND("弛緩")
		TCVAR:基本実行値 -= 10
		STR:実行値 = %STR:実行値% - 高慢(弛緩)(10)
	ELSE
		TCVAR:基本実行値 -= 15
		STR:実行値 = %STR:実行値% - 高慢(15)
	ENDIF
ENDIF
IF TALENT:謙虚
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 謙虚(5)
ENDIF
IF TALENT:喜歡引人注目
	TCVAR:基本実行値 += 2
	STR:実行値 = %STR:実行値% + 喜歡引人注目(2)
ENDIF
IF TALENT:好色
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 好色(5)
ENDIF
IF TALENT:淫乱
	TCVAR:基本実行値 += 10
	STR:実行値 = %STR:実行値% + 淫乱(10)
ENDIF

IF COND("性別嫌悪")
	TCVAR:基本実行値 -= 10 * COND("性別嫌悪")
	STR:実行値 = %STR:実行値% - 性別嫌悪({10 * COND("性別嫌悪")})
ENDIF

IF TALENT:相思相愛 && PLAYER == MASTER
	TCVAR:基本実行値 += 10
	STR:実行値 = %STR:実行値% + 相思相愛(10)
ELSEIF TALENT:恋慕 && PLAYER == MASTER
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 恋慕(5)
ENDIF

;酔酒
IF STATE("烂醉", TARGET)
	TCVAR:基本実行値 += 10
	STR:実行値 = %STR:実行値% + 烂醉(10)
ELSEIF STATE("酔酒", TARGET)
	TCVAR:基本実行値 += 3
	STR:実行値 = %STR:実行値% + 酔酒(3)
ENDIF

IF COND("種族：人形")
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 人形(5)
ENDIF
IF COND("疲労")
	TCVAR:基本実行値 -= 30
	STR:実行値 = %STR:実行値% - 疲労困憊(30)
ENDIF

;調教者の素質
IF TALENT:PLAYER:魅力
	TCVAR:基本実行値 += 3
	STR:実行値 = %STR:実行値% + 魅力(3)
ENDIF
IF TALENT:PLAYER:謎之魅力
	TCVAR:基本実行値 += TALENT:PLAYER:謎之魅力
	STR:実行値 = %STR:実行値% + %TALENT_NAME(GETNUM(TALENT, "謎之魅力"), TALENT:PLAYER:謎之魅力)%({TALENT:PLAYER:謎之魅力})
ENDIF
IF TALENT:PLAYER:鼓舞
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 鼓舞(5)
ENDIF
IF TEQUIP:室内露出PLAY
	TCVAR:基本実行値 -= 5
	STR:実行値 = %STR:実行値% - 室内露出(5)
ENDIF
IF TEQUIP:攝像機
	TCVAR:基本実行値 -= 10
	STR:実行値 = %STR:実行値% - 撮影中(10)
ENDIF
;IF TEQUIP:野外PLAY
;	IF COND("人里")
;		TCVAR:基本実行値 -= 10
;		STR:実行値 = %STR:実行値% - 野外(人里)(10)
;	ELSE
;		TCVAR:基本実行値 -= 5
;		STR:実行値 = %STR:実行値% - 野外(5)
;	ENDIF
;ENDIF
;推倒時のBOUNSを追加
IF TEQUIP:推倒
	TCVAR:基本実行値 += 10
	STR:実行値 = %STR:実行値% + 被逆推特別BOUNS(10)
ENDIF

IF COND("調教中")
	;情緒
	IF MOOD() / 15
		TCVAR:基本実行値 += MOOD() / 15
		STR:実行値 = %STR:実行値% + 情緒({MOOD() / 15})
	ENDIF
	;指导者関係
	IF ASSI && TARGET != ASSI && STATE("撃沈", ASSI) == 0
		IF TEQUIP:動画撮影
			TCVAR:基本実行値 -= 5
			STR:実行値 = %STR:実行値% - 手机で撮影中(5)
		ENDIF
		IF TALENT:ASSI:鼓舞
			TCVAR:基本実行値 += 10
			STR:実行値 = %STR:実行値% + %TEXTS("助手の名称")%の鼓舞(10)
		ENDIF
		;相性格子
		SELECTCASE RELATION:(NO:ASSI)
		CASE 1 TO 30
			TCVAR:基本実行値 -= 30
			STR:実行値 = %STR:実行値% - %TEXTS("助手の名称")%との相性が最悪(30)
		CASE 31 TO 50
			TCVAR:基本実行値 -= 15
			STR:実行値 = %STR:実行値% - %TEXTS("助手の名称")%との相性が悪い(15)
		CASE 51 TO 80
			TCVAR:基本実行値 -= 5
			STR:実行値 = %STR:実行値% - %TEXTS("助手の名称")%との相性がいまいち(5)
		CASE 120 TO 149
			TCVAR:基本実行値 += 1
			STR:実行値 = %STR:実行値% + %TEXTS("助手の名称")%との相性なかなか(1)
		CASE 150 TO 199
			TCVAR:基本実行値 += 5
			STR:実行値 = %STR:実行値% + %TEXTS("助手の名称")%との相性が良好(5)
		CASE IS >= 200
			TCVAR:基本実行値 += 10
			STR:実行値 = %STR:実行値% + %TEXTS("助手の名称")%との相性が最高(10)
		ENDSELECT
	ENDIF
ELSE
	IF CHARA_NUM("室内の邪魔者")
		TCVAR:基本実行値 -= 20
		STR:実行値 = %STR:実行値% - 二人きりじゃない(20)
	ELSEIF PLACE("自宅") == 0 && CHARA_NUM("集合予定") > 2
		TCVAR:基本実行値 -= 10
		STR:実行値 = %STR:実行値% - まだまだ人が来る予定がある(10)
	ENDIF

	;換装のお願い中
	IF COND("胖次交易中")
		IF CHECK_CLO("胖次を脱いだらＶ露出")
			TCVAR:基本実行値 -= 20
			STR:実行値 = %STR:実行値% - 胖次を脱いだらＶ露出(20)
		ELSEIF CHECK_CLO("胖次を脱掉のが難儀")
			TCVAR:基本実行値 -= 5
			STR:実行値 = %STR:実行値% - 胖次を脱ぎづらい(5)
		ENDIF
	ELSEIF COND("内衣姿取引中")
		;上下揃った内衣姿
		IF EQUIP:胖次 && (EQUIP:胸罩 || TALENT:男性)
			TCVAR:基本実行値 -= 10
			STR:実行値 = %STR:実行値% - 内衣姿になって欲しい(10)
		;没穿外衣
		ELSEIF EQUIP:胖次 && EQUIP:胸罩 == 0
			TCVAR:基本実行値 -= 15
			STR:実行値 = %STR:実行値% - 脱いだら没穿外衣(15)
		;没穿下衣
		ELSEIF EQUIP:胖次 == 0 && EQUIP:胸罩
			TCVAR:基本実行値 -= 20
			STR:実行値 = %STR:実行値% - 脱いだらＶ露出(20)
		;全裸じゃん！
		ELSE
			TCVAR:基本実行値 -= 30
			STR:実行値 = %STR:実行値% - 脱いだら全裸(30)
		ENDIF
	ELSEIF COND("全裸交易中")
		TCVAR:基本実行値 -= 30
		STR:実行値 = %STR:実行値% - 裸体希望(30)
	ENDIF
	IF COND("胖次交易中") || COND("内衣姿取引中") || COND("全裸交易中")
		IF CADV("原生主义者", TARGET)
			TCVAR:基本実行値 += 15
			STR:実行値 = %STR:実行値% + 原生主义者(15)
		ELSEIF CADV("原生主义者")
			TCVAR:基本実行値 += 15
			STR:実行値 = %STR:実行値% + %CALLNAME:ASSI%の助言(15)
		ENDIF
	ENDIF
	IF COND("合意：露出PLAY") && COND("胖次交易中") + COND("内衣姿取引中") + COND("全裸交易中")
		TCVAR:基本実行値 += 10
		STR:実行値 = %STR:実行値% + 露出PLAYの合意(10)
	ENDIF
ENDIF

;現在地による補正
IF PLACE("野外")
	TCVAR:基本実行値 -= 20
	STR:実行値 = %STR:実行値% - 野外(20)
ELSEIF PLACE("partner的房間")
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 自分的房間(5)
ELSEIF PLACE("自室")
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + %CALLNAME:MASTER%的房間(5)
ELSEIF PLACE("情人旅館")
	TCVAR:基本実行値 += 5
	STR:実行値 = %STR:実行値% + 情人旅館(5)
ELSEIF PLACE("個室") == 0
	TCVAR:基本実行値 -= 5
	STR:実行値 = %STR:実行値% - 不在個室(5)
ENDIF

SIF ARG == 0
	PRINTFORM %SUBSTRING(STR:実行値, 3)%

RETURN TCVAR:基本実行値


;ARGSにPLAYERの場所、ARGS:1にTARGETの場所
@COM_ORDER_STAIN(ARGS, ARGS:1)
#FUNCTION
#DIM STAIN_TARGET
#DIM STAIN_PLAYER

;汚臭無視
SIF TALENT:汚臭無視
	RETURNF 0

SELECTCASE ARGS
CASE "Ｍ", "手", "脚", "Ｐ", "Ｖ", "Ａ", "Ｂ"
	STAIN_PLAYER = STAIN:PLAYER:ARGS
CASE "尿道"
	IF PENIS(PLAYER)
		STAIN_PLAYER = STAIN:PLAYER:Ｐ
	ELSE
		STAIN_PLAYER = STAIN:PLAYER:Ｖ
	ENDIF
CASEELSE
	RETURNF 0
ENDSELECT

SELECTCASE ARGS:1
CASE "Ｍ", "手", "脚", "Ｐ", "Ｖ", "Ａ", "Ｂ"
	STAIN_TARGET = STAIN:(ARGS:1)
CASE "尿道"
	IF PENIS(TARGET)
		STAIN_TARGET = STAIN:Ｐ
	ELSE
		STAIN_TARGET = STAIN:Ｖ
	ENDIF
CASEELSE
	STAIN_TARGET = 0
ENDSELECT

VARSET LOCAL

;愛液的污垢
IF CHECK_STAIN(STAIN_PLAYER, "Ｖ")
	LOCAL:0 = 16
	SIF COND("性別嫌悪")
		LOCAL:0 += 5 * 2
	SIF TALENT:男性 || TALENT:兩面通吃
		LOCAL:0 -= 8
	SIF TALENT:气味弱点
		LOCAL:0 -= 2
	;場所自体がＶで出浴ならペナ軽減
	SIF LOCAL:0 > 0 && ARGS == "Ｖ" && TEQUIP:PLAYER:出浴
		LOCAL:0 /= 2
ENDIF
;阴茎的污垢
IF CHECK_STAIN(STAIN_PLAYER, "Ｐ")
	LOCAL:1 = 5
	SIF COND("性別嫌悪")
		LOCAL:1 += 5 * COND("性別嫌悪")
	SIF TALENT:气味弱点
		LOCAL:1 -= 2
	;場所自体がＰで出浴ならペナ軽減
	SIF LOCAL:1 > 0 && ARGS == "Ｐ" && TEQUIP:PLAYER:出浴
		LOCAL:1 /= 2
	SIF CALCF("阴茎好き度", TARGET) > 0
		LOCAL:1 -= CALCF("阴茎好き度", TARGET)
ENDIF
;精液的污垢(精液中毒で相殺)
IF CHECK_STAIN(STAIN_PLAYER, "精液")
	SIF COND("性別嫌悪")
		LOCAL:2 += 5 * COND("性別嫌悪")
	SIF ABL:精液中毒
		LOCAL:2 = 6 - MIN(ABL:精液中毒, 5)*2 - (TALENT:气味弱点 > 0)
	SIF TALENT:吸精体質
		LOCAL:2 -= 3
	SIF TALENT:精液嗜好
		LOCAL:2 -= 6
ENDIF
;肛門的污垢
IF CHECK_STAIN(STAIN_PLAYER, "Ａ")
	LOCAL:3 = 65 - MIN(ABL:受虐属性, 5)*5 - MIN(ABL:施虐属性, 5)*2 - MIN(ABL:Ａ感覚, 10)
	SIF TALENT:尻穴狂い
		LOCAL:3 -= 20
	SIF TALENT:气味弱点 && TALENT:恋慕
		LOCAL:3 /= 2
	;場所自体がＡで出浴ならペナ軽減
	SIF LOCAL:3 > 0 && ARGS == "Ａ" && TEQUIP:PLAYER:出浴
		LOCAL:3 /= 2
ENDIF
;母乳的污垢
IF CHECK_STAIN(STAIN_PLAYER, "乳")
	LOCAL:4 += 1
ENDIF
;尿的污垢
IF CHECK_STAIN(STAIN_PLAYER, "尿")
	LOCAL:5 += 15
	SIF TALENT:气味弱点 && TALENT:恋慕
		LOCAL:5 /= 2
ENDIF
;血液汚れ
IF CHECK_STAIN(STAIN_PLAYER, "破瓜之血")
	LOCAL:6 += 30
	SIF COND("種族：吸血鬼")
		LOCAL:6 /= 2
ENDIF
;阴部の恥垢汚れ
IF CHECK_STAIN(STAIN_PLAYER, "Ｖ恥垢")
	LOCAL:7 = 20
	SIF COND("性別嫌悪")
		LOCAL:7 += 5 * COND("性別嫌悪")
	SIF TALENT:气味弱点 && TALENT:恋慕
		LOCAL:7 /= 2
	;場所自体がＶで出浴ならペナ軽減
	SIF LOCAL:7 > 0 && ARGS == "Ｖ" && TEQUIP:PLAYER:出浴
		LOCAL:7 /= 2
ENDIF
;ちんかす的污垢
IF CHECK_STAIN(STAIN_PLAYER, "Ｐ恥垢")
	LOCAL:8 = 20
	SIF COND("性別嫌悪")
		LOCAL:8 += 5 * COND("性別嫌悪")
	SIF TALENT:气味弱点 && TALENT:恋慕
		LOCAL:8 /= 2
	;場所自体がＰで出浴ならペナ軽減
	SIF LOCAL:8 > 0 && ARGS == "Ｐ" && TEQUIP:PLAYER:出浴
		LOCAL:8 /= 2
	IF CALCF("阴茎好き度", TARGET) > 0
		LOCAL:8 /= CALCF("阴茎好き度", TARGET)
		SIF TALENT:献身
			LOCAL:8 -= 5
		SIF TALENT:倒錯的
			LOCAL:8 -= 5
	ENDIF
ENDIF


;もともとある汚れは無視する＆場所によっては汚れの強さＵＰ
SELECTCASE ARGS:1
CASE "Ｍ"
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 150), LOCAL:3 + 20)
CASE "Ｐ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:2 = 0
	LOCAL:5 = 0
CASE "Ｖ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 200), LOCAL:3 + 50)
CASE "Ａ"
	LOCAL:0 = 0
	LOCAL:1 = 0
	LOCAL:3 = 0
CASE "尿道"
	LOCAL:5 = 0
	LOCAL:3 = MIN(MULTIPLY(LOCAL:3, 200), LOCAL:3 + 50)
ENDSELECT

LOCAL = LOCAL:0 + LOCAL:1 + LOCAL:2 + LOCAL:3 + LOCAL:4 + LOCAL:5 + LOCAL:6 + LOCAL:7 + LOCAL:8

SIF TALENT:汚臭鈍感
	LOCAL = MAX(DIVIDE(LOCAL, 200), LOCAL - 20)
SIF TALENT:汚臭敏感
	LOCAL = MIN(MULTIPLY(LOCAL, 200), LOCAL + 20)

;陶酔中は汚れに鈍感になる
SIF LOCAL > 0 && COND("陶酔")
	LOCAL /= 2

LOCAL = MAX(LOCAL, 0)

RETURNF LOCAL

