@COM152
;尿道振動棒
;0bit細い, 1bit普通, 2bit粗大的, 3bit極太, 4bit腕, 5bit馬, 11bit布ル領帯プ
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
IF TEQUIP:尿道振動棒 == 0
	CALL SHOW_COM_ORDER_COM, 152
	;実行できない
	SIF TCVAR:干劲 == 0
		RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

RETURN 1



;ARGに入れられる角色、ARGSは補足。
;現状だとARGS == "強制挿入"で[100] やめる を選択不可にできる
@SELECT_VIB_U, ARG, ARGS
#DIM LCOUNT
#DIM SIZE_DILDO

VARSET LOCAL, 1
;どの振動棒を使えるかを判定（双方の施虐狂受虐狂具合で変化）
FOR LCOUNT, 1, 6
	;振動棒のサイズを代入
	SIZE_DILDO = LCOUNT

	SELECTCASE SIZE_DILDO - ABL:尿道拡張
	CASE IS >= 2
		LOCAL:LCOUNT = 0
	CASE 1
		;尺寸が１上回っているなら、施虐属性３ＬＶor（施虐属性１ＬＶに加えて相手の受虐属性１ＬＶ）が必要
		SIF MAX(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 3 || MIN(ABL:受虐属性, ABL:PLAYER:施虐属性) >= 1
			CONTINUE
	CASEELSE
		CONTINUE
	ENDSELECT

	LOCAL:LCOUNT = 0
NEXT

;道具所持による判定
SIF ITEM:極太振動棒 == 0
	LOCAL:3 = 0
SIF ITEM:肛門拉珠 == 0
	LOCAL:11 = 0

PRINTFORML 這一次要\@ ARG == PLAYER ? 讓她 # \@使用哪種大小的振動棒呢……？

VARSET LOCALS
LOCALS:0 = [ 0] 尿道用小型振動棒
LOCALS:1 = [\@ LOCAL:1 ? {1, 2} # × \@] 普通振動棒　　\@ ABL:ARG:尿道拡張 <= 0 ? ※初心者厳禁！ # \@
LOCALS:2 = [\@ LOCAL:2 ? {2, 2} # × \@] 大号振動棒　　\@ ABL:ARG:尿道拡張 <= 1 ? ※習慣者用！ # \@
LOCALS:3 = [\@ LOCAL:3 ? {3, 2} # × \@] 极粗振動棒　　\@ ABL:ARG:尿道拡張 <= 2 ? ※上級者専用！ # \@
LOCALS:4 = [\@ LOCAL:4 ? {4, 2} # × \@] 拳型振動棒　　\@ ABL:ARG:尿道拡張 <= 3 ? ※超上級者専用！ # \@
LOCALS:5 = [\@ LOCAL:5 ? {5, 2} # × \@] 馬型振動棒　　\@ ABL:ARG:尿道拡張 <= 4 ? ※面向不願回頭的人 # \@

LOCALS:11 = [\@ LOCAL:11 ? 11 # × \@] 尿道拉珠

FOR LCOUNT, 0, 20
	SIF STRLENS(LOCALS:LCOUNT) == 0
		CONTINUE

	SIF LOCAL:LCOUNT == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  %LOCALS:LCOUNT%
	RESETCOLOR
NEXT

SIF ARGS != "強制挿入"
	PRINTFORML [100] 不、再考慮一下吧。

$INPUT_LOOP
INPUT

IF RESULT == 100 && ARGS != "強制挿入"
	TEQUIP:ARG:尿道振動棒 = 0
	TFLAG:指令中止 = 1
	RETURN 0
ELSEIF RESULT == 0
	IF ABL:尿道拡張 <= -1
		TEQUIP:ARG:尿道振動棒 = 1p0
	ELSE
		TEQUIP:ARG:尿道振動棒 = 1p1
	ENDIF
ELSEIF RESULT == 1 && LOCAL:1
	TEQUIP:ARG:尿道振動棒 = 1p2
ELSEIF RESULT == 2 && LOCAL:2
	TEQUIP:ARG:尿道振動棒 = 1p3
ELSEIF RESULT == 3 && LOCAL:3
	TEQUIP:ARG:尿道振動棒 = 1p4
ELSEIF RESULT == 4 && LOCAL:4
	TEQUIP:ARG:尿道振動棒 = 1p5
ELSEIF RESULT == 5 && LOCAL:5
	TEQUIP:ARG:尿道振動棒 = 1p6
ELSEIF RESULT == 11 && LOCAL:11
	TEQUIP:ARG:尿道振動棒 = 1p1 + 1p11
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 1


@TRAIN_MESSAGE_COM152
;尿道振動棒の着脱
IF TEQUIP:尿道振動棒
	PRINTFORM %CALLNAME:TARGET%\@ TEQUIP:推倒 ? は # の \@%TEXTR("尿道口/狭孔/排泄腔")%から
	IF COND("尿道布ル")
		PRINTFORMW 尿道拉珠氣勢十足的一口氣拔出了。
	ELSE
		SELECTCASE SIZE("尿道振動棒")
		CASE 5
			PRINTFORM 幾乎不可能進入的馬型振動棒
		CASE 4
			PRINTFORM 成年男性手腕粗細的拳型振動棒
		CASE 3
			PRINTFORM 常規而言最為粗壯的振動棒
		CASE 2
			PRINTFORM 粗大的振動棒
		CASE 1
			PRINTFORM 尿道振動棒
		CASE 0
			PRINTFORM 細小的振動棒
		CASE -1
			PRINTFORM 和尾指相比也略顯纖細的珍袖振動棒
		ENDSELECT
		SIF ABL:MASTER:施虐属性 >= 3 && TEQUIP:推倒 == 0
			PRINTFORM 快速地
		PRINTFORMW 拔出了。

		SELECTCASE ABL:尿道拡張
		CASE IS >= 2
			PRINTFORMW ……%CALLNAME:TARGET%的尿道口欲求不滿的張合著、從沒能徹底閉上的腔口中、渾濁的濃郁滴滴答答的流下了。
			BASE:尿意 = MAX(BASE:尿意 - 2000, 0)
		CASEELSE
			PRINTFORMW ……%CALLNAME:TARGET%的尿道在擴張之的影響向稍稍地鬆弛了一些、在振動棒抽出之時也帶出了一點點的尿液。
			BASE:尿意 = MAX(BASE:尿意 - 500, 0)
		ENDSELECT
	ENDIF

	SIF COND("尿道布ル")
		CALL SETFLAG, "尿道布ル引き抜きフラグ", TARGET

	TFLAG:地の文分岐 = TEQUIP:尿道振動棒
	TEQUIP:尿道振動棒 = 0
	CALL CALC, "調教ソースの計算"
	RETURN 0
ENDIF

$SELECT_VIB
;振動棒選択
CALL SELECT_VIB_U, TARGET

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:尿道振動棒 == 0
	TFLAG:地の文分岐 = 1
	GOTO SELECT_VIB
ENDIF


;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"

;地の文を飛ばすフラグを立てている(ターン開始時にOFFになる)
SIF TFLAG:地の文スキップ
	RETURN 0

PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的
;尿道が露出していないで、胖次をずらしていないなら自動的にずらす
IF !CHECK_CLO("Ｖ露出")
	CALL SETFLAG, "下衣を露出：テ接吻ト有些", TARGET
	PRINT 、
ENDIF
IF PREVEX("尿道絶頂") && RAND:3 == 0
	PRINTFORM 在絕頂之後%TEXTR("劇烈顫抖著的/淫靡的顫動著的/反復地張合著的/痙攣著的")%
ELSEIF PREVEX("尿道内射精")
	PRINTFORM %TEXTR("不斷溢出精液的/緩緩流出精液的/被精液完全弄髒的/沾滿精液的")%
ELSEIF (ABL:尿道拡張 >= 4 || (PREVCOM >= 83 && PREVCOM <= 88) || PREVCOM == 15) && ABL:尿道拡張 >= 2 && RAND:3 == 0
	IF PREVCOM >= 83 && PREVCOM <= 88 && RAND:2 == 0
		PRINT 在被激烈侵犯之後難以復原的
	ELSE
		PRINT 不檢點地大張著洞口的
	ENDIF
ELSEIF ABL:尿道拡張 >= 2
	PRINT 在充分地開發下十分柔軟的
ELSEIF TALENT:恋慕 || RAND:3 == 0
	PRINT 可愛的
ELSEIF EXP:尿道経験 < 50 && RAND:2 == 0
	PRINT 經驗不足的
ELSE
	PRINT 緊張地收縮著的
ENDIF
PRINT 尿道
IF TALENT:恋慕
	PRINTFORM 溫柔地揉搓著放鬆下來之後\@ COND("下衣を露出させた") ? # 、 \@用
ELSE
	PRINTFORM に
ENDIF

IF COND("尿道布ル")
	PRINTFORMW 一粒粒分開的尿道拉珠填滿了。
ELSE
	SELECTCASE SIZE("尿道振動棒")
	CASE 5
		PRINTFORM 幾乎不可能進入的馬型
	CASE 4
		PRINTFORM 成年男性手腕粗細的
	CASE 3
		PRINTFORM 极粗的
	CASE 2
		PRINTFORM 巨大的
	CASE 1
		PRINTFORM 尿道
	CASE 0
		PRINTFORM 細小的
	CASE IS < 0
		PRINTFORM 和尾指相比也略顯纖細的珍袖
	ENDSELECT
	PRINT 振動棒
	;拡張不足
	IF ABL:尿道拡張 < SIZE("尿道振動棒")
		PRINTFORMW %TEXTR("用力地/強硬的/毫不留情地/在不斷開闢膣道的同時/在將肉體強硬推開的感觸仔細品味著的同時/將所有的抵抗毫不在意地無視的同時")%塞滿了。
	ELSE
		IF CHECK("尿道振動棒") == 0 && ABL:PLAYER:施虐属性 >= 3 && RAND:2 == 0
			PRINT 用力地
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:3 == 0
			PRINT 深深地
		ELSEIF TALENT:恋慕 && RAND:3 == 0
			PRINT 温柔地
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:2 == 0
			PRINT 一口気
		ELSE
			PRINT 溫柔緩慢地
		ENDIF
		IF RAND:4 == 0
			PRINTW 填滿了內部。
		ELSEIF CHECK("尿道振動棒") == 0 && ABL:PLAYER:施虐属性 >= 3 && RAND:2 == 0
			PRINTW 衝入其中。
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:3 == 0
			PRINTW 沉入其中。
		ELSEIF (TALENT:恋慕 || ABL:施虐属性 >= 3) && RAND:2 == 0
			PRINTW 刺入其中。
		ELSE
			PRINTW 挿入其中。
		ENDIF
	ENDIF
ENDIF

;拡張十分
IF CHECK("尿道振動棒") && CHOSECOM(SELECTCOM) == 0
	PRINTFORM %CALLNAME:TARGET%的
	IF TALENT:娇小 && EXP:尿道経験 < 100 && ABL:尿道拡張 <= 1
		PRINT 小小的
	ELSEIF TALENT:娇小 && ABL:尿道拡張 >= 2
		SIF ABL:尿道拡張 > SIZE("尿道振動棒")
			PRINT 完全
		PRINT 被充分擴張的
	ELSEIF EXP:尿道経験 >= 100 && RAND:2 == 0
		PRINT 経験豊富的
	ELSEIF ABL:尿道拡張 > SIZE("尿道振動棒")
		PRINT 十分柔軟的
	ENDIF
	PRINT 尿道
	IF SIZE("尿道振動棒") >= 2 && ABL:尿道拡張 > SIZE("尿道振動棒") && RAND:2
		IF RAND:3 == 0
			PRINT 輕而易舉地
		ELSEIF RAND:2
			PRINT 毫無抵抗地
		ELSE
			PRINT 就像是在主動吮吸一樣地
		ENDIF
	ELSEIF ABL:尿道拡張 >= SIZE("尿道振動棒") + 1 && RAND:2 == 0
		SIF TALENT:内気 || TALENT:清楚
			PRINT 與平日裡清純老實的印象完全不同、
		PRINT 輕易地張開、
	ELSEIF ABL:尿道拡張 >= SIZE("尿道振動棒")
		PRINT 向橡膠一樣伸展開來、
	ENDIF
	IF COND("尿道布ル")
		PRINT 將尿道拉珠
	ELSE
		SELECTCASE SIZE("尿道振動棒")
		CASE IS >= 3
			IF TALENT:男性 || TALENT:処女
				PRINT 將根本沒法用嘴含住的
			ELSEIF ABL:Ｖ拡張 < SIZE("尿道振動棒")
				PRINT 將就連前面的膣穴也無法接納的
			ENDIF
			PRINT 兇惡的
		CASE 2
			PRINT 粗大的
		CASE IS <= 0
			PRINT 將尿道
		ENDSELECT
		PRINT 振動棒
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW 用尿道整個吞嚥下來……
	ELSE
		PRINTFORMW 一直吞到膀胱的內壁為止……
	ENDIF
ELSEIF CHECK("尿道振動棒")
	IF COND("尿道布ル")
		PRINTFORMW 在尿道乃至於膀胱都被異物填滿所帶來的奇妙感觸、讓%CALLNAME:TARGET%無法抑制的漏出了灼熱而濡濕的低吟聲……
	ELSE
		PRINTFORMW 在%TEXTR("尿道口/狭孔/排泄腔")%中激烈震動的振動棒所傳來的異常快感、讓%CALLNAME:TARGET%無法抑制的漏出了灼熱而濡濕的低吟聲……
	ENDIF
	;道端の菠萝包加筆分
	SIF ABL:尿道拡張 - SIZE("尿道振動棒") >= 2
		PRINTFORMW しかし拡張されつくした%CALLNAME:TARGET%には小さ過ぎるようで、その声にはどこか物足りなさげな色が浮かんでいる…
ELSEIF CHECK("尿道振動棒") == 0 && ABL:尿道拡張 < SIZE("尿道振動棒")
	PRINTFORM %CALLNAME:TARGET%的
	SIF TALENT:娇小 && EXP:尿道経験 < 100 && ABL:尿道拡張 == 0
		PRINT 小小的
	PRINT 尿道
	IF SIZE("尿道振動棒") >= 1 && ABL:尿道拡張 < SIZE("尿道振動棒")
		PRINTW 無法承受振動棒的粗大、無法抑制的吐出了愈來愈高的哀鳴聲……
	ELSE
		PRINT 被強硬地擴張到幾乎要裂開的程度、但終究將
		IF COND("尿道布ル")
			PRINT 相互連接著的球體
		ELSEIF SIZE("尿道振動棒") == 0
			PRINT 尿道振動棒
		ELSE
			PRINT 振動棒
		ENDIF
		PRINTW 勉強地吞入體內……
	ENDIF
ELSEIF ABL:尿道感覚 >= 2 && CUP:苦痛 < 500
	PRINTFORMW %CALLNAME:TARGET%因腸道中傳來的奇妙感觸而迷惑了……
ELSE
	PRINTFORM 對於
	IF COND("尿道布ル")
		PRINT 尿道拉珠
	ELSE
		PRINT 振動棒
	ENDIF
	PRINTFORMW 所帶來的刺激還不習慣、%CALLNAME:TARGET%的%TEXTR("尿道口/狭孔/排泄腔")%本能的緊繃著、表現著強烈的反抗心……
ENDIF

@FLAG_COM152, ARG
CALL FLAG_INSERTION, "尿道振動棒", ARG, 100

@SOURCE_COM152, ARG
IF TEQUIP:尿道振動棒
	IF COND("尿道布ル引き抜きフラグ")
		CALL SOURCE_INSERTION, "尿道振動棒", ARG, 30
	ELSE
		CALL SOURCE_INSERTION, "尿道振動棒", ARG, 100
	ENDIF
ELSE
	IF COND("尿道布ル引き抜きフラグ")
		CALL SOURCE_INSERTION, "尿道振動棒", ARG, 100
	ELSE
		CALL SOURCE_INSERTION, "尿道振動棒", ARG, 30
	ENDIF
ENDIF
CALL SETFLAG, "尿道性感フラグ", TARGET

@EQUIP_COM152
;尿道振動棒挿入中
SIF TEQUIP:尿道振動棒 == 0
	RETURN 0
SIF SELECTCOM == 152
	RETURN 0

CALL SOURCE_COM152, 30
CALL FLAG_COM152, 30

RETURN 1

@EQUIP_COM152_2
;尿道振動棒挿入中
SIF TEQUIP:尿道振動棒 == 0
	RETURN 0
SIF SELECTCOM == 152
	RETURN 0

IF COND("尿道布ル")
	PRINTL ＜尿道拉珠挿入中＞
ELSE
	SELECTCASE SIZE("尿道振動棒")
	CASE 6
		PRINTL ＜尿道規格外振動棒挿入中＞
	CASE 5
		PRINTL ＜尿道馬型振動棒挿入中＞
	CASE 4
		PRINTL ＜尿道腕並振動棒挿入中＞
	CASE 3
		PRINTL ＜尿道極太振動棒挿入中＞
	CASEELSE
		PRINTL ＜尿道振動棒挿入中＞
	ENDSELECT
ENDIF

LOCAL = 1
;自慰実行時は経験＋１
IF SELECTCOM == 3
	SIF ABL:尿道拡張 < SIZE("尿道振動棒")
		LOCAL += 1
ENDIF

EXP:尿道経験 += LOCAL

VARSET LOCAL
;尿道拡張経験
IF SELECTCOM == 3 || SELECTCOM == 15
	SIF SIZE("尿道振動棒") >= 2
		LOCAL = MAX(SIZE("尿道振動棒") - ABL:尿道拡張 + 1, SIZE("尿道振動棒")/3, 1)
ENDIF

IF LOCAL
	EXP:尿道拡張経験 += LOCAL
	;拡張経験による追加の疲労
	DOWNBASE:体力 += LOCAL*50
ENDIF

@COM_ORDER_COM152, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:受虐属性
IF ABL:受虐属性
	LOCAL += MIN(ABL:受虐属性, 5) * 3
	LOCALS = %LOCALS% + 受虐属性LV{ABL:受虐属性}({MIN(ABL:受虐属性, 5) * 3})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 15
	LOCALS = %LOCALS% - 害羞(15)
ENDIF
;不知羞恥
IF TALENT:不知羞恥
	LOCAL += 2
	LOCALS = %LOCALS% + 不知羞恥(2)
ENDIF
;接受快感
IF TALENT:接受快感
	LOCAL += 5
	LOCALS = %LOCALS% + 接受快感(5)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 5
	LOCALS = %LOCALS% - 否定快感(5)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 10
	LOCALS = %LOCALS% + 倒錯的(10)
ENDIF
IF TEQUIP:拘束
	LOCAL += 30
	LOCALS = %LOCALS% + 拘束中(30)
ENDIF
;媚薬
IF TEQUIP:媚薬
	LOCAL += 2
	LOCALS = %LOCALS% + 媚薬(2)
ENDIF
;尿道性感
IF TALENT:尿道性感
	LOCAL += 10
	LOCALS = %LOCALS% + 尿道性感(10)
ENDIF
;视频拍摄
IF TEQUIP:攝像機
	LOCAL -= 20
	LOCALS = %LOCALS% - 撮影中(20)
ENDIF
;野外or室内露出
IF TEQUIP:野外PLAY
	LOCAL -= 10
	LOCALS = %LOCALS% - 野外PLAY(10)
ELSEIF TEQUIP:室内露出PLAY
	LOCAL -= 10
	LOCALS = %LOCALS% - 室内露出(10)
ENDIF

;合計を表示(55以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 55

RETURN RESULT
