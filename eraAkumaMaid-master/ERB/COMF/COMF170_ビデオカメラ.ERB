@COM170
;攝像機
;害羞や清楚、贞操重视持ちだと凄い反発。
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:攝像機
	STR:前回指令 = 攝像機(撮影開始)
ELSE
	STR:前回指令 = 攝像機(结束摄影)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM170
;视频拍摄がされていれば終了させる
IF TEQUIP:攝像機
	;短すぎる
	IF TEQUIP:攝像機 <= 3
		;認識阻害持ち
		IF TALENT:MASTER:認識阻害
			PRINTFORMW 如果现在停止拍摄的东西就太少了、要停止摄影吗？
		ELSE
			PRINTFORMW 如果现在停止拍摄的东西就太少了、没有保留的价值、要停止摄影吗？
		ENDIF
		CALL PRINT_SELECT, "是/否"
		IF RESULT
			TFLAG:指令中止 = 1
			RETURN 0
		ENDIF
		STR:ビデオ内容 = 
		STR:ビデオジャンル = 
	ENDIF

	TEQUIP:攝像機 = 0
	PRINTW ★★★視頻攝影結束★★★
	;助手が居ない
	IF ASSI <= 0 || ASSIPLAY

	ELSEIF ABL:ASSI:自慰中毒 >= RAND:6 && RAND:2
		IF TALENT:ASSI:冷静 + TALENT:ASSI:感情缺乏 && RAND:2
			PRINTFORMW 攝影結束後的%CALLNAME:ASSI%盡力保持著冷靜、但仍然無法抑制的伸手愛撫著因渴求快樂而發熱的股間……
		ELSE
			PRINTFORMW 在充分的觀賞過兩人的淫戲後、%CALLNAME:ASSI%無法抑制體內的情慾、向濡濕的股間伸出了手……
		ENDIF
	ELSE
		IF TALENT:ASSI:容易濕 && ABL:ASSI:欲望 >= RAND:6 && RAND:2
			PRINTFORMW %CALLNAME:ASSI%作為%TEXTS("助手の名称")%良好的完成了委託、但淫液從股間溢出、在大腿上畫出了淫靡的線條……
		ELSE
			PRINTFORMW %CALLNAME:ASSI%拍攝到十分良好的畫面、因此而開心的笑著……
		ENDIF
	ENDIF
ELSE
	IF STR:ビデオ内容 != ""
		PRINTFORMW 现在已经有収録的部分了、要放棄然后重新拍摄吗？
		CALL PRINT_SELECT, "是/否"
		IF RESULT
			TFLAG:指令中止 = 1
			RETURN 0
		ENDIF
	ENDIF

	TEQUIP:攝像機 = 1
	STR:ビデオ内容 = 
	STR:ビデオジャンル = 
	PRINTFORML ★★★視頻拍攝開始★★★

	IF EXP:撮影経験 == 0
		IF TALENT:好奇心
			PRINT 將對初次見到的機器興趣滿滿的樣子的
		ELSE
			PRINT 將像在看不可思議的東西一樣凝視著撮影機材的
		ENDIF
	ELSEIF ABL:露出癖 >= 3 || TALENT:不知羞恥
		IF TALENT:相思相愛 && TALENT:淫乱 && RAND:2
			PRINT 將為了向他人展示自己的愛而更加熱烈了的
		;ELSEIF COND("人気：ＡＶ") && RAND:2
		;	IF TALENT:強気 || TALENT:高慢
		;		PRINT 自分の人気の有些様を受けて満更でもない様子で準備をしている
		;	ELSE
		;		PRINT 作品作りにも慣れてレンズ越しのお客からの受けを思案している
		;	ENDIF
		ELSE
			PRINT 將在攝像機的鏡頭瞄準後、突然的、雙眼閃耀起來了的
		ENDIF
	ELSEIF TEQUIP:野外PLAY
		PRINT 將緊張不安的環伺著周圍的情況的
	ELSEIF TALENT:害羞
		PRINT 將臉頰通紅的偷偷窺視著這邊的情況的
	ELSE
		PRINT 將在认真地考虑着要进行什么様的PLAY的
	ENDIF
	PRINTFORM %CALLNAME:TARGET%斜視著、
	IF ASSI <= 0
		PRINTFORMW %CALLNAME:PLAYER%將攝影機小心地平穩放置在合適的高台上、重新開始了。
		PRINTFORMW （介于%TEXTS("助手の名称")%並不在場、拍攝的質量可能會有跌落、但這也是沒有辦法的事……）
	ELSEIF ASSIPLAY
		PRINTFORMW %CALLNAME:PLAYER%將攝影工作暫時給%CALLNAME:MASTER%之後、重新開始了。
	ELSE
		LOCALS = 
		IF TEQUIP:野外PLAY || TEQUIP:室内露出PLAY
			IF CHARA_NUM("室内露出観客数")
				LOCALS = 和在場的少女們一起竊笑的同時、
			ELSEIF ABL:ASSI:施虐属性 >= 3
				LOCALS = 惋惜少女們並不在場的同時、
			ELSE
				LOCALS = 確認週邊沒有人在場的同時、
			ENDIF
		ELSEIF TEQUIP:室内露出PLAY
			LOCALS = 部員たちの反応も一緒に記録することを意識し的同时、
		ELSEIF TALENT:ASSI:母親 + TALENT:ASSI:父親 && RAND:3 == 0
			LOCALS = 心中想著要將我的孩子的性的成長記錄下來的同時、
		ELSEIF TALENT:母親 + TALENT:父親 && COND("兄弟", ASSI) && RAND:3 == 0
			LOCALS = 對將自己的親人的情事記錄下來而興奮的同時、
		ELSEIF (TALENT:ASSI:淫乱 || TALENT:ASSI:相思相愛) && RAND:4 == 0
			IF TALENT:ASSI:淫乱 && RANDIF(1, TALENT:ASSI:相思相愛)
				IF TALENT:ASSI:高慢
					LOCALS = 發出製造過激場面的指示的同時、
				ELSE
					LOCALS = 對之後會出現的情事飽含期待的同時、
				ENDIF
			ELSE
				IF TALENT:ASSI:淫乱
					LOCALS = 「現在是%CALLNAME:TARGET%的時間」這樣自言自語的同時、
				ELSE
					LOCALS = 浮現出寂寞表情但還是勉強振作的同時、
				ENDIF
			ENDIF
		ELSEIF ABL:ASSI:自慰中毒 >= 3 && RAND:3 == 0
			LOCALS = 一隻手不自覺的向\@ TALENT:ASSI:男性 ? 股間 # 秘所 \@伸出、尽力忍耐的同時、

		ELSEIF ABL:ASSI:施虐属性 >= 3 && RANDIF(1, TALENT:ASSI:坦率, TALENT:ASSI:好奇心, TALENT:ASSI:鼓舞, TALENT:ASSI:粗野)
			LOCALS = 在懊惱著不能直接蹂躪對方並努力的工作著的同時、
		ELSEIF TALENT:ASSI:粗野 && RANDIF(1, TALENT:ASSI:坦率, TALENT:ASSI:好奇心, TALENT:ASSI:鼓舞)
			LOCALS = 因接下委託而挺起胸給自己鼓勁的同時、
		ELSEIF TALENT:ASSI:鼓舞 && RANDIF(1, TALENT:ASSI:坦率, TALENT:ASSI:好奇心)
			LOCALS = 為%CALLNAME:TARGET%活躍地應援著的同時、
		ELSEIF TALENT:ASSI:好奇心 && RANDIF(1, TALENT:ASSI:坦率)
			IF RELATION:ASSI:TARGET >= 150
				LOCALS = 為了不讓%CALLNAME:TARGET%的痴態白白浪費而拼命的記錄著的同時、
			ELSE
				LOCALS = 為了不讓%CALLNAME:MASTER%們的痴態白白浪費而拼命的記錄著的同時、
			ENDIF
		ELSEIF TALENT:ASSI:坦率
			LOCALS = 仔細地聽從著%CALLNAME:MASTER%命令的同時、
		ELSEIF ABL:ASSI:自慰中毒 >= 3 && RAND:3 == 0
			LOCALS = 一隻手不自覺的向\@ TALENT:ASSI:男性 ? 股間 # 秘所 \@伸出、尽力忍耐的同時、
		ENDIF

		IF LOCALS == ""
			PRINTFORMW %CALLNAME:PLAYER%將攝像機遞交給%CALLNAME:ASSI%、開始攝影了……
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%將攝像機遞交給%CALLNAME:ASSI%。
			PRINTFORMW %CALLNAME:ASSI%%LOCALS%開始攝影了……
		ENDIF
		CALL KOJO_GUEST, "助手：撮影開始", ASSI
	ENDIF
ENDIF

@EQUIP_COM170
SIF TEQUIP:攝像機 == 0
	RETURN 0
SIF SELECTCOM == 170
	RETURN 0

CALL SOURCE_COM170, 10

RETURN 1

@FLAG_COM170, ARG

@SOURCE_COM170, ARG
SIF TEQUIP:攝像機 == 0
	RETURN 0

CALL VARSET_COMF

SOURCE:0:露出 = 3000
SOURCE:0:逸脱 = 5000

;裸体
IF CHECK_CLO("裸体")
	;種族衣装が裸体でない
	IF CLO_ORI(TARGET, "服", "贴身制服", "下衣", "胖次")
		SOURCE:0:露出 += 3000
		SOURCE:0:逸脱 += 1000
	ENDIF
;胖次のみ
ELSEIF CHECK_CLO("内衣姿")
	;種族衣装が服or贴身制服or下衣を含む
	IF CLO_ORI(TARGET, "服", "贴身制服", "下衣")
		SOURCE:0:露出 += 2000
		SOURCE:0:逸脱 += 500
	ENDIF
;没穿内褲
ELSEIF CHECK_CLO("没穿内褲")
	;種族衣装に内衣有
	IF CLO_ORI(TARGET, "胖次")
		SOURCE:0:露出 += 1000
		SOURCE:0:逸脱 += 300
	ENDIF
ENDIF

;拘束の状態により露出を追加
IF GETBIT(TEQUIP:拘束, 5)
	SOURCE:0:露出 += 1000
ELSEIF GETBIT(TEQUIP:拘束, 4)
	SOURCE:0:露出 += 500
ELSEIF GETBIT(TEQUIP:拘束, 3)
	SOURCE:0:露出 += 200
ENDIF

;露出はTRACHECKで補正がかかるので、逸脱に害羞などで補正
SIF TALENT:害羞
	TIMES SOURCE:0:逸脱, 2.00
SIF TALENT:清楚
	TIMES SOURCE:0:逸脱, 1.50
SIF TALENT:喜歡引人注目
	TIMES SOURCE:0:逸脱, 0.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 1.70
SIF TALENT:聰慧
	TIMES SOURCE:0:露出, 1.50
SIF TALENT:笨蛋
	TIMES SOURCE:0:露出, 0.70

SOURCE:0:恐惧 += SOURCE:0:逸脱 / 10

CALL CALC, "調教ソース", ARG


@EQUIP_COM170_2
SIF TEQUIP:攝像機 == 0
	RETURN 0
SIF SELECTCOM == 170
	RETURN 0

PRINTFORML ＜视频拍摄中{TEQUIP:攝像機}/10＞
EXP:露出経験 += 1
EXP:撮影経験 += 1

RETURN 1

;ターン終了時の色々な格子
@EQUIP_COM170_EVENTCOMEND
;ここでは主にジャンルの格子をする
;撮影開始時
IF TEQUIP:攝像機 == 1
	IF CHECK_SHIRT("女教師")
		STR:ビデオジャンル += "女教師/"
	ELSEIF CHECK_SHIRT("女仆装")
		STR:ビデオジャンル += "女仆/"
	ELSEIF CHECK_BOTTOM("布鲁马")
		STR:ビデオジャンル += "布鲁马/"
	ELSEIF CHECK_SHIRT("体操服")
		STR:ビデオジャンル += "体操服/"
	ELSEIF CHECK_SHIRT("看護婦")
		STR:ビデオジャンル += "护士/"
	ELSEIF CHECK_SHIRT("美式校服")
		STR:ビデオジャンル += "美式校服/"
	ELSEIF CHECK_SHIRT("水手服")
		STR:ビデオジャンル += "水手服/"
	ELSEIF CHECK_SHIRT("学生服")
		STR:ビデオジャンル += "学生服/"
	ELSEIF CHECK_SHIRT("巫女")
		STR:ビデオジャンル += "巫女/"
	ELSEIF CHECK_CLO("泳装姿")
		IF CHECK_CLO("Micro比基尼姿")
			STR:ビデオジャンル += "Micro比基尼/"
		ELSEIF CHECK_CLO("比基尼姿")
			STR:ビデオジャンル += "比基尼/"
		ELSEIF CHECK_BODYS("競泳泳装")
			STR:ビデオジャンル += "競泳泳装/"
		ELSEIF CHECK_BODYS("死库水")
			STR:ビデオジャンル += "死库水/"
		ELSE
			STR:ビデオジャンル += "泳装/"
		ENDIF
	ELSEIF CHECK_CLO("拘束衣")
		STR:ビデオジャンル += "拘束衣/"
	ELSEIF CHECK_BODYS("兔")
		STR:ビデオジャンル += "兔/"
	ELSEIF CHECK_SHIRT("哥特蘿莉礼服")
		STR:ビデオジャンル += "ゴス萝莉/"
	ELSEIF CHECK_SHIRT("啦啦队制服")
		STR:ビデオジャンル += "チアガール/"
	ELSEIF CHECK_SHIRT("旗袍")
		STR:ビデオジャンル += "旗袍/"
	ENDIF

	SIF TALENT:男性 && TALENT:PLAYER:男性
		STR:ビデオジャンル += "ＢＬ/"
	SIF TALENT:女性 && TALENT:PLAYER:女性
		STR:ビデオジャンル += "レズ/"
	SIF TALENT:扶她 || TALENT:PLAYER:扶她
		STR:ビデオジャンル += "扶她/"
	SIF TEQUIP:野外PLAY
		STR:ビデオジャンル += "野外/"

	IF TALENT:巨乳 >= 3
		STR:ビデオジャンル += "超乳/"
	ELSEIF TALENT:巨乳 >= 2
		STR:ビデオジャンル += "爆乳/"
	ELSEIF TALENT:巨乳
		STR:ビデオジャンル += "巨乳/"
	ELSEIF TALENT:貧乳
		STR:ビデオジャンル += "貧乳/"
	ENDIF

	SIF TALENT:巨尻
		STR:ビデオジャンル += "巨尻/"

	SELECTCASE COND("陰毛")
	CASE 1 TO 3
		STR:ビデオジャンル += "白虎/"
	CASE IS >= 7
		STR:ビデオジャンル += "剛毛/"
	ENDSELECT

	SIF TALENT:魅力
		STR:ビデオジャンル += "美少女/"
ENDIF
SIF SELECTCOM == 170
	RETURN 0

;このターンのPLAYを記録
SIF SELECTCOM >= 0
	STR:ビデオ内容 += @"%TRAINNAME:SELECTCOM%\/"

SIF NOWEX:処女喪失
	STR:ビデオジャンル += "処女喪失/"
SIF NOWEX:噴乳 || COND("喝母乳フラグ", PLAYER)
	STR:ビデオジャンル += "母乳/"
SIF NOWEX:絶頂漏尿
	STR:ビデオジャンル += "放尿/"
SIF NOWEX:排泄
	STR:ビデオジャンル += "スカ/"
;SIF CHECK_ECST(TARGET) >= 4
;	STR:ビデオジャンル += "多重絶頂/"
SIF COND("清洁口交フラグ")
	STR:ビデオジャンル += "お掃除/"
SIF COND("Ａ清洁口交フラグ")
	STR:ビデオジャンル += "ＡＴＭ/"
SIF TEQUIP:眼罩 + TEQUIP:拘束 + TEQUIP:口枷
	STR:ビデオジャンル += "拘束/"
SIF COND("童貞喪失フラグ")
	STR:ビデオジャンル += "童貞喪失/"
SIF COND("童貞喪失フラグ", PLAYER)
	STR:ビデオジャンル += "童貞狩猎/"

;指令依存
SELECTCASE SELECTCOM
;使用前と使用後の比較
CASE TNUM("自慰")
	STR:ビデオジャンル += "自慰/"
CASE TNUM("尿道指挿入"), TNUM("尿道导管"), TNUM("尿道探针"), TNUM("尿道振動棒")
	STR:ビデオジャンル += "尿道/"
CASE TNUM("扒開阴道"), TNUM("扒開菊穴")
	STR:ビデオジャンル += "局部アップ/"
CASE TNUM("振動棒")
	SIF SIZE("振動棒") >= 3
		STR:ビデオジャンル += "拡張/"
CASE TNUM("肛用振動棒")
	STR:ビデオジャンル += "肛門/"
	SIF SIZE("Ａ振動棒") >= 3
		STR:ビデオジャンル += "拡張/"
CASE TNUM("正常位") TO TNUM("子宮挿入")
	STR:ビデオジャンル += "性交/"
CASE TNUM("肛門正常位") TO TNUM("肛門騎乗位")
	STR:ビデオジャンル += "肛門性交/"
CASE TNUM("尿道正常位") TO TNUM("尿道騎乗位")
	STR:ビデオジャンル += "尿道性交/"
CASE TNUM("強制口交")
	STR:ビデオジャンル += "強制口交/"
CASE TNUM("拳交")
	STR:ビデオジャンル += "拡張/拳交/"
CASE TNUM("肛門拳交") TO TNUM("両穴拳交")
	STR:ビデオジャンル += "拡張/拳交/肛門/"
ENDSELECT

SIF COND("飲尿フラグ", PLAYER)
	STR:ビデオジャンル += "聖水/"
SIF COND("飲尿フラグ", TARGET)
	STR:ビデオジャンル += "飲尿/"

IF TEQUIP:攝像機 >= 10
	TEQUIP:攝像機 = 0
	PRINTL 
	PRINTL 拍这么多就足够了吧。
	PRINTW ＜视频拍摄終了＞
	RETURN 1
ELSEIF BASE:体力 < 100
	TEQUIP:攝像機 = 0
	PRINTFORML 
	PRINTFORML 从%CALLNAME:TARGET%那边漂来了疲惫的气氛…
	PRINTFORMW 似乎停止撮影会比较好。
	PRINTW ＜视频拍摄終了＞
	RETURN 1
ENDIF

TEQUIP:攝像機 += 1

RETURN 1
