;-------------------------------------------------
;挿入共通のFLAG, SOURCE, 地の文をここにまとめる
;PREVPLAY("騎乗位")は前のターンに騎乗位を選択した場合にON
;INSERT("騎乗位")は前のターンから騎乗位の体勢だとON。前のターンに騎乗位状態での洗面奶などでもONとなるのに注意
;TRAIN_GENRE("騎乗位", SELECTCOM)は現在選んでいる指令が騎乗位ならばON
;-------------------------------------------------
@TRAIN_MESSAGE_INSERTION
;地の文の分岐で使う
#DIMS ACT_TABLE
;穴を変える場合などに使う
#DIMS PREVHOLE
#DIMS SELECTHOLE
#DIMS USEHOLE

;前のターンに使った穴
IF INSERT("Ａ挿入")
	PREVHOLE = %TEXTR("屁穴/肛門/後边的穴")%
ELSEIF INSERT("尿道挿入")
	PREVHOLE = %TEXTR("尿道/尿尿的洞")%
ELSE
	PREVHOLE = %TEXTR("膣/膣穴/前穴")%
ENDIF
;現在使っている穴
IF INSERT("Ａ挿入")
	USEHOLE = %TEXTR("屁穴")%
ELSEIF INSERT("尿道挿入")
	USEHOLE = \@ PENIS(TARGET) ? 阴茎 # 尿道 \@
ELSE
	USEHOLE = %TEXTR("膣内")%
ENDIF
;これから使う穴
IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
	SELECTHOLE = %TEXTS("肛門")%肛門
ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
	SELECTHOLE = %TEXTS("尿道")%尿道
ELSE
	SELECTHOLE = %TEXTS("Ｖ挿入前")%
ENDIF

IF CHECK_COM("两根挿入Ｖ中")
	IF INSERT("两根挿入Ｖ中")
		;ここから新規に書き起こした
		IF CHECK_COM("立位")
			;両方巨根以上である場合のみ
			IF SIZE("阴茎", PLAYER) >= 2 && SIZE("阴茎", PLAYER:1) >= 2 && RAND:3 == 0
				PRINTFORM %TEXTR("保持站立姿勢的/立位的/站立着前後包夹的")%%CALLNAME:PLAYER%和%CALLNAME:(PLAYER:1)%
				PRINTFORM %TEXTR("二人的/的过分粗大的")%%TEXTR("肉棒/阴茎")%%TEXTR("串刺了/贯穿了")%%CALLNAME:TARGET%的%TEXTR("膣/膣口/阴部")%、将其%TEXTR("大幅度地広開了/拡張到了极限")%…
			ELSEIF RAND:2 == 0
				PRINTFORM %TEXTR("保持站立姿勢的/立位的/站立着前後包夹的")%
				PRINTFORMW %CALLNAME:TARGET%的%TEXTR("被広開的/被撑開的/被拡張的")%%TEXTR("膣/膣穴/阴部")%被持续地同時侵犯着。
			ELSE
				IF RAND:3 == 0
					PRINTFORM 悬在空中的
				ELSEIF RAND:2 == 0
					PRINTFORM 被%CALLNAME:(PLAYER:1)%紧紧抱住的
				ELSE
					PRINTFORM 抱住%CALLNAME:(PLAYER:1)%的
				ENDIF
				PRINTFORM %CALLNAME:TARGET%的%TEXTR("膣穴/阴部")%被
				IF RAND:2 == 0
					PRINTFORMW 持续地同時侵犯着。
				ELSE
					IF PENIS(PLAYER) && PENIS(PLAYER:1)
						PRINTFORMW 用两根阴茎
					ELSE
						PRINTFORMW 用阴茎和双頭震动棒
					ENDIF
					PRINTFORMW 持续地侵犯着。
				ENDIF
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:TARGET%的膣穴被持续地同時侵犯着。
		ENDIF
	ELSE
		IF INSERT("背面座位")
			IF CHECK_COM("立位")
				PRINTFORM 抱起了正在%TEXTR("用背面立位/保持站立姿勢")%%TEXTR("＿/両腿大大張開/从背後")%%TEXTR(@"被%CALLNAME:(PLAYER:1)%侵犯着/和%CALLNAME:(PLAYER:1)%连在一起")%的%CALLNAME:TARGET%的%TEXTR("＿/両")%腿、
			ELSE
				PRINTFORM 推開被%CALLNAME:(PLAYER:1)%以結合處一覽無餘的姿勢侵犯著的%CALLNAME:TARGET%的大腿、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM 讓在%CALLNAME:(PLAYER:1)%的身上跨坐著不斷揮舞腰肢的%CALLNAME:TARGET%將身體前傾、
		ELSE
			IF CHECK_COM("立位")
				PRINTFORM 让%CALLNAME:(PLAYER:1)%采取背面立位的体勢、推開正在被侵犯的%CALLNAME:TARGET%的大腿、
			ELSE
				PRINTFORM 讓%CALLNAME:(PLAYER:1)%轉而使用背面座位的姿勢、將被激烈地不斷侵犯著的%CALLNAME:TARGET%的大腿推開、
			ENDIF
		ENDIF
		PRINTFORMW 粗暴地將%NAMES("阴茎", PLAYER)%強行塞入了尚有餘裕的膣穴……
	ENDIF
ELSEIF CHECK_COM("两根挿入Ａ中")
	IF INSERT("两根挿入Ａ中")
		PRINTFORMW 持续同时侵犯着%CALLNAME:TARGET%的肛門…
	ELSE
		IF INSERT("背面座位")
			PRINTFORM 推開被%CALLNAME:(PLAYER:1)%以結合處一覽無餘的姿勢侵犯著的%CALLNAME:TARGET%的大腿、
		ELSEIF INSERT("騎乗位")
			PRINTFORM 讓在%CALLNAME:(PLAYER:1)%的身上跨坐著不斷揮舞腰肢的%CALLNAME:TARGET%將身體前傾、
		ELSE
			PRINTFORM 讓%CALLNAME:(PLAYER:1)%轉而使用背面座位的姿勢、將被激烈地不斷侵犯著的%CALLNAME:TARGET%的大腿推開、
		ENDIF
		PRINTFORMW 粗暴地將%NAMES("阴茎", PLAYER)%強行塞入了尚有餘裕的肛門……
	ENDIF
ELSEIF CHECK_COM("二穴挿入")
	IF INSERT("二穴挿入")
		PRINTFORM %CALLNAME:PLAYER%和%CALLNAME:(PLAYER:1)%
		IF RAND:4 == 0
			PRINTFORM \@ RAND:2 ? 激烈地 # 慢吞吞地 \@動着腰
		ELSEIF RAND:3 == 0
			PRINTFORM 以相互%TEXTR("配合的/交错的")%动作
		ELSE
			PRINTFORM %TEXTR("隔著內部肉襞相互摩擦著的同时/大幅度地")%
		ENDIF
		PRINTFORMW %TEXTR("侵犯著/翻攪著/頂刺著")%た%CALLNAME:TARGET%的%TEXTR("兩個/前後的")%淫穴…
	ELSEIF SELECTCOM >= 60 && SELECTCOM <= 69
		PRINTFORM %CALLNAME:PLAYER%是%CALLNAME:(PLAYER:1)%
		IF INSERT("後背位") || INSERT("背面座位")
			IF CHECK_COM("立位")
				PRINTFORM 正在用%TEXTR("站立倚靠着的体勢/站立姿势")%被侵犯着%TEXTR("肛門/尻穴")%的%CALLNAME:TARGET%的膣穴用%NAMES("阴茎", PLAYER)%
			ELSEIF INSERT("後背位")
				IF RAND:2 == 0
					PRINTFORM 正在%TEXTR("从背後/从後方/用野獣般姿态/从后背")%侵犯着的%CALLNAME:TARGET%的%TEXTR("身体/上身")%%TEXTR("拉起/拖起")%、用%NAMES("阴茎", PLAYER)%将膣穴
				ELSE
					PRINTFORM %TEXTR("靠在/托着")%%CALLNAME:TARGET%的%TEXTR("身体/上半身")%、将%TEXTR("暴露在外的/露出来的")%膣穴用%NAMES("阴茎", PLAYER)%
				ENDIF
			ELSEIF INSERT("背面座位")
				PRINTFORM 用相互倚靠的姿勢侵犯著的%CALLNAME:TARGET%的雙腿用身體打開、用%NAMES("阴茎", PLAYER)%将膣穴
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM 的身上跨坐着揮舞腰肢的%CALLNAME:TARGET%推倒、用%NAMES("阴茎", PLAYER)%將毫無防備的陰部
		ELSE
			PRINTFORM 的姿勢改變、用%NAMES("阴茎", PLAYER)%将後穴已被填满的%CALLNAME:TARGET%的陰部
		ENDIF
		IF RAND:2 && ABL:Ｖ拡張 > SIZE("阴茎", PLAYER)
			PRINTW 直到根部地挿入了…
		ELSE
			PRINTW 深深挿入了…
		ENDIF
	ELSEIF SELECTCOM >= 70 && SELECTCOM <= 79
		PRINTFORM %CALLNAME:PLAYER%是%CALLNAME:(PLAYER:1)%
		IF INSERT("対面座位")
			IF CHECK_COM("立位")
				IF TALENT:娇小 
					IF RAND:2 == 0
						PRINTFORM 用火车便当位
					ELSE
						PRINTFORM 用%TEXTR("紧贴/抱住/托住身体/抱起両腿")%的姿势
					ENDIF
				ELSE
					PRINTFORM 用%TEXTR("紧贴/抱住/托住身体/抱起单腿")%的姿势
				ENDIF
				PRINTFORM 侵犯着的%CALLNAME:TARGET%的肛門用%NAMES("阴茎", PLAYER)%压住、
			ELSE
				PRINTFORM 以面對面相互擁抱的姿勢侵犯着的%CALLNAME:TARGET%的肛門用%NAMES("阴茎", PLAYER)%下壓、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM 的身上跨坐着揮舞著腰肢的%CALLNAME:TARGET%轉化為前傾的姿勢、對準被推開的臀瓣中暴露的肛門用%NAMES("阴茎", PLAYER)%
		ELSE
			PRINTFORM 的姿勢改变、将前穴已经被塞满的%CALLNAME:TARGET%的肛門用%NAMES("阴茎", PLAYER)%
		ENDIF
		IF RAND:2 && ABL:Ａ拡張 > SIZE("阴茎", PLAYER)
			PRINTW 直到根部地挿入了…
		ELSE
			PRINTW 深深挿入了…
		ENDIF
	ENDIF
;同時推倒(騎乗位のみ)
ELSEIF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ推倒中")%、
	ELSE
		SELECTCASE PREVCOM:1
		CASE TNUM("騎乗位"), TNUM("挿入刺激Ｇ点"), TNUM("挿入子宮口玩弄")
			PRINTFORM 从%CALLNAME:(TARGET:1)%%TEXTR("下面的嘴中刚刚解放的/的膣內拔出的")%
		CASE TNUM("肛門騎乗位")
			PRINTFORM 从%CALLNAME:(TARGET:1)%的肛門中刚刚解放的
		CASE TNUM("尿道騎乗位")
			PRINTFORM 从%CALLNAME:(TARGET:1)%的尿道中刚刚解放的
		ENDSELECT
	ENDIF
	PRINTFORM %CALLNAME:MASTER%的阴茎
	;同じ穴で挿入継続
	IF CHECK_COM("抜かずに続行")
		PRINTFORMW %TEXTR(@"、继续艶丽地摇着腰品味着/被%PREVHOLE%緊緊含住、激烈地擺動著腰肢享受快樂")%…
	;異なる穴で挿入継続
	ELSEIF CHECK_COM("穴を変えて続行")
		PRINTFORMW 从%PREVHOLE%拔出后、又再用%SELECTHOLE%%TEXTR("將其深深吞入/含入/開始尝试")%…
	;挿入開始
	ELSE
		PRINTFORMW 半蹲在上空、%TEXTR(@"對準%SELECTHOLE%慢慢降下/將腰肢慢慢降下")%…
	ENDIF
	;使う場所を露出させる
	IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
		SIF CHECK_CLO("Ａ露出") == 0
			CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
		SIF CHECK_CLO("Ｖ露出") == 0
			CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	ELSE
		SIF CHECK_CLO("Ｖ露出") == 0
			CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	ENDIF

ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P

;狂野摇摆
ELSEIF INSERT("狂野摇摆")
	CALL TEXT, "狂野摇摆"

ELSEIF CHECK_COM("結腸責め")
	IF TRAIN_GENRE("騎乗位", SELECTCOM) && TEQUIP:推倒
		PRINTFORM %CALLNAME:TARGET%没有放開%CALLNAME:PLAYER%、将勃起的阴茎
		PRINTFORMW %TEXTR("用Ｓ狀結腸从先端到根部都同樣周到地刺激著/大幅度来回抽动/貫穿直到結腸位置所帶來的異常快樂盡情的享受著")%的同時、再次開始搖晃腰肢…
	ELSEIF PREVPLAY("結腸責め")
		PRINTFORMW %CALLNAME:PLAYER%将%CALLNAME:TARGET%的尻肉紧贴腰肢、继續%TEXTR("仔細地/精心地/執拗地")%侵犯著Ｓ状結腸…
	ELSE
		PRINTFORMW 猛力抓緊%CALLNAME:TARGET%的尻肉、搖擺著腰肢用%TEXTR("比直腸更深的陰莖/Ｓ狀結腸的入口用陰莖/粗大的阴茎直达肛門的根部激烈地突刺")%侵犯着…
	ENDIF

;騎乗位は攻め手が逆転しているので別枠。推倒の場合はPENIS(PLAYER)は常にON
ELSEIF TRAIN_GENRE("騎乗位", SELECTCOM)
	IF CHECK_COM("抜かずに続行") && CHECK_COM("同じ体位で続行")
		IF TEQUIP:推倒
			IF TEQUIP:接吻
				PRINTFORM %CALLNAME:TARGET%没有放開%CALLNAME:PLAYER%、%TEXTS("接吻継続")%
				CALL SETFLAG, "追加接吻フラグ", TARGET
			ELSE
				PRINTFORM %CALLNAME:TARGET%没有放開%CALLNAME:PLAYER%、%TEXTR(@"緊緊含住阴茎/用%PREVHOLE%緊緊含住/繊細的粘膜无隙間地紧贴着")%再次開始
			ENDIF
		ELSE
			IF PENIS(PLAYER)
				IF ABL:露出癖 >= 3 && RAND:2
					PRINTFORM %CALLNAME:TARGET%将腰肢稍稍提起
					IF INSERT("Ａ挿入")
						PRINTFORM 展示不断出入于%TEXTS("肛門：視覚")%沾满粘液的肉棒之后再度插入了直腸内
					ELSEIF INSERT("尿道挿入")
						PRINTFORM 展示着被尿液濡湿而变得有光泽的肉棒之后再度插入了尿道中
					ELSE
						PRINTFORM 展示着被愛液濡湿而变得有光泽的肉棒之后再度插入了膣穴中
					ENDIF
					PRINTFORM 収めると、
				ELSE
					PRINTFORM %CALLNAME:TARGET%将插进来的陰莖緊緊包裹著、
				ENDIF
			ELSE
				PRINTFORM %CALLNAME:TARGET%将插进来的假陰莖緊緊包裹著、
			ENDIF
			IF TEQUIP:接吻
				PRINTFORM %CALLNAME:PLAYER%%TEXTS("接吻継続助詞付き")%
				CALL SETFLAG, "追加接吻フラグ", TARGET
			ELSEIF INSERT("Ａ挿入")
				PRINTFORM %TEXTR(@"激烈地/摩擦着%CALLNAME:PLAYER%的股間的同时")%
			ELSEIF ABL:Ｖ感覚 >= 3 && RAND:3 == 0
				PRINTFORM 露出恍惚的表情撫摸著下腹部
			ELSE
				PRINTFORM %TEXTR("激烈地/摩擦着恥骨的同時")%
			ENDIF
		ENDIF
		ACT_TABLE = 開始擺動起腰肢
		PRINTFORMW %TEXTR(ACT_TABLE)%。
	;これに限り你が主導
	ELSEIF CHECK_COM("抜かずに続行")
		PRINTFORMW 抱起%CALLNAME:TARGET%、将%NAMES("阴茎", PLAYER)%顶入%SELECTHOLE%。

	ELSEIF CHECK_COM("穴を変えて続行")
		PRINTFORM %CALLNAME:TARGET%缓慢地抬起腰肢、将%NAMES("阴茎", PLAYER)%从%PREVHOLE%中缓缓\@ PENIS(PLAYER) ? 释放 # 拔出 \@、
		IF INSERT("Ａ挿入")
			PRINTFORM 再次将塗滿腸液的阴茎用%SELECTHOLE%
		ELSEIF INSERT("尿道挿入")
			PRINTFORM 再次将还湿润的阴茎用%SELECTHOLE%
		ELSE
			PRINTFORM 再次将塗滿愛液的阴茎用%SELECTHOLE%
		ENDIF
		IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
			IF ABL:Ａ拡張 > SIZE("阴茎", PLAYER) && RAND:3 == 0
				IF SIZE("阴茎", PLAYER) >= 3 && EXP:結腸経験
					PRINTFORM 驅使著結腸不留餘地地
					CALL SETFLAG, "結腸責め", TARGET
				ELSE
					PRINTFORM 直達根部地。
				ENDIF
			ENDIF
		ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
			IF RAND:2 && ABL:尿道拡張 >= SIZE("阴茎", PLAYER)
				PRINTFORM 直到根部
			ELSEIF ABL:尿道拡張 >= 1
				PRINTFORM 深深地
			ENDIF
		ELSE
			IF TALENT:妊娠 == 0
				IF RAND:2 && ABL:Ｖ拡張 > SIZE("阴茎", PLAYER)
					PRINTFORM 直到根部
				ELSEIF ABL:Ｖ拡張 >= 2
					PRINTFORM 深深地
				ENDIF
			ENDIF
		ENDIF
		ACT_TABLE = 吞入/含入
		PRINTFORMW %TEXTR(ACT_TABLE)%。
	ENDIF

;Ａ挿入連続実行
ELSEIF INSERT("Ａ挿入") && CHECK_COM("連続実行")
	PRINTFORM %CALLNAME:TARGET%的
	IF TRAIN_GENRE("背面座位", SELECTCOM) && TALENT:女性
		IF CHECK_COM("立位")
			PRINTFORM 腿被抓住用被抱起来的姿势
			IF TEQUIP:攝像機 && RAND:3
				PRINTFORM 胯股之间對準攝像機大大张開的同时、
			ELSEIF CHECK("助手参加可能") && RAND:2
				PRINTFORM 胯股之间朝向%CALLNAME:ASSI%大大张開的同时、
			ELSE
				PRINTFORM 胯股之间大大张開的同时、
			ENDIF
		ELSEIF TEQUIP:攝像機 && RAND:2
			PRINTFORM 陰部對準攝像機的方向用手指撥開的同時、
		ELSE
			PRINTFORM 胸部和陰部被愛撫着的同時、
		ENDIF
	ELSE
		PRINTFORM 腰肢被緊緊地禁錮住、
	ENDIF
	SIF (PENIS(PLAYER)) && STATE("ハードスキン", PLAYER)
		PRINTFORM 用坚硬的陰莖
	IF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
		PRINTFORMW 激烈的頂刺著肛門…
	ELSE
		PRINTFORMW 隨著啪嗒啪嗒的悶響激烈地侵犯著肛門…
	ENDIF
;尿道挿入連続実行
ELSEIF INSERT("尿道挿入") && CHECK_COM("連続実行")
	PRINTFORM %CALLNAME:PLAYER%将
	IF TALENT:尿道性感 == 0 && SIZE("阴茎の直径", PLAYER) >  MIN_STR(CONDS("尿道拡張度") )
		PRINTFORM 抓住渾身顫抖著的%CALLNAME:TARGET%的腰部并拉近、
	ELSEIF CHECK("尿道挿入") && RAND:2
		PRINTFORM 與帶著充滿餘裕的表情揮舞腰肢的%CALLNAME:TARGET%相互擁抱、
	ELSE
		IF PENIS(TARGET)
			PRINTFORM 緊緊握住%CALLNAME:TARGET%的\@ SIZE("阴茎", TARGET) >= 3 ? 兇惡的巨型 # \@陰莖、
			USEHOLE = ソコ
		ELSE
			PRINTFORM 紧紧抓住%CALLNAME:TARGET%的腰、
		ENDIF
	ENDIF
	;尿道内射精直後
	IF PREVEX("尿道内射精")
		IF TALENT:尿道性感 && RAND:2
			PRINTFORM 為了將更多的精液榨取而緊緊糾纏著的%USEHOLE%
			SIF PENIS(TARGET) == 0
				PRINTFORM 用\@ SIZE("阴茎", PLAYER) >= 2 ? 粗壯 # \@陰莖
			PRINTFORMW 持續掘削著…
		ELSE
			IF USEHOLE == "尿道"
				PRINTFORMW 執拗地持續玩弄著被充分灌注了精液的排泄器官…
			ELSE
				PRINTFORMW 執拗地持續玩弄著被充分灌注了精液的%USEHOLE%…
			ENDIF
		ENDIF
	ELSE
		IF TRAIN_GENRE("対面座位", SELECTCOM) && TALENT:恋慕 && CHECK_COM("接吻")
			PRINTFORM 舌頭互相交纏的同时
		ELSEIF (ABL:PLAYER:施虐属性 >= 3 || TALENT:尿道性感) && RAND:3 == 0
			PRINTFORM 就像是在正常性交一樣
		ENDIF
		IF (ABL:PLAYER:施虐属性 >= 3 || TALENT:尿道性感) && RAND:3 == 0
			PRINTFORMW 粗暴地持續侵犯著%USEHOLE%…
		ELSEIF PENIS(TARGET) == 0 && ABL:PLAYER:施虐属性 >= 3 || (TALENT:尿道性感 && RAND:2 == 0)
			PRINTFORMW 用陰莖激烈地不斷衝刺著…
		ELSE
			PRINTFORMW 不斷地揮舞著腰肢享受著被\@ PENIS(TARGET) ? 海綿体 # 尿道 \@緊緊糾纏著的快感…
		ENDIF
	ENDIF

;同じ体位で続行。連続実行みたいな扱い
ELSEIF CHECK_COM("抜かずに続行") && CHECK_COM("同じ体位で続行")
	IF TEQUIP:接吻
		PRINTFORM %CALLNAME:PLAYER%在%CALLNAME:TARGET%%TEXTS("接吻継続助詞付き")%、
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%让%CALLNAME:TARGET%保持著双腿打開完全開放股間的羞恥的姿態、
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM) && CHECK_COM("立位") == 0
		IF TALENT:妊娠 && RAND:2 == 0
			PRINTFORM %CALLNAME:PLAYER%抚摸着%CALLNAME:TARGET%膨脹起來的腹部、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%從身後抱住%CALLNAME:TARGET%、
		ENDIF
	ELSE
		PRINTFORM %TEXTR(@"細細品味的%USEHOLE%的觸感過後/从%CALLNAME:TARGET%的%PREVHOLE%拔出後")%、
		IF TRAIN_GENRE("後背位", SELECTCOM)
			IF RAND:2
				PRINTFORM 猛力抓紧%TEXTS("お尻：感触")%
				SIF RAND:2 && TRAIN_GENRE("Ａ挿入", SELECTCOM) == 0 && ABL:PLAYER:施虐属性 && USE_A(TARGET) == 0 && CHECK_CLO("Ａ露出")
					PRINTFORM 一边看着肛門一边
			ELSE
				PRINTFORM 将%TEXTS("お尻：感触")%拉近
			ENDIF
		ENDIF
	ENDIF
	ACT_TABLE = 再次開始摆动腰肢
	SIF TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/再次開始激烈的用腰肢发起冲击"
	SIF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += @"/再び%SELECTHOLE%を犯しはじめた"
	SIF TRAIN_GENRE("対面座位", SELECTCOM)
		ACT_TABLE += "/再次開始搖晃腰肢"
	SIF TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/再次開始搖晃身体"
	SIF CHECK_COM("立位") && TALENT:PLAYER:大力士 && TRAIN_GENRE("対面座位", SELECTCOM) + TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/開始用踱步来更強烈地刺激身体最深处/一边走步一边重新開始了插拔"
	PRINTFORMW %TEXTR(ACT_TABLE)%。

;同じ穴を使用して、異なる体位で続行
ELSEIF CHECK_COM("抜かずに続行")
	IF TRAIN_GENRE("正常位", SELECTCOM)
		IF INSERT("後背位") || INSERT("屈曲位")
			IF CHECK_COM("立位")
				PRINTFORM 暂时抽出后随即与%CALLNAME:TARGET%正面相对、
			ELSE
				PRINTFORM 保持緊密結合的姿势让%CALLNAME:TARGET%仰面躺下、
			ENDIF
		ELSEIF INSERT("対面座位")
			IF CHECK_COM("立位")
				PRINTFORM 将擁抱著的%CALLNAME:TARGET%放下让%CALLNAME:TARGET%自己站起来、
			ELSE
				PRINTFORM 将擁抱著的%CALLNAME:TARGET%輕輕推到成為仰躺著的姿勢、
			ENDIF
		ELSEIF INSERT("背面座位")
			PRINTFORM 保持插入%USEHOLE%的状态将%CALLNAME:TARGET%变為了仰面躺下的姿態、
		ELSEIF INSERT("騎乗位")
			PRINTFORM 像是位置交換一樣将%CALLNAME:TARGET%仰面推倒、
		ENDIF
	ELSEIF TRAIN_GENRE("後背位", SELECTCOM)
		IF INSERT("正常位") || INSERT("屈曲位")
			IF CHECK_COM("立位")
				PRINTFORM 暂时抽出后随即让%CALLNAME:TARGET%背对着、伸出腰肢
			ELSE
				PRINTFORM 保持緊密結合的姿势让%CALLNAME:TARGET%四肢著地、趴跪著抬起腰肢、
			ENDIF
		ELSEIF INSERT("対面座位")
			PRINTFORM 保持插入%USEHOLE%的状态将%CALLNAME:TARGET%的身體向后翻轉、
		ELSEIF INSERT("背面座位")
			IF CHECK_COM("立位")
				PRINTFORM 将擁抱著的%CALLNAME:TARGET%放下让%CALLNAME:TARGET%自己站起来、
			ELSE
				PRINTFORM 将%CALLNAME:TARGET%的身体向前推倒四肢著地、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM 保持插入%USEHOLE%的状态将%CALLNAME:TARGET%的身體向后翻轉、
		ENDIF
		IF RAND:2
			PRINTFORM 猛力抓紧%TEXTS("お尻：感触")%
			SIF RAND:2 && ABL:PLAYER:施虐属性 && USE_A(TARGET) == 0 && CHECK_CLO("Ａ露出")
				PRINTFORM 一边看着肛門一边
		ELSE
			PRINTFORM 将%TEXTS("お尻：感触")%拉近
		ENDIF
	ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
		IF RAND:2
			PRINTFORM 抓住%CALLNAME:TARGET%的両脚向前推至腰部浮空、小腿放在肩上、
		ELSEIF PENIS(TARGET)
			PRINTFORM 将%CALLNAME:TARGET%翻轉過來、仔細觀賞過阴茎後
		ELSE
			PRINTFORM 将%CALLNAME:TARGET%翻轉過來、仔細觀賞過陰部後
		ENDIF
	ELSEIF TRAIN_GENRE("対面座位", SELECTCOM)
		IF INSERT("正常位") || INSERT("屈曲位")
			IF CHECK_COM("立位")
				PRINTFORM 让%CALLNAME:TARGET%抱住自己的同时从下往上抱起了%CALLNAME:TARGET%的尻部、
			ELSE
				PRINTFORM 抬起%CALLNAME:TARGET%的身體抱在懷裡、
			ENDIF
		ELSEIF INSERT("後背位")
			PRINTFORM 将%CALLNAME:TARGET%仰面向天、抱起身體
		ELSEIF INSERT("背面座位")
			PRINTFORM 保持緊密結合的姿势让%CALLNAME:TARGET%转过身体、相互擁抱著
		ELSEIF INSERT("騎乗位")
			PRINTFORM 紧紧抱住撑起身体的%CALLNAME:TARGET%、
		ENDIF
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM)
		IF CHECK_COM("立位")
			PRINTFORM 保持插入%USEHOLE%的状态让%CALLNAME:TARGET%倚靠在自己身上、用手臂将大腿从下方抱住站了起来
		ELSE
			PRINTFORM 保持插入%USEHOLE%的状态将%CALLNAME:TARGET%的身體從背後緊緊地抱在懷裡、
		ENDIF
	ENDIF
	ACT_TABLE = 再次開始摆动腰肢
	SIF TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/開始激烈的用腰肢发起冲击"
	SIF TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/侵犯起来"
	SIF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += @"/继续侵犯%SELECTHOLE%"
	SIF TRAIN_GENRE("対面座位", SELECTCOM)
		ACT_TABLE += "/開始搖晃腰肢"
	SIF TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/開始搖晃身体"
	PRINTFORMW %TEXTR(ACT_TABLE)%。

;使う穴を変えて挿入続行
ELSEIF CHECK_COM("穴を変えて続行")
	;同じ体位のまま
	IF CHECK_COM("同じ体位で続行")
		IF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
			PRINTFORM %CALLNAME:PLAYER%将%NAMES("阴茎", PLAYER)%从%CALLNAME:TARGET%的%PREVHOLE%%TEXTR("拔出/取出")%、然后将
		ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
			IF RAND:2
				PRINTFORM %CALLNAME:PLAYER%将%NAMES("阴茎", PLAYER)%从%PREVHOLE%拔出、保持着大腿张開的羞恥体勢将
			ELSE
				PRINTFORM 将%NAMES("阴茎", PLAYER)%从大腿張開高舉腰肢姿態的%CALLNAME:TARGET%的%PREVHOLE%拔出、然后将
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:PLAYER%将%NAMES("阴茎", PLAYER)%从%PREVHOLE%拔出、氣勢十足地将%CALLNAME:TARGET%的
		ENDIF
	;体位も変える
	ELSE
		IF TRAIN_GENRE("正常位", SELECTCOM)
			IF INSERT("屈曲位")
				PRINTFORM 保持着大腿张開的羞恥体勢、然后将%CALLNAME:TARGET%的
			ELSEIF INSERT("対面座位")
				PRINTFORM %NAMES("阴茎", PLAYER)%从%PREVHOLE%拔出、
				IF CHECK_COM("立位")
					PRINTFORM 放下%CALLNAME:TARGET%变成了対面立位的体勢的同时
				ELSE
					PRINTFORM %TEXTR(@"抱着%CALLNAME:TARGET%的身体、然后将/氣勢十足地将%CALLNAME:TARGET%的")%
				ENDIF
			ELSE
				PRINTFORM 让%CALLNAME:TARGET%仰面躺下、用%TEXTR(@"侵犯了%PREVHOLE%/从%PREVHOLE%拔出")%的%NAMES("阴茎", PLAYER)%
			ENDIF
		ELSEIF TRAIN_GENRE("後背位", SELECTCOM)
			IF INSERT("背面座位")
				IF CHECK_COM("立位")
					PRINTFORM 将因用背面火车便当位侵犯着而羞恥的%CALLNAME:TARGET%的身体放下变成了站立后背位体勢、然后将
				ELSE
					PRINTFORM 将%CALLNAME:TARGET%的身体向前推倒四肢著地、然后将
				ENDIF
			ELSE
				PRINTFORM 让%CALLNAME:TARGET%四肢著地趴跪著高舉腰肢、
				IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
					PRINTFORM %TEXTR("将一览无余的/猛力抓紧尻肉的同时")%
				ELSE
					PRINTFORM %TEXTR(@"抓住%TEXTS("お尻：感触")%拉近并将/猛力抓紧%TEXTS("お尻：感触")%的同时")%
				ENDIF
			ENDIF
		ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
			PRINTFORM 抓住%CALLNAME:TARGET%的双脚將身體壓倒、用从%PREVHOLE%%TEXTR("拔出/拖出/取出")%的%NAMES("阴茎", PLAYER)%将
		ELSEIF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
			IF CHECK_COM("立位")
				PRINTFORM %NAMES("阴茎", PLAYER)%从%PREVHOLE%中拔出、抱起来变为火车便当位体勢之后将
			ELSE
				PRINTFORM 将%NAMES("阴茎", PLAYER)%从%PREVHOLE%拔出、变化体勢后将
			ENDIF
		ENDIF
	ENDIF
	ACT_TABLE = %SELECTHOLE%侵犯了起来
	IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
		IF TALENT:男性 == 0
			SIF TRAIN_GENRE("正常位", SELECTCOM) + TRAIN_GENRE("対面座位", SELECTCOM)
				ACT_TABLE += "陰部之下微微颤动的肛門侵犯了起来"
			SIF TRAIN_GENRE("後背位", SELECTCOM)
				ACT_TABLE += "陰部之上正在喘息的肛門侵犯了起来"
		ENDIF
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("阴茎", PLAYER) >= 2
				ACT_TABLE += @"/直腸的最深处顶撞着/%SELECTHOLE%深深地贯穿了"
		ENDIF
	ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("阴茎", PLAYER) >= 2
				ACT_TABLE += @"/膀胱的根元处顶撞着/%SELECTHOLE%深深地贯穿了"
		ENDIF
	ELSE
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("阴茎", PLAYER) >= 2
				ACT_TABLE += @"/%SELECTHOLE%的最深处顶撞着/%SELECTHOLE%深深地贯穿了"
		ENDIF
	ENDIF
	PRINTFORMW %TEXTR(ACT_TABLE)%。
;火车便当位はここに記述
ELSEIF CHECK_COM("立位")
	IF TRAIN_GENRE("対面座位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%将
		IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
			CALL ADD_TEXT, "性器から性器へ"
			PRINTFORM %CALLNAME:TARGET%从正面抱起、
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
			PRINTFORM %CALLNAME:TARGET%的
			CALL SETFLAG, "下衣を露出：テ接吻ト有些", TARGET
			PRINTFORM 、从正面抱起的同时
		ELSE
			PRINTFORM %CALLNAME:TARGET%从正面抱起的同时
		ENDIF
		PRINTFORMW 让%SELECTHOLE%将阴茎包裹並吮吸起來后、站了起来以火车便当位開始了侵犯。
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%将
		IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
			CALL ADD_TEXT, "性器から性器へ"
			PRINTFORM %CALLNAME:TARGET%的身体倚靠在自己身上的同时
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
			PRINTFORM %CALLNAME:TARGET%的
			CALL SETFLAG, "下衣を露出：テ接吻ト有些", TARGET
			PRINTFORM 、身体倚靠在自己身上的同时
		ELSE
			PRINTFORM %CALLNAME:TARGET%的身体倚靠在自己身上的同时
		ENDIF
		PRINTFORMW 让%SELECTHOLE%将阴茎包裹並吮吸起來后、背对着站了起来、用所谓的背面火车便当式開始了侵犯。
	ENDIF
ENDIF


@FLAG_INSERTION, ARGS, ARG, ARG:1
;体位をTCVARで記録しておく(挿入刺激Ｇ点などで体位が不明になってしまう際に用いる)
TCVAR:体位記錄 = TEQUIP:体位
TCVAR:PLAYER:体位記錄 = TEQUIP:PLAYER:体位

SELECTCASE ARGS
CASE "尿道挿入"
	CALL SETFLAG, "尿道性感フラグ", TARGET
	;尿的污垢発生
	STAIN:PLAYER:Ｐ = SET_STAIN(STAIN:PLAYER:Ｐ, "尿")
	CALL SETFLAG, "Ｐ汚れ：尿"

	IF PENIS(TARGET)
		STAIN:Ｐ = SET_STAIN(STAIN:Ｐ, "尿")
		;対象のＰ⇔調教者のＰ的污垢が移動
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｐ"
	ELSE
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
	ENDIF

	;胖次をずらす
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET

CASE "尿道振動棒"
	CALL SETFLAG, "尿道性感フラグ", TARGET
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET

CASE "Ｖ挿入", "子宮挿入", "玩弄子宮口"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET

	IF TALENT:PLAYER:童貞
		CALL SETFLAG, "童貞狩猎フラグ", TARGET
		CALL SETFLAG, "童貞喪失フラグ", PLAYER
	ENDIF

	SIF COND("腹部变形")
		CALL SETFLAG, "腹部变形フラグ", TARGET

	;対象のＶ⇔調教者のＰ的污垢が移動
	SIF TEQUIP:PLAYER:避孕套 == 0
		CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"

	CALL SETFLAG, "Ｐ汚れ：Ｖ"

	IF PLAYER == MASTER
		IF ABL:Ｖ感覚 >= 3
			TFLAG:好感度BOUNS += 2
		ELSE
			TFLAG:好感度BOUNS += 1
		ENDIF
	ENDIF

	IF PENIS(PLAYER)
		;PLAYERの阴茎のＰ潤とＶ潤の相互干渉
		IF TEQUIP:PLAYER:避孕套 == 0
			IF PALAM:PLAYER:Ｐ潤 >= PALAM:Ｖ潤
				CUP:Ｖ潤 += (PALAM:PLAYER:Ｐ潤 - PALAM:Ｖ潤)/2
			ELSE
				CUP:PLAYER:Ｐ潤 += (PALAM:Ｖ潤 - PALAM:PLAYER:Ｐ潤)/2
			ENDIF
		ENDIF
		IF PLAYER == MASTER && TALENT:処女 == 0 && CEVENT("Ｖ完全挿入") == 0
			IF CHECK_COM("完全挿入可能", INSERT("対応指令") )
				;以前同じサイズの阴茎が入りきらなかったことがある
				IF CEVENT("Ｖ完全挿入不可")
					SIF SIZE("阴茎", PLAYER) == TOINT(CEVENTS("Ｖ完全挿入不可") )
						CALL SET_CEVENT, "Ｖ完全挿入初達成"
					CALL DEL_CEVENT_GROUP, "Ｖ完全挿入不可"
				ENDIF
				CALL SET_CEVENT, "Ｖ完全挿入"
			ELSE
				CALL DEL_CEVENT_GROUP, "Ｖ完全挿入不可"
				CALL SET_CEVENT, @"Ｖ完全挿入不可：{SIZE("阴茎", PLAYER)}"
			ENDIF
		ENDIF
	ENDIF
CASE "指挿入"
	;対象のＶ⇔調教者の指的污垢が移動
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
CASE "振動棒"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
CASE "Ｖ拳交"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET

	;対象のＶ⇔調教者の手的污垢が移動
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
CASE "Ａ挿入"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET

	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	;対象のＡ⇔調教者のＰ的污垢が移動
	SIF TEQUIP:PLAYER:避孕套 == 0
		CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "Ｐ"

	CALL SETFLAG, "Ｐ汚れ：Ａ"

	IF PLAYER == MASTER
		IF ABL:Ａ感覚 >= 3
			TFLAG:好感度BOUNS += 2
		ELSE
			TFLAG:好感度BOUNS += 1
		ENDIF
	ENDIF

	;浣腸中は排泄欲が300+５％上昇
	SIF TEQUIP:浣腸
		BASE:排泄欲 += 300 + MULTIPLY(MAXBASE:排泄欲, 5 + BASE:排泄物)*ARG:1/100

	;PLAYERの阴茎のＰ潤とＡ潤の相互干渉
	IF PENIS(PLAYER) && TEQUIP:PLAYER:避孕套 == 0
		IF PALAM:PLAYER:Ｐ潤 >= PALAM:Ａ潤
			CUP:Ａ潤 += (PALAM:PLAYER:Ｐ潤 - PALAM:Ａ潤)/2
		ELSE
			CUP:PLAYER:Ｐ潤 += (PALAM:Ａ潤 - PALAM:PLAYER:Ｐ潤)/2
		ENDIF
	ENDIF
CASE "Ａ指挿入"
	;対象のＡ⇔調教者の指的污垢が移動
	CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "手"

	;浣腸中は排泄欲が１％上昇
	SIF TEQUIP:浣腸
		BASE:排泄欲 += MULTIPLY(MAXBASE:排泄欲, 1 + BASE:排泄物)*ARG:1/100
CASE "Ａ振動棒"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	;浣腸中は排泄欲が300+５％上昇
	SIF TEQUIP:浣腸
		BASE:排泄欲 += 300 + MULTIPLY(MAXBASE:排泄欲, 5 + BASE:排泄物)*ARG:1/100
CASE "肛塞"
	;浣腸中は排泄欲が300+５％上昇
	SIF TEQUIP:浣腸
		BASE:排泄欲 += 300 + MULTIPLY(MAXBASE:排泄欲, 5 + BASE:排泄物)*ARG:1/100
CASE "Ａ拳交"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	;対象のＡ⇔調教者の手的污垢が移動
	CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "手"

	;浣腸中は排泄欲が1000+20％上昇
	SIF TEQUIP:浣腸
		BASE:排泄欲 += 1000 + MULTIPLY(MAXBASE:排泄欲, 20 + BASE:排泄物)*ARG:1/100
CASE "Ｖ逆挿入"
	IF TALENT:童貞
		CALL SETFLAG, "童貞狩猎フラグ", PLAYER
		CALL SETFLAG, "童貞喪失フラグ", TARGET
	ENDIF
	CALL SETFLAG, "下半身を脱がす", PLAYER
	SIF PENIS(TARGET)
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	;対象のＰ⇔調教者のＶ的污垢が移動
	SIF TEQUIP:避孕套 == 0
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｖ"
	CALL SETFLAG, "Ｐ汚れ：Ｖ", PLAYER

CASE "Ａ逆挿入"
	CALL SETFLAG, "下半身を脱がす", PLAYER
	SIF PENIS(TARGET)
		CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
	;対象のＰ⇔調教者のＡ的污垢が移動
	SIF TEQUIP:避孕套 == 0
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ａ"
	CALL SETFLAG, "Ｐ汚れ：Ａ", PLAYER
ENDSELECT

;処女でも挿入する指令など、特殊な分岐
SELECTCASE ARGS
CASE "尿道挿入"
	IF TALENT:尿道処女
		CALL SETFLAG, "尿道処女喪失フラグ", TARGET
		CALL SETFLAG, "尿道処女簒奪フラグ", PLAYER
	ENDIF
CASE "子宮挿入"
	CALL SETFLAG, "子宮責めフラグ", TARGET
	CALL SETFLAG, "子宮挿入フラグ", TARGET
CASE "玩弄子宮口"
	CALL SETFLAG, "子宮責めフラグ", TARGET
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "破瓜之血")
		SIF TEQUIP:PLAYER:避孕套 == 0
			CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"
	ENDIF
CASE "Ｖ挿入"
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "破瓜之血")
		SIF TEQUIP:PLAYER:避孕套 == 0
			CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"
	ENDIF
CASE "振動棒"
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "破瓜之血")
	ENDIF
CASE "Ｖ拳交"
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "破瓜之血")
		STAIN:PLAYER:手 = SET_STAIN(STAIN:PLAYER:手, "破瓜之血")
	ENDIF
CASE "Ｖ逆挿入"
	IF TALENT:PLAYER:処女
		CALL SETFLAG, "処女喪失フラグ", PLAYER
		CALL SETFLAG, "処女簒奪フラグ", TARGET
		STAIN:PLAYER:Ｖ = SET_STAIN(STAIN:PLAYER:Ｖ, "破瓜之血")
		SIF TEQUIP:避孕套 == 0
			CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｖ"
	ENDIF
CASE "Ａ挿入"
	IF TALENT:Ａ処女
		CALL SETFLAG, "Ａ処女喪失フラグ", TARGET
		CALL SETFLAG, "Ａ処女簒奪フラグ", PLAYER
	ENDIF
	IF COND("喜欢的体位中")
		TFLAG:好感度BOUNS += (3 + RAND:3) * ARG:1 / 100
		FLAG:情緒 += (3 + RAND:3) * ARG:1 / 100
	ENDIF
CASE "Ａ逆挿入"
	IF TALENT:PLAYER:Ａ処女
		CALL SETFLAG, "Ａ処女喪失フラグ", PLAYER
		CALL SETFLAG, "Ａ処女簒奪フラグ", TARGET
	ENDIF
ENDSELECT


@SOURCE_INSERTION, ARGS, ARG, ARG:1
#DIM SIZE_DEF
#DIM PALAM_RES
#DIM EXP_REQ
#DIM DAMAGE
#DIMS NAME_INSERT

;Ｖ挿入等の激しさのSOURCE。@SOURCE_COM60等で呼ばれる
;ARG:1は主に挿入PLAYの激しさで、疼痛と快楽が両方増える（振動棒やフィストなどは通常100のまま）
CALL VARSET_COMF
VARSET NAME_INSERT

;NAME_INSERTは挿入するもの
SELECTCASE ARGS
CASE "尿道挿入"
	NAME_INSERT = 阴茎
	CALL SOURCE_DOWNBASE_HP, 83
	SOURCE:0:不潔 = 100 + COM_ORDER_STAIN("Ｐ", "尿道")*300
	SOURCE:0:屈従 = 10000
	SOURCE:0:逸脱 = 1000 + SIZE("阴茎", PLAYER)*5000
	SOURCE:0:疼痛 = 500
	SOURCE:0:恐惧 = 500

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = CALCF("尿道刺激", 2)
	;調教者の腰靈活をみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "腰靈活", PLAYER)

CASE "尿道指挿入"
	NAME_INSERT = 指
	CALL SOURCE_DOWNBASE_HP, 15
	SOURCE:0:不潔 = 100 + COM_ORDER_STAIN("手", "尿道")*300
	SOURCE:0:露出 = 300
	SOURCE:0:屈従 = 3000
	SOURCE:0:逸脱 = 1000
	SOURCE:0:疼痛 = 500
	SOURCE:0:恐惧 = 100

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = CALCF("尿道刺激", 1)
	;調教者の腰靈活をみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "指靈活", PLAYER)

CASE "尿道探针"
	NAME_INSERT = 尿道探针
	CALL SOURCE_DOWNBASE_HP, 151
	SOURCE:0:露出 = 2000
	SOURCE:0:屈従 = 5000
	SOURCE:0:逸脱 = 2000
	SOURCE:0:恐惧 = 500

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 0), 90 + ITEM:尿道探针*10)

CASE "尿道振動棒"
	NAME_INSERT = 尿道振動棒
	CALL SOURCE_DOWNBASE_HP, 152

	SOURCE:0:露出 = 500
	SOURCE:0:屈従 = 3000
	SOURCE:0:逸脱 = 3000
	SOURCE:0:恐惧 = 1000

	SELECTCASE SIZE("尿道振動棒")
	CASE 3
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:極太振動棒*10)
	CASE 4
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:腕型振動棒*10)
	CASE 5
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:馬型振動棒*10)
	CASEELSE
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:振動棒套装*10)
	ENDSELECT
	;調教者の指靈活をみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "指靈活", PLAYER)


;子宮挿入の逸脱のソースは下で決定
CASE "子宮挿入"
	NAME_INSERT = 阴茎
	CALL SOURCE_DOWNBASE_HP, 68
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*500 + 100
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)

	;調教者の腰靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰靈活", PLAYER)
	;子宮性感がないと気持ちよくない
	IF TALENT:子宮性感
		TIMES SOURCE:0:Ｖ快楽, 1.25
	ELSE
		TIMES SOURCE:0:Ｖ快楽, 0.25
		SOURCE:0:恐惧 = 1000
	ENDIF
CASE "玩弄子宮口"
	NAME_INSERT = 阴茎
	CALL SOURCE_DOWNBASE_HP, 67
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*300 + 100
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)

	;調教者の腰靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰靈活", PLAYER)
	;子宮性感がないと気持ちよくない
	IF TALENT:子宮性感
		TIMES SOURCE:0:Ｖ快楽, 1.25
	ELSE
		TIMES SOURCE:0:Ｖ快楽, 0.25
		SOURCE:0:恐惧 = 500
	ENDIF
CASE "Ｖ挿入"
	NAME_INSERT = 阴茎
	CALL SOURCE_DOWNBASE_HP, 60
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*100 + 100
	IF PENIS(PLAYER) == 0
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:假阳具)
	ELSE
		SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)
	ENDIF

	;調教者の腰靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰靈活", PLAYER)

CASE "双頭振動棒PLAY"
	NAME_INSERT = 振動棒
	CALL SOURCE_DOWNBASE_HP, 31
	SOURCE:0:逸脱 = 1000
	SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:振動棒套装*10)

	;調教者の腰靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰靈活", PLAYER)
CASE "指挿入"
	NAME_INSERT = 指
	CALL SOURCE_DOWNBASE_HP, 8
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ｖ")*100 + 100
	SOURCE:0:露出 = 300
	SOURCE:0:屈従 = 200 + ABL:Ｖ感覚*200
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 1)
	;Gスポット
	IF CHECK_COM("玩弄Ｇ点")
		SIF ABL:Ｖ感覚 >= 3
			SOURCE:0:Ｖ快楽 += 500
		SOURCE:0:逸脱 += 500
	ENDIF
	;調教者の指靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指靈活", PLAYER)
CASE "振動棒"
	NAME_INSERT = 振動棒
	CALL SOURCE_DOWNBASE_HP, 31
	SOURCE:0:逸脱 = 500
	SELECTCASE SIZE("振動棒")
	CASE 3
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:極太振動棒*10)
	CASE 4
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:腕型振動棒*10)
	CASE 5
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:馬型振動棒*10)
	CASEELSE
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:振動棒套装*10)
	ENDSELECT
	;調教者の指靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指靈活", PLAYER)
CASE "Ｖ拳交"
	NAME_INSERT = フィスト
	CALL SOURCE_DOWNBASE_HP, 141
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ｖ")*150 + 100
	SOURCE:0:露出 = 1200
	SOURCE:0:屈従 = 2000
	SOURCE:0:逸脱 = 10000
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 3)
	;調教者の指靈活をみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指靈活", PLAYER)
CASE "Ａ挿入"
	NAME_INSERT = 阴茎
	CALL SOURCE_DOWNBASE_HP, 70

	IF COND("合意：肛門性交")
		SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ａ")*100 + 100
		SOURCE:0:屈従 = 2000
		SOURCE:0:逸脱 = 3000
	ELSE
		SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ａ")*100 + 800
		SOURCE:0:屈従 = 3000
		SOURCE:0:逸脱 = 6000
	ENDIF
	IF PENIS(PLAYER) == 0
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:假阳具)
	ELSE
		SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 2)
	ENDIF

	;調教者の腰靈活をみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "腰靈活", PLAYER)
CASE "Ａ指挿入"
	NAME_INSERT = Ａ振動棒
	CALL SOURCE_DOWNBASE_HP, 2
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ａ")*100 + 100
	SOURCE:0:露出 = 500
	SOURCE:0:屈従 = 500
	SOURCE:0:逸脱 = 1000
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 1)
	;調教者の指靈活をみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指靈活", PLAYER)
CASE "Ａ振動棒"
	NAME_INSERT = Ａ振動棒
	CALL SOURCE_DOWNBASE_HP, 33
	SELECTCASE SIZE("Ａ振動棒")
	CASE 1, 2
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:振動棒套装*10)
	CASE 3
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:極太振動棒*10)
	CASE 4
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:腕型振動棒*10)
	CASE 5
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:馬型振動棒*10)
	CASEELSE
		IF COND("肛門拉珠")
			SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:肛門拉珠*10)
		ELSE
			SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:肛用振動棒*10)
		ENDIF
	ENDSELECT

	;調教者の指靈活をみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指靈活", PLAYER)
	SOURCE:0:不潔 = 1000
	SOURCE:0:恐惧 = 100
	SOURCE:0:屈従 = 1500
	SOURCE:0:逸脱 = 3000
CASE "肛塞"
	NAME_INSERT = 肛塞
	CALL SOURCE_DOWNBASE_HP, 34
	SELECTCASE SIZE("肛塞")
	CASE 1, 2
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 0), 90 + ITEM:振動棒套装*10)
	CASE 3
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 0), 90 + ITEM:極太振動棒*10)
	CASE 4
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 0), 90 + ITEM:腕型振動棒*10)
	CASE 5
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 0), 90 + ITEM:馬型振動棒*10)
	CASEELSE
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 0), 90 + ITEM:肛用振動棒*10)
	ENDSELECT

	;基本的に快楽は低い
	TIMES SOURCE:0:Ａ快楽, 0.10
	SOURCE:0:不潔 = 100
	SOURCE:0:恐惧 = 50
	SOURCE:0:屈従 = 100
	SOURCE:0:逸脱 = 500
CASE "Ａ拳交"
	NAME_INSERT = フィスト
	CALL SOURCE_DOWNBASE_HP, 142
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ａ")*150 + 800
	SOURCE:0:露出 = 1200
	SOURCE:0:屈従 = 2000
	SOURCE:0:逸脱 = 20000
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 3)
	;調教者の指靈活をみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指靈活", PLAYER)
ENDSELECT

;SIZE_DEFは挿入しているもののSIZEから拡張度を引いた値で抵抗を表している。トントンなら０で大きいほど痛い
SELECTCASE ARGS
CASE "尿道挿入", "尿道指挿入", "尿道振動棒"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:尿道拡張
CASE "尿道探针"
	;尿道拡張は7mmに付き1LVあがる
	SELECTCASE SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20
	CASE IS < -1
		SIZE_DEF = -2
	CASE -1 TO 1
		SIZE_DEF = SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20
	CASEELSE
		SIZE_DEF = 1 + (SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20 )/2
	ENDSELECT
CASE "子宮挿入"
	SIZE_DEF = 1 + TALENT:娇小 - TALENT:淫壷
	SELECTCASE GET_EXP_LV(EXP:子宮姦経験) + TALENT:子宮性感
	CASE 3
		SIZE_DEF -= 1
	CASE IS >= 4
		SIZE_DEF -= 2
	ENDSELECT
	SIF TALENT:妊娠 || MARK:淫紋 >= 4
		SIZE_DEF -= 1
	SIF SIZE(NAME_INSERT) >= 3
		SIZE_DEF += SIZE(NAME_INSERT)/2
	SIZE_DEF = MAX(SIZE_DEF, -1)
CASE "Ｖ挿入", "玩弄子宮口"
	IF INSERT("两根挿入Ｖ中") || CHECK_COM("两根挿入Ｖ中")
		SIZE_DEF = SIZE("Ｗ阴茎") - ABL:Ｖ拡張
	ELSEIF INSERT("二穴挿入") || CHECK_COM("二穴挿入")
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張 + 1
	ELSE
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張
	ENDIF
	SIF ABL:Ｖ技術/2 > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE "指挿入", "振動棒", "Ｖ拳交", "双頭振動棒PLAY"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張
	SIF ABL:Ｖ技術/2 > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE "Ａ挿入"
	IF INSERT("两根挿入Ａ中") || CHECK_COM("两根挿入Ａ中")
		SIZE_DEF = SIZE("Ｗ阴茎") - ABL:Ａ拡張
	ELSEIF INSERT("二穴挿入") || CHECK_COM("二穴挿入")
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張 + 1
	ELSE
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張
	ENDIF
	;肛門は抵抗がある
	SIF (TALENT:Ａ性向 == 0 || ABL:Ａ感覚 <= 2) && EXP:Ａ経験 < EXP_LV(SIZE(NAME_INSERT) + 3)
		SIZE_DEF += 1
CASE "Ａ指挿入", "Ａ振動棒", "肛塞", "Ａ拳交"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張
	;肛門は抵抗がある
	SIF (TALENT:Ａ性向 == 0 || ABL:Ａ感覚 <= 2) && EXP:Ａ経験 < EXP_LV(SIZE(NAME_INSERT) + 3)
		SIZE_DEF += 1
ENDSELECT

SIF TALENT:拡張適性 && SIZE_DEF >= 0
	SIZE_DEF -= 1

SELECTCASE TEQUIP:弛緩
CASE 1
	SIF SIZE_DEF > 0
		SIZE_DEF -= 1
CASE 2
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE 3
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
ENDSELECT

;余裕有
IF SIZE_DEF < 0
	;ジャスト
	IF SIZE_DEF == -1
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:尿道快楽, 1.20
	;緩い
	ELSE
		SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, MAX(120 + SIZE_DEF*20, 50) )
		SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, MAX(120 + SIZE_DEF*20, 50) )
		SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, MAX(120 + SIZE_DEF*20, 50) )
	ENDIF
	;余裕がある場合に限り、大きく硬い方が悦ばれることにする
	SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, SIZE(NAME_INSERT)*10 + 90)
	;膣に大号的ものを挿入する場合には子宮性感の有無で少々BOUNS有
	SIF TALENT:子宮性感 && SIZE(NAME_INSERT) >= 2
		TIMES SOURCE:0:Ｖ快楽, 1.20

	SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, SIZE(NAME_INSERT)*10 + 90)

	SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, SIZE(NAME_INSERT)*10 + 90)

	;大きいものを収められると達成感
	SIF SIZE(NAME_INSERT) >= 2
		SOURCE:0:達成感 += SIZE(NAME_INSERT) * SIZE(NAME_INSERT) * 500
;余裕没有
ELSE
	DOWNBASE:0:体力 = MULTIPLY(DOWNBASE:0:体力, 100 + SIZE_DEF*20)
	SOURCE:0:Ｖ快楽 /= MAX(SIZE_DEF*2, 1)
	SOURCE:0:Ａ快楽 /= MAX(SIZE_DEF*2, 1)
	SOURCE:0:尿道快楽 /= MAX(SIZE_DEF*2, 1)
ENDIF

;PALAMLVの値は、順に1:100,2:500,3:3000,4:1万,5:3万,6:6万,7:10万,8:15万,9:25万
;HARDの基本値は阴茎なら１、振動棒や拳なら３
SOURCE:0:疼痛 += PALAM_LV(SIZE_DEF + (HARD(NAME_INSERT) + 1)/2)
SOURCE:0:恐惧 += PALAM_LV(SIZE_DEF + (HARD(NAME_INSERT) + 1)/2)
SOURCE:0:逸脱 += PALAM_LV(SIZE_DEF)

;前か後ろかで分岐
SELECTCASE ARGS
CASE "尿道挿入", "尿道指挿入", "尿道探针", "尿道振動棒"
	;PALAM_RESは入れてるものの尺寸を加味した潤滑具合
	SELECTCASE ARGS
	CASE "尿道挿入"
		;初めて
		IF TALENT:尿道処女 && TALENT:倒錯的 == 0 && ABL:受虐属性 < 3
			SOURCE:0:恐惧 += 5000
			SOURCE:0:逸脱 += 20000
		ENDIF
		PALAM_RES = 3
	CASEELSE
		;ここでの最大は３
		PALAM_RES = 3
	ENDSELECT

	IF TALENT:尿道狂
		TIMES SOURCE:0:疼痛, 0.10
		TIMES SOURCE:0:恐惧, 0.10
		TIMES SOURCE:0:逸脱, 0.05
	ELSEIF TALENT:尿道性感
		TIMES SOURCE:0:疼痛, 0.20
		TIMES SOURCE:0:恐惧, 0.20
		TIMES SOURCE:0:逸脱, 0.20
	ENDIF

	;尿道に筋弛緩剤
	SIF GETBIT(TEQUIP:筋弛緩剤, 1)
		TIMES SOURCE:0:疼痛, 0.20

	;経験は足りている？（HARDによって上限有）
	EXP_REQ = MIN(GET_EXP_LV(EXP:尿道経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )
CASE "子宮挿入"
	;淫紋持ちは充足のソースも得る
	SIF MARK:淫紋
		SOURCE:0:充足 += MARK:淫紋 * 1000

	SELECTCASE GET_EXP_LV(EXP:子宮姦経験) + TALENT:子宮性感
	CASE IS <= 1
		TIMES SOURCE:0:Ｖ快楽, 0.20
		SOURCE:0:疼痛 += 1000
		SOURCE:0:恐惧 += 3000
		SOURCE:0:逸脱 += 30000
	CASE 2
		TIMES SOURCE:0:Ｖ快楽, 0.40
		SOURCE:0:疼痛 += 500
		SOURCE:0:恐惧 += 1000
		SOURCE:0:逸脱 += 6000
	CASE 3
		TIMES SOURCE:0:Ｖ快楽, 0.70
		SOURCE:0:疼痛 += 100
		SOURCE:0:恐惧 += 500
		SOURCE:0:逸脱 += 2000
	ENDSELECT
	IF TALENT:淫壷 || MARK:淫紋 >= 4
		SOURCE:0:疼痛 /= 3
		SOURCE:0:恐惧 /= 3
		SOURCE:0:逸脱 /= 3
	ELSEIF MARK:淫紋
		SOURCE:0:疼痛 = MULTIPLY(SOURCE:0:疼痛, 100 - MARK:淫紋*15)
		SOURCE:0:恐惧 = MULTIPLY(SOURCE:0:恐惧, 100 - MARK:淫紋*15)
		SOURCE:0:逸脱 = MULTIPLY(SOURCE:0:逸脱, 100 - MARK:淫紋*15)
	ENDIF

	;PALAM_RESは入れてるものの尺寸を加味した潤滑具合
	PALAM_RES = GET_PALAMLV(PALAM:Ｖ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0) - 1

	;経験は足りている？（HARDによって上限有）
	EXP_REQ = MIN(GET_EXP_LV(EXP:子宮口経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;損傷有？
	DAMAGE = TALENT:Ｖ損傷
CASE "Ｖ挿入", "玩弄子宮口", "指挿入", "振動棒", "Ｖ拳交", "双頭振動棒PLAY"
	;淫紋持ちは充足のソースも得る
	SIF MARK:淫紋
		SOURCE:0:充足 += MARK:淫紋 * 500

	IF TALENT:処女
		SOURCE:0:疼痛 += 1000
	ELSEIF EX:処女喪失
		SOURCE:0:疼痛 += 100
	ENDIF
	IF TALENT:淫壷 || MARK:淫紋 >= 4
		SOURCE:0:疼痛 /= 3
		SOURCE:0:恐惧 /= 3
		SOURCE:0:逸脱 /= 3
	ELSEIF MARK:淫紋
		SOURCE:0:疼痛 = MULTIPLY(SOURCE:0:疼痛, 100 - MARK:淫紋*15)
		SOURCE:0:恐惧 = MULTIPLY(SOURCE:0:恐惧, 100 - MARK:淫紋*15)
		SOURCE:0:逸脱 = MULTIPLY(SOURCE:0:逸脱, 100 - MARK:淫紋*15)
	ENDIF
	;阴部に筋弛緩剤
	IF GETBIT(TEQUIP:筋弛緩剤, 2)
		TIMES SOURCE:0:Ｖ快楽, 0.70
		TIMES SOURCE:0:疼痛, 0.50
	ENDIF

	;PALAM_RESは入れてるものの尺寸を加味した潤滑具合
	SELECTCASE ARGS
	CASE "Ｖ挿入"
		PALAM_RES = GET_PALAMLV(PALAM:Ｖ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0)
	CASEELSE
		;ここでの最大は３
		PALAM_RES = MIN(GET_PALAMLV(PALAM:Ｖ潤) - MAX(SIZE_DEF, 0), 3)
	ENDSELECT

	;経験は足りている？（HARDによって上限有）
	EXP_REQ = MIN(GET_EXP_LV(EXP:Ｖ経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;常時挿入による疼痛の軽減
	IF TALENT:常時Ｖ振動棒 >= 4
		SIF ABL:Ｖ拡張 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:疼痛, 0.10
	ELSE
		SIF TALENT:常時Ｖ振動棒 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:疼痛, 0.10
	ENDIF

	;損傷有？
	DAMAGE = TALENT:Ｖ損傷
CASE "Ａ挿入", "Ａ指挿入", "Ａ振動棒", "肛塞", "Ａ拳交"
	;結腸挿入
	IF COND("結腸責めフラグ")
		SELECTCASE GET_EXP_LV(EXP:結腸経験) + TALENT:結腸性感
		CASE 0
			SOURCE:0:屈従 += 500
			SOURCE:0:疼痛 += 1000
			SOURCE:0:恐惧 += 10000
			SOURCE:0:逸脱 += 10000
		CASE 1
			SOURCE:0:屈従 += 1000
			SOURCE:0:疼痛 += 500
			SOURCE:0:恐惧 += 3000
			SOURCE:0:逸脱 += 3000
		CASE 2
			TIMES SOURCE:0:Ａ快楽, 1.20
			SOURCE:0:屈従 += 2000
			SOURCE:0:疼痛 += 100
			SOURCE:0:恐惧 += 500
			SOURCE:0:逸脱 += 1000
		CASEELSE
			TIMES SOURCE:0:Ａ快楽, 1.50
			SOURCE:0:屈従 += 3000
		ENDSELECT
	ENDIF

	IF TALENT:尻穴狂い
		SOURCE:0:疼痛 /= 3
		SOURCE:0:恐惧 /= 3
		SOURCE:0:逸脱 /= 3
	ENDIF
	;肛門に筋弛緩剤
	IF GETBIT(TEQUIP:筋弛緩剤, 0)
		TIMES SOURCE:0:Ａ快楽, 0.70
		TIMES SOURCE:0:疼痛, 0.50
	ENDIF

	;PALAM_RESは入れてるものの尺寸を加味した潤滑具合
	SELECTCASE ARGS
	CASE "Ａ挿入"
		PALAM_RES = GET_PALAMLV(PALAM:Ａ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0)
	CASEELSE
		;ここでの最大は３
		PALAM_RES = MIN(GET_PALAMLV(PALAM:Ａ潤) - MAX(SIZE_DEF, 0), 3)
	ENDSELECT

	;経験は足りている？（HARDによって上限有）
	EXP_REQ = MIN(GET_EXP_LV(EXP:Ａ経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;常時挿入による疼痛の軽減
	IF TALENT:常時Ａ肛塞 >= 4
		SIF ABL:Ａ拡張 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:疼痛, 0.10
	ELSE
		SIF TALENT:常時Ａ肛塞 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:疼痛, 0.10
	ENDIF

	;損傷有？
	DAMAGE = TALENT:Ａ損傷
ENDSELECT

;PRINTFORMW SIZE_DEF = {SIZE_DEF}, PALAM_RES = {PALAM_RES}, EXP_REQ = {EXP_REQ}, SOURCE:0:疼痛 = {SOURCE:0:疼痛}

;挿入かどうか等で分岐
SELECTCASE ARGS
CASE "尿道挿入"
	IF CALCF("阴茎好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:尿道拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐惧 * CALCF("阴茎好き度", TARGET)
		SOURCE:0:疼痛 /= CALCF("阴茎好き度", TARGET) + 1
		SOURCE:0:恐惧 /= CALCF("阴茎好き度", TARGET) + 1
	ENDIF
	IF COND("喜欢的体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:尿道快楽, 1.20
		TIMES SOURCE:0:疼痛, 0.80
		TIMES SOURCE:0:恐惧, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
CASE "子宮挿入"
	IF CALCF("阴茎好き度", TARGET) > 0 && PENIS(PLAYER) && 1 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐惧 * CALCF("阴茎好き度", TARGET)
		SOURCE:0:疼痛 /= CALCF("阴茎好き度", TARGET) + 1
		SOURCE:0:恐惧 /= CALCF("阴茎好き度", TARGET) + 1
	ENDIF
	IF COND("喜欢的体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:疼痛, 0.80
		TIMES SOURCE:0:恐惧, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
CASE "Ｖ挿入", "玩弄子宮口"
	IF CALCF("阴茎好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:Ｖ拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐惧 * CALCF("阴茎好き度", TARGET)
		SOURCE:0:疼痛 /= CALCF("阴茎好き度", TARGET) + 1
		SOURCE:0:恐惧 /= CALCF("阴茎好き度", TARGET) + 1
	ENDIF
	IF COND("喜欢的体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:疼痛, 0.80
		TIMES SOURCE:0:恐惧, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
	IF COND("臍上侵入") || COND("腹部变形")
		SELECTCASE GET_EXP_LV(EXP:子宮口経験) + TALENT:子宮性感
		CASE 0
			SOURCE:0:屈従 += 500
			SOURCE:0:疼痛 += 1000
			SOURCE:0:恐惧 += 10000
			SOURCE:0:逸脱 += 10000
		CASE 1
			SOURCE:0:屈従 += 1000
			SOURCE:0:疼痛 += 500
			SOURCE:0:恐惧 += 3000
			SOURCE:0:逸脱 += 3000
		CASE 2
			TIMES SOURCE:0:Ｖ快楽, 1.20
			SOURCE:0:屈従 += 2000
			SOURCE:0:充足 += 1000
			SOURCE:0:疼痛 += 100
			SOURCE:0:恐惧 += 500
			SOURCE:0:逸脱 += 1000
		CASEELSE
			TIMES SOURCE:0:Ｖ快楽, 1.50
			SOURCE:0:屈従 += 3000
			SOURCE:0:充足 += 1500
		ENDSELECT
		SIF COND("腹部变形")
			SOURCE:0:達成感 += 1000
	ELSEIF COND("臍下到達")
		SELECTCASE GET_EXP_LV(EXP:子宮口経験) + TALENT:子宮性感
		CASE 0
			SOURCE:0:屈従 += 500
			SOURCE:0:疼痛 += 500
			SOURCE:0:恐惧 += 3000
			SOURCE:0:逸脱 += 3000
		CASE 1
			SOURCE:0:屈従 += 1000
			SOURCE:0:充足 += 500
			SOURCE:0:疼痛 += 100
			SOURCE:0:恐惧 += 500
			SOURCE:0:逸脱 += 1000
		CASEELSE
			TIMES SOURCE:0:Ｖ快楽, 1.20
			SOURCE:0:屈従 += 1500
			SOURCE:0:充足 += 1000
		ENDSELECT
	ENDIF



CASE "Ａ挿入"
	IF CALCF("阴茎好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:Ａ拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐惧 * CALCF("阴茎好き度", TARGET)
		SOURCE:0:疼痛 /= CALCF("阴茎好き度", TARGET) + 1
		SOURCE:0:恐惧 /= CALCF("阴茎好き度", TARGET) + 1
	ENDIF
	IF COND("喜欢的体位中")
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:疼痛, 0.80
		TIMES SOURCE:0:恐惧, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF

CASE "双頭振動棒PLAY"
	;締まり対決。値が大きいほどTARGET有利（受ける快楽等が少なく、与える快楽が大きい）
	;全部の値に作用するのでARG:1を増減させる
	SELECTCASE ABL:Ｖ技術 - ABL:Ｖ拡張 - (ABL:PLAYER:Ｖ技術 - ABL:PLAYER:Ｖ拡張)
	CASE IS >= 3
		TIMES ARG:1, 0.30
	CASE 2
		TIMES ARG:1, 0.50
	CASE 1
		TIMES ARG:1, 0.80
	CASE -1
		TIMES ARG:1, 1.20
	CASE -2
		TIMES ARG:1, 1.50
	CASE IS <= -3
		TIMES ARG:1, 2.00
	ENDSELECT
CASE "振動棒"
	IF TEQUIP:振動棒 == 0
		TIMES SOURCE:0:Ｃ快楽, 0.50
		TIMES SOURCE:0:Ｖ快楽, 0.50
		TIMES SOURCE:0:疼痛, 0.50
		TIMES SOURCE:0:逸脱, 0.50
	ENDIF
CASE "Ａ振動棒"
	IF TEQUIP:肛用振動棒 == 0
		TIMES SOURCE:0:Ａ快楽, 0.50
		TIMES SOURCE:0:疼痛, 0.50
		TIMES SOURCE:0:逸脱, 0.50
		TIMES SOURCE:0:屈従, 0.50
		TIMES SOURCE:0:恐惧, 0.50
	ENDIF
CASE "肛塞"
	IF TEQUIP:肛塞 == 0
		TIMES SOURCE:0:Ａ快楽, 0.50
		TIMES SOURCE:0:疼痛, 0.50
		TIMES SOURCE:0:逸脱, 0.50
		TIMES SOURCE:0:屈従, 0.50
		TIMES SOURCE:0:恐惧, 0.50
	ENDIF
CASE "尿道振動棒"
	IF TEQUIP:尿道振動棒 == 0
		TIMES SOURCE:0:尿道快楽, 0.50
		TIMES SOURCE:0:疼痛, 0.50
		TIMES SOURCE:0:逸脱, 0.50
		TIMES SOURCE:0:屈従, 0.50
		TIMES SOURCE:0:恐惧, 0.50
	ENDIF
CASE "指挿入", "Ａ指挿入", "尿道指挿入"
	SELECTCASE ABL:PLAYER:指靈活
	CASE 1
		TIMES SOURCE:0:疼痛, 0.80
	CASE 2
		TIMES SOURCE:0:疼痛, 0.50
	CASE IS >= 3
		SOURCE:0:疼痛 = MIN(SOURCE:0:疼痛/5, 500)
	ENDSELECT
ENDSELECT

IF TEQUIP:推倒 == 0
	SELECTCASE CONDS("調教知識", PLAYER)
	CASE "性器拡張知識"
		TIMES SOURCE:0:疼痛, 0.50
		TIMES SOURCE:0:恐惧, 0.50
	ENDSELECT
ENDIF

;挿入するものの尺寸が該当箇所の拡張ＬＶ+１以下の場合
IF SIZE_DEF <= 1 && TALENT:好色 + TALENT:淫乱 + TALENT:拡張適性
	TIMES SOURCE:0:疼痛, 0.50
	TIMES SOURCE:0:恐惧, 0.50
	TIMES SOURCE:0:屈従, 0.50

	;今までの絶頂具合による脱力などで疼痛軽減措置（とりあえずは暫定）
	SELECTCASE EX:絶頂数合計 + TEQUIP:媚薬/25
	CASE IS >= MAX(20, MAXBASE:性欲*2)
		TIMES SOURCE:0:疼痛, 0.20
	CASE IS >= MAX(10, MAXBASE:性欲)
		TIMES SOURCE:0:疼痛, 0.50
	CASE IS >= MAX(5, MAXBASE:性欲/2)
		TIMES SOURCE:0:疼痛, 0.80
	ENDSELECT
ENDIF

IF TALENT:淫乱
	TIMES SOURCE:0:逸脱, 0.20
ELSEIF TALENT:好色
	TIMES SOURCE:0:逸脱, 0.50
ENDIF

;経験は足りている？
SELECTCASE EXP_REQ
CASE IS >= 3
	TIMES SOURCE:0:疼痛, 0.05
	TIMES SOURCE:0:恐惧, 0.05
	TIMES SOURCE:0:逸脱, 0.05
CASE 2
	TIMES SOURCE:0:疼痛, 0.30
	TIMES SOURCE:0:恐惧, 0.30
	TIMES SOURCE:0:逸脱, 0.30
CASE 1
	TIMES SOURCE:0:疼痛, 0.70
	TIMES SOURCE:0:恐惧, 0.70
	TIMES SOURCE:0:逸脱, 0.70
CASE 0
	TIMES SOURCE:0:恐惧, 0.70
CASE -1
	TIMES SOURCE:0:疼痛, 1.25
	TIMES SOURCE:0:逸脱, 1.20
CASE -2
	TIMES SOURCE:0:疼痛, 1.50
	TIMES SOURCE:0:恐惧, 1.50
	TIMES SOURCE:0:逸脱, 2.00
CASEELSE
	TIMES SOURCE:0:疼痛, 2.00
	TIMES SOURCE:0:恐惧, 3.00
	TIMES SOURCE:0:逸脱, 3.00
ENDSELECT

;PALAM:Ｖ潤orＡ潤をみる
;挿入されているもののサイズとか拡張度は既に加味されている
;だいたい、挿入されているもののSIZEと拡張度が等しいときに対応した潤滑のLVが２あれば問題ない
SELECTCASE PALAM_RES
CASE IS >= 4
	TIMES SOURCE:0:疼痛, 0.20
CASE 3
	TIMES SOURCE:0:疼痛, 0.50
CASE 2
	TIMES SOURCE:0:Ｖ快楽, 0.80
	TIMES SOURCE:0:Ａ快楽, 0.80
	TIMES SOURCE:0:疼痛, 0.80
CASE 1
	TIMES SOURCE:0:Ｖ快楽, 0.40
	TIMES SOURCE:0:Ａ快楽, 0.40
	SOURCE:0:疼痛 += 100
CASEELSE
	TIMES SOURCE:0:Ｖ快楽, 0.10
	TIMES SOURCE:0:Ａ快楽, 0.10
	SOURCE:0:疼痛 += 500
ENDSELECT

;損傷有？
IF DAMAGE
	TIMES SOURCE:0:疼痛, 2.00
	TIMES SOURCE:0:恐惧, 2.00
	TIMES SOURCE:0:逸脱, 2.00
	SOURCE:0:疼痛 += PALAM_LV(SIZE(NAME_INSERT) + 2)
	SOURCE:0:恐惧 += PALAM_LV(SIZE(NAME_INSERT) + 2)
	SOURCE:0:逸脱 += PALAM_LV(SIZE(NAME_INSERT) + 1)
ENDIF

;潤滑増加の処理。その後に、場所ごとに性向の補正と完美契合
SELECTCASE ARGS
;膣を使っているPLAYは疼痛に比例して愛液増加
CASE "子宮挿入", "Ｖ挿入", "玩弄子宮口"
	SOURCE:0:Ｖ液体追加 += 100*MIN(PALAM_LV(SOURCE:0:疼痛), 5)
	SIF TALENT:Ｖ性向
		CALL CALC, "好みのPLAY", SOURCE:0:Ｖ快楽

	;完美契合は快楽と屈従を増大させる
	SELECTCASE CHECK("Ｖ完美契合")
	CASE IS >= 100
		TIMES SOURCE:0:Ｖ快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:Ｖ快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:Ｖ快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:Ｖ快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:Ｖ快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:Ｖ快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT
CASE "指挿入", "振動棒", "Ｖ拳交", "双頭振動棒PLAY"
	SOURCE:0:Ｖ液体追加 += 100*MIN(PALAM_LV(SOURCE:0:疼痛), 5)
	SIF TALENT:Ｖ性向
		CALL CALC, "好みのPLAY", SOURCE:0:Ｖ快楽

;肛門PLAYなら疼痛や異物の尺寸などによって腸液分泌
CASE "Ａ挿入"
	SOURCE:0:Ａ液体追加 += MAX((GET_PALAMLV(SOURCE:0:疼痛) + SIZE(NAME_INSERT))*100, 100)
	SIF TALENT:Ａ性向
		CALL CALC, "好みのPLAY", SOURCE:0:Ａ快楽

	;完美契合は快楽と屈従を増大させる
	SELECTCASE CHECK("Ａ完美契合")
	CASE IS >= 100
		TIMES SOURCE:0:Ａ快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:Ａ快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:Ａ快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:Ａ快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:Ａ快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:Ａ快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT

	;縦深菊穴は不怕痛
	SIF TALENT:縦深菊穴
		SOURCE:0:疼痛 = MAX(MIN(DOWN_PALAMLV(SOURCE:0:疼痛), SOURCE:0:疼痛 - 100), 0)
CASE "Ａ指挿入", "Ａ振動棒", "肛塞", "Ａ拳交"
	SOURCE:0:Ａ液体追加 += MAX((GET_PALAMLV(SOURCE:0:疼痛) + SIZE(NAME_INSERT))*100, 100)
	SIF TALENT:Ａ性向
		CALL CALC, "好みのPLAY", SOURCE:0:Ａ快楽
	;縦深菊穴は不怕痛
	SIF TALENT:縦深菊穴
		SOURCE:0:疼痛 = MAX(MIN(DOWN_PALAMLV(SOURCE:0:疼痛), SOURCE:0:疼痛 - 100), 0)
CASE "尿道挿入", "尿道指挿入", "尿道探针", "尿道振動棒"
	SIF TALENT:尿道狂
		CALL CALC, "好みのPLAY", SOURCE:0:尿道快楽

	;完美契合は快楽と屈従を増大させる
	SELECTCASE CHECK("尿道完美契合")
	CASE IS >= 100
		TIMES SOURCE:0:尿道快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:尿道快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:尿道快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:尿道快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:尿道快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:尿道快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT
ENDSELECT

;フィストは余裕ある状態だと快楽増加
SELECTCASE ARGS
CASE "Ｖ拳交", "Ａ拳交"
	IF SIZE_DEF < 0 && EXP_REQ > 0 && PALAM_RES >= 3
		TIMES SOURCE:0:Ｖ快楽, 2.00
		TIMES SOURCE:0:Ａ快楽, 2.00
	ENDIF
ENDSELECT

SELECTCASE NAME_INSERT
;挿入時限定補正
CASE "阴茎"
	IF TALENT:PLAYER:業物
		SIF SOURCE:0:Ｖ快楽
			SOURCE:0:Ｖ快楽 = UP_PALAMLV(SOURCE:0:Ｖ快楽, 1) + TALENT:PLAYER:業物*500
		SIF SOURCE:0:Ａ快楽
			SOURCE:0:Ａ快楽 = UP_PALAMLV(SOURCE:0:Ａ快楽, 1) + TALENT:PLAYER:業物*500
		SIF SOURCE:0:尿道快楽
			SOURCE:0:尿道快楽 = UP_PALAMLV(SOURCE:0:尿道快楽, 1) + TALENT:PLAYER:業物*500

		IF TALENT:処女 || EX:処女喪失
			TIMES SOURCE:0:疼痛, 0.50
		ELSE
			SOURCE:0:疼痛 = DOWN_PALAMLV(SOURCE:0:疼痛, 1)
		ENDIF
	ENDIF
	;巨根は屈従のソースにBOUNS
	SIF PLAYER == MASTER && SIZE(NAME_INSERT) >= 2
		SOURCE:0:屈従 += 500 * SIZE(NAME_INSERT)
ENDSELECT

ARG = ARG * ARG:1 / 100

CALL CALC, "調教ソース", ARG


@EQUIP_COM60
#DIM MEMO_SELECTCOM
#DIM MOTIVATION

;強度（通常は20）
MOTIVATION = 20

SELECTCASE SELECTCOM
CASE 60 TO 89, 120 TO 129
	RETURN 0
;什麼也不做
CASE 139
	IF COND("疲労")
		RETURN 0
	ELSE
		MOTIVATION = 5
		;騎乗位の場合はあんまり変わらない
		IF INSERT("騎乗位")
			MOTIVATION = 20
		;PLAYERが騎乗位している側
		ELSEIF INSERT("騎乗位")
			MOTIVATION = 5
		;PLAYERが犯されている時
		ELSEIF INSERT("挿入中")
			MOTIVATION = 20
		ENDIF
	ENDIF
ENDSELECT

MEMO_SELECTCOM = SELECTCOM


IF INSERT("Ｖ挿入")
	IF INSERT("Ｖ挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("Ｖ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ｖ挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ｖ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ａ挿入")
	IF INSERT("Ａ挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("Ａ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ａ挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ａ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("尿道挿入")
	IF INSERT("尿道挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("尿道対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("尿道挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("尿道対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ｖ挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("Ｖ対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("Ａ挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("Ａ対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("尿道挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("尿道対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF

SELECTCOM = MEMO_SELECTCOM

RETURN 1

@EQUIP_COM60_2
#DIM A_JUST
#DIM V_JUST
#DIM U_JUST

CALL TEXT, "挿入具合の描写"

V_JUST = 0
A_JUST = 0
U_JUST = 0

;３ＰなどがあるのでTARGETとPLAYERを省略せずに記入（A_JUSTとJ_JUSTは片方しか1以上にはならない）
SIF COND("Ｖ完美契合") && INSERT("Ｖ挿入", TARGET, PLAYER) && CHOSECOMS("Ｖ挿入") == 0
	V_JUST = CHECK("Ｖ完美契合")
SIF COND("Ａ完美契合") && INSERT("Ａ挿入", TARGET, PLAYER) && CHOSECOMS("Ａ挿入") == 0
	A_JUST = CHECK("Ａ完美契合")
SIF COND("尿道完美契合") && INSERT("尿道挿入", TARGET, PLAYER) && CHOSECOMS("尿道挿入") == 0
	U_JUST = CHECK("尿道完美契合")

IF V_JUST || A_JUST || U_JUST
	CALL PRINT_STRL, "爱心粉紅_＜完美契合！＞"

	SELECTCASE MAX(V_JUST, A_JUST, U_JUST)
	CASE IS >= 90
		PRINT 被完全接納的
	CASE IS >= 75
		PRINT 被反复插入的
	CASEELSE
		PRINT 被整個侵入的
	ENDSELECT
	PRINTFORM 的%CALLNAME:PLAYER%的陰莖所%TEXTR("變成陰莖的形狀了/記憶住侵犯的感覺了/變成專用的飛機杯了")%的
	IF A_JUST
		PRINTFORM %CALLNAME:TARGET%的肛門
	ELSEIF U_JUST
		PRINTFORM %CALLNAME:TARGET%的尿道
	ELSE
		PRINTFORM %CALLNAME:TARGET%的膣穴
	ENDIF
	PRINTFORML %TEXTR("因為無法言喻的絕妙快感而溶化了/毫無間隙地緊緊纏繞著/如同獨立的活物一樣緊緊吸附著配合起來")%！
	;CALL PRINT_STRL, "爱心粉紅_H_因为相互给予的快乐而獲得了BOUNS_爱心粉紅_H"
ENDIF

VARSET LOCALS
IF INSERT("挿入") || SELECTCOM == 80
	IF COND("喜欢的体位中")
		SETCOLOR DEF_COLOR("爱心粉紅")
		LOCALS = 喜欢的体位
	ENDIF

	SELECTCASE SELECTCOM
	CASE 60 TO 89
	;什麼也不做
	CASE 139
		SIF COND("疲労")
			RETURN 0
	CASEELSE
		SIF LOCALS != ""
			LOCALS = 喜欢的体位で
		IF INSERT("二穴挿入")
			LOCALS = %LOCALS%二穴挿入中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("Ｖ挿入")
			LOCALS = %LOCALS%挿入中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
		ELSEIF INSERT("Ａ挿入")
			LOCALS = %LOCALS%Ａ挿入中
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("尿道挿入")
			LOCALS = %LOCALS%尿道挿入中
			EXP:尿道経験 += 1
			LOCALS:2 = 尿道経験＋１
		ENDIF
	ENDSELECT
ENDIF

SELECTCASE SELECTCOM
CASE 120 TO 129
CASEELSE
	IF INSERT("Ｖ挿入", PLAYER, TARGET)
		LOCALS = 逆挿入中
		EXP:PLAYER:Ｖ経験 += 1
	ELSEIF INSERT("Ａ挿入", PLAYER, TARGET)
		LOCALS = 逆Ａ挿入中
		EXP:PLAYER:Ａ経験 += 1
	ENDIF
ENDSELECT

SIF LOCALS != ""
	PRINTFORML ＜%LOCALS%＞
RESETCOLOR
SIF LOCALS:1 != ""
	PRINTFORML %LOCALS:1%
SIF LOCALS:2 != ""
	PRINTFORML %LOCALS:2%

RETURN 1

