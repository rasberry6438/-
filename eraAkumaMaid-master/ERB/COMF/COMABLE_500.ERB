;-------------------------------------------------
;全部の@COMABLEを一斉に管理する時に使用する式中関数
;推倒など、登場頻度が非常に多いものについてのみ扱う
;振動棒などの装備品が解除できるかどうかの判定は、この判定よりも優先度が高いことに注意
;なお、NEXTCOMの時には@COM_ABLEを通らない。
;-------------------------------------------------
@COMABLE_DEF(ARG)
#FUNCTION
;一括OFF、個別設定指令过滤器
SIF FLAG:COM_ABLEの一括OFF || DITEMTYPE:TARGET:ARG > 0
	RETURNF 1
;TARGETの指令禁止処置
SIF DITEMTYPE:TARGET:ARG > 0
	RETURNF 1

;PLAYERの指令禁止処置
SELECTCASE REVCOM(ARG)
;指令立場逆転でも変わらないものは問題ない
CASE ARG
CASEELSE
	SIF DITEMTYPE:PLAYER:REVCOM(ARG) > 0
		RETURNF 1
ENDSELECT

;指令禁止过滤器
SIF DITEMTYPE:0:ARG == 2 && CONFIG("指令过滤器") == 0
	RETURNF 1

;各種过滤器
IF TALENT:処女 && CONFIG("Ｖ調教过滤器")
	SIF TRAIN_GENRE("Ｖ調教", ARG)
		RETURNF 1
ENDIF
IF CONFIG("Ａ調教过滤器")
	SIF TRAIN_GENRE("Ａ調教", ARG)
		RETURNF 1
ENDIF
IF CONFIG("尿道調教过滤器")
	SIF TRAIN_GENRE("尿道調教", ARG)
		RETURNF 1
ENDIF
IF CONFIG("逆挿入过滤器")
	SIF TRAIN_GENRE("逆挿入", ARG)
		RETURNF 1
ENDIF
IF CONFIG("道具調教过滤器")
	SIF TRAIN_GENRE("道具調教", ARG)
		RETURNF 1
ENDIF
IF CONFIG("ハード調教过滤器")
	SIF TRAIN_GENRE("ハード調教", ARG)
		RETURNF 1
ENDIF

IF CONFIG("立位")
	SIF TRAIN_GENRE("立位可能", ARG) == 0
		RETURNF 1
	IF TRAIN_GENRE("火车便当位挿入", ARG)
		SIF SIZE("阴茎", PLAYER) == 0
			RETURNF 1
		SIF TALENT:娇小 == 0 && TALENT:PLAYER:大力士 == 0
			RETURNF 1
	ENDIF
	SIF TRAIN_GENRE("尿道挿入", ARG) && PENIS(TARGET)
		RETURNF 1

	SELECTCASE ARG
	;火车便当位挿入中は幾つかの愛撫指令をすると終了してしまうため、一見可能かよく分からないものは禁止しておく
	;火车便当位のみ禁止
	CASE TNUM("自慰")
		SIF INSERT("火车便当位")
			RETURNF 1
	;両火车便当位共に禁止
	CASE TNUM("肛門愛撫"), TNUM("胸愛撫"), TNUM("阴部愛撫"), TNUM("指挿入"), TNUM("尿道指挿入")
		SIF INSERT("火车便当位") || INSERT("背面火车便当位")
			RETURNF 1
	;禁止体位など
	CASE 60, 70, 83
		SIF INSERT("背面座位")
			RETURNF 1
	CASE 61, 71, 84
		SIF INSERT("対面座位")
			RETURNF 1
	CASE 63, 73, 86
		SIF INSERT("後背位") || INSERT("背面座位")
			RETURNF 1
	CASE 64, 74, 85
		SIF INSERT("正常位") || INSERT("対面座位")
			RETURNF 1
	ENDSELECT
ENDIF

;場所による制限
SELECTCASE TRAINNAME_EX(ARG)
CASE "浴室PLAY"
	SIF PLACE("浴室") == 0
		RETURNF 1
CASE "新妻PLAY"
	SIF PLACE("自宅") == 0
		RETURNF 1
CASE "室内露出PLAY"
	SIF PLACE("自宅") == 0 && PLACE("部室") == 0
		RETURNF 1
ENDSELECT

;口上側による行為の制限
SIF CEVENT("尿道調教禁止") && TRAIN_GENRE("尿道調教", ARG)
	RETURNF 1

;PLAYの制限
;受け手が制限されている
IF PLAYER != MASTER && COND("身嗜み：性行為の制限")
	SELECTCASE ARG
	CASE 60 TO 69
		SIF COND("身嗜み：Ｖ独占")
			RETURNF 1
	CASE 70 TO 79
		SIF COND("身嗜み：Ａ独占")
			RETURNF 1
	CASE 90, 96, 98, 99, 140, 150
		SIF COND("身嗜み：口独占")
			RETURNF 1
	CASE 6
		SIF COND("身嗜み：接吻独占")
			RETURNF 1
	CASE 120 TO 129
		SIF PENIS(TARGET) && COND("身嗜み：Ｐ独占")
			RETURNF 1
	ENDSELECT
ENDIF
;攻め手が制限されている
IF COND("身嗜み：性行為の制限", PLAYER)
	SELECTCASE ARG
	CASE 120 TO 124
		SIF COND("身嗜み：Ｖ独占", PLAYER)
			RETURNF 1
	CASE 125 TO 129
		SIF COND("身嗜み：Ａ独占", PLAYER)
			RETURNF 1
	CASE 1, 4, 9
		SIF COND("身嗜み：口独占", PLAYER)
			RETURNF 1
	CASE 6
		SIF COND("身嗜み：接吻独占", PLAYER)
			RETURNF 1
	CASE 60 TO 79, 81, 82, 83 TO 88, 140, 150
		SIF PENIS(TARGET) && COND("身嗜み：Ｐ独占", PLAYER)
			RETURNF 1
	ENDSELECT
ENDIF

;一部重口指令は屈服刻印や受虐属性が必要(実行条件があるものは除く)
SELECTCASE TRAINNAME_EX(ARG)
CASE "鼻钩", "眼罩", "口枷"
	SIF MARK:屈服刻印 == 0 && ABL:受虐属性 == 0 && ABL:信頼 < 5 && ABL:欲望 < 5 && TALENT:倒錯的 == 0
		RETURNF 1
CASE "拘束"
	SIF MARK:屈服刻印 < 1 && ABL:受虐属性 < 1 && TALENT:倒錯的 == 0
		RETURNF 1
CASE "浣腸"
	SIF MARK:屈服刻印 < 2 && ABL:受虐属性 < 2
		RETURNF 1
ENDSELECT

;挿入系指令の一括処理や汚れなどによるPLAYの制限
SELECTCASE ARG
CASE 1, 4, 6, 9, 10, 11, 99
	;女王様PLAYor推倒なら汚れで文句は言えない
	IF TEQUIP:女王様PLAY == 0 && TEQUIP:推倒 == 0
		IF TALENT:PLAYER:汚臭鈍感 || TALENT:PLAYER:汚臭無視
		;你の性癖とTARGETの属性が２個以上合致
		;ELSEIF PLAYER == MASTER && (CALCF("属性突かれ", TARGET) >= 2 || TALENT:相思相愛)
		ELSEIF PLAYER == MASTER
		ELSEIF PLAYER == ASSI && ABL:ASSI:信頼 >= 4
		ELSE
			LOCAL = 0
			SELECTCASE ARG
			CASE 1
				LOCAL = STAIN:Ｖ
			CASE 4, 10, 11
				LOCAL = STAIN:Ｐ
			CASE 6
				LOCAL = STAIN:Ｍ
			CASE 9
				LOCAL = STAIN:Ａ
			CASE 99
				SIF TALENT:男性 == 0
					LOCAL |= STAIN:Ｖ
				SIF PENIS(TARGET)
					LOCAL |= STAIN:Ｐ
			ENDSELECT

			SIF CHECK_STAIN(LOCAL, "破瓜之血")
				RETURNF 1
			IF CHECK_STAIN(LOCAL, "Ａ") && COND("肛門弱点", PLAYER) == 0
				SIF TEQUIP:出浴 == 0 || ARG != 9
					RETURNF 1
			ENDIF
			IF TALENT:PLAYER:男性 && TALENT:PLAYER:兩面通吃 == 0 && CHECK_STAIN(LOCAL, "Ｐ") && COND("♂弱点", PLAYER) == 0
				SIF TEQUIP:出浴 == 0 || ARG != 4
					RETURNF 1
			ENDIF
			IF TALENT:PLAYER:女性 && TALENT:PLAYER:兩面通吃 == 0 && CHECK_STAIN(LOCAL, "Ｖ") && COND("♀弱点", PLAYER) == 0
				SIF TEQUIP:出浴 == 0 || ARG != 1
					RETURNF 1
			ENDIF
			;SIF ABL:PLAYER:精液中毒 == 0 && TALENT:PLAYER:兩面通吃 == 0 && CHECK_STAIN(LOCAL, "精液")
			;	RETURNF 1
		ENDIF
	ENDIF
;Ｖ挿入指令
CASE 60 TO 69
	IF COND("調教中") && CONFIG("準備不足过滤器")
		SIF CHOSECOMS("Ｖ挿入") == 0 && COND("Ｖ潤充分") == 0
			RETURNF 1
	ENDIF
	SIF USE_V(TARGET) == 0 || TEQUIP:PLAYER:导管
		RETURNF 1
	;調教者が阴茎有、もしくは穿戴式假陰茎がないとダメ
	SIF PENIS(PLAYER) == 0 && COND("假阳具", PLAYER) == 0
		RETURNF 1
	;調教中でないなら2LV以上でかい阴茎は避ける
	SIF COND("調教中") == 0 && ABL:Ｖ拡張 + 1 < SIZE("阴茎", PLAYER)
		RETURNF 1
	IF TEQUIP:推倒
		SIF TALENT:淫乱 + TALENT:恋慕 == 0 && ABL:Ｖ拡張 + 1 < SIZE("阴茎", PLAYER)
			RETURNF 1
		;推倒は生性交！
		SIF COND("女性主導生性交") == 0
			RETURNF 1
		;你が恍惚＆まだ挿入した事が無いならしてこない
		SIF STATE("恍惚", PLAYER) && FIRSTCOMS("Ｖ挿入") == 0
			RETURNF 1
		;你が脫力＆まだ挿入した事が無いならしてこない
		SIF STATE("脫力", PLAYER) && FIRSTCOMS("Ｖ挿入") == 0
			RETURNF 1
	ENDIF
;Ａ挿入指令
CASE 70 TO 79
	IF COND("調教中") && CONFIG("準備不足过滤器")
		SIF CHOSECOMS("Ａ挿入") == 0 && COND("Ａ潤充分") == 0
			RETURNF 1
	ENDIF
	SIF USE_A(TARGET) == 0 || TEQUIP:PLAYER:导管
		RETURNF 1
	;調教者が阴茎有、もしくは穿戴式假陰茎がないとダメ
	SIF PENIS(PLAYER) == 0 && COND("假阳具", PLAYER) == 0
		RETURNF 1
	;調教中でないなら2LV以上でかい阴茎は避ける
	SIF COND("調教中") == 0 && ABL:Ａ拡張 + 1 < SIZE("阴茎", PLAYER)
		RETURNF 1
	SIF TEQUIP:推倒 && TALENT:恋慕 == 0 && ABL:Ａ拡張 + 1 < SIZE("阴茎", PLAYER)
		RETURNF 1
	;合意or拘束などが必要
	IF COND("合意：肛門性交") == 0 && TEQUIP:拘束 == 0
		SIF COND("无崩しでＡ挿入可") == 0
			RETURNF 1
		;你が恍惚＆まだ挿入した事が無いならしてこない
		SIF STATE("恍惚", PLAYER) && FIRSTCOMS("Ａ挿入") == 0
			RETURNF 1
		;你が脫力＆まだ挿入した事が無いならしてこない
		SIF STATE("脫力", PLAYER) && FIRSTCOMS("Ａ挿入") == 0
			RETURNF 1
	ENDIF
;尿道挿入
CASE 83 TO 88
	;調教者が阴茎有、もしくは穿戴式假陰茎がないとダメ
	SIF PENIS(PLAYER) == 0 && COND("假阳具", PLAYER) == 0
		RETURNF 1
	IF COND("調教中")
		;阴部剥き出しにできないとダメ
		SIF !CHECK_CLO("Ｖ露出可")
			RETURNF 1
		;尿道を使用済みだとダメ
		SIF TEQUIP:PLAYER:导管 || TEQUIP:导管 || TEQUIP:尿道振動棒
			RETURNF 1
	ENDIF
	;阴茎を接受ことが出来ないとダメ
	SIF ABL:尿道拡張 < SIZE("阴茎", PLAYER) && CALCF("尿道径") < SIZE("阴茎の直径", PLAYER)
		RETURNF 1
	;調教中でないなら2LV以上でかい阴茎は避ける
	SIF COND("調教中") == 0 && ABL:尿道拡張 + 1 < SIZE("阴茎", PLAYER)
		RETURNF 1
	IF TEQUIP:推倒
		SIF TALENT:恋慕 == 0 && ABL:尿道拡張 + 1 < SIZE("阴茎", PLAYER)
			RETURNF 1
		;你が恍惚＆まだ挿入した事が無いならしてこない
		SIF STATE("恍惚", PLAYER) && FIRSTCOMS("尿道挿入") == 0
			RETURNF 1
		;你が脫力＆まだ挿入した事が無いならしてこない
		SIF STATE("脫力", PLAYER) && FIRSTCOMS("尿道挿入") == 0
			RETURNF 1
	ENDIF
;特殊挿入
CASE 80 TO 89

;逆振動棒などの道具指令
CASE 106, 107

;逆挿入
CASE 120 TO 129
	;調教相手が阴茎有、もしくは穿戴式假陰茎がないとダメ
	SIF PENIS(TARGET) == 0 && COND("假阳具") == 0
		RETURNF 1
	SIF TEQUIP:导管
		RETURNF 1
;フィスト
CASE 141 TO 143
	;手が使えないとダメ
	SIF COND("指の使用", PLAYER) == 0
		RETURNF 1
	IF ARG == 141
		SIF USE_V(TARGET) == 0
			RETURNF 1
		;処女はダメ
		SIF TALENT:処女 == 1
			RETURNF 1
	ELSEIF ARG == 142
		SIF USE_A(TARGET) == 0
			RETURNF 1
		;液剤很多の浣腸中
		SIF COND("液体浣腸")
			RETURNF 1
	ELSEIF ARG == 143
		SIF USE_V(TARGET) == 0
			RETURNF 1
		SIF TALENT:処女 == 1
			RETURNF 1
		SIF USE_A(TARGET) == 0
			RETURNF 1
		;液剤很多の浣腸中
		SIF COND("液体浣腸")
			RETURNF 1
	ENDIF
;助手が必須の指令は、助手が居なかったりダウンしてたらダメ
CASE 177
	SIF ASSI <= 0 || STATE("撃沈", ASSI) || TEQUIP:攝像機
		RETURNF 1
	SIF COND("推倒騎乗位実行可能", ASSI) == 0
		RETURNF 1
CASE TNUM("抱上去")
	;手が使えないとダメ
	SIF COND("指の使用", PLAYER) == 0
		RETURNF 1
ENDSELECT

;服装による制限
SELECTCASE ARG
CASE 5, 81, 82, 92, 104
	SIF CHECK_SHIRT("鎧") && CHECK_CLO("谷間露出") + CHECK_CLO("乳頭露出") + CHECK_CLO("半脱可") == 0
		RETURNF 1
ENDSELECT


;手が拘束されている
IF COND("指の使用") == 0
	SELECTCASE ARG
	CASE 175
		SIF TEQUIP:推倒
			RETURNF 1
	CASE 3, 13, 14, 90, 92, 94, 97, 100, 104 TO 110, 172 TO 173, 176 TO 178, 180
		RETURNF 1
	ENDSELECT
ENDIF
;足が拘束されている
IF COND("脚の使用") == 0
	SELECTCASE ARG
	CASE 94, 95, 120 TO 129, 171 TO 174, 176, 177
		RETURNF 1
	ENDSELECT
ENDIF
;拘束
IF TEQUIP:拘束
	;縄：逆海老
	IF BIND("縄：逆海老", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 61, 66 TO 69, 71
		CASE 1, 9, 60 TO 89, 145, 148, 151
			RETURNF 1
		ENDSELECT
	;縄：足吊
	ELSEIF BIND("縄：足吊", TEQUIP:拘束)
		SELECTCASE ARG
		;正常位はおかしい気もするがまぁ前からの時ということで
		CASE 60, 61, 66 TO 69, 70, 71, 83, 84
		CASE 60 TO 89, 93, 99, 120, 121, 125, 126
			RETURNF 1
		ENDSELECT
	;縄
	ELSEIF BIND("縄", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 80, 93, 99, 120, 121, 125, 126
			RETURNF 1
		ENDSELECT
	;拘束テーブル
	ELSEIF BIND("四つん這い", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 61, 66 TO 69, 71, 84
		CASE 60 TO 89, 93, 99, 120 TO 129, 171
			RETURNF 1
		ENDSELECT
	;拘束床
	ELSEIF BIND("床", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 60, 66 TO 69, 70, 83, 122, 127
		CASE 60 TO 80, 93, 99, 120 TO 129, 171
			RETURNF 1
		ENDSELECT
	;M字拘束台
	ELSEIF BIND("Ｍ字開脚", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 60, 70, 83
		CASE 60 TO 89, 91, 93, 98, 99, 120 TO 129, 140, 150, 171
			RETURNF 1
		ENDSELECT
	;秘银链(吊り)
	ELSEIF BIND("吊り", TEQUIP:拘束)
		SELECTCASE ARG
		CASE 60, 61, 70, 71, 83, 84
		CASE 60 TO 89, 91, 93, 98, 99, 120 TO 129, 140, 148, 150, 151, 171
			RETURNF 1
		ENDSELECT
	ENDIF
ENDIF

;創口貼装着中
IF TEQUIP:創口貼 || CFLAG:創口貼装着
	SELECTCASE ARG
	CASE 1, 8, 15, 80, 83 TO 88, 144, 145, 149, 151, 152
		RETURNF 1
	ENDSELECT
ENDIF

IF TEQUIP:推倒
	IF COND("主導権没有")
		SELECTCASE ARG
		CASE 177, 280, 281, 286
		CASEELSE
			RETURNF 1
		ENDSELECT
	ENDIF

	SELECTCASE ARG
	;解除のみは可能
	CASE 31, 33 TO 39, 178
		SIF CHECK_TEQUIP(ARG) == 0
			RETURNF 1
	;実行可能な指令
	CASE 1, 4 ,6, 9 ,11, 16, 65 TO 69, 75, 80, 82, 88, 90 TO 99, 101 TO 105, 120, 126, 144, 145, 150, 170, 175, 177, 180, 200, 280 TO 282, 286

	CASEELSE
		RETURNF 1
	ENDSELECT
	;Ｗ推倒
	IF TEQUIP:Ｗ推倒
		SELECTCASE ARG
		CASE 0, 10 TO 12, 80, 82, 99, 105, 144, 145, 150, 175, 177, 178, 261, 282
			RETURNF 1
		ENDSELECT
	ENDIF
ENDIF

;野外、室内露出、お浴室など部屋の外でのPLAYの制限
IF TEQUIP:野外PLAY
	SELECTCASE ARG
	;通常は実行不可なグループの中で例外的に実行可能な指令
	CASE 93, 101, 130, 138, 139, 144, 150
	;解除のみは可能
	CASE 31, 33 TO 39, 133 TO 137, 149
		SIF CHECK_TEQUIP(ARG) == 0
			RETURNF 1
	;実行不可能な指令
	CASE 30 TO 59, 93 TO 119, 120 TO 129, 130 TO 159, 160 TO 169, 172 TO 174
		RETURNF 1
	ENDSELECT
ENDIF
IF TEQUIP:浴室PLAY
	SELECTCASE ARG
	;解除のみは可能
	CASE 133 TO 135, 137, 173
		SIF CHECK_TEQUIP(ARG) == 0
			RETURNF 1
	;実行不可能な指令
	CASE 131 TO 135, 160 TO 169, 171, 173, 174, 178
		RETURNF 1
	ENDSELECT
ENDIF
IF TEQUIP:室内露出PLAY
	SELECTCASE ARG
	;実行不可能な指令
	CASE 171 TO 173
		RETURNF 1
	ENDSELECT
ENDIF

;女王様PLAY(大幅な変更の予定有)
IF TEQUIP:女王様PLAY
	;実行可能な指令
	SELECTCASE ARG
	CASE 1, 4, 9, 31, 33, 130
	;LV2指令
	CASE 90, 95, 121, 126, 141 TO 145, 148
		SIF TEQUIP:女王様PLAY < 2
			RETURNF 1
	;LV3指令
	CASE 60 TO 89
		SIF TEQUIP:女王様PLAY < 3
			RETURNF 1
	CASEELSE
		RETURNF 1
	ENDSELECT
ENDIF

IF TEQUIP:新妻PLAY
	;実行不可な指令
	SELECTCASE ARG
	;CASE 36, 37, 131, 132, 135, 140 TO 143, 146, 147, 160 TO 169, 171, 172, 174, 177
	CASE 171, 174, 177
		RETURNF 1
	ENDSELECT
ENDIF

;複数参加指令の体勢的な可否(TARGET:1…主に、助手を自動で動かす時に用いる)
IF TEQUIP:Ｗ推倒 && TARGET:1
	SELECTCASE ARG
	CASE 1
		SIF FORBIDCOM(TARGET:1, "Ｐ顔", "Ｔ阴部")
			RETURNF 1
	CASE 4
		SIF FORBIDCOM(TARGET:1, "Ｐ顔", "Ｔ阴茎")
			RETURNF 1
	CASE 5
		SIF FORBIDCOM(TARGET:1, "Ｔ胸") || TEQUIP:推倒
			RETURNF 1
	CASE 6
		SIF FORBIDCOM(TARGET:1, "Ｐ顔", "Ｔ顔")
			RETURNF 1
	CASE 9
		SIF FORBIDCOM(TARGET:1, "Ｐ顔", "Ｔ肛門")
			RETURNF 1
	CASE 60, 62, 63
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ阴部", "Ｔ前面")
			RETURNF 1
	CASE 61
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ阴部", "Ｔ後面")
			RETURNF 1
	CASE 64
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ下半身", "Ｔ後面")
			RETURNF 1
	CASE 65
		SIF FORBIDCOM(TARGET:1, "性交", "Ｐ下半身", "Ｔ下半身")
			RETURNF 1
	CASE 66 TO 69
		SIF FORBIDCOM(TARGET:1, "性交", "Ｐ下半身", "Ｔ下半身")
			RETURNF 1

	CASE 70, 72, 73
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ肛門", "Ｔ前面")
			RETURNF 1
	CASE 71
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ肛門", "Ｔ後面")
			RETURNF 1
	CASE 74
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ下半身", "Ｔ後面")
			RETURNF 1
	CASE 75
		SIF FORBIDCOM(TARGET:1, "性交", "Ｐ下半身", "Ｔ下半身")
			RETURNF 1
	CASE 76 TO 79
		SIF FORBIDCOM(TARGET:1, "性交", "Ｐ下半身", "Ｔ下半身")
			RETURNF 1
	;磨鏡
	CASE 80
		SIF FORBIDCOM(TARGET:1, "Ｐ下半身", "Ｔ下半身")
			RETURNF 1
	;尿道
	CASE 83, 85, 86
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ阴部", "Ｔ前面")
			RETURNF 1
	CASE 84
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ阴部", "Ｔ後面")
			RETURNF 1
	CASE 87
		SIF FORBIDCOM(TARGET:1, "性交", "Ｔ下半身", "Ｔ後面")
			RETURNF 1
	CASE 88
		SIF FORBIDCOM(TARGET:1, "性交", "Ｐ下半身", "Ｔ下半身")
			RETURNF 1

	;手交などPLAYERの阴茎を手とかで責める
	CASE 90
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "手交")
			RETURNF 1
	CASE 91
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "口交")
			RETURNF 1
	CASE 92
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "乳交")
			RETURNF 1
	CASE 93, 101
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "素股")
			RETURNF 1
	CASE 94
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "泡泡浴")
			RETURNF 1
	CASE 95
		IF PENIS(MASTER)
			SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "足交")
				RETURNF 1
		ELSE
			SIF FORBIDCOM(TARGET:1, "Ｐ阴部", "足交")
				RETURNF 1
		ENDIF
	CASE 96
		;現状封印
		SIF FORBIDCOM(TARGET:1, "Ｐ肛門", "Ｐ後面")
			RETURNF 1
	CASE 97
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "内褲冲")
			RETURNF 1
	CASE 98
		SIF FORBIDCOM(TARGET:1, "Ｐ阴部", "Ｐ前面", "Ｔ顔")
			RETURNF 1
	CASE 104
		SIF FORBIDCOM(TARGET:1, "Ｐ顔")
			RETURNF 1
	;逆肛交
	CASE 125 TO 129
		SIF FORBIDCOM(TARGET:1, "Ｐ下半身", "Ｐ後面")
			RETURNF 1
	;逆強姦などPLAYERの阴部を責める
	CASE 120 TO 124
		SIF FORBIDCOM(TARGET:1, "Ｐ下半身", "Ｐ前面")
			RETURNF 1
	CASE 140, 150
		SIF FORBIDCOM(TARGET:1, "Ｐ阴茎", "口交")
			RETURNF 1
	CASE 141
		SIF FORBIDCOM(TARGET:1, "Ｐ指", "Ｔ阴部")
			RETURNF 1
	CASE 142
		SIF FORBIDCOM(TARGET:1, "Ｐ指", "Ｔ肛門")
			RETURNF 1
	CASE 143
		SIF FORBIDCOM(TARGET:1, "Ｐ指", "Ｔ阴部", "Ｔ肛門")
			RETURNF 1
	ENDSELECT
ENDIF


RETURNF 0

@NO_SELECTCOM, ARG
#DIM LCOUNT
LOCAL = -1
FOR LCOUNT, 0, 600
	SIF STRLENS(TRAINNAME:LCOUNT) == 0
		CONTINUE

	LOCAL += 1
	SIF ARG == LOCAL
		RETURN LCOUNT
NEXT
RETURN 999

