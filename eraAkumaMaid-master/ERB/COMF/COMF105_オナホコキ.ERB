@COM105
;飞机杯冲
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 105

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM105, ARG


@SOURCE_COM105, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "飞机杯冲"

CALL SOURCE_DOWNBASE_HP, 105

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "")*10
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 500
SOURCE:0:逸脱 = 500

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM105
;前回と同じ
IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:接吻
		PRINTFORM %CALLNAME:PLAYER%%TEXTS("接吻継続助詞付き")%、
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ELSEIF TEQUIP:面部騎乗
		PRINTFORM %CALLNAME:PLAYER%的臉頰用股間摩擦著、
	ELSEIF INSERT("合体中")
		PRINTFORM %TEXTS("合体中", TARGET)%
	ENDIF
	IF TALENT:恋慕 && TEQUIP:面部騎乗 == 0 && RAND:4 == 0
		PRINTFORMW 帶著愉快的笑容將%CALLNAME:PLAYER%挺立的陰莖細緻地用飛機杯持續擼動著……
	ELSEIF ABL:奉仕精神 >= 3 && TEQUIP:面部騎乗 + INSERT("合体中") == 0 && RAND:3 == 0
		PRINTFORMW 一邊凝視著%CALLNAME:PLAYER%恍惚的臉、一邊持續的用飛機杯擼動著陰莖……
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORMW 持續的用飛機杯擼動著陰莖、並熟練的調整頻率來延長快感……
	ELSE
		PRINTFORMW 入神的用飛機杯持續擼動著陰莖……
	ENDIF
;被逆推ているとき
ELSEIF TEQUIP:推倒
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:面部騎乗
		PRINT 跨坐在臉頰上的同時、
	;自尊心高いor淫乱or施虐属性Lv4以上
	ELSEIF (TALENT:高慢 || TALENT:淫乱 || ABL:施虐属性 >= 4) && RAND:2 == 0
		PRINT 帶著愉快的微笑
	;小悪魔
	ELSEIF TALENT:小悪魔
		PRINT 在臉上露出戲謔的壞笑的同時、
	;淫乱or欲情4LV以上
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 一邊妖艶的微笑著一邊
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 氣息絮亂的
	ELSEIF RAND:2 == 0 || TEQUIP:眼罩
		SIF RAND:2 == 0
			PRINT 好像很開心
		PRINT 微笑着一邊
	ENDIF
	PRINTFORM 將陰莖用飛機杯玩弄著、將
	IF PREVCOM == 1 || PREVCOM == 9
		PRINT 更加努力地侍奉著股間的
	ELSE
		PRINT 發出快樂而苦悶的哼聲的
	ENDIF
	PRINTFORMW %CALLNAME:PLAYER%的反應盡情享受著……
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%
	IF INSERT("合体中")
		PRINTFORM 将%TEXTS("合体中", TARGET)%%CALLNAME:PLAYER%的阴茎用飞机杯
	ELSEIF PALAM:PLAYER:Ｐ潤 < 3000
		PRINTFORM 用充分注入了黏稠的潤滑液的飛機杯、將%CALLNAME:PLAYER%的陰莖
	ELSEIF (PREVCOM >= 60 && PREVCOM <= 79) || INSERT("挿入")
		PRINTFORM 對被自己的害羞的淫液所浸透的%CALLNAME:PLAYER%的陰莖、用飛機杯
	ELSE
		PRINTFORM 對被黏液濡濕、閃爍著的%CALLNAME:PLAYER%的陰莖、用飛機杯
	ENDIF
	IF ABL:施虐属性 >= 3 && RAND:3 == 0
		PRINTFORM 套住後、用彷彿是想要捏斷一樣的大力
	ELSEIF TALENT:好奇心 && RAND:3 == 0
		PRINTFORM 套住後、在饒有興致的觀察著神態的同時、小心地
	ELSEIF MARK:屈服刻印 == 0 && TEQUIP:推倒 == 0 && RAND:3 == 0
		PRINTFORM 套住後、帶著相當複雜的表情
	ELSEIF RAND:3 == 0
		PRINTFORM 激烈地
	ELSEIF RAND:2 == 0
		PRINTFORM 噗嗤噗嗤地
	ELSE
		PRINTFORM 慢慢地、慢慢地
	ENDIF
	PRINTFORMW 擼動起來……
ENDIF


@COM_ORDER_COM105, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 5
	LOCALS = %LOCALS% - 害羞(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｐ", "")/5
	LOCAL -= COM_ORDER_STAIN("Ｐ", "")/5
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "")/5})
ENDIF

;合計を表示(25以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 25

RETURN RESULT
