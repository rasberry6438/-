@COM94
;泡泡浴
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 94

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM94, ARG
;汚れを初期化
CALL RESET_STAIN_EX, "全部", PLAYER
CALL RESET_STAIN_EX, "全部", TARGET

@SOURCE_COM94, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "泡泡浴"

CALL SOURCE_DOWNBASE_HP, 94

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 2000
SOURCE:0:逸脱 = 1500

;ABL:Ｃ感覚をみる
SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 50)
;ABL:Ｂ感覚をみる
SOURCE:0:Ｂ快楽 = CALCF("Ｂ刺激", 2, 50)

SIF TALENT:巨乳
	TIMES SOURCE:0:Ｂ快楽, 1.20

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM94
IF CHECK_COM("Ｗ奉仕")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 兩人
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT 神情恍惚、眼神濕潤的對視著的同時
		ELSEIF RAND:2 == 0
			PRINT 如同完全沉浸於快感一樣無意識的漏出涎水的同時
		ENDIF
		PRINTFORMW 將%CALLNAME:MASTER%用沾滿泡沫的胸部持續擦洗著身體……
	ELSE
		PRINTFORM %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 + TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 在用充滿慾望與依戀的雙瞳凝視著%CALLNAME:MASTER%赤裸身體的同時、
		IF RAND:2
			PRINTFORM 像是在競爭一樣的將泡沫塗滿自己的
		ELSE
			PRINTFORM 相互協助的將泡沫塗滿自己的
		ENDIF
		;それぞれの身体の差も描写すべきかなー
		IF RAND:3 == 0
			IF RAND:3 == 0
				PRINT 胸部
			ELSEIF RAND:2
				PRINT 乳房
			ELSE
				PRINT 胸
			ENDIF
		ELSEIF RAND:2
			PRINT 将身体
		ELSE
			PRINT 股間
		ENDIF
		PRINTW 來協助擦洗著…
	ENDIF
;同時推倒
ELSEIF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM 、%TEXTS("Ｗ推倒中")%、
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW 將%CALLNAME:MASTER%的身體抱住、用自己塗滿泡沫的身體持續熱情的摩擦著……
	ELSE
		PRINTFORMW 將%CALLNAME:MASTER%的身體用塗滿泡沫的胸部擦洗起來……
	ENDIF
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%
	IF TEQUIP:接吻
		PRINTFORM 在%TEXTS("接吻継続")%、
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ENDIF
	IF TALENT:貧乳 == 0 && RAND:3 == 0
		PRINTFORMW 將塗滿泡沫的綿軟胸部當做海綿、將%CALLNAME:PLAYER%的手腕和臉頰夾住擦洗起來……
	ELSEIF RAND:2
		PRINTFORMW 用沾滿泡沫的股間將手腕夾住、一邊享受著%CALLNAME:PLAYER%回禮一樣的愛撫一邊協助擦洗著身體
	ELSE
		PRINTFORMW 為%CALLNAME:PLAYER%的身體塗抹上泡沫、\@ ABL:奉仕精神 >= 3 ? 熱情地 # \@持續擦洗著……
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%
	IF RAND:2 == 0
		IF RAND:2 == 0
			PRINTFORM 跨坐在%CALLNAME:PLAYER%的手腕上、用自己沾滿泡沫的
		ELSE
			PRINTFORM 跨坐在仰躺在塑膠墊上的%CALLNAME:PLAYER%的背上、用自己沾滿泡沫的
		ENDIF
		SELECTCASE COND("陰毛")
		CASE IS >= 6
			PRINT 陰毛
		CASE 1 TO 3
			PRINT 光潔無毛的股間
		CASEELSE
			PRINT 股間
		ENDSELECT
		PRINTW 擦洗起來……
	ELSE
		PRINTFORM 將能打起泡沫的肥皂胡亂地塗抹在身上、
		SIF TALENT:恋慕
			PRINT 在陶醉地凝視著的同時

		IF TALENT:妊娠 && RAND:2
			PRINTFORM 用因%CALLNAME:PLAYER%而懷孕膨脹起來的腹部
		ELSEIF TALENT:巨乳 && RAND:2
			IF TALENT:娇小
				PRINTFORM 用和身體相比過分豐滿的胸部
			ELSE
				PRINTFORM 用豐腴柔軟的胸部
			ENDIF
		ELSEIF TALENT:娇小 && RAND:2
			PRINTFORM 用嬌小的身體
		ELSEIF RAND:2
			PRINTFORM 用全身
		ELSE
			PRINTFORM 将身体
		ENDIF
		SIF ABL:奉仕精神 >= 3
			PRINTFORM 全心地
		PRINTW 來協助擦洗著…
	ENDIF
ENDIF

@COM_ORDER_COM94, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 3
	LOCALS = %LOCALS% - 害羞(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

;汚れ有
IF COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")})
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
