@COM95
;足交
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 95

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM95, ARG

@SOURCE_COM95, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "足交"

CALL SOURCE_DOWNBASE_HP, 95

IF PENIS(PLAYER)
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "脚")*10 + 60
ELSE
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｖ", "脚")*10 + 60
ENDIF
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 400
SOURCE:0:逸脱 = 400

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM95
;TEQUIP:足交のbitで行動管理
;0bit 踏みつけ,1bit 股間見せ, 2bit 両足
IF CHECK_COM("Ｗ奉仕")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 兩人
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT 神情恍惚、眼神濕潤的對視著的同時
		ELSEIF RAND:2 == 0
			PRINT 如同完全沉浸於快感一樣無意識的漏出涎水的同時
		ENDIF
		PRINTFORM %CALLNAME:MASTER%的
		IF PENIS(PLAYER)
			PRINT 阴茎
		ELSE
			PRINTFORM \@ PREVPLAY("Ｖ挿入", MASTER) ? 黏黏糊糊被放鬆的 # \@阴部
		ENDIF
		PRINTFORMW 用兩隻靈巧柔軟的纖足蹂躪玩弄著……
	ELSE
		PRINTFORM %CALLNAME:TARGET%和%CALLNAME:(TARGET:1)%
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕
			IF RAND:3 == 0
				PRINTFORM 在用充滿慾望與依戀的雙瞳凝視著%CALLNAME:MASTER%赤裸身體的同時、
			ELSEIF RAND:2 == 0
				PRINTFORM 保持著能將股間清楚看見的良好姿勢、
				SETBIT TEQUIP:足交, 1
			ENDIF
		ENDIF
		SIF RAND:2 == 0
			PRINT 相互協助的
		IF PENIS(PLAYER)
			PRINTFORMW 將陰莖用兩隻靈活的纖足擼動起來……
		ELSE
			PRINTFORMW 將陰部用兩隻靈活的纖足戲弄起來……
		ENDIF
	ENDIF
;同時推倒
ELSEIF TEQUIP:Ｗ推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ推倒中")%、
	PRINTFORM %CALLNAME:MASTER%的
	IF PENIS(PLAYER)
		PRINT 陰莖
	ELSE
		PRINT 陰部
	ENDIF
	IF ABL:施虐属性 >= 3 && ABL:技巧 >= 3 && RAND:2 == 0
		PRINT 技巧嫻熟的
	ELSEIF ABL:施虐属性 >= 3
		PRINT 像是在享受痛苦的反應一樣粗暴地
	ELSEIF ABL:技巧 >= 3 && PENIS(PLAYER)
		PRINT 扑哧扑哧地
	ELSEIF ABL:施虐属性 < 3 || ABL:技巧 < 3
		PRINT 笨拙而小心的
	ENDIF
	IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		IF PENIS(PLAYER)
			PRINTW 持續擼動著……
		ELSE
			PRINTW 持續蹂躪著……
		ENDIF
	ELSE
		IF PENIS(PLAYER)
			PRINTW 擼動著……
		ELSE
			PRINTW 蹂躪著……
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%
	;被逆推中
	IF TEQUIP:推倒
		SIF PENIS(PLAYER) == 0
			SETBIT TEQUIP:足交, 0
		IF TEQUIP:面部騎乗 && ABL:施虐属性 >= 3
			PRINTFORM 保持著將%CALLNAME:PLAYER%的臉頰用股間壓迫著的姿勢、
			TEQUIP:足交 = 0
		ELSEIF SELECTCOM == PREVCOM
		;施虐属性3以上
		ELSEIF ABL:施虐属性 >= 3 && RAND:2 && GETBIT(TEQUIP:足交, 0) == 0
			PRINTFORM %CALLNAME:PLAYER%の
			IF TALENT:娇小
				PRINT 胸
			ELSE
				PRINT 神情恍惚的脸颊
			ENDIF
			PRINT 將腰肢毫不留情地重重落下、向下半身伸出了雙腳、
		ELSEIF GETBIT(TEQUIP:足交, 0)
			IF ABL:施虐属性 >= 3 && COND("脚の使用", PLAYER) && RAND:2
				PRINTFORM 將%CALLNAME:PLAYER%放倒在地、將雙腿腳踝抓緊後高高舉起、做出準備進行電氣按摩的姿勢、
			ELSE
				PRINTFORM 將%CALLNAME:PLAYER%佈滿緊張地臉頰
				SIF ABL:施虐属性 >= 3
					PRINT 愉快的邪笑着
				PRINT 俯瞰的同時、
			ENDIF
			;面部騎乗解除
			TEQUIP:面部騎乗 = 0
		ENDIF
	;休闲褲
	ELSEIF EQUIP:下衣 && CHECK_BOTTOM("休闲褲")
	;露出癖が低め
	ELSEIF ABL:露出癖 - TALENT:害羞 + TALENT:不知羞恥 + TALENT:好色 + TALENT:淫乱 + TALENT:小悪魔 < RAND:4
		PRINT 若無其事地將股間用
		;圍裙
		IF TEQUIP:新妻PLAY
			PRINT 圍裙的下擺
		;裙のある服
		ELSEIF EQUIP:裙
			PRINTFORM %SKIRT(TARGET, "略")%
		ELSE
			PRINTFORM 雙手
		ENDIF
		PRINT 遮掩著的同時、
	;露出癖3以上or施虐狂or淫乱で休闲褲(これは上のほうで弾き済み)でない
	ELSEIF ABL:奉仕精神 + TALENT:献身*2 >= 4 || ABL:露出癖 + ABL:施虐属性 >= 3 || TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 + TALENT:小悪魔 || PALAM:欲情 >= 10000
		;裙などを是ているなら
		IF EQUIP:裙 + TEQUIP:新妻PLAY && RAND:3
			;圍裙
			IF TEQUIP:新妻PLAY
				PRINT 將圍裙
			ELSE
				PRINTFORM 將%SKIRT(TARGET, "略")%
			ENDIF
			IF COND("口の使用") && RANDIF(2, COND("指の使用") )
				PRINT 捲起、將下擺用嘴含住、將
			ELSEIF COND("指の使用") && RAND:2
				PRINT 用指尖捏起下擺、將
			ELSE
				PRINT 卷起、
			ENDIF
		;胖次を是ているなら、ずらすことも
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可") && RAND:5 == 0
			CALL SETFLAG, "下衣を１枚ずらす", TARGET
		ENDIF
		IF TEQUIP:振動棒 || TEQUIP:肛用振動棒 || TEQUIP:肛塞
			SIF TEQUIP:振動棒 || TEQUIP:肛用振動棒
				PRINT 振動棒
			SIF TEQUIP:振動棒 && TEQUIP:肛塞
				PRINT 和
			SIF TEQUIP:肛塞
				PRINT 肛塞
			;淫乱
			IF TALENT:淫乱
				PRINT 深深吞入體內的
			ELSE
				PRINT 所貫穿的
			ENDIF
			PRINT 股間
		ELSEIF TALENT:男性
			PRINT 勃起的陰莖
		;胖次を是ていて、ずらしていない
		ELSE
			PRINTFORM 將%TEXTS("阴部：視覚")%
		ENDIF
		PRINT 用灼熱的視線視奸著的同時、
		SETBIT TEQUIP:足交, 1
	ENDIF
	IF PENIS(PLAYER)
		SIF TEQUIP:推倒 == 0
			PRINTFORM %CALLNAME:PLAYER%の
		PRINT 陰莖
	ELSE
		PRINTFORM \@ PREVPLAY("Ｖ挿入", MASTER) ? 被狠狠侵犯過的 # 毫無防備的 \@阴部
	ENDIF
	IF TEQUIP:推倒 == 0 && ABL:施虐属性 >= 3 && RAND:3 == 0
		PRINT 用雙腳
		SETBIT TEQUIP:足交, 2
	;靴を履いている
	ELSEIF EQUIP:靴
		PRINTFORM 用%SHOES(TARGET, "略")%的堅硬底端
	;短襪を是ている
	ELSEIF EQUIP:襪子
		PRINTFORM 用被%SOCKS(TARGET, "略")%包裹著的腳
	;動物足
	ELSEIF TALENT:動物足 && CHOSECOM(SELECTCOM) == 0
		PRINT 用非人的獸腳
	ELSE
		PRINT 用腳
	ENDIF
	IF GETBIT(TEQUIP:足交, 0)
		PRINT 轉動著
	ELSEIF ABL:施虐属性 >= 3 && ABL:技巧 >= 3 && RAND:2 == 0
		PRINT 技巧嫻熟的
		SIF RAND:2
			PRINT 不斷變化蹂躪節奏的
	ELSEIF ABL:施虐属性 >= 3
		PRINT 像是在享受痛苦的反應一樣粗暴地
	ELSEIF ABL:技巧 >= 3 && PENIS(PLAYER)
		PRINT 扑哧扑哧地
	ELSEIF ABL:施虐属性 < 3 || ABL:技巧 < 3
		PRINT 笨拙而小心的
	ENDIF
	IF PREVCOM == SELECTCOM && GETBIT(TEQUIP:足交, 0) && COND("前Ｔ調教者") == PLAYER
		PRINT 持續猛力踐踏著
	ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		IF PENIS(PLAYER)
			PRINT 持續擼動著
		ELSE
			PRINT 持續蹂躪著
		ENDIF
	ELSEIF GETBIT(TEQUIP:足交, 0)
		PRINT 開始猛烈地踐踏起來
	ELSE
		IF PENIS(PLAYER)
			PRINT 擼動起來
		ELSE
			PRINT 蹂躪起來
		ENDIF
	ENDIF
	PRINTW …
ENDIF


@COM_ORDER_COM95, ARG
#DIM MEMO_STAIN
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:施虐属性
IF ABL:施虐属性
	LOCAL += MIN(ABL:施虐属性, 5) * 5
	LOCALS = %LOCALS% + 施虐属性LV{ABL:施虐属性}({MIN(ABL:施虐属性, 5) * 5})
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;害羞
IF TALENT:害羞
	LOCAL -= 5
	LOCALS = %LOCALS% - 害羞(5)
ENDIF
;否定快感
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF

IF PENIS(PLAYER)
	MEMO_STAIN = COM_ORDER_STAIN("Ｐ", "脚") / 5
ELSE
	MEMO_STAIN = COM_ORDER_STAIN("Ｖ", "脚") / 5
ENDIF
;汚れ有
IF MEMO_STAIN
	LOCAL -= MEMO_STAIN
	LOCALS = %LOCALS% - 汚れ有
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({MEMO_STAIN})
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
