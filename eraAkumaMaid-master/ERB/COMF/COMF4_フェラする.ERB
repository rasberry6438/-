@COM4
;舐陰莖
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精格子
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM4, ARG
CALL SETFLAG, "口交フラグ", PLAYER
SIF CHECK_CLO("Ｖ露出") == 0
	CALL SETFLAG, "下衣を露出：テ接吻ト没有", TARGET
;対象のＰ⇔調教者の口的污垢が移動
CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｍ"
SIF CHECK_COM("清洁口交", 4)
	CALL SETFLAG, "清洁口交", PLAYER

@SOURCE_COM4, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 4

SOURCE:0:露出 = 220
SOURCE:0:逸脱 = 50

;男性か扶她
IF PENIS(TARGET)
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 1)
	;調教者の舌靈活をみる
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "舌靈活", PLAYER)
	;阴茎への刺激にはBOUNS
	SOURCE:0:Ｃ快楽 += 500
ENDIF

SIF CHECK_COM("清洁口交", 4)
	TIMES SOURCE:0:Ｃ快楽, 0.10

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM4
VARSET LOCALS
;被逆推ているとき(同時含む)
IF TEQUIP:推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ推倒中")%、
	ELSEIF SELECTCOM == PREVCOM && TALENT:MASTER:男性 == 0
		PRINTFORM 將苦悶的浮起淚滴的
	ELSE
		PRINTFORM 身體被牢牢禁錮住、動彈不得的
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORM %CALLNAME:PLAYER%の
		IF (ABL:PLAYER:技巧 >= 4 || ABL:施虐属性 >= 3) && RAND:2 == 0
			PRINT 含住陰莖的柔韌咽喉
		ELSE
			PRINT 包裹著陰莖的口腔
		ENDIF
		SIF ABL:施虐属性 >= 3
			PRINT 毫不留情地
		PRINTFORMW 持續侵犯起來……
	ELSE
		PRINTFORM 在%CALLNAME:PLAYER%的口腔中、用
		IF CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && COND("前Ｔ調教者") == PLAYER
			IF PREVPLAY("Ａ挿入", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 將腸道粘膜塗滿白濁的
				ELSE
					PRINT 從肛門中拔出的
				ENDIF
			ELSEIF PREVPLAY("Ｖ挿入", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 將子宮用精子填滿的
				ELSE
					PRINT 從膣道中拔出的
				ENDIF
			;まだ無い…っていうか作るのだろうかこれ
			ELSEIF PREVPLAY("尿道挿入", PLAYER)
				;前回中出し
				IF PREVEX("射精")
					PRINT 將膀胱用精液污染的
				ELSEIF PENIS(PLAYER)
					PRINT 從阴茎中拔出的
				ELSE
					PRINT 從尿道中拔出的
				ENDIF
			ELSEIF PREVPLAY("尿道挿入")
				;前回中出しされた
				IF PREVEX("尿道内射精")
					PRINT 剛剛被精液注滿的
				ELSE
					PRINT 剛剛尿道享受過侵犯的
				ENDIF
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				PRINT べっとりと
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				PRINT 塗滿甜膩愛液的
			ENDIF
			PRINTFORM 髒污的
		ELSE
			PRINTFORM %TEXTS("阴茎：視覚", TARGET)%
		ENDIF
		PRINTFORM %TEXTS("阴茎：呼び方", TARGET)%
		IF ABL:施虐属性 >= 5 && RAND:3 == 0
			PRINT 一口気
		ELSEIF ABL:施虐属性 >= 4 && RAND:2 == 0
			PRINT 以最深處為目標
		ELSEIF RAND:2 == 0
			PRINT 溫柔緩慢地
		ENDIF
		IF ABL:施虐属性 >= 5 && RAND:3 == 0
			PRINT 刺入其中、
		ELSEIF ABL:施虐属性 >= 3 && RAND:2 == 0
			PRINT 插入其中、
		ELSE
			PRINT 挑弄著口腔各處、
		ENDIF
		PRINT 將頭不斷地下壓
		SIF ABL:施虐属性 >= 3 && RAND:3 == 0
			PRINT 就像是在正常性交一樣
		IF ABL:施虐属性 >= 5 && RAND:3 == 0
			PRINTFORMW 粗暴地凌辱著口腔的深處……
		ELSEIF ABL:施虐属性 >= 3 && RAND:2 == 0
			PRINTFORMW 激烈地前後進出著……
		ELSE
			PRINTFORMW 前後动起了腰…
		ENDIF
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%将
	;前回尿道で楽しんでいた
	IF PREVPLAY("尿道挿入")
		;前回中出しされた
		IF PREVEX("尿道内射精")
			PRINT 剛剛灌滿精液、不禁垂落白濁涎水的陰莖
		ELSE
			PRINT 剛剛拔出陰莖、鈴口大開散發著內部氣味的陰莖
		ENDIF
	;前回尿道指挿入
	ELSEIF PREVCOM == 15
		IF RAND:2
			PRINTFORM 被手指插入蹂躪過深處的陰莖
		ELSE
			PRINTFORM 被手指反復出入、鈴口大開的陰莖
		ENDIF
	;前回ブジーで弄られた
	ELSEIF PREVCOM == 151
		PRINTFORM 被金屬小棒%TEXTR("反復輕柔地抽插/輕輕地翻攪摩挲/稍微地擴張/溫柔地蹂躪")%之後、鈴口大開的陰莖
	;チンカス口交
	ELSEIF CHECK_COM("恥垢清洁口交")
		IF COND("真性包茎")
			PRINTFORM %CALLNAME:TARGET%被%TEXTR("恥垢/精尿混合物/污垢/乳酪/白色陰垢")%堵塞最深處的%TEXTS("阴茎：呼び方", TARGET)%
			PRINTFORMW 用舌尖插入包皮、一邊細細品味著味道、一邊用涎水化開污垢、將濃稠的污漬吮吸打掃著……
		ELSEIF COND("假性包茎")
			IF RAND:3
				PRINTFORM %CALLNAME:TARGET%的%TEXTS("阴茎：呼び方", TARGET)%
				PRINTFORM 的%TEXTR("恥垢/精尿混合物/污垢/乳酪/白色陰垢")%沾滿全身的龜頭
				PRINTFORMW %TEXTR("像是嬰兒吮吸奶瓶一樣吮吸著/用涎水稀釋後飲下/用嘴唇與舌尖慢慢刮入口中")%…
			ELSEIF RAND:3
				PRINTFORM %CALLNAME:TARGET%的%TEXTS("阴茎：呼び方", TARGET)%
				PRINTFORM 用舌頭慢慢伸入、將其中積存的%TEXTR("恥垢/精尿混合物/污垢/乳酪/白色陰垢")%
				PRINTFORMW %TEXTR("用涎水稀釋後用舌尖攪拌起來/用舌尖慢慢地舔舐刮取了")%…
			ELSE
				PRINTFORM %CALLNAME:TARGET%的%TEXTS("阴茎：呼び方", TARGET)%
				PRINTFORM 的包皮剝開後將積存著的%TEXTR("恥垢/精尿混合物/污垢/乳酪/白色陰垢")%
				PRINTFORMW %TEXTR("舔舐著送入口中/啾、啾的吮吸著")%…
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:TARGET%的阴茎
			PRINTFORM %TEXTR("的恥垢/散發劇臭的殘渣/緊緊黏附的污垢/的奶酪一樣的污垢")%
			PRINTFORMW 的酸澀苦味與劇臭深深烙印在腦海深處的同時、%TEXTR("用舌尖徹底而漂亮的打掃乾淨了/用整個口腔認真精心地掃除著")%…
		ENDIF
		RETURN 1
	;汚れ有
	ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && COND("前Ｔ調教者") > 0
		IF PREVPLAY("挿入", COND("前Ｔ調教者") )
			LOCALS = \@ COND("前Ｔ調教者") == PLAYER ? 自己 # %CALLNAME:COND("前Ｔ調教者")% \@的
			IF PREVPLAY("Ａ挿入", COND("前Ｔ調教者") )
				IF PREVEX("射精")
					LOCALS = 把%LOCALS%腸道粘膜塗滿白濁的
				ELSE
					LOCALS = 從%LOCALS%肛門中拔出的
				ENDIF
			ELSEIF PREVPLAY("Ｖ挿入", COND("前Ｔ調教者") )
				IF PREVEX("射精")
					LOCALS = 把%LOCALS%子宮用精子填滿的
				ELSE
					LOCALS = 從%LOCALS%膣道中拔出的
				ENDIF
			ENDIF
			IF PREVEX("射精")
				IF TALENT:相思相愛 && RAND:3 == 0
					LOCALS = %LOCALS%、最愛的人的
				ELSEIF SIZE("阴茎", TARGET) >= 2 && RAND:2
					LOCALS = %LOCALS%、\@ ABL:施虐属性 >= 3 ? 凶悪的 # 粗壯的 \@
				ELSEIF SIZE("阴茎", TARGET) < 2 && AGE(TARGET) <= 2 && RAND:2
					LOCALS = %LOCALS%、稍稍萎縮、變得可愛的
				ELSE
					LOCALS = %LOCALS%、處於半勃起状態的
				ENDIF
			ELSEIF RAND:3 == 0
				LOCALS = %LOCALS%、灼熱的散發著熱汽的
			ELSEIF RAND:2
				LOCALS = %LOCALS%、散發著厲害的%TEXTS("气味")%的
			ELSE
				LOCALS = %LOCALS%
			ENDIF
		ENDIF
		IF LOCALS == ""
			IF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				LOCALS = 被腸內污液弄髒的
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				LOCALS = 被甜膩愛液污染的
			ENDIF
		ENDIF
		PRINTFORM %LOCALS%阴茎
	ELSE
		PRINTFORM %CALLNAME:TARGET%的%TEXTS("阴茎：視覚", TARGET)%阴茎
	ENDIF
	IF CHECK_COM("清洁口交")
		IF PREVPLAY("尿道挿入") && PREVEX("尿道内射精")
			PRINTFORMW 用%TEXTR("吮吸的方式將內部的精液處理乾淨了/舌尖漂亮的打掃乾淨/整個口腔仔細地掃除著")%……
		ELSE
			PRINTFORMW 用%TEXTR("舌尖漂亮的打掃乾淨/整個口腔仔細地掃除著")%……
		ENDIF
	ELSEIF TEQUIP:手下留情
		PRINTFORMW 用舌頭試著挑起慾望的輕輕玩弄著……
	ELSEIF ABL:PLAYER:受虐属性 >= 3 && CHECK_STAIN(STAIN:Ｐ, "Ｖ", "Ａ") && RAND:2
		PRINTFORMW 用整個口腔仔細地掃除的同時、盡情的享受著口中的淫靡觸感……
		STAIN:PLAYER:Ｍ |= STAIN:Ｐ
		CALL RESET_STAIN_EX, "Ｐ", TARGET
	ELSEIF ABL:尿道拡張 >= 0 && RAND:3 == 0
		PRINTFORMW 用舌尖%TEXTR("輕輕插入/反復進出")%的侵犯著……
	ELSEIF RAND:2
		PRINTFORMW 用嘴唇深深含住並用舌頭舔舐著……
	ELSE
		PRINTFORMW 用嘴唇深深含住後激烈地擼動起來……
	ENDIF
ENDIF
