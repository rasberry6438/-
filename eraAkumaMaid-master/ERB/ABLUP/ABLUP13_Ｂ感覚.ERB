;-------------------------------------------------
;ABL:Ｂ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP13
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP13
;何もできない場合
IF RESULT == 0 && ABL:Ｂ感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｂ感覚 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF
;特殊な選択肢
SET_CHOICE = 0
;淫乳を得る
IF TALENT:淫乳 == 0
	IF COND("淫乳取得可能")
		PRINTFORML  [10] 獲得[淫乳]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[淫乳](要:快Ｂの珠{NUM("宝珠：淫乳取得")}、Ｂ感覚4LV、今回Ｂ絶頂数10)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｂ >= NUM("宝珠：淫乳取得") && TALENT:淫乳 == 0
		JUEL:快Ｂ -= NUM("宝珠：淫乳取得")
		TALENT:淫乳 = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		IF CONFIG("乳頭奸") && TALENT:巨乳
			PRINTFORMW T嬌吟著將手指插進自己已經淫猥的盛開的乳頭中摩挲起來、展示一樣進行著輕微的自慰行為……
			PRINTFORMW ……這樣的話、對胸部使用更過激的PLAY也不是不可能了……
			CALL PRINT_STR, "爱心粉紅_※現在、可以執行乳頭奸指令了※_W"
		ELSEIF TALENT:恋慕 || TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%在性行為結束之後、仍然死乞白賴的希望%CALLNAME:MASTER%蹂躪她的胸脯……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%的胸部在和%CALLNAME:MASTER%情事之中得到了充分的開發、已經變得非常敏感了……
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[淫乳]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｂ感覚 += 1
		PRINTFORMW Ｂ感覚等級提升到{ABL:Ｂ感覚}LV了。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP13
SIF RESULT
	RESTART



;ABL:Ｂ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP13, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｂ感覚
CASE 0
	JUEL:0:快Ｂ = 500
CASE 1
	JUEL:0:快Ｂ = 1000
CASE 2
	JUEL:0:快Ｂ = 3000
CASE 3 TO 4
	JUEL:0:快Ｂ = (ABL:Ｂ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｂ = 10000 + (ABL:Ｂ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｂ = 70000 + (ABL:Ｂ感覚 - 9) * 30000
ENDSELECT

;淫乳が獲得できる？
SIF COND("淫乳取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｂ感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快Ｂ && JUEL:快Ｂ >= JUEL:0:快Ｂ
	RETURN 1
RETURN 0

