;-------------------------------------------------
;ABL:Ｃ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP10
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP10
;何もできない場合
IF RESULT == 0 && ABL:Ｃ感覚 >= 99
	PRINTFORMW 已經抵達MAX了。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｃ感覚 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF
;特殊な選択肢
SET_CHOICE = 0
;淫核を得る
IF TALENT:淫核 == 0
	IF COND("淫核取得可能")
		PRINTFORML  [10] 獲得[淫核]
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] 獲得[淫核](要:快Ｃの珠{NUM("宝珠：淫核取得")}、\@ PENIS(TARGET) ? Ｐ # Ｃ \@感覚4LV、今回Ｃ絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 不、再考慮一下吧。

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｃ >= NUM("宝珠：淫核取得") && TALENT:淫核 == 0
		JUEL:快Ｃ -= NUM("宝珠：淫核取得")
		TALENT:淫核 = 1
		PRINTFORMW %CALLNAME:TARGET%看向這邊的眼神很奇怪……
		IF TALENT:恋慕 || TALENT:淫乱
			PRINTFORM %CALLNAME:TARGET%在性行為結束之後、仍然喘息着不斷用
			PRINTFORMW \@ PENIS(TARGET) ? 陰莖 # 陰蒂 \@摩擦著%CALLNAME:MASTER%的身體、希望止住瘙痒……
		ELSE
			PRINTFORM %CALLNAME:TARGET%一邊用朦朧的眼神凝視著%CALLNAME:MASTER%、一邊用手將自己的
			PRINTFORMW \@ PENIS(TARGET) ? 陰莖 # 陰豆 \@反復地揉搓著愛撫著……
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORMW %CALLNAME:TARGET%獲得[淫核]
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｃ感覚 += 1
		ABL:Ｐ感覚 += 1
		IF PENIS(TARGET)
			PRINTFORMW Ｐ感覚等級提升到LV{ABL:Ｐ感覚}了。
		ELSE
			PRINTFORMW Ｃ感覚等級提升到LV{ABL:Ｃ感覚}了。
		ENDIF
	ENDIF
ENDSELECT

;Ｐ感覚とＣ感覚は等しい
ABL:Ｐ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)
ABL:Ｃ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)

;まだレベルが上げられたりする場合には最初に返回
CALL IS_ABLUP10
SIF RESULT
	RESTART



;ABL:Ｃ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP10, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｃ感覚
CASE 0
	JUEL:0:快Ｃ = 500
CASE 1
	JUEL:0:快Ｃ = 1000
CASE 2
	JUEL:0:快Ｃ = 3000
CASE 3 TO 4
	JUEL:0:快Ｃ = (ABL:Ｃ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｃ = 10000 + (ABL:Ｃ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｃ = 70000 + (ABL:Ｃ感覚 - 9) * 30000
ENDSELECT

;淫核が獲得できる？
SIF COND("淫核取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｃ感覚 >= 99
	RETURN 0

;条件のどれかを阴蒂アしてる？
SIF JUEL:0:快Ｃ && JUEL:快Ｃ >= JUEL:0:快Ｃ
	RETURN 1
RETURN 0

