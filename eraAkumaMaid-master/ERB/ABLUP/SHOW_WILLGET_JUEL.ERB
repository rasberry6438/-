;USERCOMから呼び出す
@SHOW_WILLGET_JUEL
#DIM LCOUNT
#DIM MEMO_LINECOUNT

PRINTL 現在的宝珠取得予定数
PRINTL 
;Ｖ潤Ａ潤Ｐ潤は無いので15番までで良い
FOR LCOUNT, 0, 16
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	;パラメータ名・桁数を揃えつつパラメータを表示
	PRINTFORM %PALAMNAME:LCOUNT% {PALAM:LCOUNT, 10} 
	;桁数を揃えつつ現在＋予定＝合計を表示
	PRINTFORM %PALAMNAME:LCOUNT%の珠×({JUEL:LCOUNT, 10} + 

	SIF GET_JUEL(LCOUNT)
		SETCOLOR DEF_COLOR("黄色")
	PRINTFORM {GET_JUEL(LCOUNT), 10}
	RESETCOLOR

	PRINTFORM ) ={JUEL:LCOUNT + GET_JUEL(LCOUNT), 10}

	;快ＣＶＡＢＭ尿は絶頂回数を表示
	SELECTCASE PALAMNAME:LCOUNT
	CASE "快Ｃ"
		SIF EX:Ｃ絶頂 >= 1
			PRINTFORM  < Ｃ絶頂:{EX:Ｃ絶頂, 2}回 >
	CASE "快Ｖ"
		SIF EX:Ｖ絶頂 >= 1
			PRINTFORM  < Ｖ絶頂:{EX:Ｖ絶頂, 2}回 >
	CASE "快Ａ"
		SIF EX:Ａ絶頂 >= 1
			PRINTFORM  < Ａ絶頂:{EX:Ａ絶頂, 2}回 >
	CASE "快Ｂ"
		SIF EX:Ｂ絶頂 >= 1
			PRINTFORM  < Ｂ絶頂:{EX:Ｂ絶頂, 2}回 >
	CASE "快Ｍ"
		SIF EX:Ｍ絶頂 >= 1
			PRINTFORM  < Ｍ絶頂:{EX:Ｍ絶頂, 2}回 >
	CASE "快尿"
		SIF EX:尿道絶頂 >= 1
			PRINTFORM  < 尿道絶頂:{EX:尿道絶頂, 2}回 >
	ENDSELECT
	PRINTL 
NEXT

PRINTL 

MEMO_LINECOUNT = LINECOUNT
;ABLUPも表示
CALL PRINT_SHOW_ABLUP_SELECT, "予定"
SIF LINECOUNT > MEMO_LINECOUNT
	PRINTL 

PRINTL 反感之珠會優先和欲情・恭順・屈服之珠相互抵消。
PRINTL 之後、才會和恥情・苦痛・恐怖之珠相互抵消。
IF EXP_GAIN("媚薬経験")
	SETCOLOR DEF_COLOR("イエロー")
	PRINTL ※因为使用了媚薬、所以獲得的珠的量減少了※
	RESETCOLOR
ENDIF
WAIT


@GET_JUEL(ARG, ARG:1)
#FUNCTION
SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

SELECTCASE ARG
;Ｖ潤、Ａ潤、Ｐ潤の珠は存在しない
CASE IS >= 20
	RETURNF 0
;反感の珠だけは対応パラメーターによる取得数が他の10倍
CASE 7
	LOCAL = CALC_JUEL(ARG, ARG:1)*10
CASEELSE
	LOCAL = CALC_JUEL(ARG, ARG:1)
ENDSELECT
RETURNF LOCAL

;JUEL:ARGをいくつ得るか計算する
@CALC_JUEL(ARG, ARG:1)
#FUNCTION

SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

;パラメータレベルによる取得数
SELECTCASE PALAM:(ARG:1):ARG
;LV 0
CASE IS < 100
	LOCAL = 0
;LV1
CASE 100 TO 499
	LOCAL = 1 + (PALAM:(ARG:1):ARG - 100) * 9 / 399
;LV2
CASE 500 TO 2999
	LOCAL = 10 + (PALAM:(ARG:1):ARG - 500) * 90 / 2499
;LV3
CASE 3000 TO 9999
	LOCAL = 100 + (PALAM:(ARG:1):ARG - 3000) * 900 / 6999
;LV4
CASE 10000 TO 29999
	LOCAL = 1000 + (PALAM:(ARG:1):ARG - 10000) * 1000 / 19999
;LV5
CASE 30000 TO 59999
	LOCAL = 2000 + (PALAM:(ARG:1):ARG - 30000) * 1000 / 29999
;LV6
CASE 60000 TO 99999
	LOCAL = 3000 + (PALAM:(ARG:1):ARG - 60000) * 2000 / 39999
;LV7
CASE 100000 TO 149999
	LOCAL = 5000 + (PALAM:(ARG:1):ARG - 100000) * 3000 / 49999
;LV8
CASE 150000 TO 249999
	LOCAL = 8000 + (PALAM:(ARG:1):ARG - 150000) * 4000 / 99999
;LV9
CASE 250000 TO 499999
	LOCAL = 12000 + (PALAM:(ARG:1):ARG - 250000) * 4000 / 249999
;LV10
CASE 500000 TO 999999
	LOCAL = 16000 + (PALAM:(ARG:1):ARG - 500000) * 4000 / 499999
;LV11以上
CASEELSE
	LOCAL = 20000 + (PALAM:(ARG:1):ARG - 1000000) * 5000 / 3999999
ENDSELECT


;快ＣＶＡＢＭ尿絶頂BOUNS＆性向によるBOUNS
SELECTCASE PALAMNAME:ARG
CASE "快Ｃ"
	LOCAL += EX:(ARG:1):Ｃ絶頂*1000 + JUEL:(ARG:1):快ＣBOUNS
	SIF TALENT:(ARG:1):Ｃ性向
		TIMES LOCAL, 1.50
CASE "快Ｖ"
	LOCAL += EX:(ARG:1):Ｖ絶頂*1000 + JUEL:(ARG:1):快ＶBOUNS
	SIF TALENT:(ARG:1):Ｖ性向
		TIMES LOCAL, 1.50
CASE "快Ａ"
	LOCAL += EX:(ARG:1):Ａ絶頂*1000 + JUEL:(ARG:1):快ＡBOUNS
	SIF TALENT:(ARG:1):Ａ性向
		TIMES LOCAL, 1.50
CASE "快Ｂ"
	LOCAL += EX:(ARG:1):Ｂ絶頂*1000 + JUEL:(ARG:1):快ＢBOUNS
	SIF TALENT:(ARG:1):Ｂ性向
		TIMES LOCAL, 1.50
CASE "快Ｍ"
	LOCAL += EX:(ARG:1):Ｍ絶頂*1000 + JUEL:(ARG:1):快ＭBOUNS
	SIF TALENT:(ARG:1):Ｍ性向
		TIMES LOCAL, 1.50
CASE "快尿"
	LOCAL += EX:(ARG:1):尿道絶頂*1000 + JUEL:(ARG:1):快尿BOUNS
ENDSELECT


;媚薬によるペナルティ
SIF EXP_GAIN("媚薬経験", ARG:1) > 0
	LOCAL /= 1 + EXP_GAIN("媚薬経験", ARG:1)
SIF TFLAG:进行性処理
	LOCAL /= 3

RETURNF LOCAL

