;ARGには作成する角色のNO、ARGSは名前が変更された場合にはそれが入っている
@CHARA_MANUAL, ARG, ARGS
#DIM MEMO_FLAG

MEMO_FLAG = FLAG:項目OFF
STR:髪記録 = 
STR:表情記録 = 
STR:淫紋記録 = 
FLAG:項目OFF = 0

CALL CHARA_MANUAL_MAIN, ARG, ARGS

FLAG:項目OFF = MEMO_FLAG
STR:髪記録 = 
STR:表情記録 = 
STR:淫紋記録 = 
SIF RESULT
	RETURN 1
RETURN 0

@CHARA_MANUAL_MAIN, ARG, ARGS
#DIMS CHOICES
#DIM ACTOR

TFLAG:システム文スキップ = 1
;解説、弱点、性格、難易度、助手能力表示
CALL PROFILE, ARG, ARGS
TFLAG:システム文スキップ = 0

ACTOR = CHARANUM-1
IF FLAG:オープニング
	CALL PRINT_STRL, "NOBUTTON_这様可以吗？（[ 3]～[ 4]在設定後也会随机属性）"
	IF ACTOR == MASTER
		CALL PRINT_STRL, "[ 0]是　_NOBUTTON_[×]否　_[ 2]随机属性　[ 3]素質的操作　[ 4]Config"
	ELSE
		CALL PRINT_STRL, @"[ 0]是　_NOBUTTON_[×]否　_[ 2]随机属性　[ 3]素質的操作　[ 4]Config　[ 5]\@ CFLAG:ACTOR:颜绘 ? 変更 # 設定 \@颜绘"
	ENDIF
ELSE
	IF COND("種族：悪魔", ACTOR) && COND("基本情報隠蔽", ACTOR) == 0
		PRINTPLAINFORM 这个悪魔可以吗？（[ 3]～[ 4]在設定後也会随机属性）
	ELSEIF CSTR:ACTOR:学年 == "初等部" && COND("基本情報隠蔽", ACTOR) == 0
		PRINTPLAINFORM 这孩子可以吗？（[ 3]～[ 4]在設定後也会随机属性）
	ELSE
		PRINTPLAINFORM 这个人可以吗？（[ 3]～[ 4]在設定後也会随机属性）
	ENDIF
	PRINTL 
	IF ACTOR == MASTER
		PRINTFORML [ 0]是　[ 1]否　[ 2]随机属性　[ 3]素質の操作　[ 4]Config
	ELSE
		IF MARK:ACTOR:淫紋
			PRINTFORML [ 0]是　[ 1]否　[ 2]随机属性　[ 3]素質的操作　[ 4]Config　[ 5]\@ CFLAG:ACTOR:颜绘 ? 変更 # 設定 \@颜绘 [ 6]淫紋変更
		ELSE
			PRINTFORML [ 0]是　[ 1]否　[ 2]随机属性　[ 3]素質的固定与除外　[ 4]Config　[ 5]\@ CFLAG:ACTOR:颜绘 ? 変更 # 設定 \@颜绘
		ENDIF
	ENDIF
ENDIF

SIF ISSKIP()
	RETURN 1

INPUTS
CHOICES = %RESULTS%

SELECTCASE CHOICES
CASE "0"
	RETURN 1
CASE "1"
	IF FLAG:オープニング == 0
		;紹介文既読フラグOFF
		CLEARBIT FLAG:状況判定, 12
		RETURN 0
	ENDIF
CASE "3"
	CALL FIX_TALENT
CASE "4"
	CALL SET_CONFIGURE, "角色定制関連"
CASE "5"
	IF ACTOR == MASTER 
	ELSE
		CALL SIMULATE_MAIDFACE, ACTOR
		CALL SETFLAG, "髪の記録", ACTOR
		CALL SETFLAG, "表情の記録", ACTOR
	ENDIF
CASE "6"
	IF MARK:ACTOR:淫紋
		PRINTL 
		PRINTFORML ※現段階是LV{MARK:ACTOR:淫紋}、可以通过其他选择来确认淫纹画数增加时的形状※
		CALL SIMULATE_INMON, ACTOR

		STR:淫紋記録 = 淫紋：%CONDS("淫紋の種類", ACTOR)%
	ENDIF
;項目ONOFFボタン
CASE "搾精排行"
	INVERTBIT FLAG:項目OFF, 0
CASE "助手的任务指示"
	INVERTBIT FLAG:項目OFF, 1
CASE "角色介绍"
	INVERTBIT FLAG:項目OFF, 2
CASE "身嗜み"
	INVERTBIT FLAG:項目OFF, 3
CASE "服装詳細"
	INVERTBIT FLAG:項目OFF, 4
CASE "合意"
	INVERTBIT FLAG:項目OFF, 5
CASE "性癖・弱点"
	INVERTBIT FLAG:項目OFF, 6
CASE "自室の状態"
	INVERTBIT FLAG:項目OFF, 7
CASE "属性与外表"
	INVERTBIT FLAG:項目OFF, 8
CASE "宝珠"
	INVERTBIT FLAG:項目OFF, 9
CASE "経験"
	INVERTBIT FLAG:項目OFF, 10
CASE "胖次收藏"
	INVERTBIT FLAG:項目OFF, 11
CASE "能力"
	INVERTBIT FLAG:項目OFF, 12
CASE "刻印"
	INVERTBIT FLAG:項目OFF, 13
CASE "素質"
	INVERTBIT FLAG:項目OFF, 14
CASE "家族構成"
	INVERTBIT FLAG:項目OFF, 15
CASE "相性"
	INVERTBIT FLAG:項目OFF, 16
CASE "性処理"
	INVERTBIT FLAG:項目OFF, 17
CASE "射精排行"
	INVERTBIT FLAG:項目OFF, 18
CASE "所持道具一覧"
	INVERTBIT FLAG:項目OFF, 19
CASE "特記事項"
	INVERTBIT FLAG:項目OFF, 20
CASE "特別経験"
	;(21, 22)が(0, 0)通常簡易表示→(1, 0)詳細表示→(1, 1)項目OFF→(0, 0)…と阴蒂ック毎に変化する
	IF GETBIT(FLAG:項目OFF, 21) == 0
		INVERTBIT FLAG:項目OFF, 21
	ELSEIF GETBIT(FLAG:項目OFF, 22) == 0
		INVERTBIT FLAG:項目OFF, 22
	ELSE
		INVERTBIT FLAG:項目OFF, 21
		INVERTBIT FLAG:項目OFF, 22
	ENDIF
CASE "平常的装扮"
	INVERTBIT FLAG:項目OFF, 23
CASE "出産の記録"
	INVERTBIT FLAG:項目OFF, 24
ENDSELECT


;表示されていた角色を白紙に戻す
DELCHARA ACTOR
CLEARLINE LINECOUNT - TFLAG:LINECOUNT_PROFILE
RESTART


;ARGには作成する角色のNO、ARGSは名前が変更されている場合にはそれが入っている
@PROFILE, ARG, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_TARGET
#DIM MEMO_MASTER
;角色メイクの時の固定素質
#DIMS TALENT_FIX
;角色メイクの時の除外素質
#DIMS TALENT_RMV

ADDCHARA ARG

REDRAW 0

MEMO_TARGET = TARGET

TARGET = CHARANUM-1

SIF CALLNAME:TARGET == ""
	CALLNAME:TARGET = %NAME:TARGET%

;いくつかの先に固定処理しておいた方が良い素質を処理（経験などに絡むもの）
;性別変化は角色によっては禁止
IF TALENT:性反転 || TALENT:具現 || TALENT:男性 || CSVTALENT(NO:TARGET, GETNUM(TALENT, "扶她") )
ELSE
	IF CEVENT("固定：扶她", MASTER)
		TALENT:扶她 = 1
		TALENT:男性 = 0
		TALENT:女性 = 0
	ELSEIF CEVENT("除外：扶她", MASTER)
		TALENT:扶她 = 0
		TALENT:女性 = 1
	ENDIF
ENDIF
IF CEVENT("固定：処女", MASTER) && TALENT:男性 == 0
	TALENT:処女 = 1
ELSEIF CEVENT("除外：処女", MASTER) && TALENT:男性 == 0
	TALENT:処女 = 0
ENDIF
IF CEVENT("固定：童貞", MASTER) && PENIS(TARGET)
	TALENT:童貞 = 1
ELSEIF CEVENT("除外：童貞", MASTER) && PENIS(TARGET)
	TALENT:童貞 = 0
ENDIF
IF CEVENT("固定：性経験皆無", MASTER)
	SIF PENIS(TARGET)
		TALENT:童貞 = 1
	SIF TALENT:男性 == 0
		TALENT:処女 = 1
ENDIF

;豐腴や苗条
IF CEVENT("固定：苗条", MASTER)
	TALENT:苗条 = 1
	TALENT:豐腴 = 0
	TALENT:並腹 = 0
ELSEIF CEVENT("除外：苗条", MASTER)
	TALENT:苗条 = 0
	IF RAND:2
		TALENT:豐腴 = 1
		TALENT:並腹 = 0
	ELSE
		TALENT:豐腴 = 0
		TALENT:並腹 = 1
	ENDIF
ELSEIF CEVENT("固定：豐腴", MASTER)
	TALENT:豐腴 = 1
	TALENT:苗条 = 0
	TALENT:並腹 = 0
ELSEIF CEVENT("除外：豐腴", MASTER)
	TALENT:豐腴 = 0
	IF RAND:2
		TALENT:苗条 = 1
		TALENT:並腹 = 0
	ELSE
		TALENT:苗条 = 0
		TALENT:並腹 = 1
	ENDIF
ENDIF

CALL CHANGE_TALENT, TARGET
CALL SETFLAG, "識別番号", TARGET
CALL RESET_EXP_GAIN, TARGET
CALL GET_EXABL, TARGET
CALL GET_EXTALENT, TARGET
CALL SET_3SIZE, TARGET
CALL SETFLAG, "陰毛と腋毛", TARGET
CALL SETFLAG, "乳頭の詳細設定", TARGET
CALL SETFLAG, "陰核の詳細設定", TARGET
CALL SETFLAG, "阴茎詳細設定", TARGET

;３围系は默认設定後に適用する
CALL EVENT_PROFILE, "能力変化", TARGET
CALL CHECK_3SIZE, TARGET

CALL VARSET_MAIDFACE, TARGET

IF COND("角色メイク中")
	SIF STR:髪記録 != ""
		CALL SETFLAG, "髪記録からの復元", TARGET
	SIF STR:表情記録 != ""
		CALL SETFLAG, "表情記録からの復元", TARGET
	IF STR:淫紋記録 != ""
		CALL DEL_CEVENT_GROUP, "淫紋", TARGET
		CALL SET_CEVENT, STR:淫紋記録, TARGET
	ENDIF
ENDIF
SIF STR:髪記録 != "" || STR:表情記録 != ""
	CFLAG:颜绘 = 1

;角色メイク時の素質固定or除外を行う
VARSET LOCALS
SPLIT TEXTS("固定可能な素質"), "/", LOCALS

;身高の固定と除外は先に行う
IF CEVENT("固定：高大", MASTER)
	CALL SETFLAG_3SIZE, "高大化", TARGET
ELSEIF CEVENT("除外：高大", MASTER)
	SIF TALENT:高大
		CALL SETFLAG_3SIZE, "普通体型化", TARGET
ELSEIF CEVENT("固定：娇小", MASTER)
	CALL SETFLAG_3SIZE, "娇小化", TARGET
ELSEIF CEVENT("除外：娇小", MASTER)
	SIF TALENT:娇小
		CALL SETFLAG_3SIZE, "普通体型化", TARGET
ENDIF

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	TALENT_FIX = 固定：%LOCALS:LCOUNT%
	TALENT_RMV = 除外：%LOCALS:LCOUNT%

	;固定素質
	IF CEVENT(TALENT_FIX, MASTER)
		SELECTCASE LOCALS:LCOUNT
		CASE "筋肉"
			SIF TALENT:筋肉
				CONTINUE
			TALENT:筋肉 = 1
		CASE "乳頭肥大"
			SIF TALENT:乳頭肥大
				CONTINUE
			TALENT:乳頭肥大 = 1
		CASE "陥没乳頭"
			SIF TALENT:陥没乳頭
				CONTINUE
			TALENT:陥没乳頭 = 1
		CASE "巨乳輪"
			SIF TALENT:巨乳輪
				CONTINUE
			TALENT:巨乳輪 = 1
		CASE "陰核肥大"
			SIF TALENT:陰核肥大
				CONTINUE
			TALENT:陰核肥大 = 1
		CASE "陰核包茎"
			SIF TALENT:陰核包茎
				CONTINUE
			TALENT:陰核包茎 = 1
		CASE "短小"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:阴茎 = 0
		CASE "並根"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:阴茎 = 1
		CASE "巨根"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:阴茎 = 2
		CASE "馬根"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:阴茎 = 3
		CASE "真性包茎"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:包茎 = 2
		CASE "假性包茎"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:包茎 = 1
		CASE "露茎"
			SIF PENIS(TARGET) == 0
				CONTINUE
			TALENT:包茎 = 0
		CASE "貧乳"
			SIF TALENT:男性
				CONTINUE
			CALL SETFLAG_3SIZE, "貧乳化", TARGET
		CASE "並乳"
			SIF TALENT:男性
				CONTINUE
			CALL SETFLAG_3SIZE, "並乳化", TARGET
		CASE "巨乳"
			SIF TALENT:男性
				CONTINUE
			CALL SETFLAG_3SIZE, "巨乳化", TARGET
		CASE "爆乳"
			SIF TALENT:男性
				CONTINUE
			CALL SETFLAG_3SIZE, "爆乳化", TARGET
		CASE "超乳"
			SIF TALENT:男性
				CONTINUE
			CALL SETFLAG_3SIZE, "超乳化", TARGET
		CASE "小尻"
			CALL SETFLAG_3SIZE, "小尻化"
		CASE "並尻"
			CALL SETFLAG_3SIZE, "並尻化"
		CASE "巨尻"
			CALL SETFLAG_3SIZE, "巨尻化"
		CASE "爆尻"
			CALL SETFLAG_3SIZE, "爆尻化"
		CASE "超尻"
			CALL SETFLAG_3SIZE, "超尻化"
		CASE "褐色肌"
			TALENT:肤色 = 1
		CASE "色白"
			TALENT:肤色 = 2
		CASE "青肌"
			TALENT:肤色 = 3
		CASE "色黒"
			TALENT:肤色 = 4
		CASE "萝莉"
			TALENT:外見年齢 = 1
		CASE "少女"
			TALENT:外見年齢 = 2
		CASE "大姐姐"
			TALENT:外見年齢 = 3
		CASE "大人"
			TALENT:外見年齢 = 4
		CASE "美穴"
			SIF TALENT:男性 || TALENT:性器年齢 == 1
				CONTINUE
			TALENT:性器年齢 = MAX(TALENT:性器年齢 -1, 1)
		CASE "色素沉淀（弱）"
			SIF TALENT:男性
				CONTINUE
			TALENT:性器年齢 = 2
		CASE "色素沉淀"
			SIF TALENT:男性
				CONTINUE
			TALENT:性器年齢 = 3
		CASE "色素沉淀（強）"
			SIF TALENT:男性
				CONTINUE
			TALENT:性器年齢 = 4
		CASE "黑木耳"
			SIF TALENT:男性
				CONTINUE
			TALENT:性器年齢 = 5
		;男性でないなら追加
		CASE "母乳体質", "子宮性感"
			SIF TALENT:男性 == 0
				TALENT:(LOCALS:LCOUNT) = 1
		;阴茎が無いなら追加
		CASE "絶頂失禁体質"
			SIF PENIS(TARGET) == 0
				TALENT:(LOCALS:LCOUNT) = 1
		;純粋追加
		CASE "腸液分泌体質", "嘔吐反射耐性", "拡張適性", "喉性感", "尿道性感", "气味弱点", "発情体質"
			TALENT:(LOCALS:LCOUNT) = 1
		ENDSELECT
	;除外素質
	ELSEIF CEVENT(TALENT_RMV, MASTER)
		SELECTCASE LOCALS:LCOUNT
		CASE "筋肉"
			TALENT:筋肉 = 0
		CASE "乳頭肥大"
			TALENT:乳頭肥大 = 0
		CASE "陥没乳頭"
			TALENT:陥没乳頭 = 0
		CASE "巨乳輪"
			TALENT:巨乳輪 = 0
		CASE "陰核肥大"
			TALENT:陰核肥大 = 0
		CASE "陰核包茎"
			TALENT:陰核包茎 = 0
		CASE "短小"
			SIF PENIS(TARGET) == 0 || SIZE("阴茎", TARGET) != 0
				CONTINUE

			SELECTCASE RAND:10
			CASE 8, 9
				TALENT:阴茎 = 3
			CASE 6, 7
				TALENT:阴茎 = 2
			CASEELSE
				TALENT:阴茎 = 1
			ENDSELECT
		CASE "並根"
			SIF PENIS(TARGET) == 0 || SIZE("阴茎", TARGET) != 1
				CONTINUE

			SELECTCASE RAND:10
			CASE 8, 9
				TALENT:阴茎 = 3
			CASE 4 TO 7
				TALENT:阴茎 = 2
			CASEELSE
				TALENT:阴茎 = 0
			ENDSELECT
		CASE "巨根"
			SIF PENIS(TARGET) == 0 || SIZE("阴茎", TARGET) != 2
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				TALENT:阴茎 = 3
			CASE 3 TO 6
				TALENT:阴茎 = 1
			CASEELSE
				TALENT:阴茎 = 0
			ENDSELECT
		CASE "馬根"
			SIF PENIS(TARGET) == 0 || SIZE("阴茎", TARGET) != 3
				CONTINUE

			SELECTCASE RAND:10
			CASE 5 TO 9
				TALENT:阴茎 = 2
			CASE 1 TO 4
				TALENT:阴茎 = 1
			CASEELSE
				TALENT:阴茎 = 0
			ENDSELECT

		CASE "真性包茎"
			SIF PENIS(TARGET) == 0 || COND("真性包茎") == 0
				CONTINUE

			SIF RAND:3
				TALENT:包茎 = 1
		CASE "假性包茎"
			SIF PENIS(TARGET) == 0 || COND("假性包茎") == 0
				CONTINUE

			SIF RAND:3
				TALENT:包茎 = 2
		CASE "露茎"
			SIF PENIS(TARGET) == 0 || TALENT:包茎
				CONTINUE
			IF RAND:3
				TALENT:包茎 = 1
			ELSE
				TALENT:包茎 = 2
			ENDIF
		CASE "貧乳"
			SIF TALENT:男性 || TALENT:貧乳 == 0
				CONTINUE

			SELECTCASE RAND:10
			CASE 9
				CALL SETFLAG_3SIZE, "超乳化", TARGET
			CASE 8
				CALL SETFLAG_3SIZE, "爆乳化", TARGET
			CASE 7
				CALL SETFLAG_3SIZE, "巨乳化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並乳化", TARGET
			ENDSELECT
		CASE "並乳"
			SIF TALENT:男性 || TALENT:貧乳 || TALENT:巨乳
				CONTINUE

			SELECTCASE RAND:10
			CASE 8, 9
				CALL SETFLAG_3SIZE, "超乳化", TARGET
			CASE 5 TO 7
				CALL SETFLAG_3SIZE, "爆乳化", TARGET
			CASE 2 TO 4
				CALL SETFLAG_3SIZE, "巨乳化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "貧乳化", TARGET
			ENDSELECT
		CASE "巨乳"
			SIF TALENT:男性 || TALENT:巨乳 != 1
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "超乳化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "爆乳化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "貧乳化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並乳化", TARGET
			ENDSELECT
		CASE "爆乳"
			SIF TALENT:男性 || TALENT:巨乳 != 2
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "超乳化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "巨乳化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "貧乳化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並乳化", TARGET
			ENDSELECT
		CASE "超乳"
			SIF TALENT:男性 || TALENT:巨乳 != 3
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "爆乳化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "巨乳化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "貧乳化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並乳化", TARGET
			ENDSELECT
		CASE "小尻"
			SIF COND("小尻") == 0
				CONTINUE

			SELECTCASE RAND:10
			CASE 9
				CALL SETFLAG_3SIZE, "超尻化", TARGET
			CASE 8
				CALL SETFLAG_3SIZE, "爆尻化", TARGET
			CASE 7
				CALL SETFLAG_3SIZE, "巨尻化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並尻化", TARGET
			ENDSELECT
		CASE "並尻"
			SIF COND("小尻") || COND("巨尻")
				CONTINUE
			SELECTCASE RAND:10
			CASE 8, 9
				CALL SETFLAG_3SIZE, "超尻化", TARGET
			CASE 5 TO 7
				CALL SETFLAG_3SIZE, "爆尻化", TARGET
			CASE 2 TO 4
				CALL SETFLAG_3SIZE, "巨尻化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "小尻化", TARGET
			ENDSELECT
		CASE "巨尻"
			SIF COND("巨尻") != 1
				CONTINUE
			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "超尻化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "爆尻化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "小尻化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並尻化", TARGET
			ENDSELECT
		CASE "爆尻"
			SIF COND("巨尻") != 2
				CONTINUE

			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "超尻化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "巨尻化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "小尻化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並尻化", TARGET
			ENDSELECT
		CASE "超尻"
			SIF COND("巨尻") != 3
				CONTINUE
			SELECTCASE RAND:10
			CASE 7 TO 9
				CALL SETFLAG_3SIZE, "爆尻化", TARGET
			CASE 4 TO 6
				CALL SETFLAG_3SIZE, "巨尻化", TARGET
			CASE 0
				CALL SETFLAG_3SIZE, "小尻化", TARGET
			CASEELSE
				CALL SETFLAG_3SIZE, "並尻化", TARGET
			ENDSELECT
		CASE "褐色肌"
			SIF	TALENT:肤色 == 1
				TALENT:肤色 = 0
		CASE "色白"
			SIF	TALENT:肤色 == 2
				TALENT:肤色 = 0
		CASE "青肌"
			SIF	TALENT:肤色 == 3
				TALENT:肤色 = 0
		CASE "色黒"
			SIF	TALENT:肤色 == 4
				TALENT:肤色 = 0
		CASE "萝莉"
			SIF TALENT:外見年齢 == 1
				TALENT:外見年齢 = 3
		CASE "少女"
			SIF TALENT:外見年齢 == 2
				TALENT:外見年齢 = 3
		CASE "大姐姐"
			SIF TALENT:外見年齢 == 3
				TALENT:外見年齢 = 2
		CASE "大人"
			SIF TALENT:外見年齢 == 4
				TALENT:外見年齢 = 3
		CASE "美穴"
			SIF TALENT:男性
				CONTINUE
			TALENT:性器年齢 = MAX(TALENT:性器年齢, TALENT:外見年齢)
		CASE "色素沉淀（弱）"
			SIF TALENT:男性
				CONTINUE
			SIF TALENT:性器年齢 == 2
				TALENT:性器年齢 = 3
		CASE "色素沉淀"
			SIF TALENT:男性
				CONTINUE
			SIF TALENT:性器年齢 == 3
				TALENT:性器年齢 = 4
		CASE "色素沉淀（強）"
			SIF TALENT:男性
				CONTINUE
			SIF TALENT:性器年齢 == 4
				TALENT:性器年齢 = 3
		CASE "黑木耳"
			SIF TALENT:男性
				CONTINUE
			SIF TALENT:性器年齢 == 5
				TALENT:性器年齢 = 4

		;純粋除外
		CASE "腸液分泌体質", "嘔吐反射耐性", "拡張適性", "喉性感", "尿道性感", "气味弱点", "母乳体質", "子宮性感", "絶頂失禁体質", "発情体質"
			TALENT:(LOCALS:LCOUNT) = 0
		ENDSELECT
	ENDIF
NEXT

CALL SETFLAG, "性器年齢", TARGET

;角色メイク専用素質の消去
TALENT:苗条 = 0
TALENT:豐腴 = 0

CALL CHECK_FAV_ONANIE, TARGET
CALL CHECK_FAV_POSITION, TARGET

;初期仲間？
IF CHARANUM == 3
	CFLAG:初期仲間 = 1
	CFLAG:初期同居 = 1
ENDIF
;夜型や初期同居具合
SIF CFLAG:夜型
	CFLAG:初期同居 = 1
SIF CFLAG:初期同居
	CALL SETFLAG, "同居：一直", TARGET
CALL SETFLAG, "同居解消禁止の自動設定", TARGET

;衣装の設定
CALL SETFLAG, "初期衣装", TARGET
;勧誘時は外套脱がせる
CALL RESET_OTHER, "外套", TARGET
CALL SET_COUNTER_BASE
CALL SETFLAG, "属性", TARGET
SIF TALENT:好色
	BASE:欲求不満 += RAND:51
;同居：一直を設定します
SIF CFLAG:夜型
	CALL SETFLAG, "同居：一直", TARGET
;もしも関係が未定義なら学年依存
SIF CSTR:関係 == ""
	CSTR:関係 = %CONDS("間柄")%

;名前変更した？
IF ARGS != ""
	NAME:TARGET = %ARGS%
	CALLNAME:TARGET = %NAME:TARGET%
ENDIF

IF FLAG:オープニング == 0 && COND("角色メイク中")
	IF COND("紹介文既読") == 0
		CALL VARSET_PROFILE
		CALL EVENT_PROFILE, "勧誘", TARGET


		;紹介文既読フラグON
		SETBIT FLAG:状況判定, 12
	ENDIF
	;勧誘イベント内で角色の数値を変更させていた場合
	FOR LCOUNT, 0, VARSIZE("CFLAG")
		SIF CFLAG:0:LCOUNT >= 0
			CFLAG:LCOUNT = CFLAG:0:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("BASE")
		SIF BASE:0:LCOUNT >= 0
			BASE:LCOUNT = BASE:0:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF TALENT:0:LCOUNT >= 0
			TALENT:LCOUNT = TALENT:0:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("ABL")
		SIF ABL:0:LCOUNT >= 0
			ABL:LCOUNT = ABL:0:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("EXP")
		SIF EXP:0:LCOUNT >= 0
			EXP:LCOUNT = EXP:0:LCOUNT
	NEXT
	FOR LCOUNT, 0, VARSIZE("CSTR")
		SIF CSTR:0:LCOUNT != "-1"
			CSTR:LCOUNT = %CSTR:0:LCOUNT%
	NEXT
ENDIF

;男、女の判定
SELECTCASE PENIS(TARGET)
CASE 0
	TALENT:女性 = 1
CASEELSE
	TALENT:女性 = 0
ENDSELECT
;Ｖ妊娠可能かどうかの設定
SIF COND("一般にＶ妊娠不能")
	TALENT:Ｖ妊娠不能 = 1

;誕生日の設定
BASE:誕生日 = RAND:120 + 1
;相性の設定
RELATION:TARGET:(NO:TARGET) = 120
BASE:排泄物 = RAND:4 + RAND:4
MAXBASE:排泄欲 = 10000
BASE:尿意 = LIMIT(500*(RAND:10 + RAND:10) - 3000, 0, 5000)
MAXBASE:尿意 = 10000
CALL SETFLAG, "安全危険日判定", TARGET
CALL SETFLAG, "学力の設定", TARGET

TFLAG:LINECOUNT_PROFILE = LINECOUNT

IF COND("角色メイク中")
	FOR LCOUNT, 90, 110
		SIF CEVENT(CSTRNAME:LCOUNT)
			CSTR:(CSTRNAME:LCOUNT) = %CEVENTS(CSTRNAME:LCOUNT)%
	NEXT
	CALL NEW_PRINT_STATE, TARGET, "角色メイク"
ENDIF
;初期变量をここで記録
CALL SETFLAG, "初期变量記録", TARGET
CALL SETFLAG, "普段の髪型記録", TARGET

TARGET = MEMO_TARGET


;主人のCEVENTで固定素質と除外素質を記録する
@FIX_TALENT
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM NUM_TALENT
#DIM ABL_SAMON
#DIM CHOICE
#DIMS TALENT_FIX
#DIMS TALENT_RMV

REDRAW 0

;固定or除外できる総数
ABL_SAMON = 3

MEMO_LINECOUNT = LINECOUNT
PRINTL 
PRINTFORML 从特定的素質选择最多３個来进固定or除外、可以进行角色定制操作
CALL PRINT_STRL, "注１：除外できないものや同時選択できないものも幾つか有ます。例：[男性]は[扶她]に出来ない 等"
CALL PRINT_STRL, "注２：固定した素質は_緑_緑色_、除外した素質は_青_青色_で表示します"
CALL PRINT_STRL, "注３：ここで変更したことで口上等で齟齬が出ることが有ます"
CALL PRINT_STRL, "那么就从以下的素质里选择吧。選択顺序是从_緑_固定_→_青_除外_→不选择 这様変化的"
PRINTL 

VARSET LOCALS
SPLIT TEXTS("固定可能な素質"), "/", LOCALS

NUM_TALENT = 0
FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK

	TALENT_FIX = 固定：%LOCALS:LCOUNT%
	TALENT_RMV = 除外：%LOCALS:LCOUNT%

	SIF CEVENT(TALENT_FIX, MASTER)
		SETCOLOR DEF_COLOR("緑")
	SIF CEVENT(TALENT_RMV, MASTER)
		SETCOLOR DEF_COLOR("青")

	PRINTFORM  [{LCOUNT, 2}]%TEXT_LJ(LOCALS:LCOUNT, 14)%

	RESETCOLOR

	NUM_TALENT += 1
	SIF NUM_TALENT%4 == 0
		PRINTL 
NEXT
SIF NUM_TALENT%4
	PRINTL 
PRINTL 
PRINTFORML  [99]重置固定与除外
PRINTFORML [100]这様就好

CALL INPUT_SELECT, NUM_TALENT, 99, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 1
CASE 99
	CALL SETFLAG, "素質的固定与除外の初期化"
CASEELSE
	;まずは固定or除外or指定无、の判定
	TALENT_FIX = 固定：%LOCALS:CHOICE%
	TALENT_RMV = 除外：%LOCALS:CHOICE%
	;固定→除外
	IF CEVENT(TALENT_FIX, MASTER)
		CALL DEL_CEVENT, TALENT_FIX, MASTER

		SELECTCASE LOCALS:CHOICE
		;除外できない要素かどうか
		CASE "性経験皆無"
		CASEELSE
			CALL SET_CEVENT, TALENT_RMV, MASTER
		ENDSELECT
	;除外→指定无
	ELSEIF CEVENT(TALENT_RMV, MASTER)
		CALL DEL_CEVENT, TALENT_RMV, MASTER
	;指定无→固定
	ELSE
		;同居できない素質の固定を消す
		SELECTCASE LOCALS:CHOICE
		CASE "処女", "童貞"
			CALL DEL_CEVENT, "固定：性経験皆無", MASTER
			CALL DEL_CEVENT, "除外：性経験皆無", MASTER
		CASE "性経験皆無"
			CALL DEL_CEVENT, "固定：処女", MASTER
			CALL DEL_CEVENT, "固定：童貞", MASTER
			CALL DEL_CEVENT, "除外：処女", MASTER
			CALL DEL_CEVENT, "除外：童貞", MASTER
		CASE "扶她", "絶頂失禁体質"
			CALL DEL_CEVENT, "固定：扶她", MASTER
			CALL DEL_CEVENT, "固定：絶頂失禁体質", MASTER
			CALL DEL_CEVENT, "除外：扶她", MASTER
			CALL DEL_CEVENT, "除外：絶頂失禁体質", MASTER
		CASE "短小", "並根", "巨根", "馬根"
			CALL DEL_CEVENT, "固定：短小", MASTER
			CALL DEL_CEVENT, "固定：並根", MASTER
			CALL DEL_CEVENT, "固定：巨根", MASTER
			CALL DEL_CEVENT, "固定：馬根", MASTER
			CALL DEL_CEVENT, "除外：短小", MASTER
			CALL DEL_CEVENT, "除外：並根", MASTER
			CALL DEL_CEVENT, "除外：巨根", MASTER
			CALL DEL_CEVENT, "除外：馬根", MASTER
		CASE "真性包茎", "假性包茎", "露茎"
			CALL DEL_CEVENT, "固定：真性包茎", MASTER
			CALL DEL_CEVENT, "固定：假性包茎", MASTER
			CALL DEL_CEVENT, "固定：露茎", MASTER
			CALL DEL_CEVENT, "除外：真性包茎", MASTER
			CALL DEL_CEVENT, "除外：假性包茎", MASTER
			CALL DEL_CEVENT, "除外：露茎", MASTER
		CASE "高大", "娇小"
			CALL DEL_CEVENT, "固定：高大", MASTER
			CALL DEL_CEVENT, "固定：娇小", MASTER
			CALL DEL_CEVENT, "除外：高大", MASTER
			CALL DEL_CEVENT, "除外：娇小", MASTER
		CASE "苗条", "豐腴"
			CALL DEL_CEVENT, "固定：苗条", MASTER
			CALL DEL_CEVENT, "固定：豐腴", MASTER
			CALL DEL_CEVENT, "除外：苗条", MASTER
			CALL DEL_CEVENT, "除外：豐腴", MASTER
		CASE "貧乳", "並乳", "巨乳", "爆乳", "超乳"
			CALL DEL_CEVENT, "固定：貧乳", MASTER
			CALL DEL_CEVENT, "固定：並乳", MASTER
			CALL DEL_CEVENT, "固定：巨乳", MASTER
			CALL DEL_CEVENT, "固定：爆乳", MASTER
			CALL DEL_CEVENT, "固定：超乳", MASTER
			CALL DEL_CEVENT, "除外：貧乳", MASTER
			CALL DEL_CEVENT, "除外：並乳", MASTER
			CALL DEL_CEVENT, "除外：巨乳", MASTER
			CALL DEL_CEVENT, "除外：爆乳", MASTER
			CALL DEL_CEVENT, "除外：超乳", MASTER
		CASE "小尻", "並尻", "巨尻", "爆尻", "超尻"
			CALL DEL_CEVENT, "固定：小尻", MASTER
			CALL DEL_CEVENT, "固定：並尻", MASTER
			CALL DEL_CEVENT, "固定：巨尻", MASTER
			CALL DEL_CEVENT, "固定：爆尻", MASTER
			CALL DEL_CEVENT, "固定：超尻", MASTER
			CALL DEL_CEVENT, "除外：小尻", MASTER
			CALL DEL_CEVENT, "除外：並尻", MASTER
			CALL DEL_CEVENT, "除外：巨尻", MASTER
			CALL DEL_CEVENT, "除外：爆尻", MASTER
			CALL DEL_CEVENT, "除外：超尻", MASTER
		CASE "褐色肌", "色白", "青肌", "色黒"
			CALL DEL_CEVENT, "固定：褐色肌", MASTER
			CALL DEL_CEVENT, "固定：色白", MASTER
			CALL DEL_CEVENT, "固定：青肌", MASTER
			CALL DEL_CEVENT, "固定：色黒", MASTER
			CALL DEL_CEVENT, "除外：褐色肌", MASTER
			CALL DEL_CEVENT, "除外：色白", MASTER
			CALL DEL_CEVENT, "除外：青肌", MASTER
			CALL DEL_CEVENT, "除外：色黒", MASTER
		CASE "萝莉", "少女", "大姐姐", "大人"
			CALL DEL_CEVENT, "固定：萝莉", MASTER
			CALL DEL_CEVENT, "固定：少女", MASTER
			CALL DEL_CEVENT, "固定：大姐姐", MASTER
			CALL DEL_CEVENT, "固定：大人", MASTER
			CALL DEL_CEVENT, "除外：萝莉", MASTER
			CALL DEL_CEVENT, "除外：少女", MASTER
			CALL DEL_CEVENT, "除外：大姐姐", MASTER
			CALL DEL_CEVENT, "除外：大人", MASTER
		CASE "美穴", "色素沉淀（弱）", "色素沉淀", "色素沉淀（強）", "黑木耳"
			CALL DEL_CEVENT, "固定：美穴", MASTER
			CALL DEL_CEVENT, "固定：色素沉淀（弱）", MASTER
			CALL DEL_CEVENT, "固定：色素沉淀", MASTER
			CALL DEL_CEVENT, "固定：色素沉淀（強）", MASTER
			CALL DEL_CEVENT, "固定：黑木耳", MASTER
			CALL DEL_CEVENT, "除外：美穴", MASTER
			CALL DEL_CEVENT, "除外：色素沉淀（弱）", MASTER
			CALL DEL_CEVENT, "除外：色素沉淀", MASTER
			CALL DEL_CEVENT, "除外：色素沉淀（強）", MASTER
			CALL DEL_CEVENT, "除外：黑木耳", MASTER
		ENDSELECT

		;既に選択可能個数分選択している？
		IF STRCOUNTS(CSTR:MASTER:CEVENT, "固定：", "除外：") >= ABL_SAMON
			PRINTFORMW 選择数超过了上限
		;指定无→固定
		ELSE
			CALL SET_CEVENT, TALENT_FIX, MASTER
		ENDIF
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;角色ARGの素質や経験などを適切な値に再設定したり、随机で変化させたりする
@CHANGE_TALENT, ARG
#DIM LCOUNT
;処女？
IF TALENT:処女 == 1
	TALENT:出産経験 = 0
	EXP:Ｖ経験 = 0
	EXP:Ｖ性交経験 = 0
	EXP:腰靈活経験 = 0
	EXP:Ｖ拡張経験 = 0
	BASE:胎児の父親 = 0
ENDIF
;童貞？
IF TALENT:童貞
	EXP:Ｖ挿入経験 = 0
ELSEIF PENIS(TARGET)
	SIF EXP:Ｖ挿入経験 == 0
		EXP:Ｖ挿入経験 = 1
ENDIF
;種族は？
SIF CSTR:種族 == ""
	CSTR:種族 = 人类

FOR LCOUNT, 0, 100
	IF CEVENT("固定：性経験皆無", MASTER)
		EXP:LCOUNT = 0
	;所持している経験の揺らぎ
	ELSEIF EXP:LCOUNT
		EXP:LCOUNT = MAX(MULTIPLY(EXP:LCOUNT, 50 + (RAND:6 + RAND:6)*10), 1)
	ENDIF
NEXT
EXP:Ｖ経験 = MAX(EXP:Ｖ経験, EXP:Ｖ性交経験, EXP:Ｖ拡張経験)
EXP:Ａ経験 = MAX(EXP:Ａ経験, EXP:Ａ性交経験, EXP:Ａ拡張経験)

;以下の随机素質は一時保留
;拡張適性はレア素質
;SIF RAND:10 == 0
;	TALENT:拡張適性 = 1
;わーい母乳が出るぞ！（レア素質）
;SIF TALENT:男性 == 0 && TALENT:貧乳 == 0 && RAND:10 + (TALENT:巨乳 + 1)/2 >= 9
;	TALENT:母乳体質 = 1
;口交経験豊富だと嘔吐反射耐性が有るかも
;SIF EXP:口交経験 + EXP:喉性交経験 >= MAX(50, RAND:300)
;	TALENT:嘔吐反射耐性 = 1
;喉性感
;SIF TALENT:Ｍ性向*3 + ABL:Ｍ感覚 + GET_EXP_LV(EXP:口交経験) + GET_EXP_LV(EXP:喉性交経験) >= RAND:20
;	TALENT:喉性感 = 1
;子宮性感
;SIF TALENT:男性 == 0 && TALENT:Ｖ性向*3 + ABL:Ｖ感覚 + GET_EXP_LV(EXP:Ｖ経験) + GET_EXP_LV(EXP:Ｖ性交経験) >= RAND:20
;	TALENT:子宮性感 = 1
;尿道性感
;SIF TALENT:倒錯的 + RAND:10 >= 9
;	TALENT:尿道性感 = 1
;气味弱点
;IF (TALENT:動物耳 > 0)*3 + (TALENT:汚臭敏感 - TALENT:汚臭鈍感)*2 + (ABL:欲望 + 1)/2 + RAND:10 >= 9
;	TALENT:气味弱点 = 1
;	TALENT:汚臭鈍感 = 0
;ENDIF

;拡張度の上限に応じて拡張度にぶれがある（上限側になりやすい）
IF CONFIG("ＶＡ拡張の手動ＬＶＵＰ")
	LOCAL = CALCF("Ｖ拡張の上限", TARGET)
	SIF LOCAL > ABL:Ｖ拡張
		ABL:Ｖ拡張 = LIMIT(RAND:LOCAL + RAND:2 + RAND:2, ABL:Ｖ拡張, LOCAL)

	LOCAL = CALCF("Ａ拡張の上限", TARGET)
	SIF LOCAL > ABL:Ａ拡張
		ABL:Ａ拡張 = LIMIT(RAND:LOCAL + RAND:2 + RAND:2, ABL:Ａ拡張, LOCAL)
ELSE
	ABL:Ｖ拡張 = CALCF("Ｖ拡張の上限", TARGET)
	ABL:Ａ拡張 = CALCF("Ａ拡張の上限", TARGET)
ENDIF

;精液弱点のも良いですよね
IF TALENT:精液弱点 == 0 && ABL:精液中毒 > RAND:5
	IF EXP:口交経験 && RANDIF(1, EXP:Ｖ性交経験, EXP:Ａ性交経験)
		TALENT:精液弱点 = 1p0
	ELSEIF EXP:Ｖ性交経験 && RANDIF(1, EXP:Ａ性交経験)
		TALENT:精液弱点 = 1p1
	ELSEIF EXP:Ａ性交経験
		TALENT:精液弱点 = 1p2
	ENDIF
ENDIF
SIF TALENT:大力士 && RAND:3 == 0
	TALENT:筋肉 = 1

CALL SETFLAG, "属性", TARGET

