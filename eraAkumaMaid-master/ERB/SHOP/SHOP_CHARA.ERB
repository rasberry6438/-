;-------------------------------------------------
;相手を紹介してもらったりします
;-------------------------------------------------
@SHOP_CHARA, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_TARGET
#DIM CHOICE
#DIMS CHOICES
#DIM MEMO_BASE, 100

REDRAW 0
;角色メイク中フラグON
SETBIT FLAG:状況判定, 13
;你の血縁フラグ等を記憶
FOR LCOUNT, 0, 100
	MEMO_BASE:LCOUNT = BASE:MASTER:LCOUNT
NEXT
SIF CONFIG("素質的固定与除外")
	CALL SETFLAG, "素質的固定与除外の初期化"

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL SHOP_CHARA_SHOW

$INPUT_LOOP
IF ISSKIP() == 0
	INPUTS
	CHOICES = %RESULTS%
	CHOICE = TOINT(CHOICES)
ELSE
	CHOICE = ARG
ENDIF

SELECTCASE CHOICE
CASE 999
	RETURN 0
;[200]中等部以下　[201]１年生　[202]２年生　[203]３年生　[204]教師　[205]其他
CASE 200 TO 206, 210
	IF TFLAG:过滤器 == CHOICE
		TFLAG:过滤器 = 0
	ELSE
		TFLAG:过滤器 = CHOICE
	ENDIF
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE 222
	INVERTBIT FLAG:颜绘勧誘, 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART

CASE IS >= 1
	SIF COND("勧誘可能角色", CHOICE) == 0
		GOTO INPUT_LOOP

	;出現条件がある場合は個別に判定
	STR:実行条件 = 
	TRYCALLFORM ADDCOND_K{CHOICE}("勧誘")

	;ダメだった場合
	IF STR:実行条件 != ""
		CALL PRINT_STRW, STR:実行条件
		[IF_DEBUG]
			PRINTL 
			PRINTFORML デバッグモードの特権で条件を無視して勧誘しますか？
			CALL PRINT_SELECT, "是/ズルはしない"
			SIF RESULT == 0
				STR:実行条件 = 
		[ENDIF]
		IF STR:実行条件 != ""
			CLEARLINE LINECOUNT - MEMO_LINECOUNT
			RESTART
		ENDIF
	ENDIF
	;紹介してもらえるか判定。その角色が既にいるかで判定する
	IF FINDCHARA(NO, CHOICE) >= 0
		SIF CFLAG:FINDCHARA(NO, CHOICE):初期仲間 == 0
			PRINTW 已经勧誘过了呦
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
;それ以外はだいたい文字列
CASEELSE
	IF COMMENT_ADVISER(CHOICES) != ""
		CLEARLINE 1
		PRINTL 
		PRINTFORML ☆%CHOICES%☆
		CALL PRINT_STRW, COMMENT_ADVISER(CHOICES)
	ENDIF

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDSELECT

MEMO_TARGET = TARGET

;注意書き…を出そうと思ったがいまさらか
;SELECTCASE CSVNAME(CHOICE)
;CASE "生実千裕"
;	CALL PRINT_STRW, @"黄色_注意！この角色はグロ描写こそ無いものの、物理的に%CALLNAME:MASTER%を食べます！！"
;	PRINTFORMW ……本当に勧誘しますか？
;	CALL PRINT_SELECT, "是/否"
;	IF RESULT
;		CLEARLINE LINECOUNT - MEMO_LINECOUNT
;		RESTART
;	ENDIF
;ENDSELECT

;角色解説を入れる
CALL CHARA_MANUAL, CHOICE
;否を選ぶと再入力
IF RESULT == 0
	;表示されていた角色を白紙に戻す
	DELCHARA CHARANUM-1
	TARGET = MEMO_TARGET

	;你のフラグの変化を元に戻す（現在だと血縁のみ）
	FOR LCOUNT, 0, 100
		BASE:MASTER:LCOUNT = MEMO_BASE:LCOUNT
	NEXT

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

;もしも指导者が居ない状態ならば、現在的TARGETを（可能なら）指导者にし、選んだ角色をTARGETにする
SIF ASSI == 0 && TARGET && COND("助手可能")
	ASSI = TARGET

TARGET = CHARANUM - 1

;時間遷移でのフラグ変更を一回呼び出しておく
TRYCALLFORM TIMEEVENT_K{NO:TARGET}

;式神などの特例
IF COND("基本情報隠蔽")
	IF COND("昼")
		PRINTFORM ★学園中发现的不法侵入者
	ELSE
		PRINTFORM ★在家的外面发现的不法侵入者
	ENDIF
	IF CSTR:関係 == "借住"
		PRINTFORMW 带回了家里★
	ELSE
		PRINTFORMW 进行了勧誘★
	ENDIF
ELSEIF COND("種族：自动人形")
	PRINTFORMW ★买了想要的[%CSTR:称号%]%NAME:TARGET%★
ELSEIF COND("種族：式神")
	PRINTFORMW ★失去了力量的原鬼神or原妖。现在是学園的%CSTR:学年%。其名为[%CSTR:称号%]%NAME:TARGET%。现在进行了仪式★
ELSEIF CSTR:学年 == "不法侵入"
	PRINTFORM ★在学園里发现的不法侵入\@ COND("種族：悪魔") ? 悪魔 # 者 \@、
	IF CSTR:関係 == "借住"
		PRINTFORMW 把[%CSTR:称号%]%NAME:TARGET%带回了家★
	ELSE
		PRINTFORMW 勧誘了[%CSTR:称号%]%NAME:TARGET%★
	ENDIF
ELSE
	PRINTFORMW ★勧誘了学園的%CSTR:学年%、[%CSTR:称号%]%NAME:TARGET%★
ENDIF

;勧誘直後内で設定されがちな各種フラグの調整
TALENT:常時尿道塞 = COND("身嗜み：尿道拡張")
TALENT:常時Ａ肛塞 = COND("身嗜み：Ａ拡張")
TALENT:常時Ｖ振動棒 = COND("身嗜み：Ｖ拡張")

FOR LCOUNT, 90, 110
	SIF CEVENT(CSTRNAME:LCOUNT) == 0
		CONTINUE
	CALL DEL_CEVENT_GROUP, CSTRNAME:LCOUNT
	CSTR:(CSTRNAME:LCOUNT) = %CSVCSTR(NO:TARGET, LCOUNT)%
NEXT

PRINTL 
MEMO_LINECOUNT = LINECOUNT
;加入後台詞（なくても大丈夫）
CALL EVENT_PROFILE, "勧誘直後", TARGET
SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

CALL CHECK_GOUI, "汎用", TARGET
;行為の禁止をしている？
SIF CEVENT("尿道調教禁止")
	CALL PRINT_STRW, @"黄色_※注意※ %NAME:TARGET%是禁止尿道調教的角色。無論如何都不會允許"

TFLAG:地の文スキップ = 0
MEMO_LINECOUNT = LINECOUNT
;出現条件の出力。特殊な場合以外は書かなくて良いです
CALL EVENT_PROFILE, "出現条件", TARGET
SIF COND("部室出現可能") && TFLAG:地の文スキップ == 0 && LINECOUNT == MEMO_LINECOUNT
	CALL PRINT_STRW, @"%NAME:TARGET%和%CALLNAME:MASTER%的関系为_黄色_%CSTR:関係%_。因此、会在(昼)的部室里露露面。"

;颜绘の設定
IF CFLAG:颜绘 == 0
	DRAWLINE
	PRINTFORML %NAME:TARGET%还没有設定頭像。要設定吗？
	CALL PRINT_SELECT, "設定/不設定"
	IF RESULT == 0
		CALL SIMULATE_MAIDFACE, TARGET
	ELSE
		PRINTFORMW 今後想要設定的时候、请在设置中调整。
	ENDIF
ENDIF

IF PLACE("部室")
	IF COND("部室出現可能")
		DRAWLINE
		CALL SETFLAG, "優先順位", TARGET
		TFLAG:地の文スキップ = 0
		MEMO_LINECOUNT = LINECOUNT
		CALL EVENT_PROFILE, "初訪問"
		IF TFLAG:地の文スキップ == 0 && LINECOUNT == MEMO_LINECOUNT
			PRINTFORMW ……总而言之现在%CALLNAME:TARGET%会到部室里来了。
		ENDIF
	ELSE
		CFLAG:不在 = 1
		CFLAG:優先順位 = CHARANUM - 2
		TARGET = MEMO_TARGET
	ENDIF
ELSEIF PLACE("自宅")
	IF COND("自宅出現可能")
		CALL SETFLAG, "優先順位", TARGET
	ELSE
		CFLAG:不在 = 1
		CFLAG:優先順位 = CHARANUM - 2
		TARGET = MEMO_TARGET
	ENDIF
ENDIF


@SHOP_CHARA_SHOW
#DIM MEMO_TFLAG

DRAWLINE
CALL PRINT_STRL, @"勧誘对%CALLNAME:MASTER%的体質有兴趣的对象作为部員。详细情报请查看各个角色的_緑_□ 特記事項 □_"
CALL PRINT_STRL, "黄色_（魂的授受的话性交是最簡単的方式、因此如果没有特殊注释的话那么所有人都是作为性伴侣的）"
IF CONFIG("颜绘勧誘")
;	CALL PRINT_STRL, "緑_※颜绘表示中は動作が重いため、マウスではなく番号直接入力が推奨です。また、全角色表示は禁止します※"
ELSE
	CALL PRINT_STRL, "関于特技的情报可以通过按下名字按钮来进行确认"
ENDIF
DRAWLINE
CALL PRINT_STRL, "爱心粉紅_H_已经写好了个人结局的角色会用特殊颜色标注出来_爱心粉紅_H"

DRAWLINE
MEMO_TFLAG = TFLAG:过滤器
IF CONFIG("颜绘勧誘")
	CALL PRINT_SHOP_MAIDFACE
ELSE
	CALL SET_SHOP_CHARA_SHOW, "ＥＮＤ格子"
ENDIF
TFLAG:过滤器 = MEMO_TFLAG
DRAWLINE

SELECTCASE TFLAG:过滤器
CASE 200
	CALL PRINT_STR, "[999]返回 _黄色_[200]中等部以下_ [201]１年 [202]２年 [203]３年 [204]教師 [205]其他 [206]男性"
CASE 201
	CALL PRINT_STR, "[999]返回 [200]中等部以下 _黄色_[201]１年_ [202]２年 [203]３年 [204]教師 [205]其他 [206]男性"
CASE 202
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 _黄色_[202]２年_ [203]３年 [204]教師 [205]其他 [206]男性"
CASE 203
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 [202]２年 _黄色_[203]３年_ [204]教師 [205]其他 [206]男性"
CASE 204
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 [202]２年 [203]３年 _黄色_[204]教師_ [205]其他 [206]男性"
CASE 205
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 [202]２年 [203]３年 [204]教師 _黄色_[205]其他_ [206]男性"
CASE 206
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 [202]２年 [203]３年 [204]教師 [205]其他 _黄色_[206]男性"
CASE 210
	CALL PRINT_STR, "[999]返回 [200]中等部以下 [201]１年 [202]２年 [203]３年 [204]教師 [205]其他 [206]男性"
CASEELSE
	PRINTFORM [999]返回 [200]中等部以下 [201]１年 [202]２年 [203]３年 [204]教師 [205]其他 [206]男性
ENDSELECT

IF TFLAG:过滤器 == 210
	CALL PRINT_STR, @" _黄色_[210]刷新_"
ELSE
	CALL PRINT_STR, @" [210]刷新"
ENDIF

CALL PRINT_STR, @" [222]颜绘\@ CONFIG("颜绘勧誘") ? 〇 # － \@"

PRINTL 


;-------------------------------------------------
;勧誘する角色の一覧表示
;ARGSに"ＥＮＤ格子"って入れるとエンディングが書かれている角色の色が変わる
;-------------------------------------------------
@SET_SHOP_CHARA_SHOW, ARGS
#DIM LCOUNT
#DIM LCOUNT2

;刷新过滤器
IF TFLAG:过滤器 == 210
	CALL PRINT_UPDATE_MAIDFACE
	RETURN 1
ENDIF

PRINTFORML 　　　%TEXT_LJ("名前", 16)%　%TEXT_LJ("学年",14)%　%TEXT_LJ("称号",22)%　特技
DRAWLINE
;実際に角色を表示
FOR LCOUNT, 1, 200
	SIF COND("勧誘画面表示", LCOUNT) == 0
		CONTINUE

	;その角色が既にいるかで判定
	IF FINDCHARA(NO, LCOUNT) >= 0
		SETCOLOR DEF_COLOR("灰色")
		FONTSTYLE 4
	ENDIF

	;エンディングが書かれているか格子（TRYCALLなのでRESULTに0を代入しておく）
	IF ARGS == "ＥＮＤ格子"
		RESULT = 0
		TRYCALLFORM IS_SINGLE_ENDING_K{LCOUNT}
		SIF RESULT
			SETCOLOR DEF_COLOR("爱心粉紅")
	ENDIF
	CALL TEXT_SHOP_CHARA_SHOW, LCOUNT
	RESETCOLOR
	FONTSTYLE 0
NEXT

@TEXT_SHOP_CHARA_SHOW, ARG
#DIM LCOUNT
;名前、称号、特技
IF ARG >= 100
	PRINTFORM [{ARG}] 
ELSE
	PRINTFORM  [{ARG, 2}] 
ENDIF
IF STRCOUNT(CSVCSTR(ARG, GETNUM(CSTR, "CEVENT") ), "情報隠蔽")
	PRINTFORM %TEXT_LJ("？？？", 16)%　
	PRINTFORM %TEXT_LJ("？？？", 14)%　
	PRINTFORM %TEXT_LJ("？？？", 22)%　
	PRINTFORML ？？？
	RETURN 1
ELSE
	PRINTFORM %TEXT_LJ(CSVNAME(ARG), 16)%　
	IF CSVTALENT(ARG, GETNUM(TALENT, "処女") )
		PRINTFORM %TEXT_LJ(@"%CSVCSTR(ARG, GETNUM(CSTR, "学年") )%(処女)", 14)%　
	ELSE
		PRINTFORM %TEXT_LJ(CSVCSTR(ARG, GETNUM(CSTR, "学年") ), 14)%　
	ENDIF
	PRINTFORM %TEXT_LJ(CSVCSTR(ARG, GETNUM(CSTR, "称号") ), 22)%　
ENDIF

VARSET LOCALS
SPLIT CSVCSTR(ARG, GETNUM(CSTR, "建议") ), "＆", LOCALS
FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		SIF LCOUNT == 0
			PRINTBUTTON "没有", "没有"
		BREAK
	CASEELSE
		PRINTBUTTON LOCALS:LCOUNT, LOCALS:LCOUNT
		SIF LOCALS:(LCOUNT + 1) != ""
			PRINT ＆
	ENDSELECT
NEXT
PRINTL 

