;-------------------------------------------------
;調教指令実行前のパラメータの記録
;TCVAR:100～119を"調教指令実行前の重要パラメータの記録"用として確保してある
;-------------------------------------------------
@SAVE_PALAM_BEFORECOM
#DIM LCOUNT
FOR LCOUNT, 1, CHARANUM
	TCVAR:LCOUNT:ターン開始時好感度 = CFLAG:LCOUNT:好感度

	;射精or絶頂ゲージの開始値＆増加量
	TCVAR:LCOUNT:絶頂開始前 = BASE:LCOUNT:絶頂
	TCVAR:LCOUNT:絶頂増加 = 0

	;母乳ゲージの開始値＆増加量
	TCVAR:LCOUNT:母乳開始前 = BASE:LCOUNT:母乳
	TCVAR:LCOUNT:母乳増加 = 0

	;魂の開始値
	TCVAR:LCOUNT:魂開始前 = BASE:LCOUNT:魂

	;体力の開始値
	TCVAR:LCOUNT:体力開始前 = BASE:LCOUNT:体力
	TCVAR:LCOUNT:体力回復 = 0

	;精力の開始値＆減少量
	TCVAR:LCOUNT:精力開始前 = BASE:LCOUNT:精力
	TCVAR:LCOUNT:精力減少 = 0

	;受精確率
	TCVAR:LCOUNT:受精確率 = COND("受精確率", LCOUNT)
NEXT


;-------------------------------------------------
;PALAMの増減を表示させる（Ｐ潤は不顯示）
;対応する珠の増加分も表示するように変更
;ARGにはTARGETかASSIかTARGET:1しか入らない
;-------------------------------------------------
@NEW_UPCHECK_PALAM, ARG
#DIM LCOUNT
;このターンでの増減を行う前のPALAMの値の記録に使う
#DIM PALAM_PRE, 30

SIF ARG == 0 || ARG == MASTER
	RETURN 0

FOR LCOUNT, 0, 30
	PALAM_PRE:LCOUNT = PALAM:ARG:LCOUNT
	PALAM:ARG:LCOUNT = MIN(PALAM_LV(16), PALAM:ARG:LCOUNT + CUP:ARG:LCOUNT - CDOWN:ARG:LCOUNT)
NEXT

FOR LCOUNT, 0, 30
	SIF CUP:ARG:LCOUNT == 0 && CDOWN:ARG:LCOUNT == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(PALAMNAME:LCOUNT, 8)% = {PALAM_PRE:LCOUNT, 8} + 

	SELECTCASE GET_PALAMLV(CUP:ARG:LCOUNT)
	CASE 3
		SETCOLOR DEF_COLOR("黄色")
	CASE IS >= 4
		SETCOLOR DEF_COLOR("粉紅")
	ENDSELECT
	PRINTFORM {CUP:ARG:LCOUNT, 7}
	RESETCOLOR

	PRINTFORM  - {CDOWN:ARG:LCOUNT, 7} = {PALAM:ARG:LCOUNT, 8}　

	;宝珠を獲得するようならそれを表示
	IF GET_JUEL(LCOUNT)
		PRINTFORM %PALAMNAME:LCOUNT%の珠＋
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(LCOUNT), 5}
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT

;-------------------------------------------------
;表示を整形したUPCHECK
;-------------------------------------------------
@NEW_UPCHECK
#DIM LCOUNT
#DIM LCOUNT2
#DIM LCOUNT3
#DIM NUM_BASE
;10刻みで你、助手、partner
#DIM BASE_PRE, 30
#DIM BASE_AFT, 30
#DIM UP_BASE, 30
#DIM DOWN_BASE, 30
#DIM BASE_MAX, 30
#DIMS NAME_BASE, 30
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
#DIM MEMO_LINECOUNT_PRE
#DIM IS_PRINTNAME
#DIMS NAME_PRINTED
#DIM NUM_PRINT
#DIM ACTOR
#DIM IS_COLORCHANGE
;パラメーターに上下どちらの矢印を出すか
#DIM SHOW_UP
#DIM SHOW_DOWN
#DIM SHOW_UPDOWN
#DIM MAX_LENGTH

;PLAYERの阴茎のＰ潤の処理
CALL SETFLAG, "Ｐ潤変動処理", MASTER
IF COND("３Ｐ")
	SWAP SELECTCOM, SELECTCOM:2
	CALL SETFLAG, "Ｐ潤変動処理", PLAYER:1
	SWAP SELECTCOM, SELECTCOM:2
ENDIF

VARSET BASE_PRE
VARSET BASE_AFT
VARSET UP_BASE
VARSET DOWN_BASE
VARSET BASE_MAX
VARSET NAME_BASE

;射精or絶頂
BASE_AFT:0 = BASE:MASTER:絶頂
BASE_PRE:0 = TCVAR:MASTER:絶頂開始前
UP_BASE:0 = MAX(TCVAR:MASTER:絶頂増加, 0)
DOWN_BASE:0 = BASE_PRE:0 + UP_BASE:0 - BASE_AFT:0
BASE_MAX:0 = MAXBASE:MASTER:絶頂
NAME_BASE:0 = \@ PENIS(MASTER) ? 射精 # 絶頂 \@

;精力
BASE_AFT:1 = BASE:MASTER:精力
BASE_PRE:1 = TCVAR:MASTER:精力開始前
DOWN_BASE:1 = TCVAR:MASTER:精力減少
;UP_BASE:1 = MAX(BASE_AFT:1 + UP_BASE:1 - BASE_PRE:1, 0)
UP_BASE:1 = MAX(BASE_AFT:1 + DOWN_BASE:1 - BASE_PRE:1, 0)
NAME_BASE:1 = 精力

;魂
BASE_AFT:2 = BASE:MASTER:魂
BASE_PRE:2 = TCVAR:MASTER:魂開始前
DOWN_BASE:2 = BASE_PRE:2 - BASE_AFT:2
UP_BASE:2 = MAX(BASE_AFT:2 - BASE_PRE:2, 0)
NAME_BASE:2 = 魂

;射精or絶頂（助手）
IF ASSI
	;Ｗ推倒以外は基本的に体力表示无
	IF TEQUIP:ASSI:Ｗ推倒
		BASE_AFT:10 = BASE:ASSI:体力
		BASE_PRE:10 = TCVAR:ASSI:体力開始前
		UP_BASE:10 = TCVAR:ASSI:体力回復
		DOWN_BASE:10 = DOWNBASE:ASSI:体力
		BASE_MAX:10 = MAXBASE:ASSI:体力
		NAME_BASE:10 = 体力
	ENDIF

	BASE_AFT:11 = BASE:ASSI:絶頂
	BASE_PRE:11 = TCVAR:ASSI:絶頂開始前
	UP_BASE:11 = MAX(TCVAR:ASSI:絶頂増加, 0)
	DOWN_BASE:11 = BASE_PRE:11 + UP_BASE:11 - BASE_AFT:11
	BASE_MAX:11 = MAXBASE:ASSI:絶頂
	NAME_BASE:11 = \@ PENIS(ASSI) ? 射精 # 絶頂 \@(助)

	;精力（助手）
	BASE_AFT:12 = BASE:ASSI:精力
	BASE_PRE:12 = TCVAR:ASSI:精力開始前
	DOWN_BASE:12 = TCVAR:ASSI:精力減少
	UP_BASE:12 = MAX(BASE_AFT:12 + DOWN_BASE:12 - BASE_PRE:12, 0)
	NAME_BASE:12 = 精力(助)

	;Ｗ推倒以外は基本的に母乳表示无
	IF TEQUIP:ASSI:Ｗ推倒 && TALENT:ASSI:母乳体質
		BASE_AFT:13 = BASE:ASSI:母乳
		BASE_PRE:13 = TCVAR:ASSI:母乳開始前
		UP_BASE:13 = MAX(TCVAR:ASSI:母乳増加, 0)
		DOWN_BASE:13 = BASE_PRE:13 + UP_BASE:13 - BASE_AFT:13
		BASE_MAX:13 = MAXBASE:ASSI:母乳
		NAME_BASE:13 = 母乳
	ENDIF
ENDIF

;体力（partner）
BASE_AFT:20 = BASE:体力
BASE_PRE:20 = TCVAR:体力開始前
UP_BASE:20 = TCVAR:体力回復
DOWN_BASE:20 = DOWNBASE:体力
BASE_MAX:20 = MAXBASE:体力
NAME_BASE:20 = 体力

;射精（partner）
IF PENIS(TARGET)
	BASE_AFT:21 = BASE:絶頂
	BASE_PRE:21 = TCVAR:絶頂開始前
	UP_BASE:21 = MAX(TCVAR:絶頂増加, 0)
	DOWN_BASE:21 = BASE_PRE:21 + UP_BASE:21 - BASE_AFT:21
	BASE_MAX:21 = MAXBASE:絶頂
	NAME_BASE:21 = 射精
ENDIF

;精力（partner）
BASE_AFT:22 = BASE:精力
BASE_PRE:22 = TCVAR:精力開始前
DOWN_BASE:22 = TCVAR:精力減少
UP_BASE:22 = MAX(BASE_AFT:22 + DOWN_BASE:22 - BASE_PRE:22, 0)
NAME_BASE:22 = 精力

;母乳（partner）
IF TALENT:母乳体質
	BASE_AFT:23 = BASE:母乳
	BASE_PRE:23 = TCVAR:母乳開始前
	UP_BASE:23 = MAX(TCVAR:母乳増加, 0)
	DOWN_BASE:23 = BASE_PRE:23 + UP_BASE:23 - BASE_AFT:23
	BASE_MAX:23 = MAXBASE:母乳
	NAME_BASE:23 = 母乳
ENDIF

MAX_LENGTH = 0
FOR LCOUNT, 0, 30
	MAX_LENGTH = MAX(MAX_LENGTH, STRLENS(TOSTR(BASE_MAX:LCOUNT) ), STRLENS(TOSTR(BASE_AFT:LCOUNT) ) )
NEXT

IS_PRINTNAME = 0
FOR LCOUNT, 0, 3
	SELECTCASE LCOUNT
	CASE 0
		ACTOR = MASTER
	CASE 1
		SIF ASSI == 0
			CONTINUE
		ACTOR = ASSI
	CASE 2
		ACTOR = TARGET
	ENDSELECT

	MEMO_LINECOUNT_PRE = LINECOUNT
	CALL BLANKLINE

	NAME_PRINTED = --- %CALLNAME:ACTOR%%BL(1)%
	;名前の横に好感度も出す
	IF ACTOR != MASTER
		NAME_PRINTED += @"好感度:{CFLAG:ACTOR:好感度} "
		SELECTCASE FAV_TGAIN(ACTOR)
		CASE IS > 0
			NAME_PRINTED += @"_粉紅_↑{ABS(FAV_TGAIN(ACTOR) )}_ "
		CASE IS < 0
			NAME_PRINTED += @"_青_↓{ABS(FAV_TGAIN(ACTOR) )}_ "
		ENDSELECT
	ENDIF
	CALL DRAWLINES, NAME_PRINTED
	SIF ACTOR == TARGET && IS_PRINTNAME == 0 && FAV_TGAIN(ACTOR) == 0
		CLEARLINE 1

	MEMO_LINECOUNT = LINECOUNT

	;BASEの動き
	FOR LCOUNT2, 0, 10
		NUM_BASE = LCOUNT2 + LCOUNT*10
		SIF NAME_BASE:NUM_BASE == ""
			CONTINUE
		SIF UP_BASE:NUM_BASE == 0 && DOWN_BASE:NUM_BASE == 0
			CONTINUE

		IS_COLORCHANGE = 0
		SHOW_UP = 0
		SHOW_DOWN = 0
		SHOW_UPDOWN = 0

		PRINTFORM 　%TEXT_LJ(NAME_BASE:NUM_BASE, 8)% = 

		SELECTCASE NAME_BASE:NUM_BASE
		;魂は小数点表示する
		CASE "魂"
			PRINTFORM %TEXT_RJ(TEXTS("0.01", BASE_PRE:NUM_BASE), 8)% + 
		CASEELSE
			PRINTFORM {BASE_PRE:NUM_BASE, 8} + 
		ENDSELECT

		;射精の時などは色を変える
		SELECTCASE EXTSTR(NAME_BASE:NUM_BASE, "", "(")
		CASE "絶頂", "射精", "母乳"
			SELECTCASE UP_BASE:NUM_BASE
			CASE IS >= BASE_MAX:NUM_BASE*4/5
				SETCOLOR DEF_COLOR("粉紅")
				IS_COLORCHANGE = 1
			CASE IS >= 1
				SETCOLOR DEF_COLOR("黄色")
				IS_COLORCHANGE = 1
			ENDSELECT
		ENDSELECT

		SELECTCASE NAME_BASE:NUM_BASE
		;魂は小数点表示する
		CASE "魂"
			PRINTFORM %TEXT_RJ(TEXTS("0.01", UP_BASE:NUM_BASE), 7)% 
		CASEELSE
			PRINTFORM {UP_BASE:NUM_BASE, 7} 
		ENDSELECT

		RESETCOLOR
		PRINTFORM - 

		;数値が動く際は色を変える
		SELECTCASE DOWN_BASE:NUM_BASE
		CASE IS >= BASE_MAX:NUM_BASE*4/5
			SIF IS_COLORCHANGE == 0
				SETCOLOR DEF_COLOR("粉紅")
		CASE IS >= 1
			SIF IS_COLORCHANGE == 0
				SETCOLOR DEF_COLOR("黄色")
		ENDSELECT

		SELECTCASE NAME_BASE:NUM_BASE
		;魂は小数点表示する
		CASE "魂"
			PRINTFORM %TEXT_RJ(TEXTS("0.01", DOWN_BASE:NUM_BASE), 7)% 
			RESETCOLOR
			PRINTFORM = %TEXT_RJ(TEXTS("0.01", BASE_AFT:NUM_BASE), 8)%　%TEXT_RJ(NAME_BASE:NUM_BASE, 8)%
		CASEELSE
			PRINTFORM {DOWN_BASE:NUM_BASE, 7} 
			RESETCOLOR
			PRINTFORM = {BASE_AFT:NUM_BASE, 8}　%TEXT_RJ(NAME_BASE:NUM_BASE, 8)%
		ENDSELECT

		SELECTCASE EXTSTR(NAME_BASE:NUM_BASE, "", "(")
		CASE "絶頂"
			CALL PRINT_BAR, "溜めゲージ", BASE:ACTOR:絶頂, MAXBASE:ACTOR:絶頂, 15
			PRINTFORM (%TEXT_RJ(TOSTR(BASE:ACTOR:絶頂), MAX_LENGTH)%/%TEXT_RJ(TOSTR(MAXBASE:ACTOR:絶頂), MAX_LENGTH)%)
			SHOW_UP = 1
		CASE "射精"
			CALL PRINT_BAR, "溜めゲージ", BASE:ACTOR:絶頂, MAXBASE:ACTOR:絶頂, 15
			PRINTFORM (%TEXT_RJ(TOSTR(BASE:ACTOR:絶頂), MAX_LENGTH)%/%TEXT_RJ(TOSTR(MAXBASE:ACTOR:絶頂), MAX_LENGTH)%)
			SHOW_UP = 1
		CASE "精力"
			CALL PRINT_BAR, "減少ゲージ", BASE:ACTOR:精力, MAXBASE:ACTOR:精力, 15
			PRINTFORM (%TEXT_RJ(TOSTR(BASE:ACTOR:精力), MAX_LENGTH)%/%TEXT_RJ(TOSTR(MAXBASE:ACTOR:精力), MAX_LENGTH)%)
			SHOW_UPDOWN = 1
		CASE "魂"
			CALL PRINT_BAR, "減少ゲージ", BASE:ACTOR:魂, MAXBASE:ACTOR:魂, 15
			PRINTFORM (%TEXT_RJ(TEXTS("0.01", BASE:ACTOR:魂), MAX_LENGTH)%/%TEXT_RJ(TEXTS("0.01", MAXBASE:ACTOR:魂), MAX_LENGTH)%)
			SHOW_UPDOWN = 1
		CASE "体力"
			CALL PRINT_BAR, "減少ゲージ", BASE:ACTOR:体力, MAXBASE:ACTOR:体力, 15
			PRINTFORM (%TEXT_RJ(TOSTR(BASE:ACTOR:体力), MAX_LENGTH)%/%TEXT_RJ(TOSTR(MAXBASE:ACTOR:体力), MAX_LENGTH)%)
			SHOW_UPDOWN = 1
		CASE "母乳"
			CALL PRINT_BAR, "溜めゲージ", BASE:ACTOR:母乳, MAXBASE:ACTOR:母乳, 15
			PRINTFORM (%TEXT_RJ(TOSTR(BASE:ACTOR:母乳), MAX_LENGTH)%/%TEXT_RJ(TOSTR(MAXBASE:ACTOR:母乳), MAX_LENGTH)%)
			SHOW_UP = 1
		ENDSELECT

		IF SHOW_UP && UP_BASE:NUM_BASE > 0
			CALL PRINT_STR, @"_粉紅_ ↑{UP_BASE:NUM_BASE}_ "
		ELSEIF SHOW_DOWN && DOWN_BASE:NUM_BASE > 0
			CALL PRINT_STR, @"_青_ ↓{DOWN_BASE:NUM_BASE}_ "

		ELSEIF SHOW_UPDOWN && UP_BASE:NUM_BASE > DOWN_BASE:NUM_BASE
			CALL PRINT_STR, @"_粉紅_ ↑{UP_BASE:NUM_BASE - DOWN_BASE:NUM_BASE}_ "
		ELSEIF SHOW_UPDOWN && DOWN_BASE:NUM_BASE > UP_BASE:NUM_BASE
			CALL PRINT_STR, @"_青_ ↓{DOWN_BASE:NUM_BASE - UP_BASE:NUM_BASE}_ "
		ENDIF

		;追加の補足
		SELECTCASE EXTSTR(NAME_BASE:NUM_BASE, "", "(")
		CASE "絶頂"

		CASE "射精"
			CALL PRINT_STR, @"黄色_ %TEXTS("射精詳細", ACTOR)%"
		CASE "精力"

		CASE "魂"

		CASE "体力"
			SIF COND("疲労", ACTOR)
				CALL PRINT_STR, "青_ ★疲労困憊★"
			SHOW_UPDOWN = 1
		CASE "母乳"

		ENDSELECT

		PRINTL 
	NEXT

	SIF LINECOUNT > MEMO_LINECOUNT
		CALL BLANKLINE

	;獲得パラの表示
	CALL NEW_UPCHECK_PALAM, ACTOR

	SIF LINECOUNT > MEMO_LINECOUNT
		CALL BLANKLINE

	;獲得経験の羅列
	NUM_PRINT = 0
	FOR LCOUNT3, 0, 100
		;ここではもともと不顯示素質かを判定
		SELECTCASE EXPNAME:LCOUNT3
		CASE "", "助手経験", "経験値", "强制绝顶回数", "Ｈ回数", "特別経験", "情人旅館經驗"
			CONTINUE
		ENDSELECT

		SIF EXP_TGAIN(EXPNAME:LCOUNT3, ACTOR) == 0
			CONTINUE

		;名称が７文字以上の物は最後の"経験"をカットする
		IF STRLENS(EXPNAME:LCOUNT3) >= 13
			PRINTFORM %TEXT_LJ(@"　%SUBSTRING(EXPNAME:LCOUNT3, 0, STRLENS(EXPNAME:LCOUNT3) - 4)%+{EXP_TGAIN(EXPNAME:LCOUNT3, ACTOR)}", 17)%
		ELSE
			PRINTFORM %TEXT_LJ(@"　%EXPNAME:LCOUNT3%+{EXP_TGAIN(EXPNAME:LCOUNT3, ACTOR)}", 17)%
		ENDIF
		NUM_PRINT += 1
		IF NUM_PRINT == 7
			PRINTL 
			NUM_PRINT = 0
		ENDIF
	NEXT
	SIF NUM_PRINT
		PRINTL 

	SIF LINECOUNT > MEMO_LINECOUNT
		CALL BLANKLINE

	;表示が無い場合は名前を消す
	IF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT_PRE
	ELSE
		IS_PRINTNAME = 1
	ENDIF
NEXT

CALL BLANKLINE

IF BASE:PLAYER:精力 <= 0 && ASSIPLAY && STATE("撃沈", ASSI)
	PRINTFORMW ★調教者を%CALLNAME:MASTER%に交代しました。★
	TEQUIP:推倒 = 0
	SIF TARGET:1 == ASSI
		CALL SETFLAG, "Ｗ推倒解除"
	CALL REMOVE_STATE, ASSI, "恍惚"
	CALL SETFLAG, "調教者交代", 1
ENDIF
