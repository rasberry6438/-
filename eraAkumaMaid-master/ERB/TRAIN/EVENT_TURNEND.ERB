@TEXT_TURNEND
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM EJECT_FLAG
#DIM OSOUJI_FLAG
#DIM MEMO_EXP, 100

;撃沈判定ON
SIF STATE("撃沈", PLAYER)
	RETURN 0

;処女喪失で射精无。処女喪失の描写について詳しく描きたい場合は@KOJO_MESSAGE_PALAMCNG_K1, ARGSの"処女喪失の分岐が推奨です"
IF NOWEX:処女喪失
	CALL BLANKLINE
	PRINTFORMW 从%CALLNAME:TARGET%的秘唇里、作为处女的证明的红色的血垂落下来了…
	RETURN 0
ENDIF

;３Ｐ中
SIF COND("３Ｐ")
	RETURN 0

;--------------------------------------------------------
;性交後に拔出
;--------------------------------------------------------
;拔出かどうかの判断(10ぐらいなら、ほぼ拔出)。とりあえず、假阳具の時には不拔出ことに。
EJECT_FLAG = 0

;TFLAG:EJECTがONなら100％拔出
IF TFLAG:EJECT
ELSEIF SHOOT("中出し") && COND("絶頂", PLAYER) && INSERT("挿入")
	;避孕套使用なら100％拔出。
	IF TEQUIP:PLAYER:避孕套
		EJECT_FLAG = 100
	ELSEIF CONFIG("自動挿入解除OFF")
	ELSE
		EJECT_FLAG = NOWEX:同時絶頂 - TALENT:吸精体質*2

		SIF TALENT:娇小
			EJECT_FLAG += 2
		;イってない
		SIF NOWEX:絶頂数合計 == 0
			EJECT_FLAG -= 10
		SIF RAND:(MAXBASE:精液欲 + 1) >= EJECT_FLAG
			EJECT_FLAG = 0

		;中に出した精液の量
		IF SHOOT("膣内")
			EJECT_FLAG += EXP_GAIN("膣内精液経験")
		ELSEIF SHOOT("肛門")
			EJECT_FLAG += EXP_GAIN("肛内精液経験")
			;精液浣腸の時には排泄欲ゲージＭＡＸなら拔出
			SIF COND("精液浣腸") && BASE:排泄欲 >= MAXBASE:排泄欲
				EJECT_FLAG = 100
		ELSEIF SHOOT("尿道")
			EJECT_FLAG += EXP_GAIN("膀胱精液経験")
		ELSE
			EJECT_FLAG = 0
		ENDIF
		IF CONFIG("自動挿入解除選択肢") && (EJECT_FLAG >= 100 || (RAND:4 == 0 && EJECT_FLAG > 0) )
			CALL BLANKLINE
			IF TEQUIP:推倒
				PRINTL ……可以暂时拔出阴茎吗？
				CALL PRINT_SELECT, "给/不给"
			ELSE
				PRINTL ……拔出阴茎吗？
				CALL PRINT_SELECT, "拔出/不拔出"
			ENDIF
			IF RESULT == 0
				EJECT_FLAG = 100
			ELSE
				EJECT_FLAG = 0
			ENDIF
		ENDIF
	ENDIF
ENDIF

;TFLAG:EJECTがONなら100％拔出。精液浣腸の時には排泄欲ゲージＭＡＸなら拔出
IF TFLAG:EJECT || EJECT_FLAG >= 100 || (CONFIG("自動挿入解除選択肢") == 0 && EJECT_FLAG > 0 && RAND:4 == 0)
	CALL BLANKLINE
	;これから起きるイベントの決定
	CALL SELECTEVENT_TURNEND
	CALL KOJO_EVENT, @"%STR:イベント名%：地の文前"
	IF TFLAG:地の文スキップ == 0
		CALL BEFORETEXT_TURNEND
		CALL BLANKLINE
		CALL KOJO_EVENT, STR:イベント名
	ENDIF

	;清洁口交(挿入後)
	CALL OSOUJI_F

	CALL RESET_INSERT, TARGET, PLAYER
	RETURN 0
ENDIF

;清洁口交…毎回だとウザいかな？
IF RAND:3 == 0 && TEQUIP:PLAYER:避孕套 == 0 && TEQUIP:深喉 == 0
	OSOUJI_FLAG = 0
	SIF SHOOT("膣内") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ｖ")
		OSOUJI_FLAG = 1
	SIF SHOOT("肛門") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ")
		OSOUJI_FLAG = 2
	SIF SHOOT("尿道") && CHECK_STAIN(STAIN:MASTER:Ｐ, "尿")
		OSOUJI_FLAG = 3

	IF OSOUJI_FLAG && COND("絶頂", PLAYER) && INSERT("挿入") == 0 && PENIS(PLAYER)
		CALL OSOUJI_F
	ELSEIF TFLAG:清洁口交 && PENIS(PLAYER) && STAIN:PLAYER:Ｐ != SET_STAIN(0, "Ｐ")
		;双龙入穴口交はひとまず中止
		;IF SELECTCOM == 
		;	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%と%CALLNAME:ASSI%の阴茎を交互に咥え、汚れを舐め取っていった…
		;ELSE
			SIF ASSI && SELECTCOM:1 == SELECTCOM
				PRINTFORM %CALLNAME:ASSI%与
			PRINTFORM %CALLNAME:TARGET%
			SIF (ABL:信頼 >= 4 || ABL:精液中毒 >= 2) && COND("絶頂", PLAYER)
				 PRINTFORM 一边吸取剩下的精液、
			PRINTW 把阴茎上的污垢舔干净了……
		;ENDIF

		TFLAG:口上分岐 = 1
		CALL KOJO_EVENT, "清洁口交"

		IF ASSI && SELECTCOM:1 == SELECTCOM
			SWAP TARGET, ASSI
			TFLAG:口上分岐 = 1
			CALL KOJO_EVENT, "清洁口交"
			SWAP TARGET, ASSI

			IF (ABL:信頼 >= 4 || ABL:精液中毒 >= 2) && (ABL:ASSI:信頼 >= 4 || ABL:ASSI:精液中毒 >= 2)
				PRINTW 两个人似乎仅仅这样还不够、互相舔着嘴里积存的精液……
				CALL EQ_STAIN, TARGET, "Ｍ", ASSI, "Ｍ"
			ENDIF
		ENDIF

		;助手のためにPLAYERのＰ汚れを記録
		LOCAL = STAIN:PLAYER:Ｐ

		FOR LCOUNT, 0, 100
			MEMO_EXP:LCOUNT = EXP:LCOUNT
		NEXT

		;経験や汚れなどの処理は全部ここで行う
		CALL SETFLAG, "清洁口交", TARGET, MASTER

;		FOR LCOUNT, 0, 100
;			SELECTCASE EXP:LCOUNT
;			CASE MEMO_EXP:LCOUNT, 0
;			CASEELSE
;				PRINTFORML %EXPNAME:LCOUNT%＋{EXP:LCOUNT - MEMO_EXP:LCOUNT}
;			ENDSELECT
;		NEXT
;		CALL BLANKLINE

		IF ASSI && SELECTCOM:1 == SELECTCOM
			STAIN:PLAYER:Ｐ = LOCAL

			FOR LCOUNT, 0, 100
				MEMO_EXP:LCOUNT = EXP:ASSI:LCOUNT
			NEXT

			CALL SETFLAG, "清洁口交", ASSI, MASTER

;			FOR LCOUNT, 0, 100
;				SELECTCASE EXP:ASSI:LCOUNT
;				CASE MEMO_EXP:LCOUNT, 0
;				CASEELSE
;					PRINTFORML %EXPNAME:LCOUNT%＋{EXP:ASSI:LCOUNT - MEMO_EXP:LCOUNT} (%TEXTS("助手の名称")%)
;				ENDSELECT
;			NEXT
;			CALL BLANKLINE
		ENDIF

		RETURN 0
	ENDIF
ENDIF

;--------------------------------------------------------
;助手のお仕事としての清洁口交等
;--------------------------------------------------------
CALL EVENT_ASSI_AUTO, "お掃除"

;--------------------------------------------------------
;指令テ接吻ト 之２
;--------------------------------------------------------
CALL TEXT_B
;--------------------------------------------------------
;自発脱ぎ(体力が限界ならしてこない)
;--------------------------------------------------------
IF BASE:体力 >= 500
	CALL EVENT_AUTO_DATUI
	CALL EVENT_ZURENAOSHI
ENDIF
IF TEQUIP:Ｗ推倒
	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	IF BASE:体力 >= 500
		CALL EVENT_AUTO_DATUI
		CALL EVENT_ZURENAOSHI
	ENDIF
	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
ENDIF


;STR:イベント名 をここで決定させる
@SELECTEVENT_TURNEND
TFLAG:地の文分岐 = 0
;イきまくって無いなら、視線に気づく。そして、サービス。
IF SHOOT("肛門") && COND("精液浣腸") && BASE:排泄欲 >= MAXBASE:排泄欲
	;イっているなら、快感に浸ったり
	IF NOWEX:絶頂数合計 && ABL:受虐属性 >= 3
		STR:イベント名 = 中出し後恍惚
		TFLAG:地の文分岐 = 1
	ELSE
		STR:イベント名 = 无塞浣腸事件
		TFLAG:地の文分岐 = 1
	ENDIF
ELSEIF (NOWEX:絶頂数合計 < 12 || PALAM:連続絶頂COMBO数 < 5 || TALENT:好色 || TALENT:淫乱) && COND("疲労") == 0 && COND("指の使用") && COND("液体浣腸") == 0 && RAND:2
	IF (TALENT:害羞 && PALAM:欲情*(TALENT:恋慕 + 1) < 30000) || (TALENT:不知羞恥 == 0 && PALAM:欲情*(TALENT:恋慕 + 1) < 10000)
		IF CFLAG:好感度 * ABL:奉仕精神 >= 4500 && RAND:3 == 0
			STR:イベント名 = 中出し後くぱぁ
			TFLAG:地の文分岐 = 1
		ELSEIF CFLAG:好感度 * ABL:奉仕精神 >= 2000 && COND("脚の使用") && RAND:2 == 0
			STR:イベント名 = 中出し後くぱぁ
			TFLAG:地の文分岐 = 2
		ELSE
			STR:イベント名 = 中出し後害羞
			TFLAG:地の文分岐 = 1
		ENDIF
	ELSEIF TALENT:淫乱 || (TALENT:不知羞恥 && TALENT:好色 && PALAM:欲情 >= 3000) || (TALENT:恋慕 && PALAM:欲情 >= 10000)
		IF ABL:精液中毒 > RAND:10 && TEQUIP:PLAYER:避孕套 == 0
			STR:イベント名 = 中出し後精液掻き出し
			TFLAG:地の文分岐 = 1
		ELSE
			STR:イベント名 = 中出し後くぱぁ
			TFLAG:地の文分岐 = 3
		ENDIF
	ELSE
		IF ABL:露出癖 == 0
			STR:イベント名 = 中出し後害羞
			TFLAG:地の文分岐 = 2
		ELSE
			STR:イベント名 = 中出し後くぱぁ
			TFLAG:地の文分岐 = 4
		ENDIF
	ENDIF
;イっているなら、快感に浸ったり
ELSEIF NOWEX:絶頂数合計
	STR:イベント名 = 中出し後恍惚
	TFLAG:地の文分岐 = 2
ELSE
	STR:イベント名 = 中出し後挿入解除
	TFLAG:地の文分岐 = 1
ENDIF

@BEFORETEXT_TURNEND
#DIM NOWEX_HOLE
#DIM EXP_HOLE
#DIM ABL_GAPEHOLE
#DIM EXPGAIN_SEMEN
#DIMS NAME_HOLE
#DIMS HOLE
#DIMS CALLNAME_HOLE
#DIMS GAPING_HOLE

IF SHOOT("膣内")
	NOWEX_HOLE = NOWEX:Ｖ絶頂
	EXP_HOLE = EXP:Ｖ経験
	ABL_GAPEHOLE = ABL:Ｖ拡張
	EXPGAIN_SEMEN = EXP_GAIN("膣内精液経験")

	NAME_HOLE = 膣
	HOLE = 膣口
	CALLNAME_HOLE = 阴部
ELSEIF SHOOT("肛門")
	NOWEX_HOLE = NOWEX:Ａ絶頂
	EXP_HOLE = EXP:Ａ経験
	ABL_GAPEHOLE = ABL:Ａ拡張
	EXPGAIN_SEMEN = EXP_GAIN("肛内精液経験")

	NAME_HOLE = 肛門
	HOLE = 菊花
	CALLNAME_HOLE = 菊門
ELSEIF SHOOT("尿道")
	NOWEX_HOLE = NOWEX:尿道絶頂
	EXP_HOLE = EXP:尿道経験
	ABL_GAPEHOLE = ABL:尿道拡張
	EXPGAIN_SEMEN = EXP_GAIN("膀胱精液経験")

	NAME_HOLE = 尿道
	HOLE = 排泄口
	CALLNAME_HOLE = オシッコの穴
ENDIF

IF ABL_GAPEHOLE >= 7 && RAND:5 == 0
	GAPING_HOLE = 保持完全张開的
ELSEIF ABL_GAPEHOLE >= 5 && RAND:4 == 0
	GAPING_HOLE = 张開口的
ELSEIF ABL_GAPEHOLE >= 3 && RAND:3 == 0
	GAPING_HOLE = 尚未完全閉合的
ELSEIF EXPGAIN_SEMEN >= 4 && RAND:3 == 0
	GAPING_HOLE = 黏黏糊糊的
ELSEIF EXP_HOLE >= 100 && RAND:3 == 0
	GAPING_HOLE = 外翻的
ELSEIF ( (SHOOT("膣内") && EX:Ｖ絶頂 >= 3) || (SHOOT("肛門") && EX:Ａ絶頂 >= 3) ) && RAND:3 == 0
	GAPING_HOLE = 红腫的
ELSEIF PALAM:欲情 >= 3000 && RAND:3 == 0
	GAPING_HOLE = 発情的
ELSEIF ABL_GAPEHOLE >= 2 && RAND:3 == 0
	GAPING_HOLE = 松緩的
ELSEIF TALENT:娇小 && RAND:2 == 0
	GAPING_HOLE = 幼嫩的
ELSEIF EXP_HOLE <= 50 && RAND:3 == 0
	GAPING_HOLE = 可愛的
ELSEIF SHOOT("膣内") && PALAM:Ｖ潤 >= 3000
	GAPING_HOLE = 粘滑的
ELSE
	GAPING_HOLE = 
ENDIF
GAPING_HOLE += @"%CALLNAME_HOLE%"

IF INSERT("騎乗位") && (TEQUIP:推倒 || ABL:奉仕精神 >= 3)
	PRINTFORM %CALLNAME:TARGET%が
	IF NOWEX_HOLE && RAND:3 == 0
		PRINTFORM 因快感而颤抖的腰
	ELSEIF EXP_HOLE >= 100 && RAND:2 == 0
		PRINTFORM 连根部都吞下的腰
	ELSE
		PRINTFORM 手贴在%CALLNAME:PLAYER%的胸膛上
	ENDIF
	IF BASE:PLAYER:絶頂 >= 3000 && RAND:2 == 0
		PRINTFORM 势頭良好
	ELSE
		PRINTFORM 慢慢地、慢慢地
	ENDIF
	PRINTFORM 举起来、
ELSE
	IF COND("絶頂", PLAYER) >= 2 && TEQUIP:PLAYER:避孕套 == 0 && RAND:2
		PRINTFORM 灌得满满的
	ELSEIF NOWEX:絶頂数合計 && RAND:2 == 0
		PRINTFORM 达到顶峰
	ELSEIF NOWEX_HOLE > 2 && ABL_GAPEHOLE >= 3
		SIF NOWEX:Ｖ絶頂 >= 2 || NOWEX:Ａ絶頂 >= 2
			PRINTFORM 用快感
		PRINTFORM 松弛了
	ELSE
		PRINTFORM 缠绕
	ENDIF
	PRINTFORM 从%NAME_HOLE%中将阴茎
	IF BASE:PLAYER:絶頂 >= 3000 && RAND:2 == 0
		PRINTFORM 势頭良好
	ELSE
		PRINTFORM 慢慢地、慢慢地
	ENDIF
	PRINTFORM 拔出来
	IF RAND:3 == 0 && NOWEX:絶頂数合計 >= 2 && COND("脚の使用")
		IF INSERT("後背位")
			PRINTFORM 高高地撅起屁股的姿势
		ELSEIF INSERT("屈曲位") && COND("指の使用") && RAND:2
			PRINTFORM 由于过于兴奋,双臂抱住了腿,胯股之间完全大開着
		ELSEIF INSERT("正常位") || INSERT("屈曲位")
			PRINTFORM 委身于快感懒散地\@ RAND:2 ? 大腿 # 脚 \@大開着
		ENDIF
	ENDIF
	PRINTFORM %CALLNAME:TARGET%的
ENDIF
IF NOWEX_HOLE >= MAX(2, RAND:5) && EX:絶頂数合計 >= 10
	PRINTFORM 荡然无存
ELSEIF TALENT:娇小 && RAND:2 == 0
	PRINTFORM 年幼
ELSEIF TALENT:娇小 && ABL_GAPEHOLE <= 1
	PRINTFORM 小小的
ENDIF
PRINTFORM 从%HOLE%中

IF SHOOT("肛門") && BASE:排泄物 && COND("液体浣腸") + COND("精液浣腸")
	PRINTFORM 脏东西
	IF COND("温泉浣腸")
		PRINTFORM 和小便
	ELSEIF COND("液体浣腸")
		PRINTFORM 和药液
	ENDIF
	PRINTFORM 混浊不清的
ELSEIF NOWEX:処女喪失 || (EX:処女喪失 && RAND:3 == 0)
	PRINTFORM 混杂着破瓜的血
ELSEIF EXPGAIN_SEMEN >= 9 && RAND:3 == 0
	PRINTFORM 惊人的量
ELSEIF EXPGAIN_SEMEN >= 4 && RAND:3 == 0
	PRINTFORM 粘度
ELSEIF NOWEX_HOLE && RAND:3 == 0
	PRINTFORM 起泡沫了
ELSEIF TEQUIP:PLAYER:避孕套
	PRINTFORM 搅拌好了
ELSE
	PRINTFORM %TEXTR("ド萝莉と/とろりと/白色")%
ENDIF
IF TEQUIP:PLAYER:避孕套
	IF SHOOT("膣内")
		PRINTFORM 爱液
	ELSE
		PRINTFORM 粘液
	ENDIF
ELSE
	PRINTFORM 精液
ENDIF
IF EXPGAIN_SEMEN >= 9 && RAND:3 == 0
	PRINTW 逆流而出
ELSEIF EXPGAIN_SEMEN >= 4 && RAND:2 == 0
	PRINTFORM 溢出来
ELSEIF EXPGAIN_SEMEN >= 4
	PRINTFORM 被推挤出来
ELSE
	PRINTFORM %TEXTR("滴落/洒出/流出")%
ENDIF

SELECTCASE STR:イベント名
CASE "中出し後恍惚"
	PRINTFORMW 。
	IF TFLAG:地の文分岐 == 1
		PRINTFORM %CALLNAME:TARGET%
		SIF MAX(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂, NOWEX:Ｍ絶頂, NOWEX:尿道絶頂) >= 2
			PRINTFORM 剧烈
		PRINTFORM 委身于绝顶的余韵中、从松弛的肛門
		SIF TEQUIP:PLAYER:避孕套 == 0 && BASE:排泄物
			PRINTFORM 继精液之后
		SELECTCASE BASE:排泄物
		CASE 0
			PRINTFORM 大量注入的精液
		CASE IS <= 2
			PRINTFORM 散发着强烈气味的%TEXTR("排泄物/汚物")%
		CASE IS <= 4
			PRINTFORM 大量的%TEXTR("排泄物/汚物")%
		CASE IS <= 6
			PRINTFORM 有几天的份儿的%TEXTR("排泄物/汚物")%
		CASEELSE
			PRINTFORM 像制造一座小山一般的%TEXTR("排泄物/汚物")%
		ENDSELECT
		PRINTFORMW 看起来很舒服%TEXTR("地喘息着/的样子")%…
		;排泄した時のSOURCEを加算
		CALL SETFLAG, "排泄"
		CALL SOURCE_COM136, 100
		CALL CALC_SOURCE
		CALL MARK_GOT_CHECK
	ELSE
		PRINTFORM %CALLNAME:TARGET%
		SIF MAX(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:Ａ絶頂, NOWEX:Ｂ絶頂, NOWEX:Ｍ絶頂, NOWEX:尿道絶頂) >= 2
			PRINTFORM 剧烈
		PRINTFORM 委身于绝顶的余韵中、
		IF NOWEX:絶頂漏尿 && PENIS(TARGET) == 0
			PRINTFORM 尿液从放松的股間中%TEXTR("汩汩/哗哗")%流下的同时
		ELSEIF COND("脚の使用") == 0 || RAND:2 == 0 || TEQUIP:拘束
		ELSE
			PRINTFORM 懒散地张開双脚
		ENDIF
		PRINTFORMW 看起来很舒服%TEXTR("地喘息着/的样子")%…
	ENDIF
CASE "无塞浣腸事件"
	PRINTFORMW 。
	PRINTFORMW ……然后
	IF NOWEX:絶頂数合計
		PRINTFORM 难道无法忍受这绝顶的快乐吗？
	ELSE
		IF CUP:快Ａ >= 1000
			PRINTFORM 难道无法忍受对肛門的刺激吗？
		ELSE
			PRINTFORM 是不是忍不住想要大便了？
		ENDIF
	ENDIF
	PRINTFORM %CALLNAME:TARGET%
	SELECTCASE BASE:排泄物
	CASE 0
		PRINTFORMW 腸内被大量注入的精液从肛門里黏黏糊糊地漏了出来…
	CASE IS <= 3
		PRINTFORMW \@ TEQUIP:PLAYER:避孕套 == 0 ? 被精液染色 # \@腸内的汚物从肛門排出去了…
	CASE IS <= 5
		PRINTFORMW \@ TEQUIP:PLAYER:避孕套 == 0 ? 涂上精液的 # \@\@ RAND:2 ? 排泄物 # 腸内的汚物 \@被盛大地排泄出去了
		PRINTFORMW 也许是因为便秘的原因、最開始很软的东西以很厉害的气势飞了出来之後、\@ RAND:2 ? 看上去很硬的 # 连成一条的 \@像粗巧克力棒一样的污物用了一些时间慢慢地推開肛門出来了…
	CASEELSE
		PRINTFORMW 腸内积存了几天的汚物\@ TEQUIP:PLAYER:避孕套 == 0 ? 和精液一起 # \@被气势磅礴地排出了…
	ENDSELECT

	;排泄した時のSOURCEを加算
	CALL SETFLAG, "排泄"
	CALL SOURCE_COM136, 100
	CALL CALC_SOURCE
	CALL MARK_GOT_CHECK
CASE "中出し後くぱぁ"
	PRINTFORMW 。
	PRINTFORM %CALLNAME:TARGET%注意到%CALLNAME:PLAYER%的视线、
	IF TFLAG:地の文分岐 == 1
		PRINTFORM 害羞地笑着、
		IF TEQUIP:PLAYER:避孕套
			PRINTFORM 刚刚从%CALLNAME:PLAYER%的阴茎中榨出精液
		ELSEIF RAND:3 == 0 && EXPGAIN_SEMEN >= 3
			PRINTFORM 榨出了一大堆
		ELSE
			PRINTFORM %TEXTR("沾满精液的/刚刚结束中出还滴落着精液的")%
		ENDIF
		SIF AGE(TARGET) == 1 && SHOOT("尿道") == 0
			PRINTFORM 孩子气、幼稚
		PRINTFORMW 为了让%CALLNAME_HOLE%更容易看见而用手指伸展開来…
	ELSEIF TFLAG:地の文分岐 == 2
		PRINTFORM 羞耻地用手遮着脸、
		IF RAND:2
			PRINTFORMW 张開双腿、露出了耻丘的害羞部分……
		ELSE
			PRINTFORMW \@ TEQUIP:PLAYER:避孕套 ? 因兴奋而 # 溢出精液\@重复着松弛和收紧动作的%HOLE%露了出来…
		ENDIF
	ELSEIF TFLAG:地の文分岐 == 3
		PRINTFORM 将%GAPING_HOLE%%TEXTR("広開/用手指拨開、")%
		IF ( (SHOOT("膣内") && TALENT:淫壷) || (SHOOT("肛門") && TALENT:尻穴狂い)  || (SHOOT("尿道") && TALENT:尿道狂) ) && RAND:3 == 0
			PRINTFORM 贪婪地蠢动
		ELSEIF ( (SHOOT("膣内") && ABL:Ｖ感覚 >= 3) || (SHOOT("肛門") && ABL:Ａ感覚 >= 3)  || (SHOOT("尿道") && TALENT:尿道性感) ) && RAND:3 == 0
			PRINTFORM 嘻嘻哈哈
		ELSEIF TEQUIP:PLAYER:避孕套 || RAND:3 == 0
			PRINTFORM 沉溺于肉欲
		ELSE
			PRINTFORM 沾满精液
		ENDIF
		IF SHOOT("尿道")
			PRINTFORM 观察情况
		ELSEIF ABL_GAPEHOLE >= 3 && RAND:3 == 0
			PRINTFORM 肉洞
			IF ( (SHOOT("膣内") && TALENT:淫壷) || (SHOOT("肛門") && TALENT:尻穴狂い) ) && RAND:3 == 0
				IF SHOOT("膣内")
					PRINTFORM 深处的子宫口
				ELSE
					PRINTFORM 直到深处
				ENDIF
			ELSE
				PRINTFORM を
			ENDIF
		ELSEIF PALAM:欲情 >= 3000 && RAND:2 == 0
			IF SHOOT("膣内")
				PRINTFORM 阴道内
			ELSE
				PRINTFORM 直肠粘膜
			ENDIF
		ELSE
			PRINTFORM 观察情况
		ENDIF
		PRINTFORMW 清楚地展示著……
	ELSE
		PRINTFORM 腿再度张開、
		IF AGE(TARGET) < 3 || ABL:Ａ拡張 < 2 || SHOOT("肛門") || RAND:3 == 0
			PRINTFORM 害羞的部分
		ELSEIF SHOOT("尿道")
			IF PENIS(TARGET) == 0 && CALCF("Ｃ発情") >= 3 && RAND:4 == 0
				IF TALENT:陰蒂環 && RAND:2
					PRINTFORM 显眼的\@ TALENT:淫核 ?自满的#充血了\@淫核勃起状况
				ELSE
					PRINTFORM 由于激烈的兴奋而膨胀的阴蒂
				ENDIF
			ELSEIF TALENT:好色 + TALENT:淫乱 && RAND:3 == 0
				PRINTFORM 一边缓慢地溢出尿一边发痒的oshiko的孔
			ELSEIF TALENT:尿道性感 && RAND:3 == 0
				PRINTFORM 被给予的快感不检点的尿道口
			ELSE
				PRINTFORM %TEXTR("因被侵犯而变得泥泞、一直敞開的粘膜/没有插入什么却合也合不上的孔")%
			ENDIF
		ELSE
			IF PENIS(TARGET) == 0 && CALCF("Ｃ発情") >= 3 && RAND:4 == 0
				IF TALENT:陰蒂環 && RAND:2
					PRINTFORM 显眼的\@ TALENT:淫核 ?自满的#充血了\@淫核勃起状况
				ELSE
					PRINTFORM 由于激烈的兴奋而膨胀的阴蒂
				ENDIF
			ELSEIF TALENT:好色 + TALENT:淫乱 && RAND:3 == 0
				PRINTFORM 一边溢出起泡的粘液一边发出光泽
			ELSEIF ABL:Ｖ感覚 >= 3 && RAND:3 == 0
				PRINTFORM 被给予的快感不检点地绽放的那个
			ELSE
				PRINTFORM %TEXTR("被侵犯的阴唇张開的阴部/没有插入什么却合也合不上的阴道口")%
			ENDIF
		ENDIF
		PRINTFORMW 被清楚地展示著…
	ENDIF
CASE "中出し後害羞"
	PRINTFORMW 。
	PRINTFORM %CALLNAME:TARGET%注意到%CALLNAME:PLAYER%的视线、
	IF TFLAG:地の文分岐 == 1
		PRINTFORMW 慌忙用手遮住了害羞的部分……
	ELSE
		PRINTFORMW 迅速地開始调整自己的姿势
	ENDIF
CASE "中出し後精液掻き出し"
	PRINTFORMW 。
	PRINTFORM %CALLNAME:TARGET%注意到%CALLNAME:PLAYER%的视线、
	IF EXPGAIN_SEMEN >= 9
		PRINTFORM 用手接住从%GAPING_HOLE%中溢出的大量精液、
	ELSEIF EXPGAIN_SEMEN >= 4
		PRINTFORM 用手指挑起从%GAPING_HOLE%中流出来的精液、
	ELSE
		PRINTFORM 把指尖插入%GAPING_HOLE%中抠出精液、
	ENDIF
	IF ABL:精液中毒 >= 5 && RAND:2 == 0
		PRINTFORM 浮现出恍惚的笑容
	ELSEIF ABL:精液中毒 >= 3 && RAND:2 == 0
		PRINTFORM 带着兴奋的神情
	ELSE
		PRINTFORM 看上去很美味一般
	ENDIF
	IF EXPGAIN_SEMEN >= 9 && RAND:2 == 0
		PRINTFORM 润喉
	ELSEIF EXPGAIN_SEMEN >= 4
		PRINTFORM 品尝
	ELSE
		PRINTFORM 舔舐
	ENDIF
	PRINTFORMW 的样子展示着…
	;口に精液汚れ
	CALL ADD_STAIN, TARGET, "Ｍ", "精液"
CASEELSE
	PRINTFORMW …
ENDSELECT


@TEXT_B
;--------------------------------------------------------
;舐陰莖
;--------------------------------------------------------
IF SELECTCOM == 4
	IF TEQUIP:推倒
		IF ABL:施虐属性 >= 5 && STAIN:Ｐ !=  SET_STAIN(0, "Ｐ") && RAND:3
			PRINTFORMW %CALLNAME:TARGET%把弄脏了的阴茎在%CALLNAME:PLAYER%的口内咯吱咯吱地擦、去除了污秽…
			STAIN:Ｐ =  SET_STAIN(0, "Ｐ")
		;PLAYERが你なら、汚臭無視が必要
		ELSEIF NO:PLAYER == 0 && ABL:PLAYER:技巧 + ABL:PLAYER:受虐属性 >= 5 && TALENT:PLAYER:汚臭無視 && STAIN:Ｐ !=  SET_STAIN(0, "Ｖ") && RAND:3
			PRINTFORM %CALLNAME:PLAYER%呢？
			IF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				PRINTFORM 被肠液弄脏了
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "精液")
				PRINTFORM 沾满精液
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				PRINTFORM 被爱液弄脏了
			ENDIF
			PRINTFORMW 精心打扫%CALLNAME:TARGET%的阴茎、咕噜咕噜地咽了下去……
			CALL RESET_STAIN_EX, "Ｐ", TARGET
			STAIN:PLAYER:Ｍ = 0
		ELSEIF ABL:PLAYER:技巧 + ABL:PLAYER:奉仕精神 + ABL:PLAYER:受虐属性 >= 8 && STAIN:Ｐ != 2 && RAND:3
			PRINTFORM %CALLNAME:PLAYER%呢？
			IF CHECK_STAIN(STAIN:Ｐ, "Ａ")
				PRINTFORM 被肠液弄脏了
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "精液")
				PRINTFORM 沾满精液
			ELSEIF CHECK_STAIN(STAIN:Ｐ, "Ｖ")
				PRINTFORM 被爱液弄脏了
			ENDIF
			PRINTFORMW 精心打扫%CALLNAME:TARGET%的阴茎、咕噜咕噜地咽了下去……
			STAIN:Ｐ = SET_STAIN(0, "Ｖ")
			STAIN:PLAYER:Ｍ = 0
		ENDIF
	ENDIF
ENDIF

;--------------------------------------------------------
;指挿入＆Gスポット刺激
;--------------------------------------------------------
IF SELECTCOM == 8
	IF NOWEX:Ｖ絶頂 && TALENT:害羞 && TEQUIP:口枷 == 0 && EXP_GAIN("舔指経験") == 0
		IF TEQUIP:振動棒
			PRINTFORMW 在快感融化的%CALLNAME:TARGET%的嘴角用爱液将湿润到根部的阴茎伸出、%CALLNAME:TARGET%害羞地舔了一下……
			;角色の口に阴部的污垢が追加
			CALL ADD_STAIN, TARGET, "Ｍ", "Ｖ"
		ELSE
			PRINTFORML 将从阴部拔出的手指伸向嘴角的%CALLNAME:TARGET%、就这么带着快感蕩漾的表情舔了起来…
			;角色の口⇔調教者の指的污垢が移動
			CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "手"
		ENDIF
		EXP:舔指経験 += 1
	ENDIF
ENDIF
;--------------------------------------------------------
;舐陰強制or69
;--------------------------------------------------------
IF SELECTCOM == 98 || (SELECTCOM == 99 && PENIS(PLAYER) == 0)
	;阴部をお掃除…奉仕精神、技巧の合計値が8以上で実行値を30以上上回っているなら汚れをなめ取る
	IF ABL:技巧 + ABL:奉仕精神 >= 8 && TCVAR:干劲 >= 30 && STAIN:PLAYER:Ｖ != SET_STAIN(0, "Ｖ")
		CALL OSOUJI_V
	ENDIF
ENDIF
;--------------------------------------------------------
;尿道探针
;--------------------------------------------------------
IF SELECTCOM == 151 && TEQUIP:尿道探针
	;オシッコが溜まっているなら排尿
	IF BASE:尿意 >= 10000
		IF GETBIT(TEQUIP:筋弛緩剤, 1)
			PRINTFORML 从%CALLNAME:TARGET%的尿道中拔出、从松弛的洞穴中咕噜咕噜地流出尿液……
		ELSE
			PRINTFORML 从%CALLNAME:TARGET%的尿道抽出、为了冲洗杂菌而使之排尿
		ENDIF

		;放尿のソース獲得
		CALL SOURCE_COM144, 80

		;利尿剤の効果は消失
		TEQUIP:利尿剤 = 0
		;主动放尿したので、最後まで出し切ります
		BASE:尿意 = 0
		;放尿
		EXP:放尿経験 += 1
	;	PRINTL 小便经验+1
		NOWEX:放尿 += 1
		EX:放尿 += 1
	ELSE
		PRINTFORML %CALLNAME:PLAYER%在用心侵虐着%CALLNAME:TARGET%的尿道后、小心翼翼地拔出了阴茎……
	ENDIF
	TEQUIP:尿道探针 = 0
ENDIF
