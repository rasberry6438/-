@SHOW_USERCOM
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;存在している調教のみを数えていって何番目か
#DIM NUM_TRAIN
#DIM NEXT_LINE
#DIM NUM_COLUMN
#DIMS SHOW_FILTER
#DIMS COM_NAMES
#DIMS COM_NAMES_HIDDEN

REDRAW 0
;主導権が無いフラグを一時的に消去
CLEARBIT FLAG:状況判定, 11

;角色独自のお願いを選ばせたい場合の処理
TFLAG:取引可能 = 0
TRYCALLFORM WISH_K{NO:TARGET}_ADDSELECT

;300番台は各種过滤器
;PRINTFORM 过滤器：
VARSET SHOW_FILTER
SIF CONFIG("Ｖ調教过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "Ｖ系_300/"

SIF CONFIG("Ａ調教过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "Ａ系_301/"

SIF CONFIG("尿道調教过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "尿道_302/"

SIF CONFIG("逆挿入过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "逆挿入_303/"

SIF CONFIG("準備不足过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "準備不足_304/"

SIF CONFIG("道具調教过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "道具_305/"

SIF CONFIG("ハード調教过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "重口指令_306/"

SIF CONFIG("消耗品过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "消耗品_307/"

;VARSET SHOW_FILTER
SIF CONFIG("実行判定过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "実行判定_308/"

;PRINTFORM %BL(17)%
;CALL PRINT_LIST, SHOW_FILTER
;PRINTL 

SIF CONFIG("極厚避孕套")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "極厚避孕套_309/"

SIF CONFIG("立位")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "立位挿入_310/"

SIF CONFIG("脱掉胖次过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "脱掉胖次_316/"
SIF CONFIG("脱掉鞋子过滤器")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "脱掉鞋子_315/"

IF TALENT:MASTER:豊穣
	SIF CONFIG("豊穣过滤器")
		SHOW_FILTER += "黄色/"
	SHOW_FILTER += "豊穣_317/"
ENDIF

SHOW_FILTER += "重置避孕套_350/"
SIF TEQUIP:手下留情
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "手下留情_351/"

CALL PRINT_LIST, SHOW_FILTER
PRINTL 
PRINTL 

IF FLAG:COM_ABLEの一括OFF || CONFIG("指令过滤器")
	FLAG:COM_ABLEの一括OFF = 0

	NUM_TRAIN = -1

	NEXT_LINE = 0
	NUM_COLUMN = 4

	;直接選択できる指令は499番までだが、現状は300までで十分すぎる
	FOR LCOUNT, 0, 300
		SIF STRLENS(TRAINNAME:LCOUNT) == 0
			CONTINUE

		NUM_TRAIN += 1

		IF CONFIG("指令过滤器：全指令表示") == 0
			TRYCALLFORM COM_ABLE{LCOUNT}
			IF RESULT == 0
				IF CONFIG("実行判定过滤器")
					TFLAG:実行値BOUNS = 1000
					TRYCALLFORM COM_ABLE{LCOUNT}
					IF RESULT == 0
						TFLAG:実行値BOUNS = 0
						CONTINUE
					ENDIF
				ELSE
					CONTINUE
				ENDIF
			ENDIF
		ENDIF

		RESETCOLOR
		SIF FIRSTCOM(LCOUNT) == 0
			SETCOLOR DEF_COLOR("明灰色")

		IF CONFIG("指令过滤器") == 0
			;指令表示过滤器
			SELECTCASE DITEMTYPE:0:LCOUNT
			;禁止設定
			CASE 2
				CONTINUE
			ENDSELECT
		ELSE
		 	SELECTCASE TRAINNAME:LCOUNT
			CASE "～予備～"
				CONTINUE
			ENDSELECT
		ENDIF

		IF CONFIG("指令过滤器") == 0
			;指令名が通常と違う場合には色を変える(推倒状態での基本指令は色を変えない)
			SIF TRAIN_NAME_SELECT(LCOUNT) != TRAIN_NAME_STANDARD(LCOUNT)
				SETCOLOR DEF_COLOR("爱心粉紅")

			;PLAYを終わらせられる時には色を変える
			SELECTCASE LCOUNT
			;潤滑不足の際には色を変える
			CASE 60 TO 69
				SIF COND("Ｖ潤充分") == 0
					SETCOLOR DEF_COLOR("青")
			CASE 70 TO 79
				SIF COND("Ａ潤充分") == 0
					SETCOLOR DEF_COLOR("青")
			CASE 31
				SIF TEQUIP:振動棒
					SETCOLOR DEF_COLOR("黄色")
			CASE 33
				SIF TEQUIP:肛用振動棒
					SETCOLOR DEF_COLOR("黄色")
			CASE 99
				SIF TEQUIP:六九式
					SETCOLOR DEF_COLOR("黄色")
			CASE 132
				SIF TEQUIP:鼻钩
					SETCOLOR DEF_COLOR("黄色")
			CASE 133
				SIF TEQUIP:眼罩
					SETCOLOR DEF_COLOR("黄色")
			CASE 134
				SIF TEQUIP:拘束
					SETCOLOR DEF_COLOR("黄色")
			CASE 135
				SIF TEQUIP:口枷
					SETCOLOR DEF_COLOR("黄色")
			CASE 136
				SIF COND("肛門栓")
					SETCOLOR DEF_COLOR("黄色")
			CASE 149
				SIF TEQUIP:导管
					SETCOLOR DEF_COLOR("黄色")
			CASE 152
				SIF TEQUIP:尿道振動棒
					SETCOLOR DEF_COLOR("黄色")
			CASE 170
				SIF TEQUIP:攝像機
					SETCOLOR DEF_COLOR("黄色")
			CASE 171
				SIF TEQUIP:野外PLAY
					SETCOLOR DEF_COLOR("黄色")
			CASE 172
				SIF TEQUIP:浴室PLAY
					SETCOLOR DEF_COLOR("黄色")
			CASE 173
				SIF TEQUIP:新妻PLAY
					SETCOLOR DEF_COLOR("黄色")
			CASE 174
				SIF TEQUIP:室内露出PLAY
					SETCOLOR DEF_COLOR("黄色")
			;消耗品
			CASE 260 TO 279
				;色が異なる場合には消耗品过滤器がONでも例外的に表示
				SIF CONFIG("消耗品过滤器") && GETCOLOR() != DEF_COLOR("黄色") && GETCOLOR() != DEF_COLOR("爱心粉紅")
					CONTINUE
			CASEELSE
				;選んでも時間が進まない指令は色を変える
				SIF NOTTIME_PLAY(LCOUNT)
					SETCOLOR DEF_COLOR("緑")
			ENDSELECT
		ELSE
			SELECTCASE DITEMTYPE:0:LCOUNT
			;通常状態
			CASE 0
				SELECTCASE LCOUNT
				CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
					SETCOLOR DEF_COLOR("暗黄色")
				CASEELSE
					RESETCOLOR
				ENDSELECT
			;禁止設定
			CASE 2
				SETCOLOR DEF_COLOR("緑")
			ENDSELECT
		ENDIF

		;実行判定过滤器ON
		IF TFLAG:実行値BOUNS
			SETCOLOR DEF_COLOR("水色")
			TFLAG:実行値BOUNS = 0
		ENDIF

		PRINTFORMC %TRAIN_NAME_SELECT(LCOUNT)%\@ CHECK_COM("喜欢的体位", LCOUNT) ? ☆ # \@[{NUM_TRAIN, 3}]

		NEXT_LINE += 1

		SIF NEXT_LINE % NUM_COLUMN == 0
			PRINTL 
	NEXT
	SIF NEXT_LINE % NUM_COLUMN
		PRINTL 
ENDIF
RESETCOLOR

CALL SETFLAG, "助手相手の恋慕外し終了"

;-------------------------------------------------
;特殊な指令
;-------------------------------------------------
PRINTL 

;HARDで被逆推ているなら、主導権が無いフラグをセット
;IF CONFIG("ＨＡＲＤ") && TEQUIP:推倒
;	SETBIT FLAG:状況判定, 11
;ELSE
;	CLEARBIT FLAG:状況判定, 11
;ENDIF


NEXT_LINE = 0
VARSET COM_NAMES
VARSET COM_NAMES_HIDDEN

IF CONFIG("指令过滤器") == 0

	PRINTC 能力表示[400]

	IF TEQUIP:推倒
		PRINTC 污渍显示[410]
	ELSE
		PRINTC 污渍显示＆管理[410]

		SIF CHECK_CLO("胖次露出") == 0
			SETCOLOR DEF_COLOR("灰色")
		PRINTFORMC 請求脫下%PANTIES(TARGET, "胖次")%[420]
		RESETCOLOR

		SIF CHECK_CLO("裸体")
			SETCOLOR DEF_COLOR("灰色")
		PRINTFORMC 变成裸体[430]
		RESETCOLOR
	ENDIF

	PRINTL 
	IF COND("魂の取引可能")
		PRINTFORMC 做交易[440]
	ELSEIF COND("天使の取引可能") || TFLAG:取引可能
		PRINTFORMC 拜托了[440]
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORMC 拜托了[440]
	ENDIF
	RESETCOLOR

	;助手とできるのは悪魔の取引のみ
	SIF ASSI == 0 || STATE("撃沈", ASSI) || (COND("悪魔の取引可能", ASSI) == 0 && COND("天使の取引可能", ASSI) == 0)
		SETCOLOR DEF_COLOR("灰色")
	PRINTFORMC 和%TEXTS("助手の名称")%做交易[450]
	RESETCOLOR

	SIF CHECK_EXCHANGE("調教者") == 0
		SETCOLOR DEF_COLOR("灰色")
	IF PLAYER == MASTER
		;PRINTFORMC %CALLNAME:ASSI%⇔你[460]
		PRINTFORMC 和%TEXTS("助手の名称")%交换[460]
	ELSE
		PRINTFORMC 和%CALLNAME:MASTER%交换[460]
	ENDIF
	RESETCOLOR

	SIF CHECK_EXCHANGE("partner") == 0
		SETCOLOR DEF_COLOR("灰色")
	IF PLAYER == MASTER
		IF TEQUIP:Ｗ推倒
			PRINTFORMC 交换主導权[470]
		ELSEIF TEQUIP:推倒
			PRINTFORMC 和%TEXTS("助手の名称")%交换[470]
		ELSEIF ASSI && TARGET == ASSI:1
			PRINTFORMC 享受一下%TEXTS("対象の名称")%[470]
		ELSE
			PRINTFORMC 享受一下%TEXTS("助手の名称")%[470]
		ENDIF
	ELSE
		PRINTFORMC 逆転立場[470]
	ENDIF
	RESETCOLOR

	PRINTC Config[555]
	PRINTC 指令过滤器[888]
	PRINTC  
	PRINTC 结束调教[999]
ELSE
	PRINTC Config[555]

	PRINTC 指令过滤器終了[888]

	IF CONFIG("指令过滤器：全指令表示")
		PRINTC 隐藏无法実行的指令[890]
	ELSE
		PRINTC 指令全显示[890]
	ENDIF
ENDIF

PRINTL 

SIF MAX(PREVCOM, PREVCOM:2) >= 0 && CONFIG("指令过滤器") == 0
	CALL SHOW_PREVCOM

MEMO_LINECOUNT = LINECOUNT
;選んだ指令次第で軽い説明表示。原則１行
SELECTCASE TFLAG:選択番号
CASE 300
	SIF CONFIG("Ｖ調教过滤器")
		PRINTFORML  Ｖ系指令过滤器ON。对方是処女時、使用膣的行為均実行不可
CASE 301
	SIF CONFIG("Ａ調教过滤器")
		PRINTFORML  Ａ系指令过滤器ON。使用对方的菊穴的行為均実行不可
CASE 302
	SIF CONFIG("尿道調教过滤器")
		PRINTFORML  尿道指令过滤器ON。使用对方的尿道的行為均実行不可
CASE 303
	SIF CONFIG("逆挿入过滤器")
		PRINTFORML  逆挿入指令过滤器ON。挿入主人公的行為均実行不可
CASE 304
	SIF CONFIG("準備不足过滤器")
		PRINTFORML  準備不足过滤器ON。潤滑不足時挿入行為均実行不可
CASE 305
	SIF CONFIG("道具調教过滤器")
		PRINTFORML  道具指令过滤器ON。使用振动棒等行為均実行不可
CASE 306
	SIF CONFIG("ハード調教过滤器")
		PRINTFORML  硬核系指令过滤器ON。使对方負担过大的行為均実行不可
CASE 307
	SIF CONFIG("消耗品过滤器")
		PRINTFORML  消耗品过滤器ON。隐藏使用润滑液等的指令
CASE 308
	SIF CONFIG("実行判定过滤器")
		CALL PRINT_STR, "実行判定过滤器ON。実行値不足的指令会以_水色_ 水色 _表示、選択的话会显示現在値与要求値_L_"
CASE 309
	SIF CONFIG("極厚避孕套")
		PRINTFORML  極厚避孕套过滤器ON。带着極厚避孕套時的挿入行為会使受到的快感激減
CASE 310
	SIF CONFIG("立位")
		PRINTFORML  立位模式ON。性交系命令会变化至立位(所有的插入状态都会被重置)

CASE 315
	SIF CONFIG("脱掉鞋子过滤器")
		CALL PRINT_STR, "脱靴过滤器ON。当是ON時、屋内Ｈ開始時会脱掉靴子_L_"
CASE 316
	SIF CONFIG("脱掉胖次过滤器")
		CALL PRINT_STR, "脱掉胖次过滤器ON。这个ON的時候与H開始的時候会把妨碍阴部露出的衣装脱掉_L_"
CASE 317
	IF CONFIG("豊穣过滤器")
		CALL PRINT_STR, "豊穣ON。对着胸部射精or尽情揉就会变大了_L_"
	ELSE
		CALL PRINT_STR, "豊穣OFF。胸部停止了无止尽的扩大_L_"
	ENDIF

CASE 350
	PRINTFORML 今回的挿入行為中、重置对安全套的选择

CASE 351
	IF TEQUIP:手下留情
		PRINTFORML %CALLNAME:PLAYER%决定让%CALLNAME:TARGET%先加忍耐（积蓄快感）！
	;	CALL PRINT_STRL, @"二人の技巧差を考慮すると、_黄色_絶頂数の合計が{CALCF("寸止め可能絶頂数") + 1}になる_までは寸止めする事が出来そうだ"
	;	CALL PRINT_STRL, "ただし、_黄色_強すぎる快感が与えられた_or_黄色_限界を超えて快感が蓄積された_ら絶頂を許してしまうかもしれない"
	ELSE
		PRINTFORML %CALLNAME:PLAYER%停止了对%CALLNAME:TARGET%的手下留情
	ENDIF

CASE 888
	SIF CONFIG("指令过滤器")
		CALL PRINT_STR, " 選択的指令_緑_禁止_（_暗黄色_这个颜色_的表示无法禁止。必须要表示出来)_L_"
CASE 890
	IF CONFIG("指令过滤器：全指令表示")
		PRINTFORML  [{TARGET, 2}]%CALLNAME:TARGET%現在无法実行的行為全部显示出来
	ELSE
		PRINTFORML  [{TARGET, 2}]%CALLNAME:TARGET%現在能够実行的行為全部表示出来
	ENDIF
ENDSELECT

SIF MEMO_LINECOUNT == LINECOUNT
	PRINTL 


@USERCOM
#DIM CHOICE
#DIM MEMO_LINECOUNT

CHOICE = RESULT

CLEARBIT FLAG:状況判定, 11

TFLAG:選択番号 = CHOICE

;指令選択
IF CHOICE >= 0 && CHOICE <= 299
	IF CONFIG("指令过滤器") == 0
		CALL NO_SELECTCOM, CHOICE

		SIF RESULT == 999
			RETURN 0

		SELECTCOM = RESULT
		TRYCALLFORM COM_ABLE{SELECTCOM}

		IF RESULT == 0
			IF CONFIG("実行判定过滤器")
				TFLAG:実行値BOUNS = 1000
				TRYCALLFORM COM_ABLE{SELECTCOM}
				TFLAG:実行値BOUNS = 0
				SIF RESULT == 0
					RETURN 0
			ELSE
				RETURN 0
			ENDIF
		ENDIF

		;主導権が無いフラグを解除
		CLEARBIT FLAG:状況判定, 11

		REDRAW 1

		DOTRAIN SELECTCOM
	ELSE
		CALL NO_SELECTCOM, CHOICE
		CHOICE = RESULT

		SELECTCASE CHOICE
		CASE 999
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
			RETURN 0
		;禁止不可指令
		CASE 0 TO 9, 60, 61, 65, 71, 75, 80 TO 95, 98, 99, 230 TO 289
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
			RETURN 0
		CASEELSE
			IF STRLENS(TRAINNAME:CHOICE) == 0
				CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
				RETURN 0
			ENDIF

		 	SELECTCASE TRAINNAME:CHOICE
			CASE "～予備～"
				CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
				RETURN 0
			ENDSELECT
		ENDSELECT

		;値が２で禁止
		INVERTBIT DITEMTYPE:0:CHOICE, 1
	ENDIF
;各種过滤器のＯＮＯＦＦ
ELSEIF CHOICE >= 300 && CHOICE <= 351
	SELECTCASE CHOICE
	;立位モード
	CASE 310
		INVERTBIT FLAG:調教过滤器, CHOICE - 300
		CALL RESET_INSERT, TARGET, PLAYER
	CASE 300 TO 310
		INVERTBIT FLAG:調教过滤器, CHOICE - 300
	;脱掉鞋子
	CASE 315
		INVERTBIT FLAG:調教过滤器, CHOICE - 300
		SIF GETBIT(FLAG:調教过滤器, 15) && PLACE("野外") == 0
			CALL SET_SHOES, ""
	;脱掉胖次
	CASE 316
		INVERTBIT FLAG:調教过滤器, CHOICE - 300
		SIF GETBIT(FLAG:調教过滤器, 16) && CHECK_CLO("下衣露出") && CHECK_CLO("Ｖ露出可") == 0
			CALL SET_BOTTOM, ""
	CASE 317
		SIF TALENT:MASTER:豊穣
			INVERTBIT FLAG:調教过滤器, CHOICE - 300
	CASE 350
		CALL SETFLAG, "初始化避孕套交渉"
	CASE 351
		INVERTBIT TEQUIP:手下留情, 0
	ENDSELECT

	CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録

	RETURN 1
;Config[555]
ELSEIF CHOICE == 555
	CALL SET_CONFIGURE
;指令过滤器へ[888]
ELSEIF CHOICE == 888
	INVERTBIT FLAG:調教过滤器, 20
;調教終了[999]
ELSEIF CHOICE == 999 
	IF COND("調教終了可能")
		REDRAW 1
		BEGIN AFTERTRAIN
	ELSE
		CALL KOJO_EVENT, "調教終了不可能"
		IF TEQUIP:PLAYER:拘束
			PRINTFORMW %CALLNAME:TARGET%似乎没有将、%CALLNAME:PLAYER%从拘束中解放出来的打算的样子……
		ELSEIF TEQUIP:推倒
			PRINTFORMW %CALLNAME:TARGET%没有打算放開%CALLNAME:PLAYER%的样子……
		ELSE
			PRINTFORMW %CALLNAME:TARGET%好像没有満足的様子……
		ENDIF
	ENDIF
ELSEIF CONFIG("指令过滤器")
	;調教指令全显示[890]
	SIF CHOICE == 890
		INVERTBIT FLAG:調教过滤器, 30
ELSE
	;能力表示[400]
	IF CHOICE == 400
		CALL NEW_PRINT_STATE, TARGET, "通常"
	;污渍显示[410]
	ELSEIF CHOICE == 410
		CALL STAIN_INFO
	ELSEIF CHOICE == 420 && CHECK_CLO("胖次露出")
		CALL EVENT_DATUI, "胖次"
	ELSEIF CHOICE == 430 && CHECK_CLO("裸体") == 0
		CALL EVENT_DATUI, "裸体"
		CALL SETFLAG, "全裸化", TARGET
	;取引
	ELSEIF CHOICE == 440
		IF COND("魂の取引可能") || COND("天使の取引可能") || TFLAG:取引可能
			CFLAG:Ｈ最中の取引 = TARGET
			CALL EVENT_WISH
			CFLAG:Ｈ最中の取引 = 0
		ELSE
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
		ENDIF
	ELSEIF CHOICE == 450
		IF ASSI && STATE("撃沈", ASSI) == 0 && (COND("悪魔の取引可能", ASSI) || COND("天使の取引可能", ASSI))
			CFLAG:Ｈ最中の取引 = TARGET
			SWAP TARGET, TARGET:1
			CALL EVENT_WISH
			SWAP TARGET, TARGET:1
			CFLAG:Ｈ最中の取引 = 0
		ELSE
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
		ENDIF
	ELSEIF CHOICE == 460
		IF CHECK_EXCHANGE("調教者")
			CALL SETFLAG, "調教者交代"
			IF FLAG:助手を調教した == 0
				MEMO_LINECOUNT = LINECOUNT
				CALL KOJO_EVENT, "調教者交代"
				SIF LINECOUNT == MEMO_LINECOUNT
					CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
			ENDIF
		ELSEIF ASSI
			IF COND("只和你Ｈ") || COND("只和你Ｈ", ASSI)
				CALL PRINT_STRW, @"イエロー_更改不能进行因为其中一个的[ＮＴＲ禁止]開関是ON"
			ELSEIF TALENT:贞操重视 + TALENT:ASSI:贞操重视 && COND("相性◎", ASSI) == 0
				CALL PRINT_STRW, @"イエロー_其中一个持有[贞操重视]的場合需要[相性：◎]才可以进行"
			ELSEIF COND("相性○", ASSI) == 0 && ABL:信頼 < 5 && COND("助手可能") == 0
				CALL PRINT_STRW, @"イエロー_%CALLNAME:TARGET%需要[相性：〇]or[信頼LV5]or[%TEXTS("助手の名称")%可能]"
			ELSEIF TEQUIP:PLAYER:推倒 || TEQUIP:推倒 || TEQUIP:新妻PLAY || TEQUIP:攝像機
				CALL PRINT_STRW, @"イエロー_在[拘束中]、[撮影中]、単独的[推倒中]这几种状态下是不能交替的"
			ENDIF
		ELSE
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
		ENDIF
	;TARGETとTARGET:1(いないならASSI)を交換[470]
	ELSEIF CHOICE == 470
		IF CHECK_EXCHANGE("partner")
			CALL SETFLAG, "対象交代"
			IF FLAG:助手を調教した == 0
				MEMO_LINECOUNT = LINECOUNT
				SIF TEQUIP:Ｗ推倒 == 0
					CALL KOJO_EVENT, "対象交代"
				SIF LINECOUNT == MEMO_LINECOUNT
					CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
			ENDIF
		ELSEIF ASSI
			IF COND("只和你Ｈ") || COND("只和你Ｈ", ASSI)
				CALL PRINT_STRW, @"イエロー_更改不能进行因为其中一个的[ＮＴＲ禁止]開関是ON"
			ELSEIF COND("助手可能") == 0
				CALL PRINT_STRW, @"イエロー_%CALLNAME:TARGET%需要[%TEXTS("助手の名称")%可能]才可进行此操作"
			ELSEIF (TEQUIP:推倒 && TEQUIP:Ｗ推倒 == 0) || TEQUIP:PLAYER:推倒 || TEQUIP:拘束 || TEQUIP:攝像機
				CALL PRINT_STRW, @"イエロー_在[拘束中]、[撮影中]、単独的[推倒中]这几种状态下是不能交替的"
			ENDIF
		ELSE
			CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録
		ENDIF
	ENDIF
ENDIF

SIF CONFIG("指令过滤器")
	CLEARLINE LINECOUNT - TFLAG:LINECOUNT記録



;ARGSには"調教者"or"partner"が入る
@CHECK_EXCHANGE(ARGS)
#FUNCTION

SELECTCASE ARGS
;PLAYERとPLAYER:1を交代させる。（調教開始時にPLAYER = MASTER、PLAYER:1 = ASSIとなっている）
CASE "調教者"
	SIF PLAYER <= 0 || PLAYER:1 <= 0
		RETURNF 0
	SIF TEQUIP:PLAYER:推倒 || TEQUIP:推倒 || TEQUIP:新妻PLAY || TEQUIP:攝像機
		RETURNF 0
	SIF COND("只和你Ｈ") || COND("只和你Ｈ", ASSI)
		RETURNF 0
	;撃沈していたら交代できない
	SIF STATE("撃沈", PLAYER) || STATE("撃沈", PLAYER:1)
		RETURNF 0
	;仲が良くないとダメ
	SIF COND("相性○", ASSI) == 0 && ABL:信頼 < 5 && COND("助手可能") == 0
		RETURNF 0
	;贞操重视持ちは相手を選ぶ
	SIF TALENT:贞操重视 + TALENT:ASSI:贞操重视 && COND("相性◎", ASSI) == 0
		RETURNF 0

	RETURNF 1
;TARGETと(TARGET:1 or ASSI)間でTARGETを交代
CASE "partner"
	;推倒中は、Ｗ推倒でないと交代できない
	SIF TEQUIP:推倒 && TEQUIP:Ｗ推倒 == 0
		RETURNF 0
	SIF TEQUIP:PLAYER:推倒 || TEQUIP:拘束 || TEQUIP:攝像機
		RETURNF 0
	SIF COND("只和你Ｈ") || COND("只和你Ｈ", ASSI)
		RETURNF 0
	;TARGET:1が居る(主にＷ推倒中)
	IF TARGET:1
		SIF BASE:精力 <= 0 || BASE:(TARGET:1):精力 <= 0
			RETURNF 0
	 	SIF STATE("撃沈", TARGET:1)
			RETURNF 0
		;指导者が調教している時
		IF PLAYER != MASTER
			SIF COND("助手可能") == 0
				RETURNF 0
		ENDIF
		RETURNF 1
	;助手は居る（交代＝助手とpartnerの立場交換）
	ELSEIF ASSI
		SIF BASE:精力 <= 0 || COND("助手可能") == 0
			RETURNF 0
	 	SIF STATE("撃沈", ASSI)
			RETURNF 0
		RETURNF 1
	ENDIF
ENDSELECT

RETURNF 0

@SHOW_PREVCOM
PRINTFORM < 前回指令 : %TEXTS("前回指令")%
SIF NUM("回合数") == 0 && PREVCOM:2 >= 0
	PRINTFORM \@ PLAYER == MASTER ? (%TEXTS("助手の名称")%) # (主人) \@
CALL PRINT_STR, @"イエロー_%STR:前回指令における絶頂%"
PRINTFORML  >
