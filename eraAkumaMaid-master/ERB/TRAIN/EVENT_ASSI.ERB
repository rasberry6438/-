;指导者能力ARGSの解説
@COMMENT_ADVISER(ARGS)
#FUNCTIONS

VARSET LOCALS
SELECTCASE ARGS
CASE "自動補充"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、可以免费補充消耗品
CASE "魔性の香"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、有情绪和快楽的加成
CASE "精力増大"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、主人公精力+10
CASE "罗曼蒂克"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、初期情绪+_HH_HH
CASE "再生"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎回合全員的体力回復50
CASE "调教师"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎单位時間内欲求不満+10％
CASE "好奇の視線"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎单位時間内欲求不満+５％＆恥情の珠的取得+５％
CASE "天才肌"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、習得の珠的取得+５％
CASE "淫蕩薬師"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、媚薬の効果+50％
CASE "縁結"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、每天早上随机对象的好感度增加
CASE "金運招来Ⅱ"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、每天早上钱包都会变得更重
CASE "金運招来"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、每天早上钱包都会稍微变重_L_
CASE "魂蒐集"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎单位時間内魂回復量+0.1
CASE "包容力"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、軽減同伴的恐怖和反感
CASE "理性主義"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、主人公不会进入恍惚状態
CASE "单人玩家"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、自慰系指令的効果少许上昇
CASE "和和闹闹"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、時間経過下全員的好感度会少许上昇
CASE "无用相談"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、実行値獲得加成＆情绪有惩罚
CASE "抑制器"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎单位時間内全員的欲求不満-10％
CASE "見習的心情"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、習得の珠的取得+５％
CASE "淫精の伝道師"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、Ｈ的射精量ＵＰ
;淫精の伝道師の類似能力
CASE "催精师"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、Ｈ的射精量ＵＰ
CASE "堕天使の甘言"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、甜言蜜语的成功率ＵＰ＆继续時間増加
CASE "冷却器"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎单位時間内全員的欲求不満-5％
CASE "動画撮影"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、同伴的恥情の上昇量+50％＆実行値-５
CASE "媚薬精製"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、不知道为什么会增加媚薬
CASE "天然潤滑液"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、不知道为什么会增加潤滑液
CASE "無能"
	LOCALS = 什么也没有、什么也做不了、所以是無能。
CASE "努力的前輩"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、同伴的恭順+10％ 反感-15％
CASE "誘惑"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、同伴的欲情+50％＆体力消費増加
CASE "狩猟の美学"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、同伴的欲情+70％＆体力消費減少
CASE "痛痛飞走啦"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、减轻同伴的疼痛和恐惧
CASE "小悪魔の轻语"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、甜言蜜语时的感度ＵＰ＆攻势ＵＰ
CASE "浄化の火"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、毎回合全員体力回復100、%CALLNAME:MASTER%的精力回復5
CASE "原生主义者"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、脱下衣物的指令更容易实现
CASE "解放感"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、野外的実行値獲得少量BOUNS
CASE "欺诈师"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、実行値獲得少量BOUNS
CASE "撒娇鬼"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、胸部相関指令実行値獲得少量BOUNS
CASE "餓狼の相"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、空腹度在【中】以上时恐怖上昇＆情緒会受到惩罚
CASE "急成長"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、根据満足BONUS%TEXTS("助手の名称")%的習得の珠大幅度上升
CASE "縄酔酒"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、拘束中情緒会有BOUNS
CASE "解読"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、反感大幅度軽減
CASE "三星大厨"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、%TEXTS("助手の名称")%的弁当効果上升
CASE "受胎呪詛"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、妊娠概率上昇 ※对于有[难以妊娠]的对象无效…
CASE "受胎祝福"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、強制排卵
CASE "純陰"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、将会释放陰気降低对象的身体機能
CASE "繁栄の光"
	LOCALS = 在被选为%TEXTS("助手の名称")%的情况下、孩子的成長速度上昇。这个能力对胎児也有效果
ENDSELECT
RETURNF LOCALS

@EVENTCHECK_A
SIF ASSI <= 0
	RETURN 0

;-------------------------------------------------------------------------------------------------
;技能の成長
;-------------------------------------------------------------------------------------------------
CALL EX_ABLUP, ASSI

;助手のLVUP
SELECTCASE ABL:ASSI:助手
CASE 0
	SIF EXP:ASSI:助手経験 < 50
		RETURN 0
CASE 1
	SIF EXP:ASSI:助手経験 < 200
		RETURN 0
CASE 2
	SIF EXP:ASSI:助手経験 < 500
		RETURN 0
CASE 3
	SIF EXP:ASSI:助手経験 < 1000
		RETURN 0
CASE 4
	SIF EXP:ASSI:助手経験 < 2000
		RETURN 0
CASEELSE
	RETURN 0
ENDSELECT

ABL:ASSI:助手 += 1
CALL BLANKLINE
PRINTFORML %CALLNAME:ASSI%对%TEXTS("助手の名称")%的职责的理解更深了
PRINTFORMW %CALLNAME:ASSI%的%TEXTS("助手の名称")%LV变成了{ABL:ASSI:助手}！
;助手への指示呼び出し
CALL ASSI_CHANGE_INDICATE, ASSI


;-------------------------------------------------
;CONDの助手能力限定版
;ARGSは / 区切りで複数の文字列を指定可能
;-------------------------------------------------
@CADV(ARGS, ARG)
#FUNCTION
#DIM LCOUNT
SIF ARG < 0
	RETURNF 0
SIF ARG == 0 && ASSI:1
	ARG = ASSI:1

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		RETURNF 0
	;それ以外はCSTR:建议を見る。添え字の強化を鑑みて同じ文字列を含んでいたら1を返す
	CASEELSE
		SIF STRCOUNT(CSTR:ARG:建议, LOCALS:LCOUNT)
			RETURNF 1
	ENDSELECT
NEXT
RETURNF 0

;-------------------------------------------------
;指导者の開始時特典
;-------------------------------------------------
@EVENTTRAIN_BONUS_ADVISER
SIF ASSI:1 <= 0
	RETURN 0

;指导者能力発動
IF CADV("魔性の香")
	DRAWLINE
	PRINTL ＜魔性の香＞
	PRINTFORMW %CALLNAME:(ASSI:1)%焚烧了有着昂揚作用的散发着複雑气味的香。

	FLAG:情緒 += 60
	PALAM:欲情 += 5000

	CALL TEXT, "情緒変化", 60
	PRINTFORML 欲情+5000
	IF ASSI
		PALAM:ASSI:欲情 += 5000
		PRINTFORML 欲情+5000(%CALLNAME:ASSI%)
	ENDIF
	WAIT
ENDIF
IF CADV("罗曼蒂克")
	DRAWLINE
	PRINTL ＜罗曼蒂克＞
	PRINTFORMW 周围弥漫着浪漫的气氛…
	FLAG:情緒 += 60
	CALL PRINT_STR, "情緒+ _HH_HH_W"
ENDIF
IF CADV("精力増大")
	DRAWLINE
	PRINTL ＜精力増大＞
	PRINTFORMW 力量从%CALLNAME:(ASSI:1)%流入了体内、%CALLNAME:MASTER%的精力增强了！
	MAXBASE:MASTER:精力 += 10
	BASE:MASTER:精力 += 10
	PRINTFORMW 精力+10(%CALLNAME:MASTER%)
ENDIF
IF CADV("包容力")
	DRAWLINE
	PRINTL ＜包容力＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%守护在一旁、大幅度抑制了%CALLNAME:TARGET%恐怖与反感上昇的趋势。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%恐怖与反感上昇的趋势被大幅度抑制了。
	ENDIF
	TEQUIP:包容力 = 1
ENDIF
IF CADV("理性主義")
	DRAWLINE
	PRINTL ＜理性主義＞
	PRINTFORMW 在%CALLNAME:MASTER%因为一時的快楽忘记了自我的時候、因为%CALLNAME:(ASSI:1)%发出的声音而取回了理性。
	TEQUIP:MASTER:理性主義 = 1
ENDIF
IF CADV("单人玩家")
	DRAWLINE
	PRINTL ＜单人玩家＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为站在一旁的%CALLNAME:(ASSI:1)%、%CALLNAME:TARGET%的自慰系指令的効果上升了少许。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%的自慰系指令的効果上升了少许。
	ENDIF
	TEQUIP:单人玩家 = 1
ENDIF
IF CADV("无用相談")
	DRAWLINE
	PRINTL ＜无用相談＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%在雑談混杂了一些商谈。虽然比预想中还有用但是气氛被破壊了！
	ELSE
		PRINTFORML %CALLNAME:(ASSI:1)%混进了一些H的雑談。
		PRINTFORMW 多少听进去了一些商谈的内容……但是气氛被破壊了！
	ENDIF
	TEQUIP:MASTER:无用相談 = 1
ENDIF
IF CADV("淫精の伝道師")
	DRAWLINE
	PRINTL ＜淫精の伝道師＞
	PRINTFORML 因为站在一旁的%CALLNAME:(ASSI:1)%、全员射精的射精量都增多了。
	SIF PENIS(ASSI:1)
		PRINTFORML ※%CALLNAME:(ASSI:1)%也不例外※
	WAIT
	TEQUIP:MASTER:淫精の伝道師 = 1
ENDIF
IF CADV("催精师")
	DRAWLINE
	PRINTL ＜催精师＞
	PRINTFORML 因为站在一旁的%CALLNAME:(ASSI:1)%、全员射精的射精量都增多了。
	SIF PENIS(ASSI:1)
		PRINTFORML ※%CALLNAME:(ASSI:1)%也不例外※
	WAIT
	TEQUIP:MASTER:淫精の伝道師 = 1
ENDIF
IF CADV("堕天使の甘言")
	DRAWLINE
	PRINTL ＜堕天使の甘言＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的助言、甜言蜜语的成功率ＵＰ＆継続時間増加了。
	ELSE
		PRINTFORMW 因为%CALLNAME:TARGET%的Ｈ、甜言蜜语的成功率上升、継続時間也増加了。
	ENDIF
	TEQUIP:堕天使の甘言 = 1
ENDIF
IF CADV("動画撮影")
	DRAWLINE
	IF TARGET != ASSI:1
		PRINTL ＜動画撮影＞
		PRINTFORMW 通过让%CALLNAME:(ASSI:1)%辅助拍摄動画撮影、恥情的上昇量+50％且実行値-５。
		TEQUIP:動画撮影 = 1
	ELSE
		PRINTL ＜動画撮影＞
		PRINTFORMW 因为在和%CALLNAME:TARGET%H的时候使用手机进行了自拍、恥情的上昇量+50％且実行値-５。
		TEQUIP:動画撮影 = 1
	ENDIF
ENDIF
IF CADV("努力的前輩")
	DRAWLINE
	PRINTL ＜努力的前輩＞
	IF TARGET != ASSI:1
		PRINTFORML %CALLNAME:(ASSI:1)%会努力地帮助的。仅此而已。
		PRINTFORMW 但是这个姿态解開了%CALLNAME:TARGET%的心、恭順の上昇量＋10％、抑制了反感的上昇。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%比平时更努力、恭順の上昇量＋10％、抑制了反感的上昇。
	ENDIF
	TEQUIP:努力的前輩 = 1
ENDIF
IF CADV("誘惑")
	DRAWLINE
	PRINTL ＜誘惑＞
	IF TARGET != ASSI:1
		PRINTFORMW 由于%CALLNAME:(ASSI:1)%醸出的空気、欲情の上昇量+50％＆体力の消費増加。
	ELSE
		PRINTFORMW 由于%CALLNAME:TARGET%醸出的空気、欲情の上昇量+50％＆体力の消費増加。
	ENDIF
	TEQUIP:誘惑 = 1
ENDIF
IF CADV("狩猟の美学")
	DRAWLINE
	PRINTL ＜狩猟の美学＞
	IF TARGET != ASSI:1
		PRINTFORMW 由于%CALLNAME:(ASSI:1)%豊富的知識、欲情の上昇量+50％＆体力の消費減低。
	ELSE
		PRINTFORMW 由于%CALLNAME:TARGET%豊富的知識、欲情の上昇量+70％＆体力の消費減低。
	ENDIF
	TEQUIP:狩猟の美学 = 1
ENDIF
IF CADV("痛痛飞走啦")
	DRAWLINE
	PRINTL ＜痛痛飞走啦＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%守护在一旁、大幅度抑制了%CALLNAME:TARGET%疼痛与恐怖上昇的趋势。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%疼痛与恐怖上昇的趋势被大幅度抑制了。
	ENDIF
	TEQUIP:痛痛飞走啦 = 1
ENDIF
IF CADV("小悪魔の轻语")
	DRAWLINE
	PRINTFORML ＜小悪魔の轻语＞
	PRINTFORML 由于%CALLNAME:(ASSI:1)%的助力、甜言蜜语比平常更能誘惑对方。
	PRINTFORMW 被甜言蜜语発揮効果的对象、感度上升的同时攻势会变得更加激烈。
	TEQUIP:MASTER:小悪魔の轻语 = 1
ENDIF
IF CADV("解放感") && PLACE("野外")
	DRAWLINE
	PRINTL ＜解放感＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%习惯于露出行为、%CALLNAME:TARGET%的紧张感也得到了缓解。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%像是在一定程度上习惯了露出行为的样子。
	ENDIF
	TEQUIP:解放感 = 1
ENDIF
IF CADV("欺诈师")
	DRAWLINE
	PRINTL ＜欺诈师＞
	IF TARGET != ASSI:1
		PRINTFORMW 由于%CALLNAME:(ASSI:1)%的帮助、%CALLNAME:TARGET%们变得稍微放松了一些。
	ELSE
		PRINTFORMW 由于%CALLNAME:TARGET%的能力、%CALLNAME:MASTER%们稍微变得放松了一些。
	ENDIF
	TEQUIP:欺诈师 = 1
ENDIF
IF CADV("撒娇鬼")
	DRAWLINE
	PRINTL ＜撒娇鬼＞
	IF TARGET != ASSI:1
		PRINTFORML 由于%CALLNAME:(ASSI:1)%的存在、调教对象的母性本能被激发、胸部相関的指令的效果獲得少许提升。
	ELSE
		PRINTFORML 由于%CALLNAME:(ASSI:1)%助力、调教对象的母性本能被激发、胸部相関的指令的效果獲得少许提升。
	ENDIF
	SIF COND("池面", MASTER) == 0 && STRCOUNT(CSTR:MASTER:建议, "撒娇鬼") == 0
		TEQUIP:MASTER:撒娇鬼 = 1
	SIF COND("池面") == 0 && STRCOUNT(CSTR:建议, "撒娇鬼") == 0
		TEQUIP:撒娇鬼 = 1
ENDIF
IF CADV("餓狼の相") && TARGET != ASSI:1
	SELECTCASE CFLAG:(ASSI:1):食欲
	CASE IS >= 50
		DRAWLINE
		PRINTL ＜餓狼の相＞
		SELECTCASE CFLAG:(ASSI:1):食欲
		CASE IS >= 150
			PRINTFORML %CALLNAME:(ASSI:1)%眯著眼睛看著%CALLNAME:MASTER%們、露出了奇妙的微笑…
		CASE IS >= 100
			PRINTFORML 气息不匀的%CALLNAME:(ASSI:1)%的肚子发出還沒填飽的叫声、死死地盯著%CALLNAME:MASTER%們…
		CASEELSE
			PRINTFORML 眯著眼睛微笑著的%CALLNAME:(ASSI:1)%不時發出咕咕的飢餓聲…
		ENDSELECT
		PRINTFORMW 情緒不容易上升、%CALLNAME:TARGET%每回合獲得恐怖。
		TEQUIP:MASTER:餓狼の相 = 1
	ENDSELECT
ENDIF
SIF CADV("縄酔酒")
	TEQUIP:MASTER:縄酔酒 = 1
IF CADV("解読")
	DRAWLINE
	PRINTL ＜解読＞
	IF TARGET != ASSI:1
		PRINTFORMW 由于%CALLNAME:(ASSI:1)%的帮助、%CALLNAME:TARGET%的心结解開了。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%的心结解開了。
	ENDIF
	TEQUIP:解読 = 1
ENDIF
IF CADV("受胎呪詛")
	DRAWLINE
	PRINTL ＜受胎呪詛＞
	IF TARGET != ASSI:1
		IF TALENT:难以妊娠 || TALENT:避妊の護符 || TALENT:Ｖ妊娠不能
			PRINTFORMW 虽然从%CALLNAME:(ASSI:1)%身上不断地溢出%TEXTR("瘴気/妖気")%、但是对%CALLNAME:TARGET%没有任何效果的样子…
		ELSE
			PRINTFORMW 因为从%CALLNAME:(ASSI:1)%身上不断地溢出%TEXTR("瘴気/妖気")%、%CALLNAME:TARGET%变得更加容易妊娠了
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%因为%TEXTR("瘴気/妖気")%、变得更加容易妊娠了
	ENDIF
	CFLAG:要受精確率 = MAX(CFLAG:要受精確率 - 30, 1)
ENDIF
IF CADV("受胎祝福")
	DRAWLINE
	PRINTL ＜受胎祝福＞
	IF TARGET != ASSI:1
		IF TALENT:避妊の護符 || TALENT:Ｖ妊娠不能
			PRINTFORMW 虽然从%CALLNAME:(ASSI:1)%身上不断地溢出%TEXTR("瘴気/妖気")%、但是对%CALLNAME:TARGET%没有任何效果的样子…
		ELSE
			PRINTFORMW 因为从%CALLNAME:(ASSI:1)%身上不断地溢出%TEXTR("仙気/神気")%、%CALLNAME:TARGET%排卵了
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%因为%TEXTR("仙気/神気")%而排卵了
	ENDIF
	CFLAG:要受精確率 = MAX(CFLAG:要受精確率 - 20, 1)
ENDIF
IF CADV("純陰")
	DRAWLINE
	PRINTL ＜純陰＞
	IF TARGET != ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的陰気、%CALLNAME:TARGET%被附加了各种各样的DEBUFF。
		CFLAG:要受精確率 = MAX(CFLAG:要受精確率 + 20, 1)
		FLAG:情緒 -= 40
		PALAM:欲情 -= 3000
	ELSE
		PRINTFORMW %CALLNAME:(ASSI:1)%吸收了自己的陰気、肉体暂时变为了最佳状态。
		CFLAG:要受精確率 = MAX(CFLAG:要受精確率 - 20, 1)
		FLAG:情緒 += 40
		PALAM:欲情 += 3000
	ENDIF
ENDIF
;指导者のエッチ中のターンごとのBOUNS
@TURN_BONUS_ADVISER
#DIM LCOUNT
SIF ASSI:1 <= 0
	RETURN 0
;再生
IF CADV("再生")
	PRINTFORML ＜再生＞
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER, TARGET, ASSI
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 50, MAXBASE:LCOUNT:体力)
		ENDSELECT
	NEXT
	PRINTFORML 全員回復50体力
ENDIF
;浄化の火
IF CADV("浄化の火")
	PRINTFORML ＜浄化の火＞
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 100, MAXBASE:LCOUNT:体力)
			BASE:LCOUNT:精力 = MIN(BASE:LCOUNT:精力 + 5, MAXBASE:LCOUNT:精力)
		CASE TARGET, ASSI
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 100, MAXBASE:LCOUNT:体力)
		ENDSELECT
	NEXT
	PRINTFORML 全員体力回復了100、%CALLNAME:MASTER%的精力回復了5。
ENDIF
IF CADV("純陰")
	PRINTFORML ＜純陰＞
		SELECTCASE LCOUNT
		CASE MASTER, TARGET 
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 - 70, MAXBASE:LCOUNT:体力)
		CASE ASSI
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 70, MAXBASE:LCOUNT:体力)
		ENDSELECT
	PRINTFORML 由于漂浮的陰気体力发生了変動。
ENDIF
IF TEQUIP:MASTER:餓狼の相 && TARGET != ASSI:1
	CALL VARSET_COMF

	SELECTCASE CFLAG:(ASSI:1):食欲
	CASE IS >= 150
		PRINTFORML ＜餓狼の相(空腹度【極高】＞
		SOURCE:0:恐惧 = 500
	CASE IS >= 100
		PRINTFORML ＜餓狼の相(空腹度【高】＞
		SOURCE:0:恐惧 = 150
	CASE IS >= 50
		PRINTFORML ＜餓狼の相(空腹度【中】＞
		SOURCE:0:恐惧 = 50
	ENDSELECT

	CALL CALC, "調教ソース", 100
ENDIF
IF CADV("縄酔酒")
	PRINTL ＜縄酔酒＞
	PRINTFORM 被拘束的
	IF TARGET != ASSI:1
		PRINTFORM %CALLNAME:TARGET%
	ELSE
		PRINTFORM %CALLNAME:(ASSI:1)%
	ENDIF
	PRINTFORMW 被甜美的空気吸引了……
ENDIF

;指导者の終了時特典
@AFTERTRA_BONUS_ADVISER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_BONUS

SIF ASSI:1 <= 0
	RETURN 0

MEMO_BONUS = 0

IF CADV("自動補充") && NUM("消耗品使用数")
	CALL SETFLAG, "消耗品補充"
	DRAWLINE
	PRINTFORML ＜自動補充＞
	PRINTFORMW 本次使用的消耗品由%CALLNAME:(ASSI:1)%自行補充了。
ENDIF
IF CADV("天才肌")
	DRAWLINE
	PRINTFORML ＜天才肌＞
	IF TARGET == ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的活跃、習得の珠的取得量獲得了５％的加成。
	ELSE
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的助言、習得の珠的取得量獲得了５％的加成。
	ENDIF
ENDIF
IF CADV("好奇の視線")
	DRAWLINE
	PRINTFORML ＜好奇の視線＞
	IF TARGET == ASSI:1
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的妄想力、恥情の珠的取得量獲得了５％的加成。
	ELSE
		PRINTFORMW 因为%CALLNAME:(ASSI:1)%的態度、恥情の珠的取得量獲得了５％的加成。
	ENDIF
ENDIF
IF CADV("急成長") && COND("満足") && TARGET == ASSI:1
	DRAWLINE
	PRINTFORML ＜急成長＞
	IF COND("性欲大満足")
		MEMO_BONUS += 10
	ELSEIF COND("性欲満足")
		MEMO_BONUS += 5
	ENDIF
	IF COND("精液欲大満足")
		MEMO_BONUS += 10
	ELSEIF COND("精液欲満足")
		MEMO_BONUS += 5
	ENDIF
	PRINTFORMW 由于让%CALLNAME:(ASSI:1)%満足了、習得の珠的取得量獲得了{MEMO_BONUS}％的加成。
	;布セント出力させようと思ったけど今は気分じゃない（無敵構文）
ENDIF
;精力増大終了
SIF CADV("精力増大")
	CALL SET_COUNTER_BASE

;指导者能力の終了
@RESET_ABL_ADVISER
CVARSET TEQUIP, GETNUM(TEQUIP, "无用相談")
CVARSET TEQUIP, GETNUM(TEQUIP, "单人玩家")
CVARSET TEQUIP, GETNUM(TEQUIP, "理性主義")
CVARSET TEQUIP, GETNUM(TEQUIP, "包容力")
CVARSET TEQUIP, GETNUM(TEQUIP, "淫精の伝道師")
CVARSET TEQUIP, GETNUM(TEQUIP, "堕天使の甘言")
CVARSET TEQUIP, GETNUM(TEQUIP, "動画撮影")
CVARSET TEQUIP, GETNUM(TEQUIP, "努力的前輩")
CVARSET TEQUIP, GETNUM(TEQUIP, "誘惑")
CVARSET TEQUIP, GETNUM(TEQUIP, "狩猟の美学")
CVARSET TEQUIP, GETNUM(TEQUIP, "痛痛飞走啦")
CVARSET TEQUIP, GETNUM(TEQUIP, "小悪魔の轻语")
CVARSET TEQUIP, GETNUM(TEQUIP, "解放感")
CVARSET TEQUIP, GETNUM(TEQUIP, "欺诈师")
CVARSET TEQUIP, GETNUM(TEQUIP, "撒娇鬼")
CVARSET TEQUIP, GETNUM(TEQUIP, "餓狼の相")
CVARSET TEQUIP, GETNUM(TEQUIP, "縄酔酒")
CVARSET TEQUIP, GETNUM(TEQUIP, "解読")

;指导者のSHOP画面特典
@SHOP_BONUS_ADVISER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM ACTOR

SIF ASSI:1 <= 0
	RETURN 0

MEMO_LINECOUNT = LINECOUNT

IF COND("昼") && CADV("縁結")
	ACTOR = RAND:(CHARANUM - 2) + 2
	LOCAL = RAND:10 + 1
	CFLAG:ACTOR:好感度 += LOCAL
	DRAWLINE
	PRINTFORML ＜縁結＞
	PRINTFORML 因为%CALLNAME:(ASSI:1)%的神通力、和%CALLNAME:ACTOR%関系变好了。
	PRINTFORMW 好感度+{LOCAL}（%CALLNAME:ACTOR%）
ENDIF

IF COND("昼") && CADV("金運招来")
	LOCAL = 1 + RAND:100 + RAND:(MAX(CFLAG:(ASSI:1):好感度/100, 1) )
	DRAWLINE
	IF CADV("金運招来Ⅱ")
		PRINTFORML ＜金運招来Ⅱ＞
		LOCAL += 1 + RAND:10
	ELSE
		PRINTFORML ＜金運招来＞
	ENDIF
	MONEY += LOCAL
	PRINTFORMW 因为%CALLNAME:(ASSI:1)%的神通力、%CALLNAME:MASTER%的钱包变重了……具体来说有%MONEY_C(LOCAL)%左右。
ENDIF

IF COND("夜") && TEXTS("曜日") != "日" && CADV("和和闹闹")
	DRAWLINE
	PRINTFORML ＜和和闹闹＞
	PRINTFORML 因为%CALLNAME:(ASSI:1)%的言動而引发的特殊事件使得部員们的距離縮短了。
	PRINTFORMW 全員的好感度+10＆関系友好度+1

	FOR LCOUNT, 2, CHARANUM
		CFLAG:LCOUNT:好感度 += 10
	NEXT
	CALL CHANGE_RELATION_ALL, 1
ENDIF

IF COND("夜") && CADV("抑制器")
	DRAWLINE
	PRINTFORML ＜抑制器＞
	PRINTFORMW %CALLNAME:(ASSI:1)%吸収了周围的人的性欲……全員的欲求不満度減少10％。
	FOR LCOUNT, 2, CHARANUM
		CALL CALC, "欲求不満解消", LCOUNT, 10
	NEXT
ENDIF

IF CADV("冷却器")
	DRAWLINE
	PRINTFORML ＜冷却器＞
	PRINTFORMW %CALLNAME:(ASSI:1)%調節了温度、提供舒适的空間……全員的欲求不満度減少5％。
	FOR LCOUNT, 2, CHARANUM
		CALL CALC, "欲求不満解消", LCOUNT, 5
	NEXT
ENDIF

IF CADV("调教师")
	DRAWLINE
	PRINTFORML ＜调教师＞
	PRINTFORMW %CALLNAME:(ASSI:1)%周围的人积攒着性的欲求……全員的欲求不満度上昇10％。
	FOR LCOUNT, 2, CHARANUM
		BASE:LCOUNT:欲求不満 = LIMIT(BASE:LCOUNT:欲求不満 + 10, BASE:LCOUNT:欲求不満, 100)
	NEXT
ELSEIF CADV("好奇の視線")
	DRAWLINE
	PRINTFORML ＜好奇の視線＞
	PRINTFORMW %CALLNAME:(ASSI:1)%周围的人积攒着性的欲求……全員的欲求不満度上昇５％。
	FOR LCOUNT, 2, CHARANUM
		BASE:LCOUNT:欲求不満 = LIMIT(BASE:LCOUNT:欲求不満 + 5, BASE:LCOUNT:欲求不満, 100)
	NEXT
ENDIF

IF COND("昼") && TEXTS("曜日") != "日" && CADV("媚薬精製") && ITEM:媚薬 < 99 && !RAND:3
	DRAWLINE
	PRINTFORML ＜媚薬精製＞
	IF CFLAG:ASSI:3900 == 0
		GETFONT
		CHKFONT "ＭＳ Ｐ哥特"
		SIF RESULT
			SETFONT "ＭＳ Ｐ哥特"
			PRINTFORML 　 〓..〓..〓..〓.〓
			PRINTFORML 　／　　　　　　　 ＼
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　―――――　｜
			PRINTFORML ｜│　・　ω ・　│｜
			PRINTFORML ｜　―――――　｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML 　￣￣￣￣￣￣￣
		SETFONT RESULTS
		CALL BLANKLINE
		PRINTFORML 普通的媚薬旁边摆放着一个奇怪的容器…
		PRINTFORML 检查了一下里面的东西、果然是媚薬…而且是质量很好的媚薬…
		CFLAG:ASSI:3900 = 1
	ELSE
		SELECTCASE RAND:3
		CASE 0
			PRINTFORML 不知不觉间增加了新的媚薬…
		CASE 1
			PRINTFORML 普通潤滑液的旁边、看起来很眼熟的奇怪容器摆放着…
		CASE 2
			GETFONT
			CHKFONT "ＭＳ Ｐ哥特"
			SIF RESULT
				SETFONT "ＭＳ Ｐ哥特"
				PRINTFORML 　 〓..〓..〓..〓.〓
				PRINTFORML 　／　　　　　　　 ＼
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　―――――　｜
				PRINTFORML ｜│　・　ω ・　│｜
				PRINTFORML ｜　―――――　｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML 　￣￣￣￣￣￣￣
			SETFONT RESULTS
			CALL BLANKLINE
			PRINTFORML 奇怪的容器摆放着…
		ENDSELECT
	ENDIF
	PRINTFORMW 媚薬数量增加了１。
	ITEM:媚薬 += 1
ENDIF

IF COND("昼") && TEXTS("曜日") != "日" && CADV("天然潤滑液") && ITEM:潤滑液 < 99 && !RAND:3
	DRAWLINE
	PRINTFORML ＜天然潤滑液＞
		IF CFLAG:ASSI:4300 == 0
			PRINTFORML 为了扔掉而洗干净潤滑液的空容器不知为何又满了…
			PRINTFORML 检查了一下里面的东西、果然是潤滑液…而且是质量很好的潤滑液…
			CFLAG:ASSI:4300 = 1
		ELSE
			SELECTCASE RAND:3
			CASE 0
				PRINTFORML 不知不觉间增加了新的潤滑液…
			CASE 1
				PRINTFORML 本来是空的潤滑液瓶又满了…
			CASE 2
				PRINTFORML 普通潤滑液的旁边、一排看起来很眼熟的运动饮料形状的容器摆放着…
			ENDSELECT
		ENDIF
	PRINTFORMW 潤滑液数量增加了１。
	ITEM:潤滑液 += 1
ENDIF

SIF LINECOUNT > MEMO_LINECOUNT
	DRAWLINE

;指导者の時間経過特典
@EVENTTURNEND_BONUS_ADVISER
#DIM LCOUNT
SIF ASSI:1 <= 0
	RETURN 0

IF CADV("魂蒐集") && BASE:MASTER:魂 < MAXBASE:MASTER:魂
	DRAWLINE
	PRINTFORML ＜魂蒐集＞
	PRINTFORM %CALLNAME:MASTER%的魂回復了0.1（%TEXTS("0.01", BASE:MASTER:魂)% 
	BASE:MASTER:魂 = MIN(BASE:MASTER:魂 + 10, MAXBASE:MASTER:魂)
	PRINTFORML → %TEXTS("0.01", BASE:MASTER:魂)%）
ENDIF

;再生
IF CADV("再生/浄化の火") && CHARA_NUM("Ｖ損傷") + CHARA_NUM("Ａ損傷")
	DRAWLINE
	IF CADV("浄化の火")
		PRINTFORML ＜浄化の火＞
	ELSE
		PRINTFORML ＜再生＞
	ENDIF

	SETCOLOR DEF_COLOR("黄色")
	FOR LCOUNT, 1, CHARANUM
		SIF TALENT:LCOUNT:Ｖ損傷 == 0 && TALENT:LCOUNT:Ａ損傷 == 0
			CONTINUE

		TALENT:LCOUNT:Ｖ損傷 = 0
		TALENT:LCOUNT:Ａ損傷 = 0
		IF LCOUNT == ASSI:1
			PRINTFORMW %CALLNAME:(ASSI:1)%治癒了体内的損傷。
		ELSE
			PRINTFORMW %CALLNAME:(ASSI:1)%治療了%CALLNAME:LCOUNT%的損傷。
		ENDIF
	NEXT
	RESETCOLOR
ENDIF

;-------------------------------------------------
;助手への指示の処理
;優先順位はアルカナ＞道具＞其他
;ARGSには 調教開始、調教中、お掃除 などが入る
;-------------------------------------------------
@EVENT_ASSI_AUTO, ARGS
;助手調教中や助手が居ないときはダメ
SIF ASSI <= 0 || ASSIPLAY
	RETURN 0
;Ｗ推倒や３Ｐに参加している場合もやってこない
SIF TEQUIP:Ｗ推倒 && SELECTCOM:1 >= 0 && TARGET:1 == ASSI
	RETURN 0
SIF SELECTCOM:2 >= 0 && PLAYER:1 == ASSI
	RETURN 0
;指示していないor精力が少ない等
SIF STATE("撃沈", ASSI) || CFLAG:ASSI:助手の指示 == 0 || BASE:ASSI:精力 <= 5
	RETURN 0
;行動済み
SIF ARGS != "調教開始" && TFLAG:助手行動済
	RETURN 0

LOCAL = TFLAG:地の文分岐

SELECTCASE ARGS
CASE "お掃除"
	CALL BLANKLINE
	CALL EVENT_ASSI_OSOUJI
CASE "調教開始"
	CALL EVENT_ASSI_ITEM, ARGS
	CALL EVENT_ASSI_OSOUJI
	IF TFLAG:助手行動済
		CALL KOJO_GUEST, "助手：調教開始前の準備後台詞", ASSI
		CALL BLANKLINE
		PRINTFORMW %CALLNAME:ASSI%侧目听着俩人的会話、好像是已经完成了自己的工作…
	ENDIF
CASEELSE
	CALL EVENT_ASSI_ITEM, ARGS
ENDSELECT

TFLAG:地の文分岐 = LOCAL

@EVENT_ASSI_OSOUJI
;誰々の～が分かるならそれを記述
#DIMS ACTOR_OF

VARSET LOCAL
;主人をお掃除
IF GETBIT(CFLAG:ASSI:助手の指示, 40) && TEQUIP:MASTER:避孕套 == 0
	IF PLAYER == MASTER && COND("絶頂", PLAYER) && INSERT("挿入") == 0 && PENIS(MASTER)
		SIF SHOOT("膣内") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ｖ")
			LOCAL = 1
		SIF SHOOT("肛門") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ")
			LOCAL = 2
		SIF SHOOT("尿道") && CHECK_STAIN(STAIN:MASTER:Ｐ, "尿")
			LOCAL = 3
	ENDIF
	SIF PLAYER == MASTER && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ｐ恥垢") && COND("真性包茎", MASTER) == 0
		LOCAL = 4
ENDIF
;対象をお掃除
IF GETBIT(CFLAG:ASSI:助手の指示, 41)
	IF COND("絶頂", PLAYER) && INSERT("挿入") == 0
		SIF SHOOT("膣内") && STAIN:Ｖ != SET_STAIN(0, "Ｖ")
			LOCAL:1 = 1
		SIF SHOOT("肛門") && STAIN:Ａ != SET_STAIN(0, "Ａ")
			LOCAL:1 = 2
		IF SHOOT("尿道")
			IF PENIS(TARGET)
				SIF STAIN:Ｐ != SET_STAIN(0, "Ｐ")
					LOCAL:1 = 3
			ELSE
				SIF STAIN:Ｖ != SET_STAIN(0, "Ｖ")
					LOCAL:1 = 3
			ENDIF
		ENDIF
	ENDIF
	SIF TALENT:女性 && CHECK_CLO("Ｖ露出") && CHECK_STAIN(STAIN:Ｖ, "尿") && NOWEX:絶頂漏尿
		LOCAL:1 = 4
ENDIF

IF LOCAL && RANDIF(1, LOCAL:1)
	ACTOR_OF = 
	SIF COND("Ｐ汚れの相手", MASTER)
		ACTOR_OF = %CONDS("Ｐ汚れの相手", MASTER)%的
	SELECTCASE LOCAL
	CASE 1
		PRINTFORM %CALLNAME:ASSI%将刚从%ACTOR_OF%膣内拔出来的%CALLNAME:MASTER%的阴茎
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚臭無視 == 0
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 用嘴含着、仔细地舔干净了……
			CALL KOJO_GUEST, "助手：主人に清洁口交", ASSI
			CALL SETFLAG, "清洁口交", ASSI, MASTER
		ELSE
			PRINTFORMW 用清潔布仔细地擦拭干净了…
		ENDIF
	CASE 2
		PRINTFORM %CALLNAME:ASSI%将刚从%ACTOR_OF%肛門内拔出来的%CALLNAME:MASTER%的阴茎
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚臭無視 == 0
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 用嘴含着、仔细地舔干净了……
			CALL KOJO_GUEST, "助手：主人に清洁口交", ASSI
			CALL SETFLAG, "清洁口交", ASSI, MASTER
		ELSE
			PRINTFORMW 用清潔布仔细地擦拭干净了…
		ENDIF
	CASE 3
		IF PENIS(TARGET)
			PRINTFORM %CALLNAME:ASSI%把%CALLNAME:TARGET%刚刚拔出了阴茎的%CALLNAME:MASTER%的那个
		ELSE
			PRINTFORM %CALLNAME:ASSI%将刚从%ACTOR_OF%尿道拔出来的%CALLNAME:MASTER%的阴茎
		ENDIF
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚臭無視 == 0
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 用嘴含着、仔细地舔干净了……
			CALL KOJO_GUEST, "助手：主人に清洁口交", ASSI
			CALL SETFLAG, "清洁口交", ASSI, MASTER
		ELSE
			PRINTFORMW 用清潔布仔细地擦拭干净了…
		ENDIF
	CASE 4
		PRINTFORM %CALLNAME:ASSI%跪在%CALLNAME:MASTER%的股間前、鼻子哼了一声、把阴茎取了出来
		;奉仕精神と献身で判定
		IF ABL:ASSI:奉仕精神 + TALENT:ASSI:献身 >= 4 && COND("口の使用", ASSI)
			PRINTFORMW 把皮卸下来了
			PRINTFORMW 然后把积累的恥垢用舌頭精巧地全部舔舐干净、喉咙咕隆一声咽了下去た……
			CALL KOJO_GUEST, "助手：主人にＰ恥垢清洁口交", ASSI
			CALL SETFLAG, "清洁口交", ASSI, MASTER
		ELSE
			PRINTFORMW 用清潔布仔细地擦拭干净了…
		ENDIF
	ENDSELECT
	CALL RESET_STAIN_EX, "Ｐ", MASTER
ELSEIF LOCAL:1
	SELECTCASE LOCAL:1
	CASE 1
		PRINTFORM %CALLNAME:ASSI%把被侵犯得十分凌乱的%CALLNAME:TARGET%的阴部押開、
		IF (TALENT:ASSI:汚臭無視 || ABL:ASSI:奉仕精神 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 用舌頭仔细地把汚垢舔舐干净了…
			CALL KOJO_GUEST, "助手：partnerに清洁舔阴", ASSI
			CALL SETFLAG, "清洁舔阴", ASSI, TARGET
		ELSE
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ENDIF
		STAIN:Ｖ = SET_STAIN(0, "Ｖ")
	CASE 2
		PRINTFORM %CALLNAME:ASSI%把被侵犯得合不拢嘴巴的%CALLNAME:TARGET%の尻穴押開、
		IF (TALENT:ASSI:汚臭無視 || ABL:ASSI:奉仕精神 >= 7) && COND("口の使用", ASSI)
			PRINTFORMW 把舌頭伸了进去仔细地舔舐汚垢…
			CALL KOJO_GUEST, "助手：partnerに清洁舔A", ASSI
			CALL SETFLAG, "清洁舔A", ASSI, TARGET
		ELSE
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ENDIF
		STAIN:Ａ = SET_STAIN(0, "Ａ")
	CASE 3
		IF PENIS(TARGET)
			PRINTFORM %CALLNAME:ASSI%把%CALLNAME:TARGET%还大张着龟頭的阴茎
			IF (TALENT:ASSI:汚臭無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
				PRINTFORMW 含了进去、把汚垢舔舐干净后仔细地用舌頭清理着…
				CALL KOJO_GUEST, "助手：partnerに清洁口交", ASSI
				CALL SETFLAG, "清洁口交", ASSI, TARGET
			ELSE
				PRINTFORMW 用热毛巾仔细地擦拭干净了…
			ENDIF
			STAIN:Ｐ = SET_STAIN(0, "Ｐ")
		ELSE
			PRINTFORM %CALLNAME:ASSI%把因为被侵犯而变得惨不忍睹的%CALLNAME:TARGET%的那里拉開、把尿道彻底的裸露了出来
			IF (TALENT:ASSI:汚臭無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
				PRINTFORMW 、用舌頭仔细地舔舐着汚垢…
				CALL KOJO_GUEST, "助手：partnerに清洁舔阴", ASSI
				CALL SETFLAG, "清洁舔阴", ASSI, TARGET
			ELSE
				PRINTFORMW 用热毛巾仔细地擦拭干净了…
			ENDIF
			STAIN:Ｖ = SET_STAIN(0, "Ｖ")
		ENDIF
	CASE 4
		PRINTFORM %CALLNAME:ASSI%把大開着腿露着尿的%CALLNAME:TARGET%的那里掰開、
		IF (TALENT:ASSI:汚臭無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
			PRINTFORMW 用舌頭仔细地舔舐了之後啾啾地吮吸着尿道…
			CALL KOJO_GUEST, "助手：partnerに清洁舔阴", ASSI
			CALL SETFLAG, "清洁舔阴", ASSI, TARGET
		ELSE
			PRINTFORMW 用热毛巾仔细地擦拭干净了…
		ENDIF
		STAIN:Ｖ = SET_STAIN(0, "Ｖ")
	ENDSELECT
ENDIF
IF LOCAL + LOCAL:1
	CALL BLANKLINE
	TFLAG:助手行動済 = 1
	RETURN 1
ENDIF


@EVENT_ASSI_ITEM, ARGS
;行動済み
SIF ARGS != "調教開始" && TFLAG:助手行動済
	RETURN 0

IF ARGS == "調教開始" && GETBIT(CFLAG:ASSI:助手の指示, 45)
	PRINTL ＜衣服を脱がす＞
	CALL SETFLAG, "衣装全脱ぎ", TARGET
	PRINTFORML %CALLNAME:ASSI%把%CALLNAME:TARGET%剥得一丝不挂！
ENDIF

;潤滑液
IF GETBIT(CFLAG:ASSI:助手の指示, 20) && TEQUIP:潤滑液 == 0 && ITEM:潤滑液 && MIN(PALAM:Ｖ潤, PALAM:Ａ潤) < 10000
	PRINTFORML ＜\@ ARGS != "調教開始" ? オート # \@潤滑液＞
	CALL SETFLAG, "調教外潤滑追加", 10000

	TEQUIP:潤滑液 = 1
	ITEM:潤滑液 -= 1
	TFLAG:助手行動済 = 1
	IF ARGS != "調教開始"
		PRINTFORML %CALLNAME:ASSI%用润滑液把%CALLNAME:TARGET%涂得黏黏糊糊的…
		CALL BLANKLINE
		RETURN 1
	ENDIF
ENDIF

;撮影(撮影されたことがある相手に対してのみ)
IF ARGS == "調教開始" && GETBIT(CFLAG:ASSI:助手の指示, 27) && TEQUIP:攝像機 == 0 && ITEM:撮影機材 && FIRSTCOM(170)
	PRINTL ＜视频拍摄開始＞
	TEQUIP:攝像機 = 1
	TFLAG:助手行動済 = 1
ENDIF


@ASSI_CHANGE_INDICATE, ARG
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0
CALL BLANKLINE
PRINTFORML %NAME:ARG%的能力一共可以胜任{ABL:ARG:助手}個助手的工作。
PRINTFORML 以下的指示要进行哪一个？
VARSET LOCALS

LOCALS:20 = [20] \@ GETBIT(CFLAG:ARG:助手の指示, 20) ? ○ # － \@ 開始時使用润滑液
LOCALS:21 = [21] \@ GETBIT(CFLAG:ARG:助手の指示, 21) ? ○ # － \@ 開始時使用媚薬

;LOCALS:22 = [22] \@ GETBIT(CFLAG:ARG:助手の指示, 22) ? ○ # － \@ 適度に利尿剤使用

;LOCALS:24 = [24] \@ GETBIT(CFLAG:ARG:助手の指示, 24) ? ○ # － \@ 適度に排卵誘発剤使用

SIF ITEM:撮影機材
	LOCALS:27 = [27] \@ GETBIT(CFLAG:ARG:助手の指示, 27) ? ○ # － \@ 開始時进行撮影（仅限有撮影経験的对象

IF ABL:ARG:奉仕精神 >= 3
	LOCALS:40 = [40] \@ GETBIT(CFLAG:ARG:助手の指示, 40) ? ○ # － \@ 擦拭主人的污迹
	LOCALS:41 = [41] \@ GETBIT(CFLAG:ARG:助手の指示, 41) ? ○ # － \@ 擦拭調教对象的污迹
ENDIF

LOCALS:45 = [45] \@ GETBIT(CFLAG:ARG:助手の指示, 45) ? ○ # － \@ 開始時让調教对象全裸

;SIF ABL:ARG:奉仕精神 >= 3
;	LOCALS:50 = [50] \@ GETBIT(CFLAG:ARG:助手の指示, 50) ? ○ # － \@ 御用商人を互动

FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL != ""
		PRINTFORML  %LOCALS:LOCAL%
NEXT
PRINTFORML [100] 这様就好

$INPUT_LOOP
INPUT

IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < 100 && LOCALS:RESULT != ""
ELSE
	GOTO INPUT_LOOP
ENDIF

LOCAL = RESULT
INVERTBIT CFLAG:ARG:助手の指示, LOCAL

IF NUM_BIT(CFLAG:ARG:助手の指示) > ABL:ARG:助手
	PRINTFORMW 给予的指示数超过了能力上限
	INVERTBIT CFLAG:ARG:助手の指示, LOCAL
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;助手に指示する画面
@ASSI_LIST_INDICATE, ARG
IF ABL:ARG:助手 == 0
	PRINTFORM 因为%TEXTS("助手の名称")%还是LV０所以无法给予指示
	RETURN 0
ENDIF

SIF GETBIT(CFLAG:ARG:助手の指示, 20)
	PRINT [潤滑液]
;SIF GETBIT(CFLAG:ARG:助手の指示, 21)
;	PRINT [媚薬]
;SIF GETBIT(CFLAG:ARG:助手の指示, 22)
;	PRINT [利尿剤]

;SIF GETBIT(CFLAG:ARG:助手の指示, 24)
;	PRINT [排卵誘発剤]

SIF GETBIT(CFLAG:ARG:助手の指示, 27)
	PRINT [撮影]

SIF GETBIT(CFLAG:ARG:助手の指示, 40)
	PRINT [对主人进行清洁]
SIF GETBIT(CFLAG:ARG:助手の指示, 41)
	PRINT [对対象进行清洁]
SIF GETBIT(CFLAG:ARG:助手の指示, 45)
	PRINT [让対象全裸]
SIF GETBIT(CFLAG:ARG:助手の指示, 50)
	PRINT [好好招待]

LOCAL:1 = ABL:ARG:助手 - NUM_BIT(CFLAG:ARG:助手の指示)
IF LOCAL:1 > 0
	FOR LOCAL, 0, LOCAL:1
		PRINT []
	NEXT
ENDIF


@ASSI_LIST_ABL, ARG
#DIM LCOUNT

SIF ARG != MASTER
	PRINTPLAINFORM 精{BASE:ARG:精力, 2} 
PRINTPLAINFORM 技{ABL:ARG:技巧, 2} 
SIF ARG != MASTER
	PRINTPLAINFORM %SUBSTRING(TEXTS("助手の名称"), 0, 2)%{ABL:ARG:助手} 

;相性。効果が微妙なものにはテコ入れorコメントアウト
IF TARGET
	IF ARG != TARGET
		IF RELATION:(NO:ARG) >= 200
			PRINTBUTTON "相性☆ ", "相性☆"
		ELSEIF RELATION:(NO:ARG) >= 150
			PRINTBUTTON "相性◎ ", "相性◎"
		ELSEIF RELATION:(NO:ARG) >= 120
			PRINTBUTTON "相性○ ", "相性○"
		ENDIF
	ENDIF
ENDIF

SIF COND("只和你Ｈ", ARG)
	PRINTBUTTON "３Ｐ禁止 ", "３Ｐ禁止"

SIF TALENT:ARG:錬金術
	PRINTBUTTON "調合 ", "調合"

;SIF TALENT:ARG:汚臭無視
;	PRINTBUTTON "汚臭無視 ", "汚臭無視"
;SIF TALENT:ARG:兩面通吃
;	PRINTBUTTON "兩面通吃 ", "兩面通吃"

SIF TALENT:ARG:鼓舞
	PRINTBUTTON "鼓舞 ", "鼓舞"
SIF TALENT:ARG:扶她
	PRINTBUTTON "扶她 ", "扶她"
SIF TALENT:ARG:男性
	PRINTBUTTON "♂ ", "♂"
;SIF TALENT:ARG:禁断的知識
;	PRINTBUTTON "禁断的知識 ", "禁断的知識"
SIF TALENT:ARG:具現
	PRINTBUTTON "具現 ", "具現"
SIF TALENT:ARG:調教知識
	PRINTBUTTON @"%CONDS("調教知識", ARG)% ", CONDS("調教知識", ARG)
SIF TALENT:ARG:甜言蜜语
	PRINTBUTTON "甜言蜜语 ", "甜言蜜语"

VARSET LOCALS
SPLIT CSTR:ARG:建议, "＆", LOCALS

FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "没有"
		CONTINUE
	CASEELSE
		PRINTBUTTON @"%LOCALS:LCOUNT% ", LOCALS:LCOUNT
	ENDSELECT
NEXT
