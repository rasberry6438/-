@SHOW_STATUS
REDRAW 0

;この値をもとにCLEARLINEを行う
TFLAG:LINECOUNT記録 = LINECOUNT

DRAWLINE
CALL SHOW_STATUS_MAIN

CALL SETFLAG, "助手相手の恋慕外し開始"

;@COMORDERで求める実行値の初期化
CVARSET TCVAR, GETNUM(TCVAR, "指令制限")
CVARSET TCVAR, GETNUM(TCVAR, "基本実行値")

FLAG:COM_ABLEの一括OFF = 1

TFLAG:カウンター中 = 0

@SHOW_STATUS_MAIN
#DIM LCOUNT
#DIMS SHOW_CONFIG
#DIMS ITEMS

;射精ゲージ設定
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
		CALL SETFLAG, "射精ゲージ", LCOUNT
	ENDSELECT
NEXT


;季節、年表示部分
PRINTFORM %TEXTS("日期表示")% 
IF TEQUIP:浴室PLAY
	PRINTFORM -浴室-
ELSE
	PRINTFORM -%TEXTS("現在位置")%-
ENDIF
SIF EQUIP:Ｈ的奖励
	CALL PRINT_STR, "爱心粉紅_ [Ｈ的奖励中]"
;圣诞节ツリー格子
IF DAYEV("圣诞节期間") && NUM("圣诞节ツリー")
	SETCOLOR DEF_COLOR("爱心粉紅")
	PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
	RESETCOLOR
ENDIF
PRINT  情緒:
CALL TEXT, "情緒Heart"
;情緒記録
TFLAG:ターン開始時の情緒 = MOOD()
;全体に効果の出ているものはここに書く
;現在効果を発揮している指导者能力
CALL TEXT, "指导者能力"
PRINTL 

ITEMS = 
;消耗品
FOR LCOUNT, 50, 60
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	ITEMS = %ITEMS%%ITEMNAME:LCOUNT% ×{ITEM:LCOUNT}/
NEXT
SIF TALENT:MASTER:男性 == 0
	ITEMS = %ITEMS%口服避孕薬 ×{ITEM:口服避孕薬}/
;避孕套
SIF ITEM:用过的避孕套
	ITEMS = %ITEMS%用过的避孕套 ×{ITEM:用过的避孕套}

CALL PRINT_LIST, ITEMS
PRINTL 
PRINTFORM %NAME:PLAYER% 
;危険日
CALL TEXT, "生理周期", PLAYER
CALL SHOW_EQUIP, PLAYER
PRINTFORM \@ PENIS(PLAYER) ? 射精 # 絶頂 \@(%CALLNAME:PLAYER%)
CALL PRINT_BAR, "溜めゲージ", BASE:PLAYER:絶頂, MAXBASE:PLAYER:絶頂, 15
PRINTFORM {BASE:PLAYER:絶頂}/{MAXBASE:PLAYER:絶頂} 

CALL TEXT, "精力", PLAYER

IF PLAYER != MASTER
	PRINTFORM 收集的魂:
ELSE
	PRINTFORM 魂
	CALL PRINT_BAR, "減少ゲージ", BASE:PLAYER:魂, MAXBASE:PLAYER:魂, 6
ENDIF
PRINTFORM %TEXTS("0.01", BASE:PLAYER:魂)% 
CALL TEXT, "尿意", PLAYER
PRINTL 
PRINTL 
CALL SHOW_STATUS_CHARA, TARGET
IF TEQUIP:Ｗ推倒
	PRINTL 
	CALL SHOW_STATUS_CHARA, TARGET:1
ENDIF


;助手がいて、ダウンしていないなら表示
IF ASSI && STATE("撃沈", ASSI) == 0 && TEQUIP:Ｗ推倒 == 0
	PRINTL 
	PRINTFORM \@ PENIS(ASSI) ? 射精 # 絶頂 \@(%CALLNAME:ASSI%)
	CALL PRINT_BAR, "溜めゲージ", BASE:ASSI:絶頂, MAXBASE:ASSI:絶頂, 15
	PRINTFORM {BASE:ASSI:絶頂}/{MAXBASE:ASSI:絶頂} 
	PRINT 体力
	CALL PRINT_BAR, "減少ゲージ", BASE:ASSI:体力, MAXBASE:ASSI:体力, 16
	PRINTFORM {BASE:ASSI:体力}/{MAXBASE:ASSI:体力} 
	CALL TEXT, "精力", ASSI
	PRINTL 
ENDIF


@SHOW_STATUS_CHARA, ARG
#DIMS MEMOS_SEMEN

PRINTFORM %NAME:ARG% 
CALL TEXT, "生理周期", ARG

;これはTARGETの数値を見ればよい
IF TEQUIP:Ｗ推倒
	PRINTFORM 好:{CFLAG:ARG:好感度}
ELSE
	PRINTFORM 好感度:{CFLAG:ARG:好感度}
ENDIF
;CALL PRINT_RANK, CFLAG:ARG:好感度, 2500
CALL SHOW_EQUIP, ARG

BASE:ARG:体力 = MAX(BASE:ARG:体力, 0)

PRINT 体力
CALL PRINT_BAR, "減少ゲージ", BASE:ARG:体力, MAXBASE:ARG:体力, 32
PRINTFORM {BASE:ARG:体力}/{MAXBASE:ARG:体力} 

CALL TEXT, "精力", ARG

PRINTL 

CALL NEW_PRINT_PALAM, ARG

IF PENIS(ARG)
	PRINTFORM 射精
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:絶頂, MAXBASE:ARG:絶頂, 24
	PRINTFORM {BASE:ARG:絶頂}/{MAXBASE:ARG:絶頂} 
ENDIF
IF TALENT:ARG:母乳体質
	SIF MAXBASE:ARG:母乳 == 0
		MAXBASE:ARG:母乳 = 10000
	PRINTFORM 母乳
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:母乳, MAXBASE:ARG:母乳, 24
	PRINTFORM {BASE:ARG:母乳}/{MAXBASE:ARG:母乳} 
ENDIF

SIF PENIS(ARG) || TALENT:ARG:母乳体質
	PRINTL 

IF EX:ARG:処女喪失 || EX:ARG:Ａ処女喪失 || EX:ARG:Ｃ絶頂 || EX:ARG:尿道絶頂 || EX:ARG:Ｖ絶頂 || EX:ARG:Ａ絶頂 || EX:ARG:Ｂ絶頂 || EX:ARG:Ｍ絶頂 || EX:ARG:射精 || EX:ARG:噴乳 || EX:ARG:吸能 || EX:ARG:嘔吐
	PRINT < 
	SIF EX:ARG:処女喪失
		CALL PRINT_STR, "爱心粉紅_処女喪失 "
	SIF EX:ARG:Ａ処女喪失
		CALL PRINT_STR, "爱心粉紅_Ａ処女喪失 "
	SIF EX:ARG:尿道絶頂
		PRINTFORM 尿道絶頂:{EX:ARG:尿道絶頂}回 
	SIF EX:ARG:Ｃ絶頂
		PRINTFORM Ｃ絶頂:{EX:ARG:Ｃ絶頂}回 
	SIF EX:ARG:Ｖ絶頂
		PRINTFORM Ｖ絶頂:{EX:ARG:Ｖ絶頂}回 
	SIF EX:ARG:Ａ絶頂
		PRINTFORM Ａ絶頂:{EX:ARG:Ａ絶頂}回 
	SIF EX:ARG:Ｂ絶頂
		PRINTFORM Ｂ絶頂:{EX:ARG:Ｂ絶頂}回 
	SIF EX:ARG:Ｍ絶頂
		PRINTFORM Ｍ絶頂:{EX:ARG:Ｍ絶頂}回 
	SIF EXP_GAIN("射精経験", ARG)
		PRINTFORM 射精:{EXP_GAIN("射精経験", ARG)}回 
	SIF EX:ARG:前列腺高潮射精
		PRINTFORM 前列腺高潮射精:{EX:ARG:前列腺高潮射精}回 
	SIF EXP_GAIN("噴乳経験", ARG)
		PRINTFORM 噴乳:{EXP_GAIN("噴乳経験", ARG)}回 
	SIF EX:ARG:吸能
		PRINTFORM ＥＤ:{EX:ARG:吸能}回 
	SIF EX:ARG:嘔吐
		PRINTFORM 嘔吐:{EX:ARG:嘔吐}回 
	PRINT >
	PRINTL 
ENDIF
IF PALAM:ARG:Extacy || EX:ARG:絶対搾精量
	IF EX:ARG:絶対搾精量
		PRINTFORM 搾精量:{EX:ARG:絶対搾精量}ml

		MEMOS_SEMEN = 
		SIF EX:ARG:Ｖ中出し量
			MEMOS_SEMEN += @"膣内{EX:ARG:Ｖ中出し量}ml"
		SIF EX:ARG:Ａ中出し量
			MEMOS_SEMEN += @"肛内{EX:ARG:Ａ中出し量}ml"
		SIF EX:ARG:口内射精量
			MEMOS_SEMEN += @"精飲{EX:ARG:口内射精量}ml"
		SIF EX:ARG:乳内射精量
			MEMOS_SEMEN += @"乳内{EX:ARG:乳内射精量}ml"
		SIF EX:ARG:尿道内射精量
			MEMOS_SEMEN += @"膀胱{EX:ARG:尿道内射精量}ml"
		SIF COND("避孕套搾精量", ARG)
			MEMOS_SEMEN += @"套套内射精{COND("避孕套搾精量", ARG)}ml"

		SIF MEMOS_SEMEN != ""
			PRINTFORM (%MEMOS_SEMEN%) 
	ENDIF
	;オバフロ対策
	IF PALAM:Extacy >= 10000000000000000
		PRINTFORM Extacy:１京ＯＶＥＲ！ 
	ELSEIF PALAM:Extacy
		PRINTFORM Extacy:{PALAM:ARG:Extacy} 
	ENDIF
	SELECTCASE PALAM:ARG:連続絶頂COMBO数
	CASE IS >= 100
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！！！ 
	CASE IS >= 50
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！！ 
	CASE IS >= 20
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！ 
	CASE IS >= 10
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！ 
	CASE IS >= 2
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！ 
	ENDSELECT
	PRINTL 
ENDIF

CALL CHECK_SATISFACTION, ARG

CALL TEXT, "性欲", ARG
CALL TEXT, "精液欲", ARG
CALL TEXT, "胎内精液量", ARG
CALL TEXT, "尿意", ARG
CALL TEXT, "排泄欲", ARG

PRINTL 

;ARGの状況の表示
@SHOW_EQUIP, ARG
IF TALENT:ARG:男性
	PRINTPLAINFORM [%CONDS("体型", ARG)%]
ELSE
	PRINTPLAINFORM [%CONDS("体型", ARG)%(%CONDS("罩杯サイズ", ARG)%)]
ENDIF
;晒痕有些？
IF COND("晒痕", ARG)
	PRINTFORM [晒痕]
ELSEIF TALENT:ARG:肤色
	PRINTFORM [%CONDS("肤色", ARG)%]
ENDIF

IF STATE("具現", ARG)
	PRINT [具現]
ELSEIF STATE("阴茎陰核化", ARG)
	PRINT [陰核化]
ENDIF
IF PENIS(ARG)
	SELECTCASE SIZE("阴茎", ARG)
	CASE 0
		PRINTFORM [小
	CASE 1
		PRINTFORM [並
	CASE 2
		PRINTFORM [巨根
	CASE 3
		PRINTFORM [像馬一样粗
	CASE 4
		PRINTFORM [腕粗
	CASEELSE
		PRINTFORM [規格外
	ENDSELECT

	PRINTFORM (尺寸LV{SIZE("阴茎", ARG)})]

	IF ARG == PLAYER
		PRINTFORM Ｐ潤%TEXTS("調教パラメータのＬＶ", GET_PALAMLV(PALAM:ARG:Ｐ潤) )%
		CALL PRINT_BAR, "ＰＡＬＡＭ", PALAM:ARG:Ｐ潤, 0, 4
	ENDIF
ELSE
	IF COND("陰核露出", ARG)
		PRINTFORM [常時阴蒂露出]
	ELSEIF CALCF("Ｃ発情", ARG) >= 2
		CALL PRINT_STR, "爱心粉紅_[陰核露出]"
	ENDIF
ENDIF
;陥没乳頭が勃起
IF COND("陥没乳頭", ARG)
	IF CALCF("Ｂ発情", ARG)
		CALL PRINT_STR, "爱心粉紅_[乳頭露出]"
	ELSE
		PRINTFORM [陥没乳頭]
	ENDIF
ENDIF

IF CFLAG:ARG:甜言蜜语 && CFLAG:ARG:心情好
	PRINT [欣喜]
ELSEIF CFLAG:ARG:甜言蜜语
	PRINT [心动不已]
ELSEIF CFLAG:ARG:心情好
	PRINT [心情好]
ENDIF
SIF STATE("恍惚", ARG)
	CALL PRINT_STR, "粉紅_[恍惚]"
SIF STATE("脫力", ARG)
	CALL PRINT_STR, "水色_[脫力]"
SIF EX:ARG:绝顶忍耐
	CALL PRINT_STR, "爱心粉紅_[要去了]"
SIF COND("発情期", ARG)
	CALL PRINT_STR, "爱心粉紅_[発情中]"

IF STATE("烂醉", ARG)
	CALL PRINT_STR, "[_青_烂醉_]"
ELSEIF STATE("酔酒", ARG)
	CALL PRINT_STR, "[_爱心粉紅_酔酒_]"
ENDIF

SELECTCASE TEQUIP:ARG:弛緩
CASE 1
	CALL PRINT_STR, "黄色_[緊張緩和]"
CASE 2
	CALL PRINT_STR, "黄色_[弛緩]"
CASE 3
	CALL PRINT_STR, "爱心粉紅_[陶酔]"
ENDSELECT

IF ARG != PLAYER
	SIF COND("Ｖ潤充分", ARG) && FIRSTCOMS("Ｖ挿入") && CHOSECOMS("Ｖ挿入") == 0
		CALL PRINT_STR, "爱心粉紅_[Ｖ潤充分]"
	SIF COND("Ａ潤充分", ARG) && FIRSTCOMS("Ａ挿入") && CHOSECOMS("Ａ挿入") == 0
		CALL PRINT_STR, "爱心粉紅_[Ａ潤充分]"
ENDIF
IF INSERT("挿入", ARG)
	VARSET LOCALS
	IF INSERT("两根挿入Ｖ中", ARG)
		LOCALS = 两根挿入Ｖ中
	ELSEIF INSERT("两根挿入Ａ中", ARG)
		LOCALS = 两根挿入Ａ中
	ELSEIF INSERT("二穴挿入", ARG)
		LOCALS = 二穴挿入
	ELSE
		IF INSERT("Ａ挿入", ARG)
			LOCALS = Ａ%CONDS("体位", ARG)%
		ELSEIF INSERT("尿道挿入", ARG)
			LOCALS = 尿道%CONDS("体位", ARG)%
		ELSE
			LOCALS = %CONDS("体位", ARG)%
		ENDIF
		SELECTCASE INSERT("挿入相手", ARG)
		CASE PLAYER
		CASE ASSI
			LOCALS += @"(%TEXTS("助手の名称")%)"
		CASE MASTER
			LOCALS += @"(主人)"
		ENDSELECT
	ENDIF
	IF COND("喜欢的体位中", ARG)
		CALL PRINT_STR, @"[_爱心粉紅_%LOCALS%☆_]"
	ELSE
		CALL PRINT_STR, @"[_黄色_%LOCALS%_]"
	ENDIF
ELSEIF INSERT("挿入中", ARG)
	IF INSERT("Ａ挿入中", ARG)
		PRINTFORM [Ａ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSEIF INSERT("尿道挿入中", ARG)
		PRINTFORM [尿道挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSE
		PRINTFORM [Ｖ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ENDIF
ENDIF
SIF TEQUIP:ARG:手下留情
	CALL PRINT_STR, "[_爱心粉紅_焦躁中_]"
SIF TEQUIP:ARG:避孕套
	PRINT [戴上套套中]

;これはTARGETの値を見る
IF TEQUIP:Ｗ推倒
	SIF ARG == PLAYER
		PRINT [Ｗ被逆推中]
ELSEIF TEQUIP:ARG:六九式
	PRINTFORM [\@ TEQUIP:ARG:推倒 ? 推倒 # \@６９(%TRAIN_NAME_STANDARD(TEQUIP:ARG:六九式)%)中]
ELSEIF TEQUIP:ARG:推倒
	PRINT [推倒
	IF TEQUIP:PLAYER:拘束
		PRINT 拘束
	ELSEIF TEQUIP:ARG:面部騎乗
		PRINT 神情恍惚的脸颊面騎乗
	ENDIF
	PRINT 中]
ENDIF
SIF TEQUIP:ARG:電話中
	PRINT [電話中]

SIF COND("指の使用", ARG) == 0
	PRINT [腕使用不可]
SIF TEQUIP:ARG:接吻
	PRINT [一有破绽就接吻]

SIF TEQUIP:ARG:攝像機
	PRINTFORM [撮影中(残{10 - TEQUIP:ARG:攝像機 + 1}回)]
IF TEQUIP:ARG:野外PLAY
	SIF COND("人里", ARG)
		PRINT [人里]
	SIF COND("森林", ARG)
		PRINT [森林]
	SIF COND("花園", ARG)
		PRINT [花園]
	SIF COND("水辺", ARG)
		PRINT [水辺]
	SIF COND("海", ARG)
		PRINT [海]
ENDIF
SIF TEQUIP:ARG:室内露出PLAY
	PRINT [室内露出中]
IF TEQUIP:ARG:女王様PLAY
	IF TALENT:ARG:男性
		PRINTFORM [ＳＭPLAY LV{TEQUIP:ARG:女王様PLAY}]
	ELSE
		PRINTFORM [女王様PLAY LV{TEQUIP:ARG:女王様PLAY}]
	ENDIF
ENDIF

SIF TEQUIP:ARG:浴室PLAY
	PRINT [浴室]
SIF TEQUIP:ARG:新妻PLAY
	PRINT [新妻]

IF TEQUIP:ARG:深喉 == PLAYER
	PRINT [深喉中]
ELSEIF TEQUIP:ARG:深喉
	PRINTFORM [深喉中(%CALLNAME:(TEQUIP:ARG:深喉)%)]
ENDIF

SIF TEQUIP:ARG:甜言蜜语 >= 1 && TEQUIP:ARG:甜言蜜语 <= 8
	PRINTFORM [甜言蜜语]
SIF TEQUIP:ARG:排卵誘発剤
	PRINTFORM [排卵誘発剤]

SELECTCASE TEQUIP:ARG:媚薬
CASE IS >= 200
	PRINTFORM [媚薬(激)]
CASE IS >= 150
	PRINTFORM [媚薬(強)]
CASE IS > 50
	PRINTFORM [媚薬]
CASE IS > 0
	PRINTFORM [媚薬(弱)]
ENDSELECT

SIF TEQUIP:ARG:利尿剤
	PRINTFORM [利尿剤]
IF TEQUIP:ARG:筋弛緩剤
	PRINTFORM [筋弛緩剤
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 0)
		PRINTFORM (Ａ)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 1)
		PRINTFORM (尿道)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 2)
		PRINTFORM (Ｖ)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 3)
		PRINTFORM (子宮口)
	PRINTFORM ]
ENDIF
IF TEQUIP:ARG:振動棒
	IF COND("双頭振動棒", ARG)
		PRINTFORM [双頭振動棒]
	ELSE
		SELECTCASE SIZE("振動棒", ARG)
		CASE 6
			PRINTFORM [規格外振動棒]
		CASE 5
			PRINTFORM [馬型振動棒]
		CASE 4
			PRINTFORM [腕並振動棒]
		CASE 3
			PRINTFORM [極太振動棒]
		CASEELSE
			PRINTFORM [振動棒]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:ARG:导管
	PRINTFORM [导管]

IF TEQUIP:ARG:肛用振動棒
	IF COND("肛門拉珠", ARG)
		PRINTFORM [肛門拉珠]
	ELSE
		VARSET LOCALS
		SIF COND("長振動棒", ARG)
			LOCALS = (結腸挿入中)
		SELECTCASE SIZE("Ａ振動棒", ARG)
		CASE 6
			PRINTFORM [Ａ規格外振動棒%LOCALS%]
		CASE 5
			PRINTFORM [Ａ馬型振動棒%LOCALS%]
		CASE 4
			PRINTFORM [Ａ腕並振動棒%LOCALS%]
		CASE 3
			PRINTFORM [Ａ極太振動棒%LOCALS%]
		CASEELSE
			PRINTFORM [肛用振動棒%LOCALS%]
		ENDSELECT
	ENDIF
ENDIF

IF TEQUIP:ARG:尿道振動棒
	IF COND("尿道布ル", ARG)
		PRINTFORM [尿道布ル]
	ELSE
		SELECTCASE SIZE("尿道振動棒", ARG)
		CASE 6
			PRINTFORM [尿道規格外振動棒]
		CASE 5
			PRINTFORM [尿道馬型振動棒]
		CASE 4
			PRINTFORM [尿道腕並振動棒]
		CASE 3
			PRINTFORM [尿道極太振動棒]
		CASEELSE
			PRINTFORM [尿道振動棒]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:ARG:拘束
	PRINTFORM [%BINDS(TEQUIP:ARG:拘束)%]

SIF COND("嘔吐確率", ARG)
	CALL PRINT_STR, "青_[嘔吐感]"

IF TEQUIP:ARG:浣腸
	PRINTFORM [浣腸
	SIF COND("精液浣腸", ARG)
		PRINTFORM (精液)
	SIF COND("温泉浣腸", ARG)
		PRINTFORM (尿)
	PRINTFORM ]
ENDIF

IF TEQUIP:ARG:肛塞
	SELECTCASE SIZE("肛塞", ARG)
	CASE IS >= 4
		PRINTFORM [超大型Ａ塞子]
	CASE 3
		PRINTFORM [極太Ａ塞子]
	CASE 2
		PRINTFORM [肛塞(大)]
	CASEELSE
		PRINTFORM [肛塞]
	ENDSELECT
ENDIF

IF TEQUIP:ARG:眼罩 || TEQUIP:ARG:鼻钩
	IF TEQUIP:ARG:眼罩 && TEQUIP:ARG:鼻钩
		PRINTFORM [眼罩＆鼻钩]
	ELSEIF TEQUIP:ARG:眼罩
		PRINTFORM [眼罩]
	ELSEIF TEQUIP:ARG:鼻钩
		PRINTFORM [鼻钩]
	ENDIF
ENDIF

IF TEQUIP:ARG:口枷 == 1
	PRINTFORM [口球]
ELSEIF TEQUIP:ARG:口枷 == 2
	PRINTFORM [環形口枷]
ENDIF

SIF TEQUIP:ARG:創口貼
	PRINTFORM [創口貼]
	
PRINTL  
;換装表示
SIF ARG != PLAYER
	CALL PRINT_DRESSL, ARG, "短縮表示"


@STAIN_INFO
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
PRINTL 
PRINTFORML 以下为各自的污渍状態。
SIF TEQUIP:推倒 == 0
	PRINTFORML 如果希望的话、手和口的污渍可以清洁干净。
;誰かが真性包茎
IF COND("真性包茎") || COND("真性包茎", MASTER) || COND("真性包茎", ASSI)
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML ※真性包茎的人的Ｐ会一直保持<Ｐ恥垢>的状态。清洗也洗不掉的
	RESETCOLOR
ENDIF
PRINTFORML （入浴中・出浴状態下可以去除污垢、奉仕調教的実行也会变简单）
CALL STAIN_INFO_SHOW, MASTER
CALL STAIN_INFO_SHOW, TARGET
CALL STAIN_INFO_SHOW, TARGET:1
DRAWLINE
IF TEQUIP:推倒
	WAIT
ELSE
	PRINTBUTTON "[Enter] 関閉", "Enter"

	ONEINPUTS

	SELECTCASE RESULTS
	CASE "0"
		STAIN:MASTER:Ｍ = 0
	CASE "1"
		STAIN:MASTER:手 = 0
	CASE "2"
		IF CHECK_ADD_STAIN("Ｐ", MASTER)
			CALL RESET_STAIN_EX, "Ｐ", MASTER
			SIF INSERT("挿入中", MASTER)
				CALL RESET_INSERT, MASTER
		ENDIF
	CASE "3"
		STAIN:Ｍ = 0
	CASE "4"
		STAIN:手 = 0
	CASE "5"
		IF CHECK_ADD_STAIN("Ｐ", TARGET)
			CALL RESET_STAIN_EX, "Ｐ", TARGET
			SIF INSERT("挿入中", TARGET)
				CALL RESET_INSERT, TARGET
		ENDIF
	CASE "6"
		SIF ASSI
			STAIN:ASSI:Ｍ = 0
	CASE "7"
		SIF ASSI
			STAIN:ASSI:手 = 0
	CASE "8"
		IF ASSI
			IF CHECK_ADD_STAIN("Ｐ", ASSI)
				CALL RESET_STAIN_EX, "Ｐ", ASSI
				SIF INSERT("挿入中", ASSI)
					CALL RESET_INSERT, ASSI
			ENDIF
		ENDIF
	CASEELSE
		REDRAW 1
		RETURN 0
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

REDRAW 1

@STAIN_INFO_SHOW, ARG
#DIM LCOUNT

SIF ARG <= 0
	RETURN 0
IF TEQUIP:ARG:出浴
	;ここはTARGETの値を見る
	IF TARGET && TEQUIP:浴室PLAY
		LOCALS = □ %NAME:ARG%（入浴中）□
	ELSE
		LOCALS = □ %NAME:ARG%（出浴）□
	ENDIF
ELSE
	LOCALS = □ %NAME:ARG% □
ENDIF
CALL DRAWLINES, LOCALS

;選択肢に使う
SELECTCASE ARG
CASE MASTER
	LOCAL = 0
CASE TARGET
	LOCAL = 3
CASE TARGET:1
	LOCAL = 6
ENDSELECT

FOR LCOUNT, 0, 7
	LOCALS = 
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ")
		LOCALS += "<Ｖ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ恥垢")
		LOCALS += "<Ｖ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ")
		LOCALS += "<Ｐ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ恥垢")
		LOCALS += "<Ｐ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "精液")
		LOCALS += "<精>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ａ")
		LOCALS += "<Ａ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "乳")
		LOCALS += "<乳>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "尿")
		LOCALS += "<尿>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "破瓜之血")
		LOCALS += "<血>"

	;SIF LCOUNT == 4 && (COND("身嗜み：腸洗浄", ARG) || EX:ARG:排泄 >= 2)
	;	LOCALS += "（洗浄済み）"

	SELECTCASE LCOUNT
	CASE 0
		PRINTFORM 　口：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推倒 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 让口\@ ARG == MASTER ? 自己清洗 # 被清洗 \@
	CASE 1
		PRINTFORM 　手：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推倒 == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 擦拭手指
	CASE 2
		SIF PENIS(ARG) == 0
			CONTINUE
		PRINTFORM 　Ｐ：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:推倒 == 0 && CHECK_ADD_STAIN("Ｐ", ARG)
			PRINTFORM [{LOCAL + LCOUNT}] 擦拭阴茎 \@ INSERT("挿入中", ARG) ? (解除挿入) # \@
	CASE 3
		SIF TALENT:ARG:男性
			CONTINUE
		PRINTFORM 　Ｖ：%TEXT_LJ(LOCALS, 32)%
	CASE 4
		PRINTFORM 　Ａ：%TEXT_LJ(LOCALS, 32)%
	CASE 5
		;SIF TALENT:ARG:男性
		;	CONTINUE
		PRINTFORM 　Ｂ：%TEXT_LJ(LOCALS, 32)%
	CASE 6
		PRINTFORM 　脚：%TEXT_LJ(LOCALS, 32)%
	ENDSELECT


	PRINTL 
NEXT
PRINTL 
