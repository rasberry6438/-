;変数の書き換えなどを伴なうバージョンアップ用
@VERSION_UP

@バージョンアップ準備処理
#DIM NEW_VERSION
#DIM NEW_ALLOWVERSION
#DIM VERCOUNT

PRINTL erablue_resort版本升级助手（开发者专用）
PRINTL 是否准备进行版本升级？
PRINTBUTTONLC "[0]确定", 0
PRINTBUTTONLC "[1]取消", 1

BINPUT
SIF RESULT == 1
	RETURN

NEW_VERSION = GAMEBASE_VERSION + 1
NEW_ALLOWVERSION = MAX(GAMEBASE_ALLOWVERSION, NEW_VERSION - 2)

CALL VERUP用ファイル生成(NEW_VERSION)

PRINTFORML 将./CSV/GameBase.csv中的"バージョン,{GAMEBASE_VERSION}"改写为"バージョン,{NEW_VERSION}"
PRINTFORML 将"バージョン違い認める,{GAMEBASE_ALLOWVERSION}"改写为"バージョン違い認める,{NEW_ALLOWVERSION}"
PRINTL 
PRINTFORML 请将./ERB_EDITFILE/VERUP_{NEW_VERSION}.ERB放置到./ERB/バージョンアップ関連/目录下，并删除以下文件：
FOR VERCOUNT, GAMEBASE_ALLOWVERSION, NEW_ALLOWVERSION
	PRINTFORML ./ERB/バージョンアップ関連/VERUP_{VERCOUNT}.ERB
NEXT
PRINTL 
PRINTL 同时请适当删除内部使用的跟进变量定义
WAIT

CLEARLINE LINECOUNT
RETURN 0

@VERUP用ファイル生成(バージョン)
#DIM バージョン

REDRAW 0
CLEARLINE LINECOUNT

PRINTFORML ;Ver%TOSTR(バージョン, "0,.000")%与旧版本兼容性处理
PRINTFORML ;升级至Ver%TOSTR(バージョン+3, "0,.000")%时删除，兼容性维持到{バージョン+1}
HTML_PRINT "<br>"
PRINTL ;新增TALENT、新增CSTR检查
PRINTFORML @VERUP_{バージョン}_CHARA(ARG)
HTML_PRINT "<br>"
PRINTL ;其他全局变量改写等
PRINTFORML @VERUP_{バージョン}()
HTML_PRINT "<br>"
PRINTL ; IF なんらかフォロー == 0
PRINTL ; 	;なんらかのフォロー処理
PRINTL ; 	;此处不设置"なんらかフォロー = 1"
PRINTL ; ENDIF
HTML_PRINT "<br>"
PRINTL ;将对应处理用标志设为已完成状态
PRINTFORML @VERUP_{バージョン}_FINISH()
PRINTL ; なんらかフォロー = 1
HTML_PRINT "<br>"

OUTPUTLOG @"ERB_EDITFILE/VERUP_{バージョン}.ERB", 1
CLEARLINE LINECOUNT
REDRAW 1
