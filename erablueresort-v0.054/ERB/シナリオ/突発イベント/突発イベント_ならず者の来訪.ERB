
@イベント発生条件_突発イベント_ならず者の来訪
;開放条件を満たしたら1を返すこと

イベント消滅日数渡し = 3
イベント消滅確率渡し = 30
イベント再発生日数渡し = 20
イベント再発生確率渡し = 10

;開放条件を満たしたら1を返すこと
IF 空艇港初来訪イベントフラグ
	RETURN 1
ENDIF
RETURN 0

@イベント関数_突発イベント_ならず者の来訪(ARG)
LOCAL = DT_CELL_GET("各イベント用変数配列", ARG, "イベント再発生CT")
IF LOCAL == 0
	;初回
	DRAWLINE
	PRINTL ［暴徒来访］
	DRAWLINE
	PRINTW
	PRINTFORMW 似乎有暴徒的船只降落在山间开阔地
	PRINTFORMW 放任不管的话他们将会行窃并掠夺资源后逃窜
	PRINTFORMW 届时度假区人气暴跌是显而易见的
	PRINTFORMW 必须在受害前将其击溃
	PRINTL
	PRINTL 将与降落山中的暴徒们交战。准备就绪了吗？（将进行两连战！）
	PRINTBUTTON "[0]确定", 0
	PRINTL
	PRINTBUTTON "[1]取消", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%一行袭击了降落山中的暴徒们！

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW 不，不能掉以轻心。还是充分准备后再行动吧
	ENDSELECT
ELSE
	;二回目以降
	DRAWLINE
	PRINTL ［暴徒来访］
	DRAWLINE
	PRINTW
	PRINTFORMW 又有暴徒的船只降落了
	PRINTFORMW 需在受害前迅速击溃
	PRINTL
	PRINTL 将与降落山中的暴徒们交战。准备就绪了吗？（将进行两连战！）
	PRINTBUTTON "[0]确定", 0
	PRINTL
	PRINTBUTTON "[1]取消", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%一行袭击了降落山中的暴徒们！

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW 不，不能掉以轻心。还是充分准备后再行动吧
	ENDSELECT
ENDIF


@ならず者の来訪イベント_戦闘部分
CALL イベント戦闘初期処理()
敵BATTLE_STATE_STR:0:エネミー名 = Lv10_流氓团伙成员
敵BATTLE_STATE_STR:2:エネミー名 = Lv10_流氓团伙成员
敵BATTLE_STATE_STR:4:エネミー名 = Lv10_流氓团伙成员
CALL イベント中戦闘用処理()
IF RESULT == 0
	敵BATTLE_STATE_STR:0:エネミー名 = Lv10_流氓团伙成员
	敵BATTLE_STATE_STR:1:エネミー名 = Lv10_流氓团伙成员
	敵BATTLE_STATE_STR:2:エネミー名 = Lv15_流氓团伙头目
	敵BATTLE_STATE_STR:3:エネミー名 = Lv10_流氓团伙成员
	敵BATTLE_STATE_STR:4:エネミー名 = Lv10_流氓团伙成员
	CALL イベント中戦闘用処理()
	IF RESULT == 0
		;勝利
		PRINTFORMW %CALLNAME:PLAYER%一行顺利清剿了暴徒
		IF RAND:100 < 5
			;5%だけ起こる処理
			PRINTW 船的残骸中发现了不少物品，但暴徒们似乎没有意识到它们的价值，只是把它们随意地放在周围
			PRINTW 心怀感激地善加利用吧
			STRDATA LOCALS
				DATA 古旧戒指・银
				DATA 白银古纹
				DATA 古旧耳饰・银
				DATA 宝晶原石
			ENDDATA
			SIF !FLAG:クリア済み視聴フラグ
				CALL 素材入手処理(LOCALS, 1)
			PRINTFORMW 获得「%LOCALS%」
		ELSEIF !乗機所持チェック("试制大型陆行艇") && !RAND:4
			;設計図未所持時、25％で発見
			PRINTW 船的残骸中有一些独特的快艇设计图纸，但暴徒们似乎没有意识到它们的价值，把它们随意丢弃
			PRINTW 心怀感激地善加利用吧
			PRINTW 获得「试制大型陆行艇设计图」
			SIF !FLAG:クリア済み視聴フラグ
				CALL 乗機取得処理_設計図("试制大型陆行艇")
		ELSE
			SELECTCASE RAND(10)
				CASE 0
					LOCAL = RAND(2500, 7501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 从船的残骸中查获{LOCAL}卢比
				CASE 1
					PRINTW 船体残骸里空空如也，只留下一张潦草的纸条，看起来像是暴徒头目留下的
					IF FLAG:クリア済み視聴フラグ
						PRINTW 似乎是目标财宝的情报，但字迹过于凌乱难以辨认……
					ELSE
						PRINTW 似乎是目标财宝的情报，但字迹过于凌乱只能部分解读……
						SELECTCASE RAND(4)
							CASE 0
								PRINTL 「双足飞龙的体内────结晶化────
								PRINTW 　作为稀有──────星尘────高价交易───」
							CASE 1
								PRINTL 「电鳗分泌的────硅砂中─────
								PRINTW 　吞噬────侥幸─────可售────高价」
							CASE 2
								PRINTL 「日珥反应炉────火焰────增幅────
								PRINTW 　钢铁巨人────星晶────」
							CASE 3
								PRINTL 滚地兽────攻击会自────
								PRINTW 　从未────狩猎─────可获────高价」
						ENDSELECT
					ENDIF
				CASE 2
					LOCALS = %ランダム素材名("迷宫素材", 0)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 从船的残骸中查获「%LOCALS%」
				CASE 3
					LOCALS = %ランダム素材名("食物素材", 1)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 从船的残骸中查获未开封的「%LOCALS%」
				CASE 4
					LOCAL = RAND(1000, 5001)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 将船骸零件出售获得{LOCAL}卢比
				CASEELSE
					LOCAL = RAND(500, 2501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 从船的残骸中查获{LOCAL}卢比
			ENDSELECT
		ENDIF
		CALL イベント完了フラグ汎用処理("ならず者の来訪")
	ELSEIF RESULT == 1
		;全滅
		PRINTW 遭遇惨败，暂且撤退重整旗鼓
	ELSEIF RESULT == -1
		;逃走
		PRINTW 敌方实力超乎预期，暂且撤退从长计议
	ENDIF
ELSEIF RESULT == 1
	;全滅
	PRINTW 遭遇惨败，暂且撤退重整旗鼓
ELSEIF RESULT == -1
	;逃走
	PRINTW 敌方实力超乎预期，暂且撤退从长计议
ENDIF
CALL ダンジョン終了時処理
