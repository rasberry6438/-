@汎用イベント発生条件_キャライベント_恋慕不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:恋心度 < 経験値テーブル:99
	RETURN 0

RETURN 1

@汎用イベント関数_キャライベント_恋慕不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

DRAWLINE
PRINTFORML ［【通用】%NAME:対象キャラ%[无法爱慕]解除］
DRAWLINE
PRINTW
PRINTFORMW %CALLNAME:対象キャラ%的状态似乎有些异常。
PRINTFORMW 搭话时脸颊微红，用湿润的眼眸凝视着%CALLNAME:PLAYER%…
PRINTL 
PRINTFORMW 经过多次交流，被压抑的爱慕之情终于满溢而出。
PRINTFORMW 今后或许能发展更深层的关系。
PRINTL 
SETCOLOR 0xFFFF00
IF GETBIT(TALENT:対象キャラ:陥落不可, 1)
	PRINTFORML %CALLNAME:対象キャラ%的[无法攻略]状态已转变为[无法炮友]。
ELSE
	PRINTFORML %CALLNAME:対象キャラ%的[无法爱慕]状态已解除。
ENDIF
CLEARBIT TALENT:対象キャラ:陥落不可, 0
RESETCOLOR
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%[恋慕不可]解除")





@汎用イベント発生条件_キャライベント_セフレ不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:性愛度 < 経験値テーブル:99
	RETURN 0

RETURN 1

@汎用イベント関数_キャライベント_セフレ不可解除(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

DRAWLINE
PRINTFORML ［【通用】%NAME:対象キャラ%[不可炮友]解除］
DRAWLINE
PRINTW
PRINTFORMW %CALLNAME:対象キャラ%的状态似乎有些异常。
PRINTFORMW 搭话时眼含媚态，向%CALLNAME:PLAYER%露出撩人的微笑…
PRINTL 
PRINTFORMW 经过多次接触，被压抑的性欲终于突破界限。
PRINTFORMW 今后或许能发展更深层的关系。
PRINTL 
SETCOLOR 0xFFFF00
IF GETBIT(TALENT:対象キャラ:陥落不可, 0)
	PRINTFORML %CALLNAME:対象キャラ%的[无法攻略]状态已转变为[无法爱慕]。
ELSE
	PRINTFORML %CALLNAME:対象キャラ%的[无法炮友]状态已解除。
ENDIF
CLEARBIT TALENT:対象キャラ:陥落不可, 1
RESETCOLOR
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%[セフレ不可]解除")




@汎用イベント発生条件_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 0
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 0

RETURN 1

@汎用イベント消滅条件_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

;結婚相手は滞在していること
SIF CFLAG:対象キャラ:滞在期間 < 1
	RETURN 1
;一応フラグもチェック
SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 1
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 1

RETURN 0

@汎用イベント関数_キャライベント_結婚イベント(対象キャラ)
#DIM 対象キャラ
#DIM 花嫁キャラ
#DIM 花婿キャラ
#DIM レイヤー記録

IF 対象キャラ > 90000
	対象キャラ = 対象キャラ - 90000
ELSE
	対象キャラ = GETCHARA(対象キャラ)
ENDIF

IF TALENT:MASTER:性別 == 2 && TALENT:対象キャラ:性別 != 2
	花嫁キャラ = 対象キャラ
	花婿キャラ = MASTER
ELSEIF TALENT:MASTER:性別 != 2 && TALENT:対象キャラ:性別 == 2
	花嫁キャラ = MASTER
	花婿キャラ = 対象キャラ
ELSEIF RAND:2
	花嫁キャラ = 対象キャラ
	花婿キャラ = MASTER
ELSE
	花嫁キャラ = MASTER
	花婿キャラ = 対象キャラ
ENDIF


DRAWLINE
PRINTFORML ［【通用】%NAME:対象キャラ%结婚事件］
DRAWLINE
PRINTW
PRINTFORMW 晴朗的吉日，在上午的阳光中。
PRINTFORMW 婚礼会场的门扉开启，%CALLNAME:花嫁キャラ%身披纯白婚纱，开始在柔和的光晕中缓步前行。
PRINTFORMW 祭坛上的%CALLNAME:花婿キャラ%展露微笑，用混杂着紧张与喜悦的神情凝视着对方。
レイヤー記録 = TFLAG:口上用画像表示番号
CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/汎用結婚イベント一枚絵")
IF レイヤー記録 == TFLAG:口上用画像表示番号
	;専用画像が表示されてないということなので、ドレス画像を見る
	IF TALENT:花嫁キャラ:性別 == 2
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/短冊_白色无尾礼服")
	ELSE
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花嫁キャラ:画像フォルダ%/短冊_婚纱")
	ENDIF
ENDIF
レイヤー記録 = TFLAG:口上用画像表示番号
CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/汎用結婚イベント一枚絵")
IF レイヤー記録 == TFLAG:口上用画像表示番号
	;専用画像が表示されてないということなので、ドレス画像を見る
	IF TALENT:花婿キャラ:性別 == 2
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/短冊_白色无尾礼服")
	ELSE
		CALL PRINT_STRL(@"IMAGEPATH_%CSTR:花婿キャラ:画像フォルダ%/短冊_婚纱")
	ENDIF
ENDIF
PRINTL 
PRINTFORMW 彩绘玻璃的斑斓光芒温暖地映照着坐在木质长椅上的宾客们。
PRINTFORMW 管风琴的庄严音色回荡会场，%CALLNAME:花嫁キャラ%的头纱在微风中轻轻摇曳。
PRINTFORMW %CALLNAME:花嫁キャラ%手中捧着纯白玫瑰与淡粉色芍药组成的花束。
PRINTL 
PRINTFORMW 牧师用温和的嗓音引导誓约之词，为两人的爱情献上祝福。
PRINTFORMW 交换戒指的瞬间，会场陷入寂静，只能听见两人的呼吸声。
PRINTFORMW %CALLNAME:花婿キャラ%牵起%CALLNAME:花嫁キャラ%的手轻柔一吻，掌声与欢呼顿时响彻全场。
PRINTL 
PRINTFORMW 花瓣纷飞，钟声鸣响，宣告着新生活的启程。
PRINTFORMW %CALLNAME:花婿キャラ%与%CALLNAME:花嫁キャラ%必将构筑起永恒幸福的未来────
PRINTL 
SETCOLOR 0xFFFF00
PRINTFORMW %CALLNAME:対象キャラ%获得了［配偶］特质。
RESETCOLOR
IF TALENT:対象キャラ:浮気 > 0
	PRINTFORM %CALLNAME:対象キャラ%的
	SETCOLOR カラーパレット("えっちな色")
	PRINT [出轨]
	RESETCOLOR
	PRINT 特质已消失……
ENDIF
IF TALENT:対象キャラ:恋人持ち != 2
	SIF TALENT:対象キャラ:浮気 > 0
		WAIT
	PRINTFORM %CALLNAME:対象キャラ%的
	SETCOLOR カラーパレット("えっちな色")
	IF TALENT:対象キャラ:性別 == 1 || TALENT:対象キャラ:性別 == 3
		PRINT [人妻]
	ELSE
		PRINT [已婚者]
	ENDIF
	RESETCOLOR
	PRINT 特质已激活！
ENDIF
IF !FLAG:クリア済み視聴フラグ
	TALENT:対象キャラ:恋慕 = 2
	初体験日:対象キャラ:伴侶 = DAY
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:対象キャラ%被[配偶]攻略</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>获得攻略特质[配偶]</font><img src='えっちハート'>","うふふ実績")
	IF TALENT:対象キャラ:浮気 > 0
		TALENT:対象キャラ:浮気 = 0
	ENDIF
	IF TALENT:対象キャラ:恋人持ち != 2
		TALENT:対象キャラ:恋人持ち = 2
	ENDIF
ENDIF
PRINTL
PRINTL
FORCEWAIT

CALL イベント完了フラグ汎用処理(@"【汎用】%NAME:対象キャラ%結婚イベント")

