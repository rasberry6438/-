@よろず屋輸入品店処理
#DIM 値段一時登録
#DIM 表示アビ番号
#DIM 関数総数
#DIMS 表示文字列

VARSET 表示文字列
DRAWLINE
PRINTL 正在售卖岛外进口的珍稀商品
DRAWLINE
表示文字列 += "<div rect='62,0,4375,3000' border='31' bcolor='#C0C0C0'></div>"
表示文字列 += "<div rect='4469,0,1562,625' border='31' bcolor='#C0C0C0'></div>"
表示文字列 += @"<div rect='4531,62,1562,625'>持有卢比：%NUM_FORMAT(MONEY)%卢比<br>持有ZP　：%ZP所持量全文字列()%</div>"

;商品の補充は @輸入品店品揃え変動処理 でなされる
FOR LOCAL, 0, 5
	;輸入品店_品揃え登録文字列が空文字列の場合は売り切れなどで商品がない
	SIF 輸入品店_品揃え登録文字列:LOCAL == ""
		CONTINUE

	表示文字列 += @"<div rect='{200 + LOCAL % 2 * 2100},{100 + LOCAL / 2 * 600},2000,550' border='20' bcolor='#C0C0C0' padding='31'><nobr>"

	;1行目 商品名表示行 素材のみランク表示や属性の色つけをする
	表示文字列 += "■"
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_素材
		CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:LOCAL%
		表示文字列 += @"等级%TOFULL(TOSTR(素材性能_素材ランク))%：<font color='#%カラーパレット_HTML(@"%素材性能_属性%属性")%'>"
	ENDIF
	表示文字列 += @"%輸入品店_品揃え登録文字列:LOCAL%"
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_素材
		表示文字列 += "</font>"
	ENDIF
	表示文字列 += "<br>"

	;2行目(指輪のみ) オプション表示行
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_指輪
		表示文字列 += @"　附加属性数量：{輸入品店_品揃え登録:LOCAL:品目性能, 2}<br>"
	ENDIF

	;2行目 価格・付属情報表示行 セール品の場合は 輸入品店_品揃え登録:LOCAL:値段 が負であることに注意
	表示文字列 += @"　价格：{ABS(輸入品店_品揃え登録:LOCAL:値段)}卢比"
	IF 輸入品店_品揃え登録:LOCAL:値段 < 0
		表示文字列 += @"　<font color='#%カラーパレット_HTML("黄")%'>SALE！</font>"
	ENDIF
	;店頭に並ぶ証は必ず未入手
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_証 || (輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_素材 && 素材アイテム_数値データ取得("累計入手素材数", 輸入品店_品揃え登録文字列:LOCAL) == 0)
		表示文字列 += @"　<font color='#%カラーパレット_HTML("真っ赤")%'>未获取！</font>"
	;素材の場合のみ複数所持が可能なので所持数を表示
	ELSEIF 輸入品店_品揃え登録:LOCAL:品目種別 == 品目種別_素材
		表示文字列 += "　持有数："
		IF !素材アイテム数量算出(輸入品店_品揃え登録文字列:LOCAL)
			表示文字列 += @"<font color='#%カラーパレット_HTML("黄")%'>０！</font>"
		ELSE
			表示文字列 += @"{素材アイテム数量算出(輸入品店_品揃え登録文字列:LOCAL)}"
		ENDIF
	ENDIF

	;指輪だけオプション表示行があるのでそれ以外の品目は改行を追加
	IF 輸入品店_品揃え登録:LOCAL:品目種別 != 品目種別_指輪
		表示文字列 += "<br>"
	ENDIF
	表示文字列 += "<br>"

	表示文字列 += @"　　　　　　　　<button value='{LOCAL + 100}'>[详情]</button>　　<button value='{LOCAL}'>[购买]</button>"
	表示文字列 += "</div>"
NEXT

表示文字列 += "<div rect='130,2812,5125,281'>"
表示文字列 += "<button value='998'>[998] 系统说明</button>　　"
IF ITEM:进口商品刷新券 > 0
	表示文字列 += @"<button value='997'>[997] 使用进口商品刷新券（剩余{ITEM:进口商品刷新券,2}张）</button>　　"
ELSE
	表示文字列 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[997] 使用进口商品刷新券（剩余{ITEM:进口商品刷新券,2}张）</font>　　"
ENDIF
表示文字列 += "<button value='999'>[999] 返回</button>"
表示文字列 += "</div>"


HTML_PRINT 表示文字列, 1
FOR LOCAL, 0, 27
	PRINTL
NEXT

IF チュートリアルフラグ:輸入品店 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 997
		CALL 輸入品店品揃え変動処理
		ITEM:进口商品刷新券 -= 1
		RESTART
	CASE 999
		RETURN -1
	CASE 998
		DRAWLINE
		PRINTL ［进口商品店教程］
		DRAWLINE
		HTML_PRINT "<img src='顔エラ' height='900' width='900'>"
		PRINTL
		PRINTL
		PRINTL
		PRINTL
		PRINTL
		PRINTL
		PRINTL
		CALL 輸入品店システムチュートリアル
		DRAWLINE
		RESTART
	CASE IS >= 100
		;詳細表示
		DRAWLINE
		PRINTFORML ■%輸入品店_品揃え登録文字列:(LOCAL - 100)%
		SELECTCASE 輸入品店_品揃え登録:(LOCAL - 100):品目種別
			CASE 品目種別_証
				;証
				TRYCALLFORM 証関数_%輸入品店_品揃え登録文字列:(LOCAL - 100)%("フレーバー")
				表示文字列 = %詳細文文字列受け渡し変数%<br>
				表示文字列 += @"・追加技能<br>"
				VARSET RESULTS
				関数総数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%輸入品店_品揃え登録文字列:(LOCAL - 100)%_")
				FOR 表示アビ番号, 0, 関数総数
					TSTR:コマンド名受渡 = 
					TRYCALLFORM %RESULTS:表示アビ番号%("アビ名")
					SIF TSTR:コマンド名受渡 == ""
						CONTINUE
					詳細文文字列受け渡し変数 = 
					TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
					表示文字列 += @"%詳細文文字列受け渡し変数%<br>"
				NEXT
				表示文字列 += "<br>"
				HTML_PRINT 表示文字列
			CASE 品目種別_指輪
				;指輪
				CALLFORM 指輪基礎効果文章表示_%輸入品店_品揃え登録文字列:(LOCAL - 100)%(0, 輸入品店_品揃え登録:(LOCAL - 100):品目性能２)
				表示文字列 = %詳細文文字列受け渡し変数%<br>
				表示文字列 += @"【属性效果】<br>%オプション効果表示文字列作成(輸入品店_オプション固定登録:(LOCAL - 100))%"
				HTML_PRINT 表示文字列
			CASE 品目種別_耳飾り
				;耳飾り
				CALLFORM 耳飾り基礎効果文章表示_%輸入品店_品揃え登録文字列:(LOCAL - 100)%(0, 輸入品店_品揃え登録:(LOCAL - 100):品目性能２)
				表示文字列 = %詳細文文字列受け渡し変数%
				HTML_PRINT 表示文字列
			CASE 品目種別_素材
				;素材
				CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:(LOCAL - 100)%
				PRINTFORM 类别：%素材性能_カテゴリ, 12, LEFT%
				IF 素材性能_カテゴリ == "食物素材"
					PRINTFORML 　　分类：%素材性能_食材分類%
				ELSEIF 素材性能_カテゴリ == "礼物类道具"
					PRINTFORML 　　分类：%素材性能_プレゼント%
				ELSE
					PRINTL
				ENDIF
				PRINTFORM 　等级：%TOFULL(TOSTR(素材性能_素材ランク)), 12, LEFT%　　属性：
				SETCOLOR カラーパレット(@"%素材性能_属性%属性")
				PRINTFORML %素材性能_属性%属性
				RESETCOLOR
		ENDSELECT
		PRINTFORML
		WAIT
		RESTART
	CASEELSE
		;購入
		IF ABS(輸入品店_品揃え登録:LOCAL:値段) > MONEY
			PRINTW 卢比不足。
			RESTART
		ENDIF
		SELECTCASE 輸入品店_品揃え登録:LOCAL:品目種別
			CASE 品目種別_証
				;証
				CALL 汎用証取得処理(輸入品店_品揃え登録文字列:LOCAL)
			CASE 品目種別_指輪
				;指輪
				CALL 汎用指輪取得処理(輸入品店_品揃え登録文字列:LOCAL, 0, 輸入品店_品揃え登録:LOCAL:品目性能２ - 1)
				IF RESULT > -1
					;オプション固定のため一旦オプション無しで登録してから手動で入れ込む
					DT_CELL_SET "所持指輪データベース", DT_ROW_LENGTH("所持指輪データベース") - 1, "オプションステータス補正", 輸入品店_オプション固定登録:LOCAL
				ENDIF
			CASE 品目種別_耳飾り
				;耳飾り
				CALL 汎用耳飾り取得処理(輸入品店_品揃え登録文字列:LOCAL, 輸入品店_品揃え登録:LOCAL:品目性能２)
			CASE 品目種別_素材
				;食材
				CALL 素材入手処理(輸入品店_品揃え登録文字列:LOCAL, 1)
		ENDSELECT
		IF RESULT > -1
			MONEY -= ABS(輸入品店_品揃え登録:LOCAL:値段)
			PRINTFORMW 已购入%輸入品店_品揃え登録文字列:LOCAL%
			輸入品店_品揃え登録文字列:LOCAL =
		ENDIF
		RESTART
ENDSELECT





@輸入品店品揃え変動処理
#DIM 登録品目上限
#DIM 登録品目番号
#DIM 証品目数
#DIM 装飾品品目数
#DIM 食材品目数
#DIMS 候補保存配列, 200
#DIM 候補食材数
#DIM オプションランク一時保存

;輸入品店_品揃え登録:LOCAL:品目種別
;	0:登録無し 1:証 2:指輪 3:耳飾り 4:素材
VARSET 輸入品店_品揃え登録
VARSET 輸入品店_品揃え登録文字列
VARSET 輸入品店_オプション固定登録
VARSET 候補保存配列
候補食材数 = 0

;現状５品目
登録品目上限 = 5
登録品目番号 = 0
証品目数 = 0

;まず証が出現するかどうかを見る
;1/6で持ってない証からランダムで出現
IF RAND:6 == 0
	CALL ランダム証("輸入")
	IF RESULTS != ""
		輸入品店_品揃え登録:登録品目番号:品目種別 = 品目種別_証
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS
		輸入品店_品揃え登録:登録品目番号:値段 = 50000
		証品目数 = 1
		登録品目番号 += 1
	ENDIF
ENDIF

;残りの枠のうち、１～３枠を指輪・耳飾りで埋める
;証なしの場合は２～３枠
IF 証品目数
	装飾品品目数 = RAND:3 + 1
ELSE
	装飾品品目数 = RAND:2 + 2
ENDIF

FOR LOCAL, 0, 装飾品品目数
	輸入品店_品揃え登録:登録品目番号:品目種別 = RAND:2 + 品目種別_指輪
	IF 輸入品店_品揃え登録:登録品目番号:品目種別 == 品目種別_指輪
		CALL ランダム指輪("輸入")
		IF RESULTS == ""
			THROW 輸入品店に並べる指輪の候補がありません。開発者に報告してください。
		ENDIF
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS

		;基礎販売額の取得
		基礎販売額 = 0
		CALLFORM 指輪個別文章表示_%輸入品店_品揃え登録文字列:登録品目番号%()
		輸入品店_品揃え登録:登録品目番号:値段 = 基礎販売額

		;オプションを２個～４個つける
		輸入品店_品揃え登録:登録品目番号:品目性能 = RAND:3 + 2
		CALLFORM 指輪ステータス決定_%輸入品店_品揃え登録文字列:登録品目番号%(0, 1)

		;オプションランクがRESULTに入っているので、それに応じた数のオプションを記録
		オプションランク一時保存 = RESULT
		オプション補正文字列 = 
		FOR LOCAL:1, 0, 輸入品店_品揃え登録:登録品目番号:品目性能
			CALL オプション効果決定(オプションランク一時保存)
		NEXT
		輸入品店_オプション固定登録:登録品目番号 = %オプション補正文字列%

		;ランダム効果を決定
		CALLFORM 指輪基礎効果文章表示_%輸入品店_品揃え登録文字列:登録品目番号%
		SIF RESULT > 1
			輸入品店_品揃え登録:登録品目番号:品目性能２ = RAND:RESULT + 1

		;値段決定(オプション数１個につき50%値段UP)
		輸入品店_品揃え登録:登録品目番号:値段 = (輸入品店_品揃え登録:登録品目番号:値段 * (10 + 輸入品店_品揃え登録:登録品目番号:品目性能 * 5) / 10) * RAND(80, 121) / 100
		;10%の確率でセール品
		SIF RAND:10 == 0
			輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2

	ELSEIF 輸入品店_品揃え登録:登録品目番号:品目種別 == 品目種別_耳飾り
		CALL ランダム耳飾り("輸入")
		IF RESULTS == ""
			THROW 輸入品店に並べる耳飾りの候補がありません。開発者に報告してください。
		ENDIF
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS

		;基礎販売額の取得
		基礎販売額 = 0
		CALLFORM 耳飾り個別文章表示_%輸入品店_品揃え登録文字列:登録品目番号%()
		輸入品店_品揃え登録:登録品目番号:値段 = 基礎販売額

		;ランダム効果を決定
		CALLFORM 耳飾り基礎効果文章表示_%輸入品店_品揃え登録文字列:登録品目番号%
		SIF RESULT > 1
			輸入品店_品揃え登録:登録品目番号:品目性能２ = RAND:RESULT + 1

		;値段決定
		輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 * RAND(80, 121) / 100
		;10%の確率でセール品
		SIF RAND:10 == 0
			輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2
	ENDIF
	登録品目番号 += 1
NEXT

;残りは食材・プレゼントを登録
;まずは候補食材を羅列
FOR LOCAL, 0, DT_ROW_LENGTH("所持素材データベース")
	;登録されてないアイテム判定のため比較用配列に保存
	;食べ物のみ
	SIF DT_CELL_GETS("所持素材データベース", LOCAL, "素材カテゴリ") != "食物素材" && DT_CELL_GETS("所持素材データベース", LOCAL, "素材カテゴリ") != "礼物类道具"
		CONTINUE
	;ランク２以上
	SIF DT_CELL_GET("所持素材データベース", LOCAL, "素材ランク") < 2
		CONTINUE
	候補保存配列:候補食材数 = %DT_CELL_GETS("所持素材データベース", LOCAL, "素材アイテム名")%
	候補食材数 += 1
NEXT
食材品目数 = 候補食材数

FOR LOCAL, 登録品目番号, 登録品目上限
	輸入品店_品揃え登録:登録品目番号:品目種別 = 品目種別_素材
	輸入品店_品揃え登録文字列:登録品目番号 = %候補保存配列:(RAND:候補食材数)%

	CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:登録品目番号%
	;値段決定
	IF 素材性能_カテゴリ == "食物素材"
		輸入品店_品揃え登録:登録品目番号:値段 = 素材性能_素材ランク * 素材性能_素材ランク * 500 * RAND(80, 121) / 100
	ELSE
		輸入品店_品揃え登録:登録品目番号:値段 = 素材性能_素材ランク * 素材性能_素材ランク * 1000 * RAND(80, 121) / 100
	ENDIF
	;10%の確率でセール品
	SIF RAND:10 == 0
		輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2

	登録品目番号 += 1
NEXT
