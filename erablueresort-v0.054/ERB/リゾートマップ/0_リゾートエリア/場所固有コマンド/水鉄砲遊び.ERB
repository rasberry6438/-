
;-------------------------------------------------
;水鉄砲遊び
;-------------------------------------------------
		
@水鉄砲遊び(キャラ番号)
#DIM キャラ番号
#DIM 難易度, 2
#DIM タゲ行
#DIM タゲ列
#DIM クリック成功回数
;ここからマップ描画用DIM
#DIMS AA, 40
#DIM SPOT
#DIM MODE
#DIM LOCATION
#DIM LINENUM
#DIM X_POINT
;ここまでマップ描画用DIM

クリック成功回数 = 0

$難易度_LOOP
;難易度選択
DRAWLINE
PRINTL 请选择游戏难度：
PRINTBUTTON "[1]简单（反应时间２秒：次数５次）", 1
PRINTL 
PRINTBUTTON "[2]普通（反应时间１秒：次数７次）", 2
PRINTL 
PRINTBUTTON "[3]困难（反应时间０．５秒：次数１０次）", 3
PRINTL 
PRINTBUTTON "[4]十天众（反应时间０．１秒：次数１５次）", 4
PRINTL 
DRAWLINE
PRINTBUTTONLC "[说明]", 998
PRINTBUTTON "[返回]", 999
PRINTL

$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASE 998
		DRAWLINE
		PRINTL 这是需要及时点击随机出现目标的迷你游戏
		PRINTL 成功点击次数越多，角色互动越深入，参数提升幅度越大
		PRINTW 难度越高可获得更多点击机会
		PRINTL 
		PRINTL ■关于反应时间和次数
		PRINTL 反应时间：目标显示后允许操作的时间
		PRINTL 次数：总共可挑战的目标点击次数
		PRINTW 反应时间越长越简单，次数越多越容易积累成功
		PRINTL 
		PRINTL （十天众难度并非为人类玩家设计）
		PRINTW （计划在未来增加一个系统来增加反应时间和次数，当前未实装）
		GOTO 難易度_LOOP
	CASE 1
		難易度:0 = 2000
		難易度:1 = 5
	CASE 2
		難易度:0 = 1000
		難易度:1 = 7
	CASE 3
		難易度:0 = 500
		難易度:1 = 10
	CASE 4
		難易度:0 = 100
		難易度:1 = 15
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

;クリック領域生成
AA:00 = ┌────────────────────┐
AA:01 = │0102030405060708091011121314151617181920│
AA:02 = │0102030405060708091011121314151617181920│
AA:03 = │0102030405060708091011121314151617181920│
AA:04 = │0102030405060708091011121314151617181920│
AA:05 = │0102030405060708091011121314151617181920│
AA:06 = │0102030405060708091011121314151617181920│
AA:07 = │0102030405060708091011121314151617181920│
AA:08 = │0102030405060708091011121314151617181920│
AA:09 = │0102030405060708091011121314151617181920│
AA:10 = │0102030405060708091011121314151617181920│
AA:11 = │0102030405060708091011121314151617181920│
AA:12 = │0102030405060708091011121314151617181920│
AA:13 = │0102030405060708091011121314151617181920│
AA:14 = │0102030405060708091011121314151617181920│
AA:15 = │0102030405060708091011121314151617181920│
AA:16 = │0102030405060708091011121314151617181920│
AA:17 = └────────────────────┘

$描画_LOOP
;ターゲットの再設定
タゲ行 = RAND:16 + 1
タゲ列 = RAND:20 + 1

;クリック領域描画
FOR LINENUM, 0, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;行ごとに初期化
	X_POINT = 0
	RESULTS = I
	;行の終端までループ
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		IF ISNUMERIC(RESULTS)
			;数字は２文字取る
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			IF LINENUM == タゲ行 && TOINT(RESULTS) == タゲ列
				SETCOLOR すごろくカラーパレット(1)
				PRINTBUTTON "○", 999
				RESETCOLOR
			ELSE
				PRINT 　
			ENDIF
			X_POINT += 2
		ELSE
			PRINTS RESULTS
			X_POINT ++
		ENDIF
	WEND
	PRINTL 
NEXT

;制限時間付きクリックタイム
TINPUT 難易度, 0, 1, "失敗！"
CLEARLINE 1

IF RESULT == 999
	PRINTL 成功！
	クリック成功回数 += 1
ENDIF

難易度:1 -= 1
IF 難易度:1
	PRINTL 
	PRINTFORM 剩余次数：%TOFULL(TOSTR(難易度:1))%次　　　
	PRINTFORMW 成功次数：%TOFULL(TOSTR(クリック成功回数))%次
	GOTO 描画_LOOP
ELSE
	PRINTW 
	DRAWLINE
	PRINTL 游戏结束！
	PRINTFORMW 成功次数：%TOFULL(TOSTR(クリック成功回数))%次
	履歴用実績:MASTER:水鉄砲回数 += 1
	CALL 履歴登録チェック(MASTER, "水鉄砲回数")
	履歴用実績:キャラ番号:水鉄砲回数 += 1
	CALL 履歴登録チェック(キャラ番号, "水鉄砲回数")
ENDIF

IF 難易度:0 == 100 && クリック成功回数 > 0
	CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"难度：在十天众难度的水枪游戏中获得１次胜利！","実績")
ENDIF

RETURN クリック成功回数

