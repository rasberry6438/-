;-------------------------------------------------
;玩弄乳头
;愛撫系コマンド、レベル1
;-------------------------------------------------
@RAPE_COMNAME6
#FUNCTIONS
IF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	TSTR:コマンド名受渡 = 发掘凹陷乳头
	;こっちのほうが「オモチャにしてる感」強くて征服感煽れるかなって
ELSE
	TSTR:コマンド名受渡 = 玩弄乳头
ENDIF



;===================================================
;コマンドタイプ
;===================================================
@RAPE_COMTYPE_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = RAPE_COMTYPE("うふふ_被挿入中")


@RAPE_COM_TOOLTIP_6
LOCALS = <br>■玩弄乳头<br>
LOCALS += "将对方乳头当作玩具把玩。包括抠弄、掐捏等行为。"
LOCALS += "<br>强奸指令类型：嘿咻or被插入中<br>获取来源：快Ｂ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：乳头开发(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@RAPE_COM6
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

RCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 120)

SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 60)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 50)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 5
	EXP:PLAYER:同性愛経験 += 5
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@RAPE_COM_ABLE6
;乳首責め
;一括管理
SIF GLOBAL_RAPE_COMABLE(6)
	RETURN RESULT
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
	
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0
;乳头夹使用中は不可
SIF TEQUIP:16
	RETURN 0
;挤奶机使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指 && MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) < 0 && MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) < 0
	RETURN 0
;胸に届かないなら無理
CALL MODE_部位届く判定(部位_胸, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0
;寝てるときは痛み刺激系はオフに
SIF CFLAG:TARGET:睡眠
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@RAPE_MESSAGE_COM6

PRINTFORM %CALLNAME:PLAYER%


IF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	PRINTFORM 在与%CALLNAME:TARGET%保持插入的状态下，对%OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM 对%CALLNAME:TARGET%的%OPPAI_DESCRIPTION(TARGET)%
ENDIF
SELECTCASE TALENT:陥没乳首
	CASE 0
		;欲情のPALAMLV3以上
		IF PALAM:欲情 >= PALAMLV:3
			PRINTFORM 的顶端，尽管正在被强奸仍%TEXTR("完全硬挺的/充血勃起的/")%%TEXTR("突起/乳头")%
			IF MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTFORML 用舌头舔舐取乐…
			ELSE
				PRINTFORML %TEXTR("用指腹狠狠碾压/用指腹掐捏/用指尖拧绞般拉扯")%…
			ENDIF
		ELSE
			PRINTFORM 的顶端尚柔软的%TEXTR("突起/乳头")%
			IF MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTFORML 用舌头舔舐取乐…
			ELSE
				PRINTFORML %TEXTR("用指腹狠狠碾压/用指腹掐捏/用指尖拧绞般拉扯")%…
			ENDIF
		ENDIF
	CASE 1
		IF !TCVAR:陥没乳首露出フラグ
			PRINTFORM 的凹陷处
			IF MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTFORM 用舌头
			ELSE
				PRINTFORM 指
			ENDIF
			PRINTFORML 游走，抠弄隐藏的乳头…
		ELSEIF TCVAR:陥没乳首露出フラグ == 10
			;フラグの数値が10になるのは勃起直後のみのはず
			PRINTFORM 的凹陷处
			IF MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTFORM 用舌头
			ELSE
				PRINTFORM 指
			ENDIF
			PRINTFORML 抠弄时，
			PRINTFORML %CALLNAME:TARGET%硬挺的乳头完全外露…
		ELSE
			PRINTFORM 的顶端，尽管正在被强奸仍%TEXTR("完全硬挺的/充血勃起的/")%%TEXTR("突起/乳头")%
			IF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTFORML 用舌头舔舐取乐…
			ELSE
				PRINTFORML %TEXTR("用指腹狠狠碾压/用指腹掐捏/用指尖拧绞般拉扯")%…
			ENDIF
		ENDIF
ENDSELECT

