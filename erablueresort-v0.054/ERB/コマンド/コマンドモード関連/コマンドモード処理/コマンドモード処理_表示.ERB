;-------------------------------------------------------------------------------------------
;モード表示や設定表示などの関数群
;-------------------------------------------------------------------------------------------
@SHOW_MODE()
#DIM 配列番号
#DIM 配列番号受け渡し
#DIM 最大番号
#DIM モード数カウント
#DIM 実行キャラ番号
#DIM 対象キャラ番号
#DIMS 実行モード名
#DIMS 実行显示名
#DIM 優先度計算
#DIM ソート用配列_優先度, 体位モード検索上限
#DIM ソート用配列_配列番号, 体位モード検索上限
#DIM CONST 表示上限 = 4		;原为10
#DIM 改行カウント
#DIMS モード名記録

モード数カウント = 0
VARSET ソート用配列_優先度
VARSET ソート用配列_配列番号

;モードに優先度を付けて配列へ格納
;昇順ソートのため負数で優先度を付ける
FOR 配列番号, 0, DT_ROW_LENGTH("体位モードデータベース")
	SIF 配列番号 >= 体位モード検索上限
		BREAK
	実行キャラ番号 = DT_CELL_GET("体位モードデータベース", 配列番号, "実行キャラ")
	対象キャラ番号 = DT_CELL_GET("体位モードデータベース", 配列番号, "対象キャラ")
	SIF !IS_SAME_ROOM(PLAYER, 対象キャラ番号)
		CONTINUE
	IF OPTION変数:MODE全表示フラグ == 2
		モード名記録 '= DT_CELL_GETS("体位モードデータベース", 配列番号, "モード名")
		IF 系統リスト_アイテム系(モード名記録) || 系統リスト_自慰系(モード名記録)
			SIF 対象キャラ番号 != MASTER && 対象キャラ番号 != TARGET
				CONTINUE
		ELSE
			SIF 実行キャラ番号 != MASTER && 実行キャラ番号 != TARGET && 対象キャラ番号 != MASTER && 対象キャラ番号 != TARGET
				CONTINUE
		ENDIF
	ENDIF
	優先度計算 = 配列番号 - 1000
	SIF 実行キャラ番号 == PLAYER
		優先度計算 -= 200000
	SIF 対象キャラ番号 == PLAYER
		優先度計算 -= 100000
	SIF 実行キャラ番号 == TARGET
		優先度計算 -= 20000
	SIF 対象キャラ番号 == TARGET
		優先度計算 -= 10000
	ソート用配列_優先度:モード数カウント = 優先度計算
	ソート用配列_配列番号:モード数カウント = 配列番号
	モード数カウント++
NEXT

ARRAYMSORT ソート用配列_優先度, ソート用配列_配列番号

SIF モード数カウント > 0
	DRAWLINE

;優先度の高いモードから順に表示上限まで表示
IF OPTION変数:MODE全表示フラグ > 0
	最大番号 = モード数カウント
ELSE
	最大番号 = MIN(モード数カウント, 表示上限)
ENDIF
FOR 配列番号, 0, 最大番号
	配列番号受け渡し = ソート用配列_配列番号:配列番号
	実行キャラ番号 = DT_CELL_GET("体位モードデータベース", (配列番号受け渡し), "実行キャラ")
	対象キャラ番号 = DT_CELL_GET("体位モードデータベース", (配列番号受け渡し), "対象キャラ")
	実行モード名 = %DT_CELL_GETS("体位モードデータベース", (配列番号受け渡し), "モード名")%

	SETCOLOR カラーパレット("えっちな色")
	IF DT_CELL_GET("体位モードデータベース", 配列番号, "弱点フラグ")
		PRINT 弱
		PRINT_IMG "えっちハート"
	ENDIF
	IF 系統リスト_アイテム系(実行モード名)
		PRINTFORM [%CALLNAME:対象キャラ番号%正在装备%実行モード名%中
	ELSEIF 系統リスト_触手系(実行モード名)
		VARSET 実行显示名
		SELECTCASE 実行モード名
			CASE "触手乳首責め"
				実行显示名 = 触手刺激乳头
			CASE "触手クリ責め"	
				実行显示名 = 触手刺激阴蒂
			CASE "触手ペニス責め"
				実行显示名 = 触手刺激阴茎
			CASE "触手挿入"
				実行显示名 = 触手插入
			CASE "触手アナル挿入"
				実行显示名 = 触手插入肛门
			CASE "触手緊縛"
				実行显示名 = 触手緊縛
			CASE "触手口辱"
				実行显示名 = 触手羞辱口腔
		ENDSELECT
		PRINTFORM [触手正在对%CALLNAME:対象キャラ番号%%実行显示名%中
	ELSEIF 系統リスト_自慰系(実行モード名)
		VARSET 実行显示名
		SELECTCASE 実行モード名
			CASE "セルフ手コキ"
				実行显示名 = 自我手淫
			CASE "クリオナ"
				実行显示名 = 刺激阴蒂
			CASE "膣オナ"
				実行显示名 = 阴道自慰
			CASE "アナニー"
				実行显示名 = 肛门自慰
			CASE "チクニー"
				実行显示名 = 挑逗乳头
			CASE "セルフ乳首吸い"
				実行显示名 = 吸吮自己乳头
		ENDSELECT
		PRINTFORM [%CALLNAME:対象キャラ番号%正在%実行显示名%中
	ELSE
		IF TFLAG:調教モード == 2 && 実行モード名 == "肩組み乳揉み"
			PRINTFORM [%CALLNAME:実行キャラ番号%正在揉%CALLNAME:対象キャラ番号%的胸
		ELSE
			VARSET 実行显示名
			SELECTCASE 実行モード名
				CASE "キス"
					実行显示名 = 接吻
				CASE "ディープキス"
					実行显示名 = 深情接吻
				CASE "肩組み乳揉み"
					実行显示名 = 环肩揉胸
				CASE "尻揉み"
					実行显示名 = 揉捏臀部
				CASE "クンニ"
					実行显示名 = 舔阴
				CASE "ハーヴィンオナホＶ"
					実行显示名 = 哈文飞机杯Ｖ
				CASE "ハーヴィンオナホＡ"
					実行显示名 = 哈文飞机杯Ａ
				CASE "挿入歩きＶ"
					実行显示名 = 插入行走Ｖ
				CASE "挿入歩きＡ"
					実行显示名 = 插入行走Ａ
				CASE "ハーヴィンディルドＶ"
					実行显示名 = 哈文假阳具Ｖ
				CASE "ハーヴィンディルドＡ"
					実行显示名 = 哈文假阳具Ａ
				CASE "パイズリ"
					実行显示名 = 乳交
				CASE "パイズリフェラ"
					実行显示名 = 乳夹口交
				CASE "縦パイズリ"
					実行显示名 = 纵向乳交
				CASE "フェラチオ"
					実行显示名 = 口交
				CASE "イラマチオ"
					実行显示名 = 深喉插入
				CASE "乳首吸い"
					実行显示名 = 吸吮乳头
				CASE "授乳手コキ"
					実行显示名 = 哺乳手交
				CASE "手コキ"
					実行显示名 = 手交
				CASE "素股"
					実行显示名 = 股间摩擦
				CASE "疑似性交"
					実行显示名 = 模拟性交
				CASE "貝合わせ"
					実行显示名 = 磨豆腐
				CASE "兜合わせ"
					実行显示名 = 龟头交合
				CASE "正常位"
					実行显示名 = 正常位
				CASE "後背位"
					実行显示名 = 后背位
				CASE "寝バック"
					実行显示名 = 仰卧位
				CASE "対面座位"
					実行显示名 = 対面座位
				CASE "背面座位"
					実行显示名 = 背面座位
				CASE "騎乗位"
					実行显示名 = 骑乘位
				CASE "側位"
					実行显示名 = 侧卧位
				CASE "屈曲位"
					実行显示名 = 屈曲位
				CASE "駅弁"
					実行显示名 = 火车便当位
				CASE "背面駅弁"
					実行显示名 = 背面火车便当位
				CASE "対面立位"
					実行显示名 = 站立正面位
				CASE "立ちバック"
					実行显示名 = 站立背面位
				CASE "同時挿入"
					実行显示名 = 同時挿入
				CASE "Ａ正常位"
					実行显示名 = Ａ正常位
				CASE "Ａ後背位"
					実行显示名 = Ａ后背位
				CASE "Ａ寝バック"
					実行显示名 = Ａ仰卧位
				CASE "Ａ対面座位"
					実行显示名 = Ａ対面座位
				CASE "Ａ背面座位"
					実行显示名 = Ａ背面座位
				CASE "Ａ騎乗位"
					実行显示名 = Ａ骑乘位
				CASE "Ａ側位"
					実行显示名 = Ａ侧卧位
				CASE "Ａ屈曲位"
					実行显示名 = Ａ屈曲位
				CASE "Ａ駅弁"
					実行显示名 = Ａ火车便当位
				CASE "Ａ背面駅弁"
					実行显示名 = Ａ背面火车便当位
				CASE "Ａ対面立位"
					実行显示名 = Ａ站立正面位
				CASE "Ａ立ちバック"
					実行显示名 = Ａ站立背面位
				CASE "Ａ同時挿入"
					実行显示名 = Ａ同時挿入
				CASEELSE
					;PRINTFORM [%CALLNAME:実行キャラ番号%正在对%CALLNAME:対象キャラ番号%进行%実行モード名%中
			ENDSELECT
			IF 実行显示名 == ""
				PRINTFORM [%CALLNAME:実行キャラ番号%正在对%CALLNAME:対象キャラ番号%进行%実行モード名%中
			ELSE
				PRINTFORM [%CALLNAME:実行キャラ番号%正在对%CALLNAME:対象キャラ番号%进行%実行显示名%中
			ENDIF
		ENDIF
	ENDIF
	PRINT_IMG "えっちハート"
	PRINT ]
	RESETCOLOR
	IF 系統リスト_ハーヴィンオナホ系(実行モード名) == 0 && 実行モード名 != "全裸散歩"
		PRINTBUTTON "[✕]", @"モード中断_{配列番号受け渡し}"
	ENDIF
	CALL MODE詳細設定表示処理(対象キャラ番号, 実行モード名, 実行キャラ番号)
	PRINTL 
NEXT

IF 表示上限 < DT_ROW_LENGTH("体位モードデータベース")
	IF OPTION変数:MODE全表示フラグ == 0
		PRINTFORM 他{モード数カウント - 表示上限, 2}件　　
		PRINTBUTTON "[全部显示]", "モード全表示"
		PRINT / 
		PRINTBUTTON "[仅显示PLAYER和TARGET]", "モード制限表示"
		PRINTL
	ELSEIF OPTION変数:MODE全表示フラグ == 1
		PRINTBUTTON "[仅显示PLAYER和TARGET]", "モード制限表示"
		PRINT / 
		PRINTBUTTON "[省略显示]", "モード省略表示"
		PRINTL
	ELSE
		PRINTBUTTON "[全部显示]", "モード全表示"
		PRINT / 
		PRINTBUTTON "[省略显示]", "モード省略表示"
		PRINTL
	ENDIF
ENDIF


@MODE詳細設定表示処理(対象キャラ番号, 体位モード, 実行キャラ番号)
#DIM 対象キャラ番号
#DIM 実行キャラ番号
#DIM 判定数値保存
#DIMS 体位モード
;速度 >= 润滑+3 就会产生痛苦
V挿入強さ:対象キャラ番号:0 = Min(5,ABL:対象キャラ番号:Ｖ感覚)
IF        GETPALAMLV(PALAM:対象キャラ番号:潤滑, 15) < 8
        V挿入速さ:対象キャラ番号:0 = Max(0,GETPALAMLV(PALAM:対象キャラ番号:潤滑, 15) - 3)
ELSE
        V挿入速さ:対象キャラ番号:0 = 5
ENDIF
A挿入強さ:対象キャラ番号:0 = Min(5,ABL:対象キャラ番号:Ａ感覚)
IF        GETPALAMLV(PALAM:対象キャラ番号:潤滑, 15) < 8
        A挿入速さ:対象キャラ番号:0 = Max(0,GETPALAMLV(PALAM:対象キャラ番号:潤滑, 15) - 3)
ELSE
        A挿入速さ:対象キャラ番号:0 = 5
ENDIF
振动棒強さ:対象キャラ番号:0 = Min(5,ABL:対象キャラ番号:Ｖ感覚)
挤奶机強さ:対象キャラ番号:0 = Min(5,ABL:対象キャラ番号:Ｂ感覚)
肛门振动棒強さ:対象キャラ番号:0 = Min(5,ABL:対象キャラ番号:Ａ感覚)

IF 実行キャラ番号 == PLAYER
	IF MODE系統確認(体位モード, "Ｖ挿入系")
		PRINT 　　阴道插入强度：
		PRINTBUTTON "[-]", @"MODE詳細設定処理_V挿入強さ_{対象キャラ番号}_下降"
		FOR LOCAL, 0, 6
			IF V挿入強さ:対象キャラ番号:0 >= LOCAL
				PRINTBUTTON "■", @"MODE詳細設定処理_V挿入強さ_{対象キャラ番号}_{LOCAL}"
			ELSE
				PRINTBUTTON "□", @"MODE詳細設定処理_V挿入強さ_{対象キャラ番号}_{LOCAL}"
			ENDIF
		NEXT
		PRINTBUTTON "[+]", @"MODE詳細設定処理_V挿入強さ_{対象キャラ番号}_上昇"
		PRINT 　　
		PRINT 阴道插入速度：
		PRINTBUTTON "[-]", @"MODE詳細設定処理_V挿入速さ_{対象キャラ番号}_下降"
		FOR LOCAL, 0, 6
			IF V挿入速さ:対象キャラ番号:0 >= LOCAL
				PRINTBUTTON "■", @"MODE詳細設定処理_V挿入速さ_{対象キャラ番号}_{LOCAL}"
			ELSE
				PRINTBUTTON "□", @"MODE詳細設定処理_V挿入速さ_{対象キャラ番号}_{LOCAL}"
			ENDIF
		NEXT
		PRINTBUTTON "[+]", @"MODE詳細設定処理_V挿入速さ_{対象キャラ番号}_上昇"
	ENDIF
	IF MODE系統確認(体位モード, "Ａ挿入系")
		PRINT 　　肛门插入强度：
		PRINTBUTTON "[-]", @"MODE詳細設定処理_A挿入強さ_{対象キャラ番号}_下降"
		FOR LOCAL, 0, 6
			IF A挿入強さ:対象キャラ番号:0 >= LOCAL
				PRINTBUTTON "■", @"MODE詳細設定処理_A挿入強さ_{対象キャラ番号}_{LOCAL}"
			ELSE
				PRINTBUTTON "□", @"MODE詳細設定処理_A挿入強さ_{対象キャラ番号}_{LOCAL}"
			ENDIF
		NEXT
		PRINTBUTTON "[+]", @"MODE詳細設定処理_A挿入強さ_{対象キャラ番号}_上昇"
		PRINT 　　
		PRINT 肛门插入强度：
		PRINTBUTTON "[-]", @"MODE詳細設定処理_A挿入速さ_{対象キャラ番号}_下降"
		FOR LOCAL, 0, 6
			IF A挿入速さ:対象キャラ番号:0 >= LOCAL
				PRINTBUTTON "■", @"MODE詳細設定処理_A挿入速さ_{対象キャラ番号}_{LOCAL}"
			ELSE
				PRINTBUTTON "□", @"MODE詳細設定処理_A挿入速さ_{対象キャラ番号}_{LOCAL}"
			ENDIF
		NEXT
		PRINTBUTTON "[+]", @"MODE詳細設定処理_A挿入速さ_{対象キャラ番号}_上昇"
	ENDIF
ELSE
	IF MODE系統確認(体位モード, "Ｖ挿入系")
		PRINT 　　阴道插入强度：   
		FOR LOCAL, 0, 6
			IF V挿入強さ:対象キャラ番号:0 >= LOCAL
				PRINT ■
			ELSE
				PRINT □
			ENDIF
		NEXT
		PRINT 　   　
		PRINT 阴道插入速度：   
		FOR LOCAL, 0, 6
			IF V挿入速さ:対象キャラ番号:0 >= LOCAL
				PRINT ■
			ELSE
				PRINT □
			ENDIF
		NEXT
	ENDIF
	IF MODE系統確認(体位モード, "Ａ挿入系")
		PRINT 　　肛门插入强度：   
		FOR LOCAL, 0, 6
			IF A挿入強さ:対象キャラ番号:0 >= LOCAL
				PRINT ■
			ELSE
				PRINT □
			ENDIF
		NEXT
		PRINT 　   　
		PRINT 肛门插入强度：   
		FOR LOCAL, 0, 6
			IF A挿入速さ:対象キャラ番号:0 >= LOCAL
				PRINT ■
			ELSE
				PRINT □
			ENDIF
		NEXT
	ENDIF
ENDIF
IF 実行キャラ番号 == PLAYER || 対象キャラ番号 == PLAYER
	IF 系統リスト_アイテム系(体位モード) && 体位モード != "肛门拉珠" && 体位モード != "假阳具"
		PRINTFORM 　　%体位モード%强度：
		PRINTBUTTON "[-]", @"MODE詳細設定処理_%体位モード%強さ_{対象キャラ番号}_下降"
		判定数値保存 = GETVAR(@"%体位モード%強さ:{対象キャラ番号}:0")
		FOR LOCAL, 0, 6
			IF 判定数値保存 >= LOCAL
				PRINTBUTTON "■", @"MODE詳細設定処理_%体位モード%強さ_{対象キャラ番号}_{LOCAL}"
			ELSE
				PRINTBUTTON "□", @"MODE詳細設定処理_%体位モード%強さ_{対象キャラ番号}_{LOCAL}"
			ENDIF
		NEXT
		PRINTBUTTON "[+]", @"MODE詳細設定処理_%体位モード%強さ_{対象キャラ番号}_上昇"
	ENDIF
ELSE
	IF 系統リスト_アイテム系(体位モード) && 体位モード != "肛门拉珠" && 体位モード != "假阳具"
		PRINTFORM 　　%体位モード%强度：   
		判定数値保存 = GETVAR(@"%体位モード%強さ:{対象キャラ番号}:0")
		FOR LOCAL, 0, 6
			IF 判定数値保存 >= LOCAL
				PRINT ■
			ELSE
				PRINT □
			ENDIF
		NEXT
	ENDIF
ENDIF


@MODE設定変更処理(入力値)
#DIMS 入力値
#DIMS 分割文字列, 4
#DIM モードID
#DIMS モード名
#DIM 実行キャラ番号
#DIM 対象キャラ番号
#DIM 口上無しフラグ

IF STRCOUNT(入力値, "MODE詳細設定処理_") > 0
	;モードの強さや速さを変動させるボタン
	;_でSPLITすれば分割文字列:1に変動種類、2にキャラ番号、3に変動内容が入る
	SPLIT 入力値, "_", 分割文字列
	IF ISNUMERIC(分割文字列:3)
		SETVAR @"%分割文字列:1%:{TOINT(分割文字列:2)}:0", TOINT(分割文字列:3)
	ELSEIF 分割文字列:3 == "上昇"
		SETVAR @"%分割文字列:1%:{TOINT(分割文字列:2)}:0", MIN(5, GETVAR(@"%分割文字列:1%:{TOINT(分割文字列:2)}:0") + 1)
	ELSEIF 分割文字列:3 == "下降"
		SETVAR @"%分割文字列:1%:{TOINT(分割文字列:2)}:0", MAX(0, GETVAR(@"%分割文字列:1%:{TOINT(分割文字列:2)}:0") - 1)
	ENDIF
	RETURN 1
ELSEIF STRCOUNT(入力値, "モード中断_") > 0
	;モードを中止させるボタン
	;_でSPLITすれば分割文字列:1に中止させるモードの配列が入る
	SPLIT 入力値, "_", 分割文字列
	;削除で行番号がズレることを考慮し先にIDを取得しておく
	モードID = DT_CELL_GET("体位モードデータベース", TOINT(分割文字列:1), "id")
	モード名 '= DT_CELL_GETS("体位モードデータベース", モードID, "モード名", 1)
	実行キャラ番号 = DT_CELL_GET("体位モードデータベース", モードID, "実行キャラ", 1)
	対象キャラ番号 = DT_CELL_GET("体位モードデータベース", モードID, "対象キャラ", 1)
	
	IF モード名 == "ディープキス" && 連れ込みパターン != "" && 対象キャラ番号 == MASTER
		PRINTFORMW %CALLNAME:(実行キャラ番号)%不肯放开…
		RETURN 1
	ENDIF
	
	TFLAG:コマンド条件制御用 = 1
	LOCAL = TARGET
	TARGET = 対象キャラ番号
	RESULT = 1
	SELECTCASE モード名
		CASE "阴蒂夹"
			CALL COM_ABLE42
		CASE "振动棒"
			CALL COM_ABLE44
		CASE "肛门振动棒"
			CALL COM_ABLE45
		CASE "肛门拉珠"
			CALL COM_ABLE46
		CASE "乳头夹"
			CALL COM_ABLE47
	ENDSELECT
	TARGET = LOCAL
	TFLAG:コマンド条件制御用 = 0
	IF !RESULT
		PRINTFORMW 这身衣服脱不下来…
		RETURN 1
	ENDIF
	
	TRYCALLFORM MESSAGE_MODE_%モード名%解除(実行キャラ番号, 対象キャラ番号)
	PRINTL
	口上無しフラグ = 1
	IF 実行キャラ番号 != MASTER
		TRYCCALLFORM KOJO_MODE_%モード名%プレイヤー側解除_{NO:実行キャラ番号}(実行キャラ番号, 対象キャラ番号)
			PRINTL 
			口上無しフラグ = 0
		CATCH
		ENDCATCH
	ENDIF
	IF 対象キャラ番号 != MASTER
		TRYCCALLFORM KOJO_MODE_%モード名%ターゲット側解除_{NO:対象キャラ番号}(実行キャラ番号, 対象キャラ番号)
			PRINTL 
			口上無しフラグ = 0
		CATCH
		ENDCATCH
	ENDIF
	SIF 口上無しフラグ
		TRYCALLFORM MESSAGE_MODE_%モード名%解除_非口上時(実行キャラ番号, 対象キャラ番号)
	WAIT
	CALL MODE_DELETE_FROM_ID(モードID)
	RETURN 1
ELSEIF STRCOUNT(入力値, "モード省略表示") > 0
	OPTION変数:MODE全表示フラグ = 0
	RETURN 1
ELSEIF STRCOUNT(入力値, "モード全表示") > 0
	OPTION変数:MODE全表示フラグ = 1
	RETURN 1
ELSEIF STRCOUNT(入力値, "モード制限表示") > 0
	OPTION変数:MODE全表示フラグ = 2
	RETURN 1
ENDIF

RETURN 0
