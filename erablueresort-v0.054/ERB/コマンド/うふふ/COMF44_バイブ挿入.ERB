;-------------------------------------------------
;振动棒
;道具使用コマンド，レベル2
;-------------------------------------------------
@COMNAME44
#FUNCTIONS
TSTR:コマンド名受渡 = 振动棒插入

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 振动棒


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_44
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系&Ｖ系")
弱点コマンドカテゴリ = V

同時モード許可 = 999


@COM_TOOLTIP_44
LOCALS = <br>■振动棒<br>
LOCALS += "插入电动振动棒刺激阴道的指令。<br>进入振动棒装备状态，在卸除或性行为结束前将持续产生快感。<br>将失去[处女]状态。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ｖ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)・Ｖ经验(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM44
#DIM 強さ初期値記録

;処女
IF TALENT:TARGET:処女
	DRAWLINE
	PRINTL 确认使用振动棒失去处女？
	PRINTBUTTONLC "[0] 确定", 0
	PRINTBUTTONLC "[1] 取消", 1
	PRINTL
	BINPUT
	IF RESULT == 1
		RETURN -1
	ENDIF
ENDIF

IF OPTION変数:振动棒強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■请选择初始强度
	PRINT 　　振动棒强度：
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 装备振动棒", 900
	PRINTBUTTONLC "[999] 返回", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("振动棒", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("振动棒", PLAYER, TARGET, , , OPTION変数:振动棒強さ初期値)
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

;苦痛
LOCAL = 100 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;処女喪失
CALL LOST_VIRGIN(TARGET, PLAYER, 2)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE44
;振动棒実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(44)
	RETURN RESULT
;すでに使ってるとだめ
;SIF MODE_存在判定_ターゲット側("振动棒", TARGET)
;	RETURN 0
;Vがない場合駄目
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧，めくれない服，タイツ，前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:膣 && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM44
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%将
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 振动棒尖端插入全体对象的
	RETURN 0
ENDIF

SIF ABL:Ｖ感覚 >= 4 && TALENT:淫乱
	PRINT 扭动腰肢主动索求
PRINTFORM %CALLNAME:TARGET%的
SIF TEQUIP:下半身下着あり
	PRINTFORM 褪下内衣，
;潤滑LV5以上でさらに追加
SIF PALAM:潤滑 >= PALAMLV:5
	PRINT 充分
;潤滑LV3以上でさらに追加
SIF PALAM:潤滑 >= PALAMLV:3
	PRINT 浸润着粘液的
;小柄体型
IF TALENT:種族 < 0
	PRINT 幼嫩的花蕊
ELSE
	PRINT 秘裂
ENDIF
PRINTL 用振动棒尖端缓缓拓开甬道…

PRINTFORM %CALLNAME:TARGET%
;処女＋淫乱か娼婦
IF TALENT:処女 == 1 && TALENT:淫乱
	PRINT 仿佛忘却自己处女身份般露出媚笑
;処女＋恋慕か親愛
ELSEIF TALENT:処女 == 1 && TALENT:恋慕 && PLAYER == MASTER
	PRINTFORM 用略带寂寞的眼神望向%CALLNAME:PLAYER%
;処女＋淫乱
ELSEIF TALENT:処女 == 1 && TALENT:淫乱
	PRINT 毫不在意处女身份扭腰索求
;処女＋一線越えない
ELSEIF TALENT:処女 == 1 && TALENT:一線越えない
	PRINT 带着放弃抵抗的表情眼中泛起泪光
;処女
ELSEIF TALENT:処女 == 1
	PRINT 因丧失贞洁的恐惧而剧烈颤抖

;Ｖ経験200以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 200 && TALENT:淫乱
	PRINT 仅凭对插入的期待就颤抖着濒临高潮
;Ｖ経験150以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 150 && TALENT:淫乱
	PRINTFORM 用迷离眼神凝视%CALLNAME:PLAYER%并主动摩擦阴唇
;Ｖ経験120以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 120 && TALENT:淫乱
	PRINT 按捺不住地扭动腰肢
;Ｖ経験70以上＋淫乱か娼婦
ELSEIF EXP:Ｖ経験 > 70 && TALENT:淫乱
	PRINTFORM 湿润双眼渴求地望向%CALLNAME:PLAYER%
;Ｖ経験40以上
ELSEIF EXP:Ｖ経験 > 40
	PRINT 沉醉于振动棒摩擦感发出甜腻喘息
;Ｖ経験20以上
ELSEIF EXP:Ｖ経験 > 20
	PRINT 在振动棒刺激下渗出爱液扭动身躯
;それ以外
ELSE
	PRINT 阴道褶皱因振动棒刺激不断痉挛
ENDIF
PRINTL …
PRINTFORM %CALLNAME:TARGET%的
;潤滑LV4以上
SIF PALAM:潤滑 >= PALAMLV:4
	PRINT 湿漉漉的
PRINTFORML 性器被振动棒完全插入

