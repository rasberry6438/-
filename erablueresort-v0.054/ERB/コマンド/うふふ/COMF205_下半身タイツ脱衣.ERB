;-------------------------------------------------
;下半身脱衣
;-------------------------------------------------
@COMNAME205
#FUNCTIONS
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	TSTR:コマンド名受渡 = 撕破紧身衣
ELSE
	TSTR:コマンド名受渡 = 脱掉紧身衣
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_205
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_205
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	LOCALS:1 = 撕破
	LOCALS:2 = 撕破
ELSE
	LOCALS:1 = 脱掉
	LOCALS:2 = 脱下
ENDIF
LOCALS = <br>■%LOCALS:1%紧身衣<br>
LOCALS += @"%LOCALS:2%目标下半身紧身衣的指令。<br>穿着连体紧身衣时同步%LOCALS:1%上半身部分。"
LOCALS += "<br>指令类型：嘿咻<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM205
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
		TEQUIP:下タイツ破れ = 1
	ELSE
		TEQUIP:下タイツあり = 0
		TEQUIP:下タイツ破れ = 0
	ENDIF
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM205(ARG)
#DIM CONST 必要実行値 = 18
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	LOCALS = 破き
ELSE
	LOCALS = 脱衣
ENDIF
PRINTFORML %LOCALS:1%紧身衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 暴露癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 10
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 10})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 害羞(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 厌男(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 炮友(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 5
		PRINT 野外暴露(5)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(18以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE205
;下半身タイツ脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(205)
	RETURN RESULT
;既に着ていない
SIF !TEQUIP:下タイツあり
	RETURN 0
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;鎧がある
SIF TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり
	RETURN 0
;服がある
IF TEQUIP:下半身服あり
	SIF !TEQUIP:スカートめくり可 && !TEQUIP:ずらし可
		RETURN 0
	SIF TEQUIP:下タイツ破れ && !TEQUIP:下半身下着抜き取り可
		RETURN 0
ENDIF
;振动棒以外で膣を使ってるなら無理
SIF TEQUIP:TARGET:膣 && !MODE_存在判定_ターゲット側("振动棒", TARGET)
	RETURN 0
;振动棒とビーズ以外でアナルを使ってるなら無理
SIF TEQUIP:TARGET:アナル && !MODE_存在判定_ターゲット側("肛门振动棒", TARGET) && !MODE_存在判定_ターゲット側("肛门拉珠", TARGET)
	RETURN 0
;ペニスを使ってると駄目
SIF TEQUIP:TARGET:ペニス
	RETURN 0
;足コキ中は駄目
SIF MODE_存在判定_プレイヤー側("足コキ", TARGET)
	RETURN 0
;上下一体服は上半身にまとめてある
SIF TEQUIP:上下一体タイツ && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM205
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	LOCALS = 破いた
	LOCALS:1 = 破かれた
ELSE
	LOCALS = 脱がせた
	LOCALS:1 = 脱いだ
ENDIF
PRINTFORML 将%CALLNAME:TARGET%的紧身衣%LOCALS%…


IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 当众%LOCALS:1%的行为使%CALLNAME:TARGET%显得局促不安…
ENDIF
