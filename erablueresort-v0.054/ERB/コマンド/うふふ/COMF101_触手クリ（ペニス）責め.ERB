;-------------------------------------------------
;触手クリ責め
;触手系コマンド，レベル1
;-------------------------------------------------
@COMNAME101
#FUNCTIONS
IF GETBIT(TALENT:TARGET:性別, 1)
	TSTR:コマンド名受渡 = 触手刺激阴茎
ELSE
	TSTR:コマンド名受渡 = 触手刺激阴蒂
ENDIF


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_101
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので，弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = C
同時モード許可 = 999


@COM_TOOLTIP_101
LOCALS = <br>■触手刺激阴蒂（阴茎）<br>
LOCALS += "召唤专门刺激阴蒂的触手，施加强烈快感的指令。<br>目标具有男性器官时转为刺激阴茎模式"
LOCALS += "<br>指令类型：触手系<br>获取来源：快Ｃ・暴露・接触"
LOCALS += "<br>专属经验值：触手操控经验(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM101
#DIM 快楽基礎値

IF GETBIT(TALENT:TARGET:性別, 1)
	CALL MODE_CHANGE("触手ペニス責め", PLAYER, TARGET)
ELSE
	CALL MODE_CHANGE("触手クリ責め", PLAYER, TARGET)
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 快楽基礎値 * 100)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 101)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 30)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE101
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(101)
	RETURN RESULT
;すでにあると禁止
SIF MODE_存在判定_ターゲット側("触手ペニス責め", TARGET) || MODE_存在判定_ターゲット側("触手クリ責め", TARGET)
	RETURN 0
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;前張りだと不可
SIF TEQUIP:下半身下着あり == 2
	RETURN 0
IF GETBIT(TALENT:TARGET:性別, 1)
	;ペニスを何かに使ってるなら無理
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
ELSE
	;クリトリスを何かに使ってるなら無理
	SIF TEQUIP:TARGET:クリトリス
		RETURN 0
ENDIF
;将来的には許可する予定だけど（ユリウスとか）一旦あなた以外は封印
SIF PLAYER != MASTER
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM101
#DIM 同時キャラ番号

IF GETBIT(TALENT:TARGET:性別, 1)
	LOCALS = ペニス
ELSE
	LOCALS = クリ
ENDIF

PRINTFORM %CALLNAME:PLAYER%

IF GETBIT(FLAG:モード管理, モードビット_同時)
	PRINT 将多条触手
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINTFORM 的私密部位吸附缠绕
	RETURN 0
ENDIF

PRINTFORM 将触手伸向%CALLNAME:TARGET%的
IF TEQUIP:下腹部前鎧あり || TEQUIP:下腹部後鎧あり
	PRINT 从护甲缝隙
	IF TEQUIP:下タイツあり
		PRINT 透过紧身衣
	ENDIF
	PRINT 潜入，
ELSEIF TEQUIP:下半身服あり
	PRINT 隔着衣物缝隙
	IF TEQUIP:下タイツあり
		PRINT 透过紧身衣
	ENDIF
	PRINT 潜入，
ELSEIF TEQUIP:下半身下着あり
	IF TEQUIP:下タイツあり
		PRINT 从下身紧身衣
	ENDIF
	PRINT 潜入，
ENDIF
IF PALAM:欲情 >= PALAMLV:3
	PRINT 傲然挺立的
ENDIF
PRINTFORML %LOCALS%开始吸附刺激



