;-------------------------------------------------
;足コキ
;奉仕系コマンド、レベル4
;-------------------------------------------------

@COMNAME85
#FUNCTIONS
TSTR:コマンド名受渡 = 足交

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_85
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
オートコマンド許可 = 3
反転オート許可 = 3

@COM_TOOLTIP_85
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■足交侍奉<br>
	LOCALS += "用双足爱抚对方阴茎的指令。<br>进入足交模式，在切换模式或结束性行为前将持续触发特定效果。"
ELSE
	LOCALS = <br>■足交<br>
	LOCALS += "要求对方用双足爱抚阴茎的指令。<br>进入足交模式，在切换模式或结束性行为前将持续触发特定效果。"
ENDIF
LOCALS += "<br>指令类型：嘿咻<br>获取来源：征服・暴露・接触・侍奉・不洁"
LOCALS += "<br>专属经验值：嗜虐快感经验(TARGET・特定条件时)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM85
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("足コキ", TARGET, PLAYER)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_征服(TARGET, PLAYER, 350)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 80)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, MIN(ABL:TARGET:技巧 * 50, 200))

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4
;サドは嗜虐快楽
SIF ABL:サド性感
	EXP:嗜虐快楽経験 ++

RETURN 1

@CAN_COM85(ARG)
#DIM CONST 必要実行値 = 22
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 足交(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 侍奉精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 1
	PRINTFORM 精液成瘾LV{ABL:精液中毒}({ABL:精液中毒 * 1})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 3
	PRINT 害羞(3)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 污臭钝感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 1
	PRINT 污臭敏感(1)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 3
	PRINT 否定快感(3)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 8
	PRINT 厌男(8)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 炮友(3)
	＋表示フラグ = 1
ENDIF

;倒錯的
IF TALENT:倒錯的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 4
	PRINT 反常(4)
	＋表示フラグ = 1
ENDIF
;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT 扶她(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(22以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE85
;足コキ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(85)
	RETURN RESULT
;すでに足コキ中は駄目
SIF MODE_存在判定_プレイヤー側("足コキ", TARGET)
	RETURN 0
;ちんこがない場合駄目
SIF !GETBIT(TALENT:PLAYER:性別, 1) || TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;ある程度勃起してないとダメ
SIF BASE:PLAYER:勃起 < 1000
	RETURN 0
;下半身着衣(PLAYER)
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
;ペニスを何かに使ってるなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM85
PRINTFORM %CALLNAME:TARGET%用足底
;恋慕か親愛
SIF TALENT:恋慕 && PLAYER == MASTER
	PRINT 细致地
PRINTFORML 摩擦%CALLNAME:PLAYER%的阴茎…

