;-------------------------------------------------
;飞机杯
;道具使用コマンド，レベル2
;-------------------------------------------------
@COMNAME43
#FUNCTIONS
TSTR:コマンド名受渡 = 飞机杯

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 飞机杯


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_43
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C_ちんこ
同時モード許可 = 999


@COM_TOOLTIP_43
LOCALS = <br>■飞机杯<br>
LOCALS += "为阴茎安装自动振动器具的指令。<br>进入飞机杯装备状态，在卸除或性行为结束前将持续产生快感。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ｃ・暴露・接触"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM43
#DIM 強さ初期値記録

IF OPTION変数:オナホール強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■请选择初始强度
	PRINT 　　飞机杯强度：
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 装备飞机杯", 900
	PRINTBUTTONLC "[999] 返回", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("飞机杯", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("飞机杯", PLAYER, TARGET, , , OPTION変数:オナホール強さ初期値)
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE43
;飞机杯実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(43)
	RETURN RESULT
;使用済みだとだめ
SIF MODE_存在判定_ターゲット側("飞机杯", TARGET)
	RETURN 0
;ちんこがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 1) == 0
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:3 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧，めくれない服，タイツ，下着があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:ペニス
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM43
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 将全体对象的阴茎插入各自的飞机杯
	RETURN 0
ENDIF
PRINTFORML 将%CALLNAME:TARGET%的阴茎插入飞机杯



