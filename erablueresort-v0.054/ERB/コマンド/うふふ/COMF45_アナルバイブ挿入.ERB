;-------------------------------------------------
;肛门振动棒
;道具使用コマンド，レベル2
;-------------------------------------------------
@COMNAME45
#FUNCTIONS
TSTR:コマンド名受渡 = 肛门振动棒插入

;弱点コマンド欄に専用の文字列を表示したい場合，これを設定すること
弱点コマンド欄表示名 = 肛门振动棒



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_45
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系&Ａ系")
弱点コマンドカテゴリ = A
同時モード許可 = 999


@COM_TOOLTIP_45
LOCALS = <br>■肛门振动棒<br>
LOCALS += "插入电动肛门振动棒刺激肠道的指令。<br>进入肛门振动棒装备状态，在卸除或性行为结束前将持续产生快感。"
LOCALS += "<br>指令类型：道具系<br>获取来源：快Ａ・暴露・接触・痛苦"
LOCALS += "<br>专属经验值：道具使用经验(PLAYER)・Ａ经验(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM45
#DIM 強さ初期値記録

IF OPTION変数:肛门振动棒強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■请选择初始强度
	PRINT 　　肛门振动棒强度：
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] 装备肛门振动棒", 900
	PRINTBUTTONLC "[999] 返回", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("肛门振动棒", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("肛门振动棒", PLAYER, TARGET, , , OPTION変数:肛门振动棒強さ初期値)
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

LOCAL = 250 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE45
;肛门振动棒実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(45)
	RETURN RESULT
;すでに使ってるとだめ
;SIF MODE_存在判定_ターゲット側("肛门振动棒", TARGET)
;	RETURN 0
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;アイテムを持っているか，アイテムなし設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;後鎧，めくれない服，タイツがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:アナル && !TFLAG:コマンド条件制御用
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM45
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%将
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 肛门振动棒尖端插入全体对象的
	RETURN 0
ENDIF

PRINTFORM %CALLNAME:TARGET%的
SIF TEQUIP:下半身下着あり
	PRINTFORM 褪下内衣，
PRINTFORML 菊花处抵住肛门振动棒…
PRINTFORM %CALLNAME:TARGET%
;Ａ経験180以上＋淫乱か娼婦
IF EXP:Ａ経験 > 180 && TALENT:淫乱
	PRINT 满脸陶醉地主动掰开臀瓣
;Ａ経験140以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 140 && TALENT:淫乱
	PRINT 焦渴难耐地扭动腰肢
;Ａ経験100以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 100 && TALENT:淫乱
	PRINT 绯红着脸期待地摇晃臀部
;Ａ経験70以上＋淫乱か娼婦
ELSEIF EXP:Ａ経験 > 70 && TALENT:淫乱
	PRINT 因括约肌受到的刺激全身颤抖
;Ａ経験40以上
ELSEIF EXP:Ａ経験 > 40
	PRINT 露出矛盾交织的复杂表情
;Ａ経験20以上
ELSEIF EXP:Ａ経験 > 20
	PRINT 羞耻地低下涨红的脸
;それ以外
ELSE
	PRINT 因不洁的菊穴被侵犯露出屈辱神情
ENDIF
PRINTL …
PRINTFORM %CALLNAME:TARGET%的
;潤滑LV4以上
SIF PALAM:潤滑 >= PALAMLV:4
	PRINT 沾满粘液的
PRINTFORML 肛门被振动棒完全插入

