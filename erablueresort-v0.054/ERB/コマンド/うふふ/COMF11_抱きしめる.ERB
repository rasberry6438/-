;-------------------------------------------------
;深情拥抱
;愛撫系コマンド　レベル2
;-------------------------------------------------
@COMNAME11
#FUNCTIONS
TSTR:コマンド名受渡 = 深情拥抱
SIF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 一起拥抱




;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_11
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
同時モード許可 = 3
同時モード拡張許可 = 0
オートコマンド許可 = 1


@COM_TOOLTIP_11
LOCALS = <br>■深情拥抱<br>
LOCALS += "用力拥抱对方的指令。<br>在V・A插入状态下使用时，提升爱慕好感度。"
LOCALS += "<br>指令类型：嘿咻<br>获取来源：接触・暴露"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM11
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


CALL SOURCE_CALC_露出(TARGET, PLAYER, 80)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 110)

IF MODE_存在判定_順不同("ＶＡ挿入系", PLAYER, TARGET)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 300)
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 5
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 5


RETURN 1

;-------------------------------------------------
;深情拥抱実行判定
;-------------------------------------------------
@COM_ABLE11
#DIMS モード名
#DIM モード番号
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(11)
	RETURN RESULT

;指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;足コキは体勢的に無理
SIF MODE_存在判定_完全一致("足コキ", TARGET, PLAYER)
	RETURN 0
;挿入されてる時は体位による
モード番号 = MODE_確認_完全一致("ＶＡ挿入系", TARGET, PLAYER)
IF モード番号 > -1
	モード名 '= DT_CELL_GETS("体位モードデータベース", モード番号, "モード名")
	;後ろ側系ならダメ
	SIF GROUPMATCH(モード名, "後背位", "寝バック", "屈曲位", "側位", "背面駅弁", "背面座位", "立ちバック")
		RETURN 0
	SIF GROUPMATCH(モード名, "Ａ後背位", "Ａ寝バック", "Ａ屈曲位", "Ａ側位", "Ａ背面駅弁", "Ａ背面座位", "Ａ立ちバック")
		RETURN 0
ENDIF
RETURN 1




;-------------------------------------------------
;深情拥抱時メッセージ
;-------------------------------------------------
@MESSAGE_COM11
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 拉入怀中紧紧拥抱
	RETURN 0
ENDIF

PRINTFORML 将%CALLNAME:TARGET%拉入怀中紧紧拥抱。
