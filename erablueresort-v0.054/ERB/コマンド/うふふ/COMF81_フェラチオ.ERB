;-------------------------------------------------
;フェラチオ
;奉仕系コマンド，レベル2
;-------------------------------------------------
@COMNAME81
#FUNCTIONS

IF MODE_存在判定_完全一致("シックスナイン可能", PLAYER, TARGET) && GETBIT(FLAG:モード管理, モードビット_同時) == 0
	TSTR:コマンド名受渡 = 69式
ELSEIF MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	TSTR:コマンド名受渡 = 乳交口交
ELSE
	TSTR:コマンド名受渡 = 口交
ENDIF
SIF 派生名フラグ
	TSTR:コマンド名受渡 = 口交

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_81
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
同時モード許可 = 3
同時モード拡張許可 = 0
オートコマンド許可 = 3
反転オート許可 = 3


@COM_TOOLTIP_81
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■口交侍奉<br>
	LOCALS += "用口腔侍奉对方阴茎的指令。<br>进入口交模式，在切换模式或结束性行为前将持续触发特定效果。<br>乳交模式下转为[乳交口交]。"
ELSE
	LOCALS = <br>■口交<br>
	LOCALS += "要求对方用口爱抚自己阴茎的指令。<br>进入口交模式，在切换模式或结束性行为前将持续触发特定效果。<br>乳交模式下转为[乳交口交]。"
ENDIF
LOCALS += "<br>若对象持有[舌头灵巧][猫舌]特性，执行者效果将增减。"
LOCALS += "<br>指令类型：嘿咻<br>获取来源：暴露・接触・侍奉・不洁"
LOCALS += "<br>专属经验值：口交经验(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM81
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
SIF !MODE_シックスナイン中_完全一致(PLAYER, TARGET) && MODE_存在判定_完全一致("シックスナイン可能", PLAYER, TARGET) && GETBIT(FLAG:モード管理, モードビット_同時) == 0
	RFLAG:コマンド結果受渡し変数 = 1
IF MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	RFLAG:同時モード体位変化フラグ = 1
	CALL MODE_DELETE_プレイヤー側("パイズリ", TARGET)
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSEIF RFLAG:同時モード体位変化フラグ
	CALL MODE_CHANGE("パイズリフェラ", TARGET, PLAYER)
ELSE
	CALL MODE_CHANGE("フェラチオ", TARGET, PLAYER)
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 120)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 300)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌使いによって，快感のソース増加
IF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
ENDIF

;猫舌では苦痛ちょいある
SIF TALENT:TARGET:猫舌
	CALL SOURCE_CALC_苦痛(PLAYER, TARGET, 20)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
EXP:口淫経験 += 1

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 7
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 7

;愛情経験
SIF TALENT:恋慕
	EXP:愛情経験 += 1

;奴隷が猫舌持ちの場合は，調教者に嗜虐快楽経験を追加
SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "陰茎", "", 1)
IF RESULT
	RCVAR:TARGET:ファーストキス中フラグ = 1
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>向%CALLNAME:PLAYER%的[阴茎]献上初吻</font><img src='えっちハート'>","うふふ")
ENDIF

RETURN 1


@CAN_COM81(ARG)
#DIM CONST 必要実行値 = 24
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 口交(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 侍奉精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 3
	PRINTFORM 精液成瘾LV{ABL:精液中毒}({ABL:精液中毒 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 害羞(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 污臭钝感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 污臭敏感(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱，娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 否定快感(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕，親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 厌男(12)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 挚爱(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT 扶她(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(24以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE81
;フェラチオ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE(81)
	RETURN RESULT
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;下半身着衣
;前鎧，タイツ，前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
;猫舌の奴隷の場合は技巧Lv2以上必要
SIF TALENT:猫舌 && ABL:技巧 < 2
	RETURN 0
;ボールギャグ使用中は不可
; SIF TEQUIP:ボールギャグ
; 	RETURN 0
IF !MODE_存在判定_完全一致("パイズリ", TARGET, PLAYER)
	;ペニスを何かに使ってるなら無理
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
ENDIF
;口を何かに使ってるなら無理
SIF TEQUIP:TARGET:口
	RETURN 0

;オートコマンド中の制限
IF FLAG:オートコマンド判定中フラグ
	SIF 初体験済みチェック("ファーストキス", PLAYER) == 0
		RETURN 0
ENDIF

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM81
#DIM 同時キャラ番号
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT ，
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
		PRINTFORML 们挤压着乳房同时用舌头舔舐%CALLNAME:PLAYER%的阴茎…
	ELSE
		PRINTFORML 们分工协作地舔弄着%CALLNAME:PLAYER%的阴茎…
	ENDIF
	RETURN 0
ENDIF

;VARSET LOCAL

;;お掃除
;IF LOCAL && (TCVAR:LOCAL:103 == PLAYER || TCVAR:LOCAL:105 == PLAYER)
;		PRINTFORML %CALLNAME:LOCAL%の中にたっぷりと射精してからペニスを抜き，
;		PRINTFORML ペニスに付着した精液を%CALLNAME:TARGET%に舐め取らせた…
;		SIF TFLAG:8
;			PRINTFORML %CALLNAME:TARGET%尿道に残った精液まで残らずきれいにしてから\@ ABL:精液中毒 > 2 ? 名残を惜しむかのように # \@ペニスから唇を離した…
;ELSEIF NOWEX:MASTER:11
;	IF LOCAL == TARGET
;		PRINTFORML 射精の直前に\@ MASTER_POSE(6,1,1) ? ヴァギナ # アナル \@からペニスを抜き%CALLNAME:TARGET%に咥えさせた…
;	ELSEIF LOCAL
;		PRINTFORML 射精の直前に%CALLNAME:LOCAL%からペニスを抜き%CALLNAME:TARGET%に咥えさせた…
;	ELSE
;		PRINTFORML 射精寸前のペニスを%CALLNAME:TARGET%に咥えさせた…
;	ENDIF
;	IF ABL:技巧 > 2
;		IF ABL:精液中毒 > 2
;			PRINTFORML %CALLNAME:TARGET%唇で亀頭を包んでから高速で舌を動かし%CALLNAME:PLAYER%に射精を促している…
;		ELSE
;			PRINTFORML %CALLNAME:TARGET%ペニスの敏感な部分に舌をはわせ%CALLNAME:PLAYER%に射精を促している…
;		ENDIF
;	ELSE
;		PRINTFORML %CALLNAME:TARGET%%CALLNAME:PLAYER%の反応を伺いながら懸命に舌を動かしている…
;	ENDIF
;ELSE

IF MODE_存在判定_完全一致("パイズリフェラ", TARGET, PLAYER)
	PRINTFORM %CALLNAME:TARGET%
	;恋慕か親愛
	SIF TALENT:恋慕 && PLAYER == MASTER
		PRINT 带着宠溺的神情
	;淫乱か娼婦
	SIF TALENT:淫乱
		PRINT 主动
	PRINTFORML 含住了正在乳交的%CALLNAME:PLAYER%的阴茎…
ELSE
	PRINTFORM %CALLNAME:TARGET%
	;69式に派生
	SIF RFLAG:コマンド結果受渡し変数
		PRINTFORM 调整体位与%CALLNAME:PLAYER%头部交错，
	;恋慕か親愛
	SIF TALENT:恋慕 && PLAYER == MASTER
		PRINT 带着宠溺的神情
	;淫乱か娼婦
	SIF TALENT:淫乱
		PRINT 主动
	;69式体勢中
	SIF !RFLAG:コマンド結果受渡し変数 && MODE_シックスナイン中_完全一致(TARGET, PLAYER)
		PRINT 对着眼前的
	PRINTFORML %CALLNAME:PLAYER%的阴茎细细舔弄着…
ENDIF
;ENDIF

