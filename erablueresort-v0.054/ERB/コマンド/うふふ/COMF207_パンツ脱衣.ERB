;-------------------------------------------------
;パンツ脱衣
;-------------------------------------------------
@COMNAME207
#FUNCTIONS
IF TEQUIP:下半身下着あり == 2
	TSTR:コマンド名受渡 = 撕掉阴贴
ELSEIF TEQUIP:下半身下着あり == 3
	TSTR:コマンド名受渡 = 脱掉兜裆布
ELSE
	TSTR:コマンド名受渡 = 脱掉内裤
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_207
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_207
LOCALS = <br>■脱掉内裤<br>
LOCALS += "脱去目标下半身内衣的指令。<br>当内衣为阴贴时转为[撕掉阴贴]指令。<br>当内衣为兜裆布时转为[脱掉兜裆布]指令。<br>若存在可撕掉标记，即使穿着下装也可执行。"
LOCALS += "<br>指令类型：嘿咻<br>获取来源：无"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM207
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:下半身下着あり = 0
ENDIF

;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM207(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 脱去下半身内衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 暴露癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快乐刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 情欲LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 害羞(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 厌男(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 爱慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT 炮友(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 10
		PRINT 野外暴露(10)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT 绝对服从(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 执行值{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE207
;下半身下着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(207)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:下半身下着あり
	RETURN 0
;服がある
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	;前張りならめくりorずらし可でOK
	SIF TEQUIP:下半身下着あり != 2 || (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
		RETURN 0
ENDIF
;タイツがある
SIF TEQUIP:下タイツあり
	RETURN 0
;振动棒以外で膣を使ってるなら無理
SIF TEQUIP:TARGET:膣 && !MODE_存在判定_ターゲット側("振动棒", TARGET)
	RETURN 0
;前張り以外
IF TEQUIP:下半身下着あり != 2
	;振动棒とビーズ以外でアナルを使ってるなら無理
	SIF TEQUIP:TARGET:アナル && !MODE_存在判定_ターゲット側("肛门振动棒", TARGET) && !MODE_存在判定_ターゲット側("肛门拉珠", TARGET)
		RETURN 0
	;ペニスを使ってると駄目
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
	;足コキ中は駄目
	SIF MODE_存在判定_プレイヤー側("足コキ", TARGET)
		RETURN 0
ENDIF

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM207
IF TEQUIP:下半身下着あり == 2
	PRINTFORML 撕掉了%CALLNAME:TARGET%的阴贴…
ELSEIF TEQUIP:下半身下着あり == 3
	PRINTFORML 脱掉了%CALLNAME:TARGET%的兜裆布…
ELSE
	PRINTFORML 脱掉了%CALLNAME:TARGET%的内裤…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 在他人注视下脱衣的行为让%CALLNAME:TARGET%显得心神不宁…
ENDIF

