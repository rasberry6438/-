;-------------------------------------------------
;性服务协商
;-------------------------------------------------
@COMNAME315
#FUNCTIONS
IF TALENT:従業員 == 0
;なんとなくつけてみた
	IF TALENT:PLAYER:性別 == 2
		TSTR:コマンド名受渡 = 爸爸活价格协商
	ELSE
		TSTR:コマンド名受渡 = 妈妈活价格协商
	ENDIF
ELSE
	TSTR:コマンド名受渡 = 性服务协商
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_315
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_315
LOCALS = <br>■性服务协商<br>
LOCALS += "与员工协商提供性服务的指令。<br>"
LOCALS += "请求内容将影响成功率和所需报酬。<br>"
LOCALS += "<br>指令类型：日常<br>获取来源：根据选项变化"
LOCALS += "<br>专属经验值：根据选项变化<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM315
#DIMS CONST 選択先順番制御 = "手交服务", "乳交服务", "口交服务", "私处爱抚服务", "舔阴服务"
#DIM 選択先保存
#DIMS DYNAMIC 性処理選択肢, 100
#DIM DYNAMIC 性処理数


DRAWLINE
TRYCALLFORM KOJO_COM_{NO:TARGET}_315_BEFORE(TARGET)
PRINTFORML 想向%CALLNAME:TARGET%请求什么性服务？
PRINTL

FOR LOCAL, 0, VARSIZE("選択先順番制御")
	;固有条件
	IF GETMETH(@"固有COMABLE_ターゲット側_%選択先順番制御:LOCAL%_{NO:TARGET}", 1, TARGET) == 0
		CONTINUE
	ENDIF
	IF GETMETH(@"固有COMABLE_プレイヤー側_%選択先順番制御:LOCAL%_{NO:PLAYER}", 1, PLAYER) == 0
		CONTINUE
	ENDIF
	CALLFORM 性処理_ABLE_%選択先順番制御:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<button value='{LOCAL}' title='%TSTR:ツールチップ受渡%'>[{LOCAL}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL}] %TSTR:コマンド名受渡%", LOCAL
			PRINTL 
		ENDIF
	ENDIF
NEXT

VARSET RESULTS
VARSET 性処理選択肢
ENUMFUNCBEGINSWITH "性処理_ABLE_"
性処理数 = RESULT
ARRAYCOPY "RESULTS", "性処理選択肢"
FOR LOCAL, 0, 性処理数
	性処理選択肢:LOCAL = %REPLACE(性処理選択肢:LOCAL, "性処理_ABLE_", "")%
	SIF MATCH(選択先順番制御, 性処理選択肢:LOCAL)
		CONTINUE
	;固有条件
	IF GETMETH(@"固有COMABLE_ターゲット側_%RESULTS:LOCAL%_{NO:TARGET}", 1, TARGET) == 0
		CONTINUE
	ENDIF
	IF GETMETH(@"固有COMABLE_プレイヤー側_%RESULTS:LOCAL%_{NO:PLAYER}", 1, PLAYER) == 0
		CONTINUE
	ENDIF
	CALLFORM %RESULTS:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL + 100}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<button value='{LOCAL + 100}' title='%TSTR:ツールチップ受渡%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL + 100}] %TSTR:コマンド名受渡%", LOCAL + 100
			PRINTL 
		ENDIF
	ENDIF
NEXT


PRINTL
PRINTBUTTON "[999] 返回", 999
PRINTL

DRAWLINE
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN -1
	CASE IS < 100
		CALL 性服务协商処理(TARGET, 選択先順番制御:LOCAL)
		SIF RESULT == -1
			RESTART
			
		IF STRFIND(コマンド初回フラグ:TARGET:0, @"-WORKING_%選択先順番制御:LOCAL%-") < 0
			コマンド初回フラグ_一時保存:TARGET:0 += @"-WORKING_%選択先順番制御:LOCAL%-"
		ENDIF
	CASEELSE
		CALL 性服务协商処理(TARGET, 性処理選択肢:(LOCAL - 100))
		SIF RESULT == -1
			RESTART
		
		IF STRFIND(コマンド初回フラグ:TARGET:0, @"-WORKING_%性処理選択肢:(LOCAL - 100)%-") < 0
			コマンド初回フラグ_一時保存:TARGET:0 += @"-WORKING_%性処理選択肢:(LOCAL - 100)%-"
		ENDIF
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE315

;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(315)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;従業員もしくルピえっち交渉術必須
SIF TALENT:従業員 == 0 && あなた特殊能力:ルピえっち交渉術 != 1
	RETURN 0
;他人とデート中はダメ
SIF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100 && TCVAR:TARGET:デートちょっかい値 <= 0
	RETURN 0

RETURN 1


;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM315(対象キャラ, 実行性処理)
#DIM 対象キャラ
#DIMS 実行性処理

PRINTFORM %CALLNAME:PLAYER%向%CALLNAME:TARGET%提出了

IF RSTR:コマンド結果受渡し文字列 == "手交服务"
	PRINTFORML %TEXTR("进行手交/用手撸管/用手放松/用手打飞机/用手做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "乳交服务"
	PRINTFORML %TEXTR("使用胸部舒服一下/希望进行乳交服务/在此实施乳交/用胸部做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "口交服务"
	PRINTFORML %TEXTR("用嘴巴舒服一下/希望进行口交服务/因性欲高涨寻求口交服务/用嘴做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "私处爱抚服务"
	PRINTFORML %TEXTR("请求手抠阴户/让私处舒服一下/用手抚摸私处/用手做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "舔阴服务"
	PRINTFORML %TEXTR("要求用舌头让私处舒服一下/在此实施舔阴服务/用舌头做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "セックス処理"
	PRINTFORML %TEXTR("用阴道舒服一下/希望使用阴道/要求插入阴道/用阴道做『那种事情』/借用小穴来自慰/请求把精液射入子宫")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "被セックス処理"
	PRINTFORML %TEXTR("用肉棒摩擦阴部/充当活体假阳具/作为活体振动棒使用/用阴茎做『那种事情』")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "アナルセックス処理"
	PRINTFORML %TEXTR("用肛门舒服一下/用肛门来自慰/用肛门做『那种事情』/把精液射进你的屁眼里")%的诉求。
ELSEIF RSTR:コマンド結果受渡し文字列 == "被アナルセックス処理"
	PRINTFORML %TEXTR("用肉棒插入肛门/充当活体假阳具/作为活体肛门振动棒使用/用肛门做『那种事情』")%的诉求。
ELSE
	;あとから増えた時用に一応おいとく
	PRINTFORML 请求换上%RSTR:コマンド結果受渡し文字列%装扮。
ENDIF
CALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%


@MESSAGE_COM315_非口上時

TRYCALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%_非口上時



@性服务协商処理(対象キャラ, 実行性処理)
#DIM 対象キャラ
#DIM 値段計算
#DIM 余裕金額
#DIMS 実行性処理

CALLFORM CAN_性処理_%実行性処理%(対象キャラ)
PRINTL

IF RESULT == -1
	値段計算 = 0
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	値段計算 = RESULT
ENDIF

;地の文もオプションが勝手につく仕様にしたので、すまんがハードルを上げさせてもらう。
;高貴処女とかの相手を考えるとこれでも多分借金まで到達する可能性がある。
PRINTFORML 持有卢比：%MON_FORMAT(MONEY)%
IF 実行性処理 == "セックス処理"
	余裕金額 = 値段計算 + 30000 + 性処理_セックス処理_中出し価格(対象キャラ) 
	IF 知識素質:対象キャラ:高貴 > 0
		IF 初体験済みチェック("ファーストキス", 対象キャラ) == 0
			余裕金額 += 100000
		ENDIF
	ENDIF
ELSE
	余裕金額 = 値段計算 + 20000
ENDIF
IF MONEY < 余裕金額
	PRINTL 考虑到附加选项，建议准备充足资金。
	PRINTFORMW 手头最好预留大约%MON_FORMAT(余裕金額)%左右的资金……
	RETURN -1
ENDIF

PRINTL
PRINTFORML 是否确认向%CALLNAME:対象キャラ%申请%実行性処理%服务？
PRINTBUTTON "[0] 确定", 0
PRINTL
PRINTBUTTON "[1] 取消", 1
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 0
		RSTR:コマンド結果受渡し文字列 = %実行性処理%
		RFLAG:コマンド結果受渡し変数２ = 値段計算
		MONEY -= 値段計算
		CALLFORM 性処理_%実行性処理%()
		RETURN RESULT
	CASE 1
		RETURN -1
ENDSELECT


@補正後好感度算出(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後好感度

補正後好感度 = CFLAG:対象キャラ:好感度レベル
補正後好感度 += CFLAG:対象キャラ:恋慕レベル / 3

補正後好感度 -= TALENT:対象キャラ:自制心 * 7
補正後好感度 -= TALENT:対象キャラ:一線越えない * 10
補正後好感度 -= TALENT:対象キャラ:貞操 * 10
補正後好感度 -= TALENT:対象キャラ:羞恥心 * 5
補正後好感度 -= TALENT:対象キャラ:抵抗 * 15
補正後好感度 -= TALENT:対象キャラ:態度 * 5
SIF GETBIT(TALENT:対象キャラ:陥落不可, 1)
	補正後好感度 -= 20
SIF (TALENT:対象キャラ:恋人持ち == 1 || TALENT:対象キャラ:恋人持ち == 2) && EXP:対象キャラ:売春経験 < 10
	補正後好感度 -= 15

IF 同性愛性癖判定(対象キャラ, PLAYER)
	SELECTCASE 性癖素質:対象キャラ:同性愛
		CASE 1
			;補正なし
		CASE 2
			補正後好感度 -= 5
		CASE 3
			補正後好感度 -= 15
		CASEELSE
			補正後好感度 += 15
	ENDSELECT
ENDIF

SELECTCASE TALENT:対象キャラ:性別嗜好
	CASE 1
		SIF TALENT:PLAYER:性別 == 2
			補正後好感度 += 20
	CASE 2
		SIF TALENT:PLAYER:性別 == 1
			補正後好感度 += 20
	CASE 3
		補正後好感度 += 20
ENDSELECT

補正後好感度 += TALENT:対象キャラ:性的興味 * 5
補正後好感度 += TALENT:対象キャラ:経験豊富 * 5
補正後好感度 += TALENT:対象キャラ:ギャル * 5
補正後好感度 += TALENT:対象キャラ:弱味 * 20
補正後好感度 += EXP:対象キャラ:売春経験 / 10 * 5
SIF TALENT:対象キャラ:マイペース < 0
	補正後好感度 += 10
SIF 初体験日取得("初うふふ", 対象キャラ, MASTER) || フィールド展開:无视嘿咻行为
	補正後好感度 += 10
SIF TALENT:対象キャラ:恋人持ち == 3 && TALENT:対象キャラ:子持ち
	補正後好感度 += 10

SIF TALENT:対象キャラ:セフレ
	補正後好感度 = 999

RETURNF 補正後好感度


@経過時間算出(行為回数)
#FUNCTION
#DIM CONST 減衰閾値 = 24
#DIM 行為回数

IF 行為回数 <= 減衰閾値
	;2時間までは線形
	RETURNF 行為回数 * 5
ELSE
	;以降は減衰
	RETURNF (減衰閾値 + LOG(行為回数 - 減衰閾値)) * 5
ENDIF
