;-------------------------------------------------
;舔阴服务
;-------------------------------------------------
@性処理NAME_舔阴服务
#FUNCTIONS
TSTR:コマンド名受渡 = 舔阴服务

@性処理_TOOLTIP_舔阴服务
LOCALS = <br>■舔阴服务<br>
LOCALS += @"%CALLNAME:PLAYER%申请进行舔舐阴道口和阴蒂的性服务。<br>"
LOCALS += "若存在污臭敏感或洁癖特质时，所需好感度与费用将大幅提升。<br>"
LOCALS += "<br>指令类型：性处理<br>获取来源：暴露・接触・侍奉・不洁"
LOCALS += "<br>专属经验值：口交经验(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@性処理_舔阴服务
#DIM 行為回数

;射精するまで刺激する
行為回数 = 0
DO
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 300)
SIF TALENT:TARGET:舌使い
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 150)
SIF TALENT:TARGET:猫舌
	CALL SOURCE_CALC_苦痛(PLAYER, TARGET, 20)
	行為回数++
LOOP CUP:PLAYER:快Ｃ + PALAM:PLAYER:快Ｃ < 10000
TIME += 経過時間算出(行為回数)

CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 120)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 300)
CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
EXP:口淫経験 += 3

;同性愛
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 8
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 8

;履歴登録はMESSAGEに移動
CALL 初体験日登録処理(TARGET, PLAYER, "ファーストキス", DAY * 1440 + TIME, GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置), "クリトリス", "", 1)
IF RESULT
	RFLAG:コマンド結果受渡し変数３ = 1
	RCVAR:TARGET:ファーストキス中フラグ = 1
ENDIF

;売春経験がない相手に交渉を成立させた場合、あなたの買春カウントが進む
;これにより「あなたが何人買ったか」の記録とする。
;最初から売春経験を持ってるキャラに関しては……知らん！！！
SIF EXP:TARGET:売春経験 == 0
	EXP:PLAYER:買春経験 += 1

EXP:TARGET:売春経験 += 1


RETURN 1


@性処理_射精時処理_舔阴服务
;射精時にどこに出すかを呼び出す
;とはいってもフィニッシュ場所を入れて関数呼ぶだけ
CALL 射精実処理(PLAYER, TARGET, 5)
RETURN 1


;-------------------------------------------------
;値段設定
;-------------------------------------------------
@CAN_性処理_舔阴服务(対象キャラ)
#DIM 対象キャラ
#DIM 値段計算
#DIMS 素質名

PRINTFORML 舔阴服务计算(%CALLNAME:TARGET%)
DRAWLINE

;基礎値段
IF NO:TARGET > 1000
;EXキャラ
;元いた世界でのカラダの価値はわかっても、空の世界での売春相場を知らない。おまけにルピもたいして持ってないのでまとまったお金がありがたい。
;現実の一般JKが空の世界に来て1万円のつもりで1000ルピでカラダを売るイメージ。総じてリゾート内の価格帯から見ると格安。
;……グラブルキャラたちの基本料金が高いだけ？それはそう。
	SELECTCASE RAND:2
	CASE 0
	;15000円のつもり。これでもちょっと吹っ掛けたつもりで強気の値段交渉してる。
		値段計算 = 1500
	CASE 1
	;5000円のつもり。これでも相場よりちょい高めのつもりで値段交渉してる。
		値段計算 = 1000
	ENDSELECT
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSEIF TALENT:幼稚
	;幼稚
	;性知識うんぬん以前に、そもそもお金の価値に対する理解が足りていない。
	;自分のカラダの価値も理解していないので、「お菓子いっぱいあげるから」とかでも許しちゃったりするイメージ。でも値段表示する仕様上お菓子たくさんとかでは買えない。
	;本人は精一杯吹っ掛けているつもりで「じゅーまんえんくれるならいいよ」って言っても、他キャラの相場から見れば格安だったりする。
	;でもちょいちょい「ひゃくまんえん！」とか言いはじめる。
	SELECTCASE RAND:10
	;４割で超高額提示
	CASE 0
		PRINTFORML %"基础费用", 25, LEFT%：　十万卢比！
		値段計算 = 100000
	CASE 1 TO 3
		PRINTFORML %"基础费用", 25, LEFT%：　一万卢比！
		値段計算 = 10000
	CASE 4 TO 8
		PRINTFORML %"基础费用", 25, LEFT%：　一千卢比！
		値段計算 = 1000
	CASE 9
		PRINTFORML %"基础费用", 25, LEFT%：　一百卢比就可以了！
		値段計算 = 100
	ENDSELECT
ELSEIF 知識素質:高貴 > 0
;高貴
;基本的に実家が太い子らなので、そもそも目先の金銭のために体を売っているわけではない。
;お金が欲しいからではなくあなたが頼むから、あなたに喜んでんでほしいから売ってくれているのである。つまり純愛。
	IF 知識素質:性知識 < 0
	;性知識がなくとも、自身のカラダは民草のそれよりも価値があるということを知っている。
	;……のだが、相場知識がないため結局吹っ掛けたつもりでも高貴キャラとしてはお安めの価格設定
		値段計算 = 5000
	ELSE
	;王侯貴族に連なるような高貴なおくちを10万円で堪能できると考えるとこれでも結構リーズナブル。良心的な価格設定でございます。
		値段計算 = 10000
	ENDIF
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSEIF 知識素質:性知識 < 0
;性知識マイナス勢
;お金の価値が分からないわけではないが、カラダの価値の相場を知らないので直感的にいま欲しい金額を言ってくる。
;幼稚ほどブッ飛んだ値段は提示してこないが結局値段はブレる。
	SELECTCASE RAND:4
	;４割で高額提示
	CASE 0
		値段計算 = 10000
	CASE 1
		値段計算 = 5000
	CASE 2
		値段計算 = 3000
	CASE 3
		値段計算 = 2000
	CASE 4
		値段計算 = 1000
	ENDSELECT
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSEIF TALENT:非戦闘キャラ
;非戦闘キャラは、「戦うことで稼げる」キャラより相対的に肉体の価値が安く、「カラダを売って稼ぐ」ことへの抵抗感が戦闘キャラより薄いイメージ
	値段計算 = 2000
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSEIF TALENT:性転換 == 1
;元女だと嫌がって値段を高めに設定する(でも結局買われてしゃぶらされてしまう)
	値段計算 = 5000
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSE
;「基本的にみんな美女である」「普段売っていないので市場価値が高い」「戦闘能力のあるキャラは相応のプライドというものがある」などの理由から
;補正抜きで3000ルピ≒3万円とする。ここに素質で補正がかかり、地の文や口上でのプレイ内容に応じてオプションが上乗せされていく。
;さすがにフェラで30万円は高すぎるんよ……3万ですらかなり高いのに
	値段計算 = 3000
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ENDIF

;好感度
;好感度閾値の表現からして、親友あたりからお友達料金にしてくれるんじゃないかなと
LOCAL = (50 - CFLAG:好感度レベル) * -10
IF LOCAL < 0
	IF CFLAG:好感度レベル <= 20
	;友達未満だと大きく値上げ。初対面で2万円上乗せされるイメージ。普段身売りしてない美女に値段交渉するんだからそんくらい吹っ掛けてきてもおかしくないと思う。なお従業員採用に必要な好感度……
		PRINTFORML %"关系尚浅", 25, LEFT%：＋%NUM_FORMAT(LOCAL * -4), 8%卢比
		値段計算 -= LOCAL * 4
	ELSE
	;ただの友達だと若干の値上げ。値上げっていうかここまで含めて基本料金みたいなとこがある
		PRINTFORML %"普通朋友", 25, LEFT%：＋%NUM_FORMAT(LOCAL * -1), 8%卢比
		値段計算 -= LOCAL
	ENDIF
ELSE
	;親友以上で値下げしてくれる。基本料金3万円で、好感度100で500ルピ≒5000円引きならまぁ妥当っちゃ妥当か
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %"友情折扣", 25, LEFT%：－%NUM_FORMAT(LOCAL), 8%卢比
	RESETCOLOR
	値段計算 -= LOCAL
ENDIF

;容姿
IF TALENT:容姿 == -1
;あなたがブサイクの場合。あえてつけてる素質のはずだけど、ストレートに「醜い」だの「ブサイク」だの書くとプレイヤーの心にトゲが刺さりそうなため表現はマイルドに
;ブサイクに対する奉仕行為はプライドが傷つくだろうから値段高め
	値段計算 += 1000
	PRINTFORML %"长相不合胃口", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
ENDIF
IF TALENT:容姿 == 1
;イケメン
	値段計算 -= 100
	PRINTFORML %"长相不错", 25, LEFT%：－%NUM_FORMAT(100), 8%卢比
ENDIF
IF TALENT:容姿 == 2
;絶世。
	値段計算 -= 200
	PRINTFORML %"长相超正！", 25, LEFT%：－%NUM_FORMAT(200), 8%卢比
ENDIF

;体力
;疲れてる時に顎とか疲れそうな奉仕を頼まれたら当然吹っ掛ける
IF BASE:TARGET:体力 < MAXBASE:TARGET:体力 / 4
	値段計算 += 3000
	SETCOLOR カラーパレット("薄ピンク")
	PRINTFORML %"体力低下", 25, LEFT%：＋%NUM_FORMAT(3000), 8%卢比
	RESETCOLOR
ENDIF

;酩酊は値段ではなくむしろ実行判定にかかわったほうがいい気がするので移動。
;「安く売ってしまう」よりも、「ね、いいでしょ？」ってあなたのお願いに頷いてしまうほうがイメージに近い。

;ファーストキス
;使い切りの付加価値なので基本料金よりも値が張る
IF 初体験済みチェック("ファーストキス", TARGET) == 0

	IF NO:TARGET > 1000
	;空の相場を知らずに安売りしてしまう哀れなEX娘たち。鮫トレ最高！
		値段計算 += 1000
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"初吻", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
		RESETCOLOR
	ELSEIF 知識素質:高貴 > 0
	;高貴
	;ロイヤルキッス100まんえん
		値段計算 += 100000
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"高贵纯洁的嘴唇", 25, LEFT%：＋%NUM_FORMAT(100000), 8%卢比
		RESETCOLOR
	ELSEIF TALENT:プライド > 0
	;プライド高い
	;基本料金と合わせて10万円になる計算
		値段計算 += 7000
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"初吻", 25, LEFT%：＋%NUM_FORMAT(7000), 8%卢比
		RESETCOLOR
	ELSEIF TALENT:プライド < 0
	;プライド低い
	;自己評価が低いため、基本料金の倍額貰えればいいやと思っている
		値段計算 += 3000
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"初吻", 25, LEFT%：＋%NUM_FORMAT(3000), 8%卢比
		RESETCOLOR
	ELSE
		値段計算 += 4000
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"初吻", 25, LEFT%：＋%NUM_FORMAT(4000), 8%卢比
		RESETCOLOR
	ENDIF
ENDIF

;恋人持ち
IF TALENT:恋人持ち == 2
	値段計算 += 1500
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORML %"已有配偶", 25, LEFT%：＋%NUM_FORMAT(1500), 8%卢比
	RESETCOLOR
ENDIF
IF TALENT:恋人持ち == 1
	値段計算 += 1000
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORML %"已有恋人", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
	RESETCOLOR
ENDIF
IF TALENT:恋人持ち == -1
;別に恋人になってるワケでもないのに「好きな人がいるから」ってだけで生意気にも値段を釣り上げてくるし、上昇幅は恋人持ちと同じ。
;「ゆ、許せねぇよ……絶対買って俺のものにしてやる……！」
;＊ あなたにケツイがみなぎった。
	値段計算 += 1000
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %"心有所属", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
	RESETCOLOR
ENDIF
;淫乱
IF TALENT:淫乱
	値段計算 -= 2500
	PRINTFORML %"淫乱", 16, LEFT%：－%NUM_FORMAT(2500), 8%卢比
ENDIF

;同性愛
IF 同性愛性癖判定(TARGET,PLAYER) == 1
	;性癖素質:同性愛
	IF TALENT:TARGET:性愛傾向 == 1 ||TALENT:TARGET:性愛傾向 == 2
		素質名 = 異性愛
	ELSE
		素質名 = 同性愛
	ENDIF
	SELECTCASE 性癖素質:同性愛
		CASE 1
			;補正なし
		CASE 2
			値段計算 -= 100
			PRINTFORML %@"%素質名%(喜好)", 25, LEFT%：－%NUM_FORMAT(100), 8%卢比
		CASE 3
			値段計算 -= 250
			PRINTFORML %@"%素質名%(成瘾)", 25, LEFT%：－%NUM_FORMAT(250), 8%卢比
		CASEELSE
			;好奇心
			IF TALENT:性的興味 > 0
				値段計算 -= 50
				PRINTFORML %@"好奇(%素質名%)", 25, LEFT%：－%NUM_FORMAT(50), 8%卢比
			ENDIF
			;保守的
			IF TALENT:性的興味 < 0
				値段計算 += 150
				SETCOLOR カラーパレット("薄ピンク")
				PRINTFORML %@"保守(%素質名%)", 25, LEFT%：＋%NUM_FORMAT(150), 8%卢比
				RESETCOLOR
			ENDIF
	ENDSELECT
ENDIF


;気丈
;「物怖じせず値段交渉に臨む」とするなら基本料金の一割増しくらいで吹っ掛けそう
IF TALENT:度胸 > 0
	値段計算 += 300
	SETCOLOR カラーパレット("薄ピンク")
	PRINTFORML %"刚毅", 25, LEFT%：＋%NUM_FORMAT(300), 8%卢比
	RESETCOLOR
ENDIF
;臆病
;気丈の逆で「高値を吹っ掛けて値段交渉するのが苦手」とするならば一割くらいまけてくれないかな……
IF TALENT:度胸 < 0
	値段計算 -= 300
	PRINTFORML %"怯懦", 25, LEFT%：－%NUM_FORMAT(300), 8%卢比
ENDIF

;プライド
;処女のとこでも参照しているが、こちらは基本料金にかかるので別枠。
;プライド高い
IF TALENT:プライド > 0
	値段計算 += 300
	SETCOLOR カラーパレット("薄ピンク")
	PRINTFORML %"高自尊", 25, LEFT%：＋%NUM_FORMAT(300), 8%卢比
	RESETCOLOR
ENDIF
;プライド低い
IF TALENT:プライド < 0
	値段計算 -= 150
	PRINTFORML %"低自尊", 25, LEFT%：－%NUM_FORMAT(150), 8%卢比
ENDIF

;謎の魅力
IF TALENT:PLAYER:謎の魅力
	値段計算 -= 100
	PRINTFORML %"迷之魅力", 25, LEFT%：－%NUM_FORMAT(100), 8%卢比
ENDIF

;ABL:奉仕精神
;行為が奉仕そのものであるため金額に対する奉仕精神の影響がでかい
IF ABL:奉仕精神
	値段計算 -= ABL:奉仕精神 * 200
	PRINTFORML %"侍奉精神", 25, LEFT%：－%NUM_FORMAT(ABL:奉仕精神 * 200), 8%卢比
ENDIF

;汚臭鈍感
;鈍感だろうと奉仕内容自体に変わりはないので大した値引きにはならない
IF TALENT:汚臭耐性 > 0
	値段計算 -= TALENT:汚臭耐性 * 50
	PRINTFORML %GET_TALENTNAME(61, TALENT:汚臭耐性,,TARGET), 25, LEFT%：－%NUM_FORMAT(TALENT:汚臭耐性 * 50), 8%卢比
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	値段計算 -= TALENT:汚臭耐性 * 200
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %GET_TALENTNAME(61, TALENT:汚臭耐性,,TARGET), 25, LEFT%：＋%NUM_FORMAT(ABS(TALENT:汚臭耐性) * 200), 8%卢比
	RESETCOLOR
ENDIF

;献身的
IF TALENT:献身的
	値段計算 -= 1000
	PRINTFORML %"献身", 25, LEFT%：－%NUM_FORMAT(1000), 8%卢比
ENDIF

;貞操観念による売春への抵抗感。これは値段高めのほうが「らしさ」が出る
IF TALENT:貞操 == 1
	値段計算 += 3000
	SETCOLOR カラーパレット("ピンク")
	PRINTFORML %"贞操观念", 25, LEFT%：＋%NUM_FORMAT(3000), 8%卢比
	RESETCOLOR
ENDIF

;女嫌い
IF TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1
	値段計算 += 2000
	SETCOLOR カラーパレット("薄ピンク")
	PRINTFORML %"厌女", 25, LEFT%：＋%NUM_FORMAT(2000), 8%卢比
	RESETCOLOR
ENDIF

;この時点で値段がマイナスになっていた場合、強制的に0まで戻す。買ってるはずなのに買われてんじゃねぇ！
SIF 値段計算 < 0
	値段計算 = 0

;割引クーポンはひとつまでです！
IF TALENT:対象キャラ:恋慕
	DRAWLINE
	PRINTFORML 折扣
;恋慕は完全無料
	SETCOLOR カラーパレット("ピンク")
	PRINTFORM %"挚爱特惠", 25, LEFT%：　　　　　%CALLNAME:PLAYER%专属100％折扣
	CALL PRINT_STRL(@"_H_")
	RESETCOLOR
	値段計算 *= 0
ELSEIF TALENT:対象キャラ:弱味
	DRAWLINE
	PRINTFORML 折扣
;弱みを握ってはした金で体を売らせたい！という鉄の意志
;せっかく値段が出るんだしタダで抱かせてもらうよりも「100円で買ってあげた」みたいののほうが尊厳を性的搾取できている感が出るので
;本来は30,000ルピ……ですが！なんと！今回に限り！お客様だけに！99%オフの300ルピで！！のほうが絶対にシコれる。古事記にもそう書かれている。
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORM %"弱点折扣", 25, LEFT%：　　　　　99％折扣
	CALL PRINT_STRL(@"_H_")
	RESETCOLOR
	値段計算 /= 100
;ELSEIF TALENT:金欠 || TALENT:貧乏
;お金に困っているときはちょっとお安くなるが、発情期のムラムラは値段に適用されなくなる。買っては欲しいがお金も欲しいため。
;	SETCOLOR カラーパレット("薄ピンク")
;	PRINTFORM %"金欠中", 25, LEFT%：　　　　　25％オフ
;	CALL PRINT_STRL(@"_H_")
;	RESETCOLOR
;	値段計算 /= 4 *= 3
ELSEIF CFLAG:TARGET:発情期フラグ < 0 && BASE:TARGET:性欲 >= 500
	DRAWLINE
	PRINTFORML 折扣
;発情期
	IF BASE:TARGET:性欲 >= 1000
		SETCOLOR カラーパレット("ピンク")
		PRINTFORM %"发情期饥渴中", 25, LEFT%：　　　　　30％折扣
		CALL PRINT_STRL(@"_H_")
		RESETCOLOR
		値段計算 /= 10
		値段計算 *= 7
	ELSE
		SETCOLOR カラーパレット("ピンク")
		PRINTFORM %"发情期饥渴中", 25, LEFT%：　　　　　10％折扣
		CALL PRINT_STRL(@"_H_")
		RESETCOLOR
		値段計算 /= 10
		値段計算 *= 9
	ENDIF
ENDIF
DRAWLINE
IF TALENT:対象キャラ:恋慕
	値段計算 = -1
ELSE
	値段計算 = MAX(0, 値段計算)
ENDIF
IF 値段計算 > 0
	PRINTFORML %"合计价格", 25, LEFT%：　%NUM_FORMAT(値段計算), 8%卢比
ELSE
	PRINTFORM %"合计价格", 25, LEFT%：　%"免费", 8%
	CALL PRINT_STRL(@"_H_")
ENDIF

RETURN 値段計算

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@性処理_ABLE_舔阴服务
#DIM ループ用
#DIM 許可好感度
#DIM 補正後好感度

;RETURN 0は非表示
SIF GETBIT(TALENT:PLAYER:性別, 0) == 0
	RETURN 0
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
IF GETBIT(TALENT:PLAYER:性別, 1)
	SIF TALENT:PLAYER:射精不可
		RETURN 0
ENDIF

;RETURN -1は表示するが選択出来ない
許可好感度 = 70
補正後好感度 = 補正後好感度算出(TARGET)
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	補正後好感度 -= TALENT:汚臭耐性 * -10
ENDIF
IF 初体験済みチェック("ファーストキス", TARGET) == 0
	補正後好感度 -= 20
ENDIF
SIF BASE:酩酊 > MAXBASE:酩酊 / 2
	補正後好感度 += 20
TRYCCALLFORM 性処理業務固有補正_可不可_{NO:TARGET}_舔阴服务(TARGET, 補正後好感度)
	補正後好感度 = RESULT
CATCH
ENDCATCH
SIF 許可好感度 > 補正後好感度
	RETURN -1

RETURN 1

;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_性処理_舔阴服务
#DIM 対象キャラ


対象キャラ = TARGET

PRINTFORM %CALLNAME:TARGET%
IF TALENT:TARGET:恋慕
	PRINTFORM 似乎对被需求感到高兴而微笑着
ELSEIF TALENT:TARGET:弱味 || (TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1)
;これだけ露骨に浮かぶバリエーションが多い……なんでやろな？
	PRINTFORM %TEXTR("不情不愿地/面露嫌恶却不甘心地/眉头紧蹙勉强地/瞪视着这边却还是")%
ELSEIF TALENT:TARGET:恋人持ち == 3
	PRINTFORM 向着逝去的伴侣轻声致歉后
ELSEIF TALENT:TARGET:恋人持ち == 2
	PRINTFORM 向着誓约的伴侣轻声致歉后
ELSEIF TALENT:TARGET:恋人持ち == 1
	PRINTFORM 向着现任恋人轻声致歉后
ELSEIF TALENT:TARGET:恋人持ち == -1
	PRINTFORM 向着心仪之人轻声致歉后
ELSEIF TALENT:TARGET:セフレ
	PRINTFORM 脸上浮现出戏谑的坏笑
ELSEIF TALENT:TARGET:精神的余裕 == 2
	PRINTFORM 轻叹有失体统却仍
ELSEIF TALENT:TARGET:ギャル
	PRINTFORM 落落大方地
ELSEIF TALENT:TARGET:羞恥心
	PRINTFORM %TEXTR("双颊绯红地/含羞垂首")%
ELSEIF TALENT:TARGET:性的興味
	PRINTFORM %TEXTR("饶有兴致地/满怀期待地")%
ELSEIF TALENT:TARGET:性欲 <= -1
	PRINTFORM 公事公办地
ELSE
	PRINTFORM 略带羞涩却仍
ENDIF
PRINTFORMW %TEXTR("应允/首肯")%，与%CALLNAME:PLAYER%%TEXTR("隐入就近的遮蔽处/至隐蔽角落/进入藏身暗处")%……
PRINTL
PRINTFORMW ────
PRINTL
;履歴

IF RFLAG:コマンド結果受渡し変数３ == 1
	;ファーストキス
	IF RFLAG:コマンド結果受渡し変数 == 1
	;うまいこと思いつかないので完成度が低い
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>让尚未接吻过的%CALLNAME:TARGET%自愿用嘴唇免费侍奉%CALLNAME:PLAYER%的[阴蒂]</font><img src='えっちハート'>","うふふ実績")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>将初吻献给%CALLNAME:PLAYER%的[阴蒂]</font><img src='えっちハート'>","うふふ実績")
	ELSEIF TALENT:対象キャラ:弱味
	;弱味想定。いつか必要になるかもしれないし……
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:TARGET%的嘴唇被%CALLNAME:PLAYER%以%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%的价格买下，用[阴蒂]玷污了其纯洁</font><img src='えっちハート'>","うふふ実績")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%抓住把柄仅用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%就让对方舔阴，迫使%CALLNAME:TARGET%将初吻献给了自己的[阴蒂]</font><img src='えっちハート'>","うふふ実績")
	ELSE
		CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>对连接吻都未经历过的%CALLNAME:TARGET%，用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%让其进行了舔阴</font><img src='えっちハート'>","うふふ実績")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>明明连接吻都未曾有过，却被%CALLNAME:PLAYER%用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%强迫舔阴，将初吻献给了%CALLNAME:PLAYER%的[阴蒂]</font><img src='えっちハート'>","うふふ実績")
	ENDIF
ELSEIF RFLAG:コマンド結果受渡し変数 == 1
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>以工作名义最终让%CALLNAME:TARGET%免费舔阴</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>为心爱的%CALLNAME:PLAYER%免费舔阴</font><img src='えっちハート'>","うふふ実績")
ELSEIF TALENT:対象キャラ:弱味
	;弱味想定。いつか必要になるかもしれないし……
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>以%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%买下被抓住把柄的%CALLNAME:TARGET%的嘴唇，让其进行了舔阴</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%抓住把柄，仅用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%就迫使对方进行了舔阴</font><img src='えっちハート'>","うふふ実績")
ELSE
	CALL 履歴データベース登録(CFLAG:PLAYER:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%买下%CALLNAME:TARGET%的嘴唇，让其进行了舔阴</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%用%MON_FORMAT(RFLAG:コマンド結果受渡し変数２)%为对方进行了舔阴</font><img src='えっちハート'>","うふふ実績")
ENDIF

@MESSAGE_性処理_舔阴服务_非口上時
#DIM 対象キャラ
#DIM 値段計算
#DIM オプション込み最終価格
#DIM 手コキ
#DIM 精液ごっくん
#DIM お掃除フェラ
#DIM クリ吸い
#DIM 陰唇お掃除

手コキ = 0
精液ごっくん = 0
お掃除フェラ = 0
クリ吸い = 0
陰唇お掃除 = 0


対象キャラ = TARGET

;風呂とかで「浴槽に腰掛け～」とか書いてもいい気がするけど、別にそれは個人口上でやりゃいいかなって
PRINTFORM 在隐蔽处%CALLNAME:PLAYER%裸露着性器站立，%CALLNAME:TARGET%在%CALLNAME:PLAYER%面前%TEXTR("蹲下/跪下/屈膝")%，将嘴贴在私处开始用舌头舔舐。
PRINTL
CALL PRINT_STR(@"ぴちゃ_H_ ぴちゅっ_H_")
PRINTFORMW 隐蔽处回响着淫靡的水声。 
PRINTL

;プレイ内での行為はソース変化なしで経験だけ増える感じでいいかね？
;相手が低技巧のいい感じのシチュが思い浮かばない

IF GETBIT(TALENT:PLAYER:性別, 1) == 1
;ふたなり
	PRINTFORML 在%CALLNAME:TARGET%黏腻地舔舐阴唇的%TEXTR("触感/快感")%中扭动腰肢，%CALLNAME:PLAYER%要求同时手淫自己的鸡巴时，
	IF 陥落チェック(TARGET)
		PRINTFORMW %CALLNAME:TARGET%在%CALLNAME:PLAYER%胯间露出无可奈何的笑容。
		PRINTL
		PRINTFORM %CALLNAME:TARGET%将%CALLNAME:PLAYER%的扶她鸡巴
		CALL PRINT_STR(@"啾噗_H_")
		PRINTFORMW 地含住涂抹唾液后，一边口交一边%TEXTR("用指尖搓揉龟头/用手指圈住冠状沟重点刺激/用手撸动整根肉棒")%爱抚。
		PRINTL
		PRINTFORML 在%CALLNAME:TARGET%献身般的口交和手交快感中，%CALLNAME:PLAYER%最终挺腰达到高潮，
		PRINTFORMW %CALLNAME:TARGET%用手掌接住精液%TEXTR("避免飞溅/防止弄脏衣服/不让沾到头发")%……
		;一瞬咥えただけだから口淫経験とかは増えない方針で
		EXP:PLAYER:射精経験 += 1
		EXP:対象キャラ:手淫経験 += 1
		EXP:対象キャラ:精液経験 += 1
		SELECTCASE RAND:4
		CASE 0
			PRINTL
			PRINTFORM 确认%CALLNAME:PLAYER%射精结束后，%CALLNAME:TARGET%将手掌中积存的精液
			CALL PRINT_STR(@"嗯_H_")
			PRINTFORMW 地嗅了一下，抬眼对%CALLNAME:PLAYER%露出坏笑，像挑逗般用舌头慢慢舔干净……
			PRINTL
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
		CASE 1
			PRINTL
			PRINTFORM 当%CALLNAME:PLAYER%要求喝下手掌中的精液，%CALLNAME:TARGET%苦笑着
			CALL PRINT_STR(@"咕嘟_H_ 哧溜_H_")
			PRINTFORMW 地出声吮吸，挑逗刚射精的%CALLNAME:PLAYER%……
			PRINTL
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
		CASE 2
			PRINTL
			PRINTFORM 确认%CALLNAME:PLAYER%射精结束后，%CALLNAME:TARGET%
			CALL PRINT_STR(@"咕啾_H_")
			PRINTFORML 地含住龟头，
			CALL PRINT_STR(@"啾_H_ 哧溜_H_")
			PRINTFORMW 温柔地吸出%CALLNAME:PLAYER%尿道残留的精液进行清理……
			PRINTL
			EXP:対象キャラ:口淫経験 += 1
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
			お掃除フェラ = 1
		CASEELSE
			PRINTL
		ENDSELECT
		手コキ = 1
		RFLAG:性処理射精先 = 9 + 10
	ELSEIF TALENT:TARGET:弱味 || (TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1) || TALENT:TARGET:恋人持ち >= 1 || TALENT:TARGET:恋人持ち == -1
	;嫌がりそうな子ら
		PRINTFORMW %CALLNAME:TARGET%在%CALLNAME:PLAYER%胯间%TEXTR("皱起眉头/面露难色")%叹了口气。
		PRINTL
		PRINTFORMW %CALLNAME:TARGET%一边不情愿地在%CALLNAME:PLAYER%的扶她鸡巴上滴下唾液，一边口交并%TEXTR("用指尖搓揉龟头/用手指圈住冠状沟重点刺激/用手撸动整根肉棒")%爱抚。
		PRINTL
		PRINTFORML 在%CALLNAME:TARGET%献身般的口交和手交快感中，%CALLNAME:PLAYER%最终挺腰达到高潮，
		PRINTFORMW %CALLNAME:TARGET%用手掌接住精液%TEXTR("避免飞溅/防止弄脏衣服/不让沾到头发")%……
		EXP:PLAYER:射精経験 += 1
		EXP:対象キャラ:手淫経験 += 1
		EXP:対象キャラ:精液経験 += 1
		SELECTCASE RAND:3
		CASE 0
			PRINTL
			PRINTFORM 当%CALLNAME:PLAYER%要求喝下手掌中的精液时，%CALLNAME:TARGET%露出极其厌恶的表情，却仍
			CALL PRINT_STR(@"咕嘟_H_ 哧溜_H_")
			PRINTFORMW 地大口吞下……
			PRINTL
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
		CASE 1
			PRINTL
			PRINTFORMW %CALLNAME:PLAYER%要求喝下手掌中的精液，但%CALLNAME:TARGET%露出极其厌恶的表情断然拒绝……
			PRINTL
		CASE 2
			PRINTL
		ENDSELECT
		手コキ = 1
		RFLAG:性処理射精先 = 9 + 10
	ELSE
		PRINTFORMW %CALLNAME:TARGET%表示同意，将嘴从%CALLNAME:PLAYER%胯间移开。
		PRINTL
		PRINTFORMW %CALLNAME:TARGET%在%CALLNAME:PLAYER%的扶她鸡巴上滴下唾液，重新开始口交并%TEXTR("用指尖搓揉龟头/用手指圈住冠状沟重点刺激/用手撸动整根肉棒")%爱抚。
		PRINTL
		PRINTFORML 在%CALLNAME:TARGET%献身般的口交和手交快感中，%CALLNAME:PLAYER%最终挺腰达到高潮，
		PRINTFORMW %CALLNAME:TARGET%用手掌接住精液%TEXTR("避免飞溅/防止弄脏衣服/不让沾到头发")%……
		;一瞬咥えただけだから口淫経験とかは増えない方針で
		EXP:PLAYER:射精経験 += 1
		EXP:対象キャラ:手淫経験 += 1
		EXP:対象キャラ:精液経験 += 1
		SELECTCASE RAND:4
		CASE 0
			PRINTL
			PRINTFORM 当%CALLNAME:PLAYER%要求喝下手掌中的精液时，%CALLNAME:TARGET%皱眉犹豫后还是
			CALL PRINT_STR(@"咕嘟_H_ 哧溜_H_")
			PRINTFORMW 地把%CALLNAME:PLAYER%的精液大口吞下……
			PRINTL
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
		CASE 1
			PRINTL
			PRINTFORMW %CALLNAME:PLAYER%要求喝下手掌中的精液，但%CALLNAME:TARGET%厌恶地皱眉断然拒绝……
			PRINTL
		CASE 2
			PRINTL
			PRINTFORML %CALLNAME:TARGET%刚从%CALLNAME:PLAYER%阴部移开嘴，%CALLNAME:PLAYER%就挺着鸡巴要求『清理』，
			PRINTFORM %CALLNAME:TARGET%叹着气勉强含住龟头
			CALL PRINT_STR(@"咕啾_H_")
			PRINTFORML ，
			CALL PRINT_STR(@"啾_H_ 哧溜_H_")
			PRINTFORMW 温柔地吸出%CALLNAME:PLAYER%尿道残留的精液进行清理……
			PRINTL
			EXP:対象キャラ:口淫経験 += 1
			EXP:対象キャラ:精飲経験 += 1
			精液ごっくん = 1
			お掃除フェラ = 1
		CASEELSE
			PRINTL
		ENDSELECT
		手コキ = 1
		RFLAG:性処理射精先 = 9 + 10
	ENDIF
ELSE
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM 在%CALLNAME:TARGET%黏腻舔舐阴唇的%TEXTR("触感/快感")%中扭动腰肢，%CALLNAME:PLAYER%
		IF 陥落チェック(TARGET)
			PRINTFORML 撒娇要求好好照顾阴蒂时，
			PRINTFORM %CALLNAME:TARGET%用眼神温柔微笑，用抿起的嘴唇对%CALLNAME:PLAYER%的阴蒂
			CALL PRINT_STR(@"啾_H_ 啾啪_H_")
			PRINTFORMW 进行吮吸，持续用唇舌爱抚直到%CALLNAME:PLAYER%高潮……
			PRINTL
		ELSEIF TALENT:TARGET:弱味 || (TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1) || TALENT:TARGET:恋人持ち >= 1 || TALENT:TARGET:恋人持ち == -1
			PRINTFORML 要求更重点舔舐阴蒂时，
			PRINTFORM %CALLNAME:TARGET%虽面露难色仍响应要求，用抿起的嘴唇对%CALLNAME:PLAYER%的阴蒂
			CALL PRINT_STR(@"啾_H_ 啾啪_H_")
			PRINTFORMW 进行吮吸，持续用唇舌爱抚直到%CALLNAME:PLAYER%高潮……
			PRINTL
		ELSE
			PRINTFORML 要求更重点舔舐阴蒂时，
			PRINTFORM %CALLNAME:TARGET%响应要求，用抿起的嘴唇对%CALLNAME:PLAYER%的阴蒂
			CALL PRINT_STR(@"啾_H_ 啾啪_H_")
			PRINTFORMW 进行吮吸，持续用唇舌爱抚直到%CALLNAME:PLAYER%高潮……
			PRINTL
		ENDIF
		クリ吸い = 1
	CASE 1
		PRINTFORM 在%CALLNAME:TARGET%湿滑刺激阴蒂的%TEXTR("触感/快感")%中扭动腰肢，%CALLNAME:PLAYER%
		IF 陥落チェック(TARGET)
			PRINTFORML 撒娇要求更细致地全面舔舐时，
		ELSEIF TALENT:TARGET:弱味 || (TALENT:性別嗜好 == 2 && TALENT:PLAYER:性別 == 1) || TALENT:TARGET:恋人持ち >= 1 || TALENT:TARGET:恋人持ち == -1
			PRINTFORML 要求更细致地全面舔舐时，
			PRINTFORMW %CALLNAME:TARGET%虽面露难色仍响应要求，用舌尖沿着阴唇缝隙游走，直到%CALLNAME:PLAYER%高潮都持续%TEXTR("细致地/彻底地")%舔舐吸吮……
			PRINTL
		ELSE
			PRINTFORML 要求更细致地全面舔舐时，
			PRINTFORMW %CALLNAME:TARGET%响应要求，用舌尖沿着阴唇缝隙游走，直到%CALLNAME:PLAYER%高潮都持续%TEXTR("细致地/彻底地")%舔舐吸吮……
			PRINTL
		ENDIF
		陰唇お掃除 = 1
	ENDSELECT
ENDIF

DRAWLINE

;最終的な料金　1ルピ＝10円程度という想定で書いてる
IF RFLAG:コマンド結果受渡し変数２ > 0
	PRINTFORML %"基础费用", 25, LEFT%：　%NUM_FORMAT(RFLAG:コマンド結果受渡し変数２), 8%卢比
ELSE
	PRINTFORM %"基础费用", 25, LEFT%：　%"免费", 8%
	CALL PRINT_STRL(@"_H_")
ENDIF

DRAWLINE


値段計算 = 0
PRINTFORML 附加选项

IF クリ吸い == 1 || 陰唇お掃除 == 1
;これは基本料に含まれてていいだろとは思うものの、オプションとして表示されたほうがえっちなため安くても追加料金を発生させる
	値段計算 += 300
	PRINTFORML %"吸吮阴蒂", 25, LEFT%：＋%NUM_FORMAT(150), 8%卢比
ENDIF
IF 手コキ == 1
	値段計算 += 1000
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORML %"手交", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
	RESETCOLOR
ENDIF
IF 精液ごっくん == 1
	IF TALENT:汚臭耐性 < 0
	;汚臭敏感
		値段計算 -= TALENT:汚臭耐性 * 500
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"吞咽精液", 25, LEFT%：＋%NUM_FORMAT(ABS(TALENT:汚臭耐性) * 1000), 8%卢比
		RESETCOLOR
	ELSE
		値段計算 += 300
		SETCOLOR カラーパレット("赤ピンク")
		PRINTFORML %"吞咽精液", 25, LEFT%：＋%NUM_FORMAT(300), 8%卢比
		RESETCOLOR
	ENDIF
ENDIF
IF お掃除フェラ == 1
	値段計算 += 1000
	SETCOLOR カラーパレット("赤ピンク")
	PRINTFORML %"事后清理", 25, LEFT%：＋%NUM_FORMAT(1000), 8%卢比
	RESETCOLOR
ENDIF

;オプションは基本的に割引クーポン対象外となっております～
;恋慕は完全無料
IF TALENT:対象キャラ:恋慕
	DRAWLINE
	PRINTFORML 折扣
	SETCOLOR カラーパレット("ピンク")
	PRINTFORM %"挚爱特惠", 25, LEFT%：　　　　　%CALLNAME:PLAYER%专属100％折扣
	RESETCOLOR
	CALL PRINT_STRL(@"_H_")
	値段計算 *= 0
ELSEIF TALENT:対象キャラ:弱味
	DRAWLINE
	PRINTFORML 折扣
	SETCOLOR カラーパレット("赤")
	PRINTFORM %"弱点折扣", 25, LEFT%：　　　　　99％折扣
	RESETCOLOR
	CALL PRINT_STRL(@"_H_")
	値段計算 /= 100
ENDIF
DRAWLINE
値段計算 = MAX(0, 値段計算)
;オプション分のMONEY処理
MONEY -= 値段計算

オプション込み最終価格 = 値段計算 + RFLAG:コマンド結果受渡し変数２
IF オプション込み最終価格 > 0
	PRINTFORML %"含附加选项最终价格", 25, LEFT%：　%NUM_FORMAT(オプション込み最終価格), 8%卢比
ELSE
	PRINTFORM %"含附加选项最终价格", 25, LEFT%：　%"免费", 8%
	CALL PRINT_STRL(@"_H_")
ENDIF
DRAWLINE

PRINTFORML %NUM_FORMAT(MONEY + オプション込み最終価格)% - %NUM_FORMAT(オプション込み最終価格)% = %NUM_FORMAT(MONEY)%卢比
