
@COMNAME_S306_9
#FUNCTIONS
TSTR:コマンド名受渡 = 挠腋窝

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_306_9
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")
弱点コマンドカテゴリ = S
睡眠中許可 = 1


@COM_TOOLTIP_S306_9
LOCALS = <br>■挠腋窝<br>
LOCALS += "搔痒对方腋下的指令。<br>需目标穿着暴露腋下的服饰方可执行。"
LOCALS += "<br>指令类型：性骚扰<br>获取来源：接触・暴露・快感Ｓ・欢愉(特定条件)・不快(特定条件)・反感(特定条件)"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S306_9

;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 50)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 70)
TIME += 10

SIF CFLAG:TARGET:睡眠
	RETURN 1

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ  3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れ時は反応なし
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 3
				;喜び時はちょっと喜ぶ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
		ENDSELECT
	CASE 3 TO 4
		;嫌悪と拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 200)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 20)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 20)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
		ENDSELECT
	CASEELSE
		;上記以外は同じソースを発生
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
ENDSELECT

;成功度を判定して笑ったら歓楽に＋
;無反応時は絶対笑わない
IF RFLAG:コマンド結果受渡し変数２ != 2
	IF RAND:100 < 30 + (ABL:PLAYER:技巧 * 10) + (TALENT:PLAYER:器用な指 * 20)
		;技巧が高いほど笑う
		;技巧５＋器用な指で確定
		RFLAG:コマンド結果受渡し変数３ = 1
		CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)
	ENDIF
ENDIF
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S306_9
;選択から飛んでくるのでABLEは常に0
RETURN 0

@身体に触れる_ABLE_挠腋窝
;一括管理
SIF GLOBAL_COMABLE_S("306_9") && !RFLAG:罰ゲーム中
	RETURN RESULT
;鎧があると駄目
SIF TEQUIP:上半身鎧あり
	RETURN 0
;脇露出必須
SIF TEQUIP:上半身服あり &&  TEQUIP:脇出し == 0
	RETURN 0
;関数実行のため、派生番号を返す
RETURN 9

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S306_9

PRINTFORML %CALLNAME:PLAYER%悄悄接近，轻搔%CALLNAME:TARGET%的腋下
IF !CFLAG:TARGET:睡眠
	IF RFLAG:コマンド結果受渡し変数３
		PRINTFORML %CALLNAME:TARGET%忍不住痒意笑出声来
	ELSE
		PRINTFORML %CALLNAME:TARGET%强忍痒意没有笑出来…
	ENDIF
ENDIF
IF CUP:TARGET:快Ｓ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	IF CFLAG:TARGET:睡眠
		PRINTFORML 突如其来的快感让%CALLNAME:TARGET%在睡梦中发出细微呻吟…
	ELSE
		PRINTFORML 突如其来的快感让%CALLNAME:TARGET%不禁漏出些许声音…
	ENDIF
	PRINTL
ENDIF

@MESSAGE_COM_S306_9_非口上時

IF CFLAG:TARGET:睡眠
	PRINTFORML %CALLNAME:TARGET%仍在安稳沉睡……
	RETURN 0
ENDIF

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れに違い無し
				PRINTFORML %CALLNAME:TARGET%歪着头似乎在问有什么事情
			CASE 3
				;喜び時は喜ぶ
				PRINTFORML %CALLNAME:TARGET%虽不太明白，但似乎当作游戏而显得开心
		ENDSELECT
	CASE 2
		;無反応
		PRINTFORML %CALLNAME:TARGET%毫无反应地保持平静
	CASE 3
		;嫌悪
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%面露嫌恶地拍开%CALLNAME:PLAYER%的手…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%紧闭双臂防御笑穴…
			CASE 3
				;喜び
				IF RFLAG:コマンド結果受渡し変数３
					PRINTFORML %CALLNAME:TARGET%急忙捂住嘴，用不甘心的眼神瞪着%CALLNAME:PLAYER%…
				ELSE
					PRINTFORML %CALLNAME:TARGET%露出得意的表情，双臂交叉进入防御姿态…
				ENDIF
		ENDSELECT
	CASE 4
		;拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%推开了%CALLNAME:PLAYER%的手，表示不该随意触碰
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%虽面露不满却未阻止%CALLNAME:PLAYER%的动作…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%嘴上抱怨却放任%CALLNAME:PLAYER%继续…
		ENDSELECT
	CASE 5
		;余裕
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%呵斥%CALLNAME:PLAYER%不该随意触碰他人
			CASE 2 TO 3
				;受け入れ喜び
				IF RFLAG:コマンド結果受渡し変数３
					PRINTFORML %CALLNAME:TARGET%无奈的对%CALLNAME:PLAYER%进行口头警告
				ELSE
					PRINTFORML %CALLNAME:TARGET%苦笑着对%CALLNAME:PLAYER%进行口头警告
				ENDIF
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%红着脸与%CALLNAME:PLAYER%拉开距离…
			CASE 2 TO 3
				;受け入れ喜び
				PRINTFORML %CALLNAME:TARGET%红着脸任由%CALLNAME:PLAYER%继续动作
		ENDSELECT
	CASE 7
		;愉悦
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				IF RFLAG:コマンド結果受渡し変数３
					PRINTFORML %CALLNAME:TARGET%称赞%CALLNAME:PLAYER%的手法不错
				ELSE
					PRINTFORML %CALLNAME:TARGET%有趣地微笑着，并告诉%CALLNAME:PLAYER%触摸的方式不对
				ENDIF
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%戏谑地问%CALLNAME:PLAYER%是否只搔痒就满足…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%用妩媚表情暗示%CALLNAME:PLAYER%可以更深入接触…
		ENDSELECT
	CASE 8
		;汎用
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%明显抗拒%CALLNAME:PLAYER%的触碰…
			CASE 2 TO 3
				;受け入れ喜び
				PRINTFORML %CALLNAME:TARGET%默许了%CALLNAME:PLAYER%的触碰
		ENDSELECT
ENDSELECT

