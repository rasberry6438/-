
@COMNAME329
#FUNCTIONS
TSTR:コマンド名受渡 = 展示乳头

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_329
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_329
LOCALS = <br>■展示乳头<br>
LOCALS += "通过悄悄展示自己乳头来诱惑对方的指令。<br>在场人数越多越易引发反感。"
LOCALS += "<br>指令类型：性骚扰<br>获取来源：欢乐・反感"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM329

CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ  3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

IF GET_TARGETNUM() > 1 && !CFLAG:PLAYER:隠密 && !CFLAG:TARGET:隠密
	;自身とあなた以外のキャラが居る場合
	CALL SOURCE_CALC_反感(TARGET, PLAYER, (GET_TARGETNUM() - 1) * 5 + 50)
ELSE
	;二人きりの場合
	IF 陥落チェック(TARGET) == 0
		CALL SOURCE_CALC_反感(TARGET, PLAYER, 50)
	ELSEIF 初体験日取得("初うふふ", TARGET, MASTER) && BASE:TARGET:性欲 > 1000 && RFLAG:コマンド結果受渡し変数２ > 2
		;陥落済みかつ初うふふ済みかつ性欲一定以上なら押し倒し
		IF 現在地自宅チェック(TARGET) || 現在地自宅チェック(PLAYER)
			RFLAG:コマンド結果受渡し変数３ = 1
		ELSE
			RFLAG:コマンド結果受渡し変数３ = 2
			CFLAG:PLAYER:隠密 = 1
			CFLAG:TARGET:隠密 = 1
			PALAM:TARGET:欲情 = 30000
			SIF GETBIT(TALENT:TARGET:性別, 0)
				PALAM:TARGET:潤滑 = 30000
		ENDIF
		CALL うふふ開始キャラ処理(TARGET, "あなたの挑発に乗る")
		CFLAG:PLAYER:うふふ = 1
		FLAG:モード管理 = 0
	ENDIF
ENDIF

TIME += 3

;性別によっては同性愛がないと歓楽発生しない
IF 同性愛性癖判定(TARGET,PLAYER)
	SIF 性癖素質:TARGET:同性愛 < 1
		RETURN 1
ENDIF
;相手が無性だと歓楽発生しない
SIF TALENT:TARGET:性別 == 0
	RETURN 1
;性欲なしも同様
SIF TALENT:TARGET:性欲 == -2
	RETURN 1

CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 200)
CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE329

;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(329)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;乳が無いとダメ
SIF GETBIT(TALENT:PLAYER:性別, 0) == 0
	RETURN 0
SIF TALENT:PLAYER:Ｂ感度 == -2
	RETURN 0
;上服着用必須
SIF TEQUIP:PLAYER:上半身服あり == 0 && TEQUIP:PLAYER:上半身下着あり == 0
	RETURN 0
SIF TEQUIP:PLAYER:上半身鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:上タイツあり
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM329

PRINTFORML %CALLNAME:PLAYER%对着%CALLNAME:TARGET%微微露出乳头…

IF RFLAG:コマンド結果受渡し変数３ == 1
	WAIT
	PRINTFORML 被诱惑的%CALLNAME:TARGET%将%CALLNAME:PLAYER%拉近推倒在床
ELSEIF RFLAG:コマンド結果受渡し変数３ == 2
	WAIT
	PRINTFORML 被诱惑的%CALLNAME:TARGET%把%CALLNAME:PLAYER%带到暗处紧紧拥抱
ENDIF


@MESSAGE_COM329_非口上時

PRINTFORM %CALLNAME:TARGET%

IF RFLAG:コマンド結果受渡し変数３
	PRINTFORML 用充满情欲的眼神凝视着%CALLNAME:PLAYER%…
ELSE
	SELECTCASE RFLAG:コマンド結果受渡し変数２
		CASE 1
			;無知
			PRINTFORM 露出不解神情注视着%CALLNAME:PLAYER%的动作。
		CASE 2
			;無反応
			PRINTFORM 面无表情地无视%CALLNAME:PLAYER%的行为。
		CASEELSE
			;汎用
			IF GET_TARGETNUM() > 1
				PRINTFORM 急忙提醒%CALLNAME:PLAYER%注意场合。
			ELSE
				IF RFLAG:コマンド結果受渡し変数 == 1
					;拒絶
					PRINTFORM 急忙提醒%CALLNAME:PLAYER%注意场合。
				ELSEIF RFLAG:コマンド結果受渡し変数 == 2
					;受け入れ
					PRINTFORM 苦笑着轻拍%CALLNAME:PLAYER%示意收敛。
				ELSE
					;喜び
					PRINTFORM 叮嘱%CALLNAME:PLAYER%不可对他人如此。
				ENDIF
			ENDIF
	ENDSELECT
ENDIF
