@デート取り合い度算出(対象キャラ, 先約キャラ, 後からキャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 先約キャラ
#DIM 後からキャラ
#DIM 先約優先値
#DIM 後から優先値
#DIM 先約陥落
#DIM 後から陥落

先約優先値 = 0
後から優先値 = 0
先約陥落 = 0
後から陥落 = 0
;----------------------------------------------
;関係性チェック
;----------------------------------------------
IF (先約キャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 0)) || 関係性チェック(対象キャラ, 先約キャラ, "恋慕")
	IF (後からキャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 0)) || 関係性チェック(対象キャラ, 後からキャラ, "恋慕")
		;先約と後から両方恋慕なら補正なし
	ELSE
		;先約に陥落補正
		先約陥落 = 1
	ENDIF
ELSE
	IF (後からキャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 0)) || 関係性チェック(対象キャラ, 後からキャラ, "恋慕")
		;後からに陥落補正
		後から陥落 = 1
	ELSE
		;恋慕がどっちもないならセフレを見る
		IF (先約キャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 1)) || 関係性チェック(対象キャラ, 先約キャラ, "セフレ")
			IF (後からキャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 1)) || 関係性チェック(対象キャラ, 後からキャラ, "セフレ")
				;先約と後から両方セフレなら補正なし
			ELSE
				;先約に陥落補正
				先約陥落 = 1
			ENDIF
		ELSE
			IF (後からキャラ == MASTER && GETBIT(陥落チェック(対象キャラ), 1)) || 関係性チェック(対象キャラ, 後からキャラ, "セフレ")
				;後からに陥落補正
				後から陥落 = 1
			ELSE
				;どっちもセフレではないので補正なし
			ENDIF
		ENDIF
	ENDIF
ENDIF

;----------------------------------------------
;先約補正
;----------------------------------------------
;基礎値
先約優先値 += 40
;誘った側なら更に補正
SIF CFLAG:対象キャラ:デート > 0
	先約優先値 += 15
;真面目
先約優先値 += TALENT:対象キャラ:自制心 * 15
;マイペース
先約優先値 += TALENT:対象キャラ:マイペース * 10
;保守的
先約優先値 += TALENT:対象キャラ:性的興味 * -5
;羞恥心
SIF TALENT:対象キャラ:羞恥心 > 0
	先約優先値 += 5
;紳士的,淑女的
SIF TALENT:対象キャラ:精神的余裕 > 0
	先約優先値 += 5
;献身的
SIF TALENT:対象キャラ:献身的 > 0
	先約優先値 += 5
;陥落補正
IF 先約陥落
	;陥落基礎値
	先約優先値 += 15
	;貞操
	先約優先値 += TALENT:対象キャラ:貞操 * 5
ELSE
	IF 先約キャラ == MASTER
		先約優先値 += MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) / 10
	ELSE
		先約優先値 += MAX(縁結び進展度取得(対象キャラ, 先約キャラ, "恋慕"), 縁結び進展度取得(対象キャラ, 先約キャラ, "セフレ")) / 10
	ENDIF
ENDIF
;あなた用補正
IF 先約キャラ == MASTER
	SIF TALENT:対象キャラ:弱味
		先約優先値 = 999
	SIF TALENT:対象キャラ:淫乱
		先約優先値 = 999
	SIF TALENT:対象キャラ:快感応答 > 0
		先約優先値 += 10
	SIF TALENT:対象キャラ:従業員 > 0
		先約優先値 += 3
	SIF TALENT:対象キャラ:定住者 > 0
		先約優先値 += 3
	SIF TALENT:対象キャラ:容姿 > 0
		先約優先値 += TALENT:対象キャラ:容姿 * 3
ENDIF
;好感度補正


;----------------------------------------------
;後から補正
;基本的に後からは不利なので補正は少ない
;----------------------------------------------
;基礎値
後から優先値 += 0
;陥落補正
IF 後から陥落
	;陥落基礎値
	後から優先値 += 15
	;貞操
	後から優先値 += TALENT:対象キャラ:貞操 * 5
ELSE
	IF 後からキャラ == MASTER
		後から優先値 += MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) / 10
	ELSE
		後から優先値 += MAX(縁結び進展度取得(対象キャラ, 後からキャラ, "恋慕"), 縁結び進展度取得(対象キャラ, 後からキャラ, "セフレ")) / 10
	ENDIF
ENDIF
;あなた用補正
IF 後からキャラ == MASTER
	SIF TALENT:対象キャラ:弱味
		後から優先値 = 999
	SIF TALENT:対象キャラ:淫乱
		後から優先値 = 999
	SIF TALENT:対象キャラ:快感応答 > 0
		後から優先値 += 10
	SIF TALENT:対象キャラ:従業員 > 0
		後から優先値 += 3
	SIF TALENT:対象キャラ:定住者 > 0
		後から優先値 += 3
	SIF TALENT:対象キャラ:容姿 > 0
		後から優先値 += TALENT:対象キャラ:容姿 * 3
ENDIF


RETURNF 後から優先値 - 先約優先値
