

@COMNAME302
#FUNCTIONS
TSTR:コマンド名受渡 = 肢体接触

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_302
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_302
LOCALS = <br>■肢体接触<br>
LOCALS += "通过身体接触增进亲密的指令。根据概率分为大成功、成功、失败三种结果。"
LOCALS += "<br>指令类型：日常<br>获取来源：欢乐・征服・接触・暴露・不快(失败时)"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM302
;肢体接触
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	RFLAG:コマンド結果受渡し変数 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	RFLAG:コマンド結果受渡し変数 = -1
ELSE
	;残りは成功
	RFLAG:コマンド結果受渡し変数 = 0
ENDIF




;歓楽強度
LOCAL:0 = 200
;征服
LOCAL:1 = 100
IF RFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF


CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
IF RFLAG:コマンド結果受渡し変数 == -1
	CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
ELSE
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, LOCAL:0 / 2)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, LOCAL:0 / 5)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, LOCAL:0 / 3)
ENDIF
TIME += 15
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE302
;肢体接触実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(302)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM302
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)
	PRINTFORML %CALLNAME:PLAYER%为%CALLNAME:TARGET%搓背
ELSE
	PRINTFORML %CALLNAME:PLAYER%尝试与%CALLNAME:TARGET%进行肢体接触
ENDIF
IF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORML %CALLNAME:TARGET%露出了欣喜的神色…
ELSEIF RFLAG:コマンド結果受渡し変数 == 0
	PRINTFORML %CALLNAME:TARGET%似乎并不讨厌这样的接触…
ELSEIF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORML …看来有些过于唐突了…
ENDIF

