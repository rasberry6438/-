@COMNAME316
#FUNCTIONS
TSTR:コマンド名受渡 = 介入约会

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_316
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_316
LOCALS = <br>■介入约会<br>
LOCALS += "以有事为由让约会对象暂时陪伴的指令。<br>可陪伴时长根据角色资质与话术技能浮动。"
LOCALS += "<br>指令类型：日常<br>获取来源：欢乐・征服・接触"
LOCALS += "<br>专属经验值：无<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM316
#DIM 基礎時間

基礎時間 = ABL:MASTER:話術技能 * 20
;素質補正
基礎時間 += デート取り合い度算出(TARGET, ABS(CFLAG:TARGET:デート) - 100, MASTER)
;0以下の場合は断る
IF 基礎時間 <= 0
	;ここ口上入れたいけどとりあえず汎用のみ
	PRINTFORMW %CALLNAME:TARGET%以正在忙碌为由，拒绝了%CALLNAME:PLAYER%的邀请。
	RETURN -1
ENDIF

;何分までOKかを格納
TCVAR:TARGET:デートちょっかい値 = 基礎時間
TCVAR:TARGET:ちょっかい経過時間 = 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE316

;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(316)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;デート時のみ
SIF CFLAG:デート == 0 || CFLAG:デート == MASTER + 100
	RETURN 0
;自分がデート中はダメ
SIF CFLAG:PLAYER:デート
	RETURN 0
;一日一回
SIF TCVAR:TARGET:デートちょっかい値 <= -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM316
PRINTFORML %CALLNAME:TARGET%暂停约会，表示愿意倾听%CALLNAME:PLAYER%的诉求。
PRINTFORML 预计可陪伴约{TCVAR:TARGET:デートちょっかい値, 3}分钟。

@デートちょっかい終了処理(ARG)
;デートちょっかい値は正の時はちょっかいの許容時間、負の時はすでに今日実行済みであることを示す
TCVAR:ARG:デートちょっかい値 = -1
TCVAR:ARG:ちょっかい経過時間 = 0
RETURN 1
