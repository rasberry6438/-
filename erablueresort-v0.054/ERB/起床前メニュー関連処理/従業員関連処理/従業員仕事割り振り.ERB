@従業員仕事割り振り
#DIM 従業員一覧, 500
#DIM ソート配列, 500
#DIM ページ数
#DIM 変更キャラ
#DIMS 屋号保存
#DIM ソートタイプ
#DIMS 改行判定文字列
#DIM 空き店舗あり
#DIMS 常駐業務名
#DIM 固有仕事数
#DIMS 固有仕事配列, 50
#DIMS 固有仕事名
#DIM 固有仕事キー

変更キャラ = -1

$表示ループ
VARSET 従業員一覧, -1
VARSET ソート配列
VARSET 仕事人数表示用

DRAWLINE
PRINTL 可在此设定员工负责的工作内容。
PRINTL ※当前功能处于开发阶段，可能出现运作异常。
DRAWLINE
LOCALS = <div rect='81,31,1937,3762' border='31' bcolor='#C0C0C0'></div><div rect='2050,710,3875,3080' border='31' bcolor='#C0C0C0'></div>

;現在いる従業員を配列へ
従業員一覧:0 = 0
LOCAL:1 = 1
仕事人数表示用:(現在仕事:0:0) += 1
ソート配列:0 = 現在仕事:0:0 * 100 + 現在仕事:0:1 + 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	SIF 現在仕事:LOCAL:0 >= 0 && 現在仕事:LOCAL:0 < 100000
		仕事人数表示用:(現在仕事:(従業員一覧:(LOCAL:1)):0) += 1
	IF 現在仕事:LOCAL:0 == -1
		ソート配列:(LOCAL:1) = 99999
	ELSE
		ソート配列:(LOCAL:1) = 現在仕事:LOCAL:0 * 100 + 現在仕事:LOCAL:1 + 1
	ENDIF
	LOCAL:1 += 1
NEXT
IF ソートタイプ
	ARRAYMSORT ソート配列, 従業員一覧
ENDIF

;一覧表示
LOCALS += "<div rect='156,156,1737,3662'><nobr>"
FOR LOCAL, ページ数 * 17, ページ数 * 17 + 17
	IF 従業員一覧:LOCAL < 0
		LOCALS += "<br>"
		CONTINUE
	ENDIF
	IF 現在仕事:(従業員一覧:LOCAL):0 == -1
		LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'><nonbutton>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[店铺管理]</nonbutton></font>"
	ELSE
		SIF 変更キャラ == 従業員一覧:LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>"
		LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[%現在仕事数字文字列変換(現在仕事:(従業員一覧:LOCAL):0, 0, 従業員一覧:LOCAL)%"
		IF 現在仕事:(従業員一覧:LOCAL):0 == 2
			LOCALS = %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 4)%
			IF 現在仕事:(従業員一覧:LOCAL):1 >= 10003 && 現在仕事:(従業員一覧:LOCAL):1 <= 10010
				屋号保存 = 『%空艇港店舗屋号記録:(現在仕事:(従業員一覧:LOCAL):1 % 10000)%』
			ELSE
				屋号保存 = 
			ENDIF
			LOCALS += @"：%MAP_GET("常駐業務名", REPLACE(GETPLACENAME(現在仕事:(従業員一覧:LOCAL):1 / 10000, 現在仕事:(従業員一覧:LOCAL):1 % 10000), "『.*』", ""))%%屋号保存%"
		ELSEIF (現在仕事:(従業員一覧:LOCAL):0 == 8 || 現在仕事:(従業員一覧:LOCAL):0 == 7)
			LOCALS += "："
			SIF 現在仕事:(従業員一覧:LOCAL):1 > 0
				LOCALS += @"%ERDNAME(知識素質, 現在仕事:(従業員一覧:LOCAL):1)%"
		ENDIF
		LOCALS += "]</button>"
		SIF 変更キャラ == 従業員一覧:LOCAL
			LOCALS += @"</font>"
	ENDIF
	LOCALS += "<br>"
NEXT
IF ページ数 > 0
	LOCALS += "<button value='1000'>　＜ 上一页</button>"
ELSE
	LOCALS += "　　　　　　　 "
ENDIF
LOCALS += "　　　　"
IF 従業員一覧:(ページ数 * 17 + 17) > -1
	LOCALS += "<button value='1001'>下一页 ＞</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 返回</button>　　"
IF ソートタイプ
	LOCALS += "按业务排序 <button value='998'>[切换]</button>"
ELSE
	LOCALS += "按编号排序 <button value='998'>[切换]</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "</nobr></div>"


;仕事の種類と現在そこに従事している人数を表示
LOCALS += "<div rect='2125,770,3675,2812'><nobr>"
IF 変更キャラ > -1
	LOCALS += @"当前负责业务：%NAME表示(変更キャラ)%<br>"
	LOCALS += @"■%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 0, 変更キャラ), 16, LEFT%<br><br>"
	LOCALS += @"%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 1, 変更キャラ)%<br><br>"
	;仕事レベルが存在するなら表示
	IF 現在仕事:変更キャラ:0 == 2 && 現在仕事:変更キャラ:1 > 0
		常駐業務名 '= MAP_GET("常駐業務名", REPLACE(GETPLACENAME(現在仕事:変更キャラ:1 / 10000, 現在仕事:変更キャラ:1 % 10000), "『.*』", ""))
		LOCALS += @"现在的%常駐業務名%等级：{常駐業務レベル:変更キャラ:(GETNUM(常駐業務レベル, 常駐業務名))}"
	ELSEIF 仕事レベル存在チェック(現在仕事:変更キャラ:0)
		LOCALS += @"现在的%現在仕事数字文字列変換(現在仕事:変更キャラ:0, 0, 変更キャラ)%等级：{仕事レベル:変更キャラ:(現在仕事:変更キャラ:0)}"
	ELSEIF 現在仕事:変更キャラ:0 == 9
		LOCALS += @"当前知识特质：厨师等级：{知識素質:変更キャラ:料理人}"
	ELSE
		LOCALS += @"无工作等级"
	ENDIF
ELSE
	LOCALS += "工作内容与人员配置清单<br><br>"
	FOR LOCAL, 0, 50
		IF 現在仕事数字文字列変換(LOCAL) != ""
			LOCALS += @"■%現在仕事数字文字列変換(LOCAL), 16, LEFT%：在岗人数{仕事人数表示用:LOCAL,3}人<br>"
		ENDIF
	NEXT
ENDIF
LOCALS += "</nobr></div>"

;仕事切り替え用枠
LOCALS += "<div rect='2050,31,3875,650' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
IF 変更キャラ > -1
	改行判定文字列 = 
	FOR LOCAL, 0, 50
		IF 現在仕事数字文字列変換(LOCAL) != ""
			SIF 仕事条件判定関数(変更キャラ, LOCAL) == 0
				CONTINUE
			IF LOCAL > 0
				IF STRLENS(改行判定文字列 + 現在仕事数字文字列変換(LOCAL)) > 75
					LOCALS += "<br>"
					改行判定文字列 = 
				ELSE
					LOCALS += " / "
					改行判定文字列 += " / "
				ENDIF
			ENDIF
			IF 現在仕事:変更キャラ:0 == LOCAL
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[%現在仕事数字文字列変換(LOCAL)%]</font>"
				改行判定文字列 += @"[%現在仕事数字文字列変換(LOCAL)%]"
			ELSE
				LOCALS += @"<button value='{LOCAL + 2000}'>[%現在仕事数字文字列変換(LOCAL)%]</button>"
				改行判定文字列 += @"[%現在仕事数字文字列変換(LOCAL)%]"
			ENDIF
		ENDIF
	NEXT

	VARSET RESULTS
	固有仕事数 = ENUMFUNCBEGINSWITH(@"固有仕事設定_{NO:変更キャラ}_")
	ARRAYCOPY "RESULTS", "固有仕事配列"
	IF 固有仕事数 > 0
		FOR LOCAL, 0, 固有仕事数
			SIF GETMETH(固有仕事配列:LOCAL, 0, 変更キャラ, "条件判定") == 0
				CONTINUE
			CALLFORMF %固有仕事配列:LOCAL%(変更キャラ, "仕事名")
			固有仕事名 '= TSTR:コマンド名受渡
			固有仕事キー = TOINT(REPLACE(固有仕事配列:LOCAL, @"固有仕事設定_{NO:変更キャラ}_", ""))
			SIF 固有仕事キー < 0
				CONTINUE
			IF STRLENS(改行判定文字列 + 固有仕事名) > 75
				LOCALS += "<br>"
				改行判定文字列 = 
			ELSE
				LOCALS += " / "
				改行判定文字列 += " / "
			ENDIF
			IF 現在仕事:変更キャラ:0 == 固有仕事キー + 100000
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[%固有仕事名%]</font>"
				改行判定文字列 += @"[%固有仕事名%]"
			ELSE
				LOCALS += @"<button value='{固有仕事キー + 100000}'>[%固有仕事名%]</button>"
				改行判定文字列 += @"[%固有仕事名%]"
			ENDIF
		NEXT
	ENDIF

	空き店舗あり = 0
	;空き店舗を検索
	FOR LOCAL, 0, VARSIZE("商業区店割り配列", 1)
		IF 商業区店割り配列:LOCAL:店舗名 == "" || 商業区店割り配列:LOCAL:店員配列 == "-1_"
			空き店舗あり = 1
			BREAK
		ENDIF
	NEXT
	IF 空き店舗あり && 港改造度:0:0 && TALENT:変更キャラ:定住者 && EXISTFUNCTION(@"特殊店舗_建設条件_{NO:変更キャラ}")
		LOCALS += "<br><button value='997'>[开设商店]</button>"
	ENDIF
ENDIF
LOCALS += "</div>"



HTML_PRINT LOCALS
FOR LOCAL, 0, 34
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE IS >= 100000
		現在仕事:変更キャラ:0 = LOCAL
		現在仕事:変更キャラ:1 = 0
	CASE IS >= 3000
		CALL 仕事追加処理関数(変更キャラ, LOCAL)
	CASE IS >= 2000
		現在仕事:変更キャラ:0 = LOCAL - 2000
		現在仕事:変更キャラ:1 = 0
	CASE 1000
		ページ数 -= 1
	CASE 1001
		ページ数 += 1
	CASE 999
		CALL 仕事切替時補正(変更キャラ)
		ページ数 = 0
		IF 高速船記録:派遣先登録 != ""
			CALLFORM 高速船派遣先関数_%高速船記録:派遣先登録%("派遣条件")
			IF RESULT == 0
				CALLFORM 高速船派遣先関数_%高速船記録:派遣先登録%("解説文")
				DRAWLINE
				HTML_PRINT TSTR:ツールチップ受渡
				PRINTL
				DRAWLINE
				PRINTL 因不满足高速船派遣条件，已终止派遣流程。
				PRINTL 是否确认执行？
				PRINTBUTTONLC "[0] 确定", 0
				PRINTBUTTONLC "[1] 取消", 1
				PRINTL
				BINPUT
				IF RESULT == 0
					高速船記録:派遣先登録 = 
					高速船記録:派遣状態 = 
					PRINTL
					PRINTL
					PRINTW 派遣任务已取消。
				ELSE
					GOTO 表示ループ
				ENDIF
			ENDIF
		ENDIF
		RETURN 0
	CASE 998
		INVERTBIT ソートタイプ, 0
	CASE 997
		PRINTL
		PRINTL
		DRAWLINE
		PRINTL ※※※※※注意※※※※※
		PRINTFORML %CALLNAME:変更キャラ%将在商业区开设店铺。
		PRINTL 该角色将无法继续从事常规工作。
		PRINTL 仅限拥有特殊店铺配置的角色显示此选项，
		PRINTL 请注意开设店铺需额外满足特定条件。
		DRAWLINE
		PRINTL
		PRINTFORML 确认让%NAME表示(変更キャラ)%开设商店？
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTFORMW 已命令%NAME表示(変更キャラ)%开展店铺经营业务。
			現在仕事:変更キャラ:0 = -1
			RETURN 0
		ELSE
			RESTART
		ENDIF
	CASE 0 TO 499
		CALL 仕事切替時補正(変更キャラ)
		変更キャラ = 従業員一覧:LOCAL
		;CALL 従業員仕事割り振り_切り替え(従業員一覧:LOCAL)
ENDSELECT

GOTO 表示ループ

@仕事切替時補正(対象キャラ)
#DIM 対象キャラ

SIF 対象キャラ < 0
	RETURN 0

;キャラ切り替え時や戻るボタンを押した時、設定必須の項目が設定されてなかった場合
;仕事をなしに戻す
IF 現在仕事:対象キャラ:0 == 2 && 現在仕事:対象キャラ:1 == 0
	現在仕事:対象キャラ:0 = 0
ENDIF
IF 現在仕事:対象キャラ:0 == 7
	SIF 現在仕事:対象キャラ:1 == 0
		現在仕事:対象キャラ:0 = 0
	IF 知識素質:対象キャラ:(現在仕事:対象キャラ:1) > 1
		現在仕事:対象キャラ:0 = 0
		現在仕事:対象キャラ:1 = 0
	ENDIF
ENDIF
IF 現在仕事:対象キャラ:0 == 8
	SIF 現在仕事:対象キャラ:1 == 0
		現在仕事:対象キャラ:0 = 0
	IF 知識素質:対象キャラ:(現在仕事:対象キャラ:1) < 2
		現在仕事:対象キャラ:0 = 0
		現在仕事:対象キャラ:1 = 0
	ENDIF
ENDIF

@従業員仕事割り振り_切り替え(ARG)
#DIM 空き店舗あり

DRAWLINE
PRINTL 可在此设定员工负责的工作内容。
PRINTL ※当前功能处于开发阶段，可能出现运作异常。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += @"■%CALLNAME:ARG%的当前业务<br>"
FOR LOCAL, 0, 50
	IF 現在仕事数字文字列変換(LOCAL) != ""
		SIF 仕事条件判定関数(ARG, LOCAL) == 0
			CONTINUE
		IF 現在仕事:ARG:0 == LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%</font><br>"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] 变更为%現在仕事数字文字列変換(LOCAL), 14, LEFT%</button><br>"
		ENDIF
	ENDIF
NEXT
LOCALS += "<br>"

RESULT = 0
RESULTS = 
空き店舗あり = 0
;空き店舗を検索
FOR LOCAL, 0, VARSIZE("商業区店割り配列", 1)
	IF 商業区店割り配列:LOCAL:店舗名 == "" || 商業区店割り配列:LOCAL:店員配列 == "-1_"
		空き店舗あり = 1
		BREAK
	ENDIF
NEXT
IF 空き店舗あり && 港改造度:0:0 && TALENT:ARG:定住者 && EXISTFUNCTION(@"特殊店舗_建設条件_{NO:ARG}")
	LOCALS += "<button value='998'>[998] 开设商店</button><br>"
	LOCALS += "<br>"
ENDIF

LOCALS += "<button value='999'>[999] 返回</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の説明を表示
LOCALS:1 = <div rect='2125,156,3675,2812'>
LOCALS:1 += "当前负责业务<br>"
LOCALS:1 += @"■%現在仕事数字文字列変換(現在仕事:ARG:0, 0, ARG), 16, LEFT%"
LOCALS:1 += "</div>"
LOCALS:1 += "<div rect='2312,437,3675,2812'>"
LOCALS:1 += @"<nobr>%現在仕事数字文字列変換(現在仕事:ARG:0, 1, ARG)%</nobr>"
LOCALS:1 += "<br><br>"
;仕事レベルが存在するなら表示
IF 仕事レベル存在チェック(現在仕事:ARG:0)
	LOCALS:1 += @"现在的%現在仕事数字文字列変換(現在仕事:ARG:0, 0, ARG)%等级：{仕事レベル:ARG:(現在仕事:ARG:0)}"
ELSEIF 現在仕事:ARG:0 == 9
	LOCALS:1 += @"当前知识特质：厨师等级：{知識素質:ARG:料理人}"
ELSE
	LOCALS:1 += @"无工作等级"
ENDIF
LOCALS:1 += "</div>"


HTML_PRINT LOCALS, 1
HTML_PRINT LOCALS:1, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		IF 現在仕事:ARG:0 == 2 && 現在仕事:ARG:1 == 0
			現在仕事:ARG:0 = 0
		ENDIF
		IF 現在仕事:ARG:0 == 7
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) > 1
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		IF 現在仕事:ARG:0 == 8
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) < 2
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		RETURN 0
	CASE 998
		PRINTL
		PRINTL
		DRAWLINE
		PRINTL ※※※※※注意※※※※※
		PRINTFORML %CALLNAME:ARG%将在商业区开设店铺。
		PRINTL 该角色将无法继续从事常规工作。
		PRINTL 仅限拥有特殊店铺配置的角色显示此选项，
		PRINTL 请注意开设店铺需额外满足特定条件。
		DRAWLINE
		PRINTL
		PRINTFORML 确认让%NAME表示(ARG)%开设商店？ 
		PRINTBUTTONLC "[0] 确定", 0
		PRINTBUTTONLC "[1] 取消", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			PRINTFORMW 已命令%NAME表示(ARG)%开展店铺经营业务。
			現在仕事:ARG:0 = -1
			RETURN 0
		ELSE
			RESTART
		ENDIF
	CASE 0 TO 11
		IF 仕事条件判定関数(ARG, LOCAL) == 0
			PRINTFORMW %CALLNAME:ARG%无法胜任%現在仕事数字文字列変換(LOCAL)%岗位。
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		現在仕事:ARG:0 = LOCAL
		現在仕事:ARG:1 = 0
		RESTART
	CASEELSE
		CALL 仕事追加処理関数(ARG, LOCAL)
		RESTART
ENDSELECT

@現在仕事数字文字列変換(ARG, ARG:1 = 0, キャラ番号 = 0)
#FUNCTIONS
#DIM キャラ番号
#DIMS 一時文字列

;固有業務
IF ARG >= 100000
	IF ARG:1
		CALLFORMF 固有仕事設定_{NO:キャラ番号}_{ARG - 100000}(キャラ番号, "仕事解説")
		RETURNF TSTR:ツールチップ受渡
	ENDIF
	CALLFORMF 固有仕事設定_{NO:キャラ番号}_{ARG - 100000}(キャラ番号, "仕事名")
	RETURNF TSTR:コマンド名受渡
ENDIF

SELECTCASE ARG
	CASE -1
		IF ARG:1
			RETURNF "在港口区域经营商店的状态。<br>效果：无特殊增益<br>　　　行为模式与普通商店老板相同。"
		ENDIF
		RETURNF "经营商店"
	CASE 0
		IF ARG:1
			RETURNF "未分配具体工作，处于待命状态。<br>效果：无特殊增益<br>　　　行为模式与普通客人相同。"
		ENDIF
		RETURNF "闲置"
	CASE 1
		IF ARG:1
			RETURNF "处理度假村内各项杂务的岗位。<br>效果：每日结束时，所有区域的待处理事务值<br>　　　减少［（工作等级＋１）＾２×１００］。<br>　　　在度假村内随机移动。"
		ENDIF
		RETURNF "处理杂务"
	CASE 2
		IF ARG:1
			LOCALS = 在特定区域执行专项工作的岗位。<br>效果：每日结束时，卢比收入<br>　　　增加［滞留人数×（工作等级＋１）＾３］。
			LOCALS += "<br><br>"
			IF 現在仕事:キャラ番号:1 == 2
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3002] 大厅：当前常驻区域</font><br>"
			ELSE
				LOCALS += @"<button value='3002'>[3002] 在大厅担任前台接待</button><br>"
			ENDIF
			IF 施設改造度:5:0
				IF 現在仕事:キャラ番号:1 == 503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3503] 酒吧区：当前常驻区域</font><br>"
				ELSE
					LOCALS += @"<button value='3503'>[3503] 在酒吧区担任调酒师</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:5:6
				IF 現在仕事:キャラ番号:1 == 506
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3506] 图书室：当前常驻区域</font><br>"
				ELSE
					LOCALS += @"<button value='3506'>[3506] 在图书室担任图书管理员</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:15:0
				IF 現在仕事:キャラ番号:1 == 1503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[4503] 泳池休息区 ：当前常驻区域</font><br>"
				ELSE
					LOCALS += @"<button value='4503'>[4503] 在泳池休息区担任救生员</button><br>"
				ENDIF
			ELSE
				IF 現在仕事:キャラ番号:1 == 15
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[3015] 泳池 ：当前常驻区域</font><br>"
				ELSE
					LOCALS += @"<button value='3015'>[3015] 在泳池担任救生员</button><br>"
				ENDIF
			ENDIF
			IF 空艇港初来訪イベントフラグ
				FOR LOCAL, 3, 9
					IF 空艇港店舗状況:LOCAL:0 > 0
						CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:LOCAL:0}(LOCAL)
						IF 現在仕事:キャラ番号:1 == LOCAL + 10000
							LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{10000 + LOCAL}] 店铺{LOCAL}:%TSTR:場所名受渡% ：当前常驻区域</font><br>"
						ELSE
							LOCALS += @"<button value='{10000 + LOCAL}'>[{10000 + LOCAL}] 店铺{LOCAL}:%TSTR:場所名受渡% 店员岗位</button><br>"
						ENDIF
					ENDIF
				NEXT
			ENDIF
			IF 海改造度:6:0
				IF 現在仕事:キャラ番号:1 == 30103
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[30103] 渔船团 ：当前常驻地点</font><br>"
				ELSE
					LOCALS += @"<button value='30103'>[30103] 渔船团 作为渔夫工作</button><br>"
				ENDIF
			ENDIF
			IF 海改造度:6:1 || 海改造度:6:2
				IF 海改造度:6:1
					IF 海改造度:6:2
						一時文字列 = 遊覧船・海底散歩
					ELSE
						一時文字列 = 遊覧船
					ENDIF
				ELSE
					一時文字列 = 海底散歩
				ENDIF
				IF 現在仕事:キャラ番号:1 == 30104
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[30104] %一時文字列% ：当前常驻地点</font><br>"
				ELSE
					LOCALS += @"<button value='30104'>[30104] %一時文字列% 作为海洋向导工作</button><br>"
				ENDIF
			ENDIF
			RETURNF LOCALS
		ENDIF
		RETURNF "常驻业务"
	CASE 3
		IF ARG:1
			RETURNF @"担任%CALLNAME:PLAYER%的辅助工作。<br>效果：每日结束时，信赖度<br>　　　增加［（工作等级＋１）＾２×２００］。<br>　　　在度假村内随机移动。"
		ENDIF
		RETURNF "秘书"
	CASE 4
		IF ARG:1
			RETURNF @"通过传单和广告提升度假村知名度的岗位。<br>效果：每日结束时，度假村人气值<br>　　　增加［（工作等级＋１）＾２×１］。<br>　　　在员工宿舍制作宣传物料。"
		ENDIF
		RETURNF "宣传专员"
	CASE 5
		IF 施設改造度:3:8 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"在训练场提升战斗能力的岗位。<br>效果：每日结束时获得经验值。<br>　　　获取量根据全角色最高等级动态调整。<br>　　　（无法超过当前全角色最高等级）<br>　　　常驻训练场。"
		ENDIF
		RETURNF "战斗训练"
	CASE 6
		IF あなた特殊能力:ダンジョン探索PRO < 1
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"前往已通关的迷宫自动收集素材的岗位。<br>效果：每日获取素材和少量经验值。<br>　　　获取量根据目标迷宫难度浮动。<br>　　　（需在「外出探索」中指定目标迷宫）<br>　　　当%CALLNAME:MASTER%未进行探索时效果提升。<br>　　　日间外出探索，傍晚返回宿舍。<br>　　　本岗位无工作等级机制。"
		ENDIF
		RETURNF "自动探索"
	CASE 7
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 前往学校进修特定知识领域的岗位。<br>效果：上午、下午课程结束后获得对应知识经验。<br>　　　非上课时段留在宿舍。<br>　　　已达成Lv2的知识不可重复选修。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL > 1
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%学习(現在Lv{知識素質:キャラ番号:LOCAL})</font>　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%选择学习(現在Lv{知識素質:キャラ番号:LOCAL})</font>　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 3000}'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%学习(现在Lv{知識素質:キャラ番号:LOCAL})</button>　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "获取知识"
	CASE 8
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 前往学校进行指定知识领域授课的岗位。<br>效果：提升在校学生角色的学习效率。<br>　　　非授课时段留在宿舍。<br>　　　仅限Lv2及以上知识可担任。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL < 2
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教授(现在Lv{知識素質:キャラ番号:LOCAL})</font>　　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%选择教授(现在Lv{知識素質:キャラ番号:LOCAL})</font>　　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 3000}'>[{LOCAL + 3000}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教授(现在Lv{知識素質:キャラ番号:LOCAL})</button>　　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "教师"
	CASE 9
		IF ARG:1
			RETURNF "负责度假村食堂烹饪工作的岗位。<br>效果：料理菜单带来的ZP收益。<br>　　　增加［（知识特质：厨师等级＋１）✕５］％。<br>　　　多人效果叠加计算，上限为<br>　　　［滞留人数÷１０］％。（含NPC与特殊角色）<br>　　　禁止安排厨师等级为负值的角色担任。"
		ENDIF
		RETURNF "主厨"
	CASE 10
		IF ARG:1
			RETURNF "负责度假村食堂侍应工作的岗位。<br>效果：料理菜单带来的卢比收益。<br>　　　提升［（工作等级＋１）✕５］％。<br>　　　多人效果叠加计算，上限为<br>［滞留人数÷１０］％。（含NPC与特殊角色）"
		ENDIF
		RETURNF "服务员"
	CASE 11
		IF ARG:1
			RETURNF @"搭乘高速艇执行外派任务的岗位。<br>效果：根据派遣地点产生不同收益。"
		ENDIF
		RETURNF "高速船船员"
	CASE 12
		IF ARG:1
			RETURNF "负责清除度假村外来威胁的防卫岗位。<br>效果：每日结束时，若存在特定异常事件<br>　　　按工作等级概率成功解决事件。<br>　　　（多人叠加概率）<br>　　　在度假村内随机巡逻。"
		ENDIF
		RETURNF "警卫"
ENDSELECT

@仕事条件判定関数(キャラ番号, 仕事番号)
#FUNCTION
#DIM キャラ番号
#DIM 仕事番号

SELECTCASE 仕事番号
	CASE 2,3,7,8,9,10,11
		SIF キャラ番号 == PLAYER
			RETURNF 0
	CASE 5,6,12
		SIF キャラ番号 == PLAYER
			RETURNF 0
		SIF TALENT:キャラ番号:非戦闘キャラ
			RETURNF 0
ENDSELECT

RETURNF 1


@仕事追加処理関数(キャラ番号, 選択番号)
#DIM キャラ番号
#DIM 選択番号

SELECTCASE 現在仕事:キャラ番号:0
	CASE 2
		;常駐する店舗を選べる
		IF 選択番号 > 10000
			現在仕事:キャラ番号:1 = 選択番号
		ELSE
			現在仕事:キャラ番号:1 = 選択番号 - 3000
		ENDIF
	CASE 7
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 3000
	CASE 8
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 3000
ENDSELECT

@仕事レベル存在チェック(チェック番号)
#FUNCTION
#DIM チェック番号

IF GROUPMATCH(チェック番号, 1, 2, 3, 4, 8, 10, 12)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@従業員休暇判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ

;ゲーム中に変動しない数値として、人物番号を使う
IF DAY % 7 == CFLAG:対象キャラ:人物番号 % 7
	RETURNF 1
ENDIF

RETURNF 0
