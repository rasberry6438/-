
@性癖開拓チャレンジ処理_寝取られ(ARG)
;寝取られ

;気になってどういうものか見に来たあなたに向けて簡単な仕様説明
;
;・性癖開拓システムでリゾート客(仮にＡとする)にエロ本を押し付けてＡの脳を破壊し、性癖素質[寝取られ(興味)]を与えます。
;・縁結びシステムを使って、Ａと他のキャラ(仮にＢとする)をくっつけます。関係性は恋慕でもセフレでもＯＫ。
;・あなたとＡの「信頼度」を、一定以上にします。その際あなたが「遊び人」として極まってきているなら、このボーダーはちょっとだけ下がります。Ａが「従業員」であるなら、このボーダーは劇的に下がります。
;上記のみっつの条件を、すべて満たせば準備完了！(別にＡもＢも両方寝取られ性癖持ってても大丈夫だよ！)
;
;「ＡとＢがどちらも島にいる」状態で、あなたが「自室」で「就寝」する際、「他に同衾するキャラがいなければ(一人寝であるなら)」確率でイベントが発生。
;一日の終わりに、度し難い性癖に脳を破壊されたＡがあなたの部屋を訪ねてきて、「Ｂを抱いてくれ」とお願いされます。性癖名は「寝取られ」だけど、Ａがやる実際のプレイは「寝取らせ」が正しいわけですな。
;いやぁ、頼まれちゃったからなぁ～！しょうがないなぁ～～～！！ほな、いただきま～～～っす！！
;
;毎日来られても困るし特別感もないので、発生確率は低め。「こんなもんかな～」で適当に決めてるので、低すぎる！と思ったら本スレとかDiscordで教えてください。調整します。
;実際問題、発生に一人寝を要求すること自体がプレイに対する一つの縛りになっちゃってるから、もうちょい高くてもいいかもな～とは書いた本人も思ってます。
;抽出処理とタイミングの関係で、条件を満たしたカップルが増えるほどに発生確率は平均化されちゃうし、狙った組み合わせも出づらくなるし。
;あなたの「遊び人」が５とかだと確率がちょっと上がる。理由は確率処理んとこに書いた。
;
;さいごに
;エロ本ばら撒きには要注意。みんながみんな性癖持ちだと、「男あなたにグラン(♂)を差し出してくるジータ(寝取られ性癖持ち)」とかそういう事故が起きます。
;まぁ、意図して除外処理しなかったんですけども。だ、だってそういう需要もあるかもだし……。

CSTR:ARG:性癖開拓報告_記録文字列 += @" %CALLNAME:PLAYER%巧妙地将关于「重要之人与他人交合情境」的色情书籍传递给了%CALLNAME:ARG%…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;固有処理
RESULT = 0
CALL 口上変数初期化
TRYCALLFORM 性癖開拓_固有条件_寝取られ_{NO:ARG}(ARG)
LOCAL = RESULT
SIF 口上有無確認(0, 1)
	CSTR:ARG:性癖開拓報告_記録文字列 += @"性癖開拓_固有条件"
IF LOCAL == 1
	CSTR:ARG:性癖開拓報告_記録文字列 += @"<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%获得了性癖特质[绿帽癖(兴趣)]<br>"
	性癖素質:ARG:寝取られ = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>获得性癖特质[绿帽癖(兴趣)]</font>","うふふ")
	RETURN 1
ELSEIF LOCAL == -1
	RETURN 0
ENDIF

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 性知識条件_性癖開拓チャレンジ()
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%随手翻开不知何时出现在身边的色情书籍，但似乎完全无法理解内容…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;叡智の殿堂補正
SIF 高速船記録:派遣先登録 == "智慧殿堂"
	LOCAL += 1
;自制心、プライド、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL -= TALENT:ARG:プライド
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;マゾ性感が影響
LOCAL += ABL:ARG:マゾ性感

;男性器サイズ
;自分の「男としての才能/素養」にコンプレックスがあるほどこういうのに陥りやすいんじゃなかろうか、という発想
IF TALENT:ARG:男性器サイズ == -2
	LOCAL += 2
ELSEIF TALENT:ARG:男性器サイズ == -1
	LOCAL += 1
ENDIF

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%发现了不知何时出现在身边的色情书籍。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"慌张地环顾四周确认无人后，%CALLNAME:ARG%偷偷翻开了书页……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"书中描绘重要之人被他人占有甚至夺走的情节，令%CALLNAME:ARG%"
	IF GETBIT(TALENT:ARG:性別, 1)
	;ちんちんあるなら 鬱勃起(七五調)(字余り)
		CSTR:ARG:性癖開拓報告_記録文字列 += "抑郁性勃起"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += "兴奋"
	ENDIF
	CSTR:ARG:性癖開拓報告_記録文字列 += @"难以抑制！<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"……最终被故事彻底侵蚀理智的%CALLNAME:ARG%，竟觉醒了性癖特质[绿帽癖(兴趣)]！<br>"
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>获得性癖特质[绿帽癖(兴趣)]</font>","うふふ")
	性癖素質:ARG:寝取られ = 1
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%对突然出现的色情书籍露出嫌恶表情，将其扔进了垃圾桶<br>"
ENDIF

RETURN 0

@性癖段階成長_寝取られ(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:寝取られ
	CASE 1
		;経験が足りないとダメ
		;いままで興味だったものが、実践を経て確信に変わり、自身の中に深く根付く。故に、愛好になるのは「最初の一歩を踏み出してしまったその直後」だと思う。異論は認める。
		SIF EXP:キャラ番号:寝取られ経験 < 1
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖特质[绿帽癖(兴趣)]已进化为[绿帽癖(喜好)]</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		;事が事だけに、5回もリピれば立派な中毒だと思う
		SIF EXP:キャラ番号:寝取られ経験 < 5
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖特质[绿帽癖(喜好)]已进化为[绿帽癖(成瘾)]</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_寝取られ(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%通过实际体验被戴绿帽行为，使绿帽癖作为确凿的性癖深深植根于灵魂……
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%的性癖特质[绿帽癖(兴趣)]已蜕变为[绿帽癖(喜好)]！
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%已然在向%CALLNAME:PLAYER%献上伴侣的过程中，感受到毁灭性的致命吸引力……
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%的性癖素质[绿帽癖(喜好)]已进化为[绿帽癖(成瘾)]！
		PRINTL
ENDSELECT

