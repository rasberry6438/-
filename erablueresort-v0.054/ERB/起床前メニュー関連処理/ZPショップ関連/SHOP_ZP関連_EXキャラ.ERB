;-------------------------------------------------
;ZPショップ：EXキャラの招待
;-------------------------------------------------
@ZP_SHOP_EXキャラ()
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 招待不可キャラリスト, キャラクタ数上限
#DIM 表示候補, キャラクタ数上限
#DIM 現在ページ
#DIM ページ数記憶
#DIM ページ判定
#DIM 候補番号
#DIM 入力番号
#DIM 描画開始行
#DIM 対象キャラ
#DIM 候補キャラ数
#DIM 候補キャラ数_シリーズ
#DIM START_NO
#DIM END_NO
#DIM NO_LEN
#DIM REDRAW_TEMP
#DIM L_CNT
#DIM リスタートフラグ
#DIMS BUTTON_TEXT
#DIM DYNAMIC 初回実行フラグ = 1
#DIM CONST 表示人数 = 15
#DIMS 出典作品配列, 500
#DIM 出典情報配列, 500
#DIM 出典表示候補, 500
#DIM 出典番号
#DIM 配列番号
#DIM 最初から詳細
#DIM 表示用番号
#DIM 値段保存
#DIM 選択シリーズ
#DIM 選択モード
#DIM シリーズモード
#DIM リスト更新判定
#DIM ソートモード
#DIM ソートモード前回値
#DIMS 招待後作品保存
#DIMS ソートキー, 2
#DIM BTN_NUM
#DIMS BTN_TEXT
#DIM CONST LINE_LEN  = 80
#DIM CONST SPACE_LEN = 56
#DIMS ページ番号文字列
#DIM ページ番号文字列長
#DIM 最大ページ
#DIMS ソート情報
; 内部変数：フィルタソートボタンセット用
#DIM フィルタボタン番号
#DIM ソートボタン番号
#DIM ソート順ボタン番号
#DIM 素質絞込ボタン番号

; RESTART時に現在ページをリセットしない
IF 初回実行フラグ
	初回実行フラグ = 0
	選択シリーズ = -1
	リスト更新判定 = 1
	現在ページ = 0
	ページ数記憶 = 0
	描画開始行 = LINECOUNT
	ソートモード = 0
	ソートモード前回値 = -1
	招待後作品保存 = 
	;招待不可キャラを抽出
	;・フィルタ/ソート操作時に毎回抽出すると処理速度が遅くなるためリストを作って使いまわす
	候補キャラ数 = 0
	FOR 対象キャラ, 1, CHARANUM
		CALL ZP_SHOP_EXキャラ_招待不可判定(対象キャラ)
		SIF !RESULT
			CONTINUE
		招待不可キャラリスト:候補キャラ数 = 対象キャラ
		候補キャラ数 ++
	NEXT
	;ソート設定をEXキャラ用に入れ替え
	CALL ZP_SHOP_EXキャラ_ソート設定入替()
	;描画設定を変更
	REDRAW_TEMP = CURRENTREDRAW()
	REDRAW 1
ENDIF

;表示キャラを登録
;・フィルタ処理の追加でリスト処理が重くなったので必要なタイミングでのみ更新するようにする
IF リスト更新判定
	リスト更新判定 = 0
	
	VARSET 表示候補, -1
	VARSET 出典作品配列
	VARSET 出典情報配列
	ソートキー:0 = 出典情報配列
	ソートキー:1 = 出典作品配列
	; リスト更新後にソートを実行するように前回値をリセット
	ソートモード前回値 = -1
	候補キャラ数 = 0
	出典番号 = 1
	出典作品配列:0 = 碧蓝幻想
	;表示キャラを登録
	ARRAYCOPY "招待不可キャラリスト", "表示候補"
	CALL キャラ配列フィルタ処理(表示候補, -1)
	候補キャラ数 = RESULT
	
	;-リスト表示対象の出典作品を記録
	FOR L_CNT, 0, 候補キャラ数
		対象キャラ = 表示候補:L_CNT
		IF CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品")) == ""
			;出典なし＝グラブルキャラとしてカウント
			出典情報配列:0 ++
		ELSEIF シリーズモード && MAP_HAS("シリーズ出典", CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品")))
			IF MATCH(出典作品配列, MAP_GET("シリーズ出典", CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品"))))
				;出典あり＆リストに登録済みの場合は人数をカウント
				配列番号 = FINDELEMENT(出典作品配列, MAP_GET("シリーズ出典", CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品"))), , , 1)
				出典情報配列:配列番号 ++
			ELSE
				;出典あり＆リストに未登録の場合は新規登録
				出典作品配列:出典番号 = %MAP_GET("シリーズ出典", CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品")))%
				出典情報配列:出典番号 ++
				出典番号 ++
			ENDIF
		ELSEIF MATCH(出典作品配列, CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品")))
			;出典あり＆リストに登録済みの場合は人数をカウント
			配列番号 = FINDELEMENT(出典作品配列, CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品")), , , 1)
			出典情報配列:配列番号 ++
		ELSE
			;出典あり＆リストに未登録の場合は新規登録
			出典作品配列:出典番号 = %CSVCSTR(NO:対象キャラ, GETNUM(CSTR, "出演作品"))%
			出典情報配列:出典番号 ++
			出典番号 ++
		ENDIF
	NEXT
ENDIF

;ソートモードが切り替わった場合はソートする
;・ARRAYMSORTEXが安定ソートではないようで、モード切替、リスト更新時のみソートを実行
;　(制限しないと人数順ソートでリストを表示するごとに人数が同じの作品の順番が変わる)
IF ソートモード != ソートモード前回値
	ソートモード前回値 = ソートモード
	SELECTCASE ソートモード
		CASE 0
			;人数順
			ARRAYMSORTEX "出典情報配列", ソートキー, 0, 出典番号
		CASE 1
			;シリーズ名順
			ARRAYMSORT 出典作品配列, 出典情報配列
	ENDSELECT
ENDIF

IF 招待後作品保存 != ""
	選択シリーズ = FINDELEMENT(出典作品配列, 招待後作品保存)
ENDIF

;広告対象の表示
PRINTL
DRAWLINE
PRINTL 消耗ZP，引导原本无法造访度假村的角色来到度假村。
PRINTL 要引导哪位角色？
IF ZPキャラ無料回数
	SETCOLOR カラーパレット("黄")
	PRINTFORML 剩余{ZPキャラ無料回数, 2}次免费引导！
	RESETCOLOR
ENDIF
PRINTFORML 持有ZP:%ZP所持量全文字列()%
DRAWLINE
IF 選択モード
	; 個別選択
	最大ページ = MAX((候補キャラ数 + 表示人数 - 1) / 表示人数, 1) - 1
	PRINTBUTTON "[系列选择]", 9100
	PRINT / 
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [单独选择]
	RESETCOLOR
	PRINTL
	DRAWLINE
	;- キャラの表示
	; 現在のページで表示する番号を計算
	START_NO = (現在ページ * 表示人数)
	END_NO   = (現在ページ * 表示人数) + 表示人数 - 1

	; 番号の最大桁数を取得
	NO_LEN = LOG10(END_NO) + 1

	; キャラ表示ループ
	FOR L_CNT, START_NO, END_NO + 1
		対象キャラ = 表示候補:L_CNT
		; 表示するキャラがいなくなったら表示終了
		IF 対象キャラ == -1
			PRINTL
			CONTINUE
		ENDIF
		; ZPショップ値段の設定がないキャラは50に設定
		SIF CFLAG:対象キャラ:ZPショップ値段 == 0
			CFLAG:対象キャラ:ZPショップ値段 = 50
		; ボタン表示
		BUTTON_TEXT '= @"[{L_CNT, NO_LEN}] %GET_NAME(対象キャラ, 1)%"
		BUTTON_TEXT += @"：花费ZP{CFLAG:対象キャラ:ZPショップ値段, 4, RIGHT}"
		SIF ZPキャラ無料回数
			BUTTON_TEXT += " → 0"
		ソート情報 '= ソート情報表示(対象キャラ)
		SIF ソート情報 != ""
			BUTTON_TEXT += "　" + ソート情報
		PRINTBUTTON BUTTON_TEXT, L_CNT
		PRINTL
	NEXT
ELSE
	; シリーズ選択
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [系列选择
	IF 選択シリーズ >= 0
		PRINTFORM ：%出典作品配列:選択シリーズ%
	ENDIF
	PRINT ]
	RESETCOLOR
	PRINT / 
	PRINTBUTTON "[单独选择]", 9100
	PRINT 　　　
	IF 選択シリーズ < 0
		IF シリーズモード
			PRINTBUTTON "[单独显示系列]", 9101
		ELSE
			PRINTBUTTON "[批量显示系列]", 9101
		ENDIF
	ENDIF
	PRINTL
	DRAWLINE
	IF 選択シリーズ >= 0
		; シリーズ選択/キャラ表示
		最大ページ = MAX((候補キャラ数_シリーズ + 表示人数 - 1) / 表示人数, 1) - 1
		;- キャラの表示
		; 現在のページで表示する番号を計算
		START_NO = (現在ページ * 表示人数)
		END_NO   = (現在ページ * 表示人数) + 表示人数 - 1

		; 番号の最大桁数を取得
		NO_LEN = LOG10(END_NO) + 1

		; キャラ表示ループ
		FOR L_CNT, START_NO, END_NO + 1
			対象キャラ = 出典表示候補:L_CNT
			; 表示するキャラがいなくなったら表示終了
			IF 対象キャラ == -1
				PRINTL
				CONTINUE
			ENDIF
			; ZPショップ値段の設定がないキャラは50に設定
			SIF CFLAG:対象キャラ:ZPショップ値段 == 0
				CFLAG:対象キャラ:ZPショップ値段 = 50
			; ボタン表示
			BUTTON_TEXT '= @"[{L_CNT, NO_LEN}] %GET_NAME(対象キャラ, 1)%"
			BUTTON_TEXT += @"：花费ZP{CFLAG:対象キャラ:ZPショップ値段, 4, RIGHT}"
			SIF ZPキャラ無料回数
				BUTTON_TEXT += " → 0"
			ソート情報 '= ソート情報表示(対象キャラ)
			SIF ソート情報 != ""
				BUTTON_TEXT += "　" + ソート情報
			PRINTBUTTON BUTTON_TEXT, L_CNT
			PRINTL
		NEXT
	ELSE
		; シリーズ選択/シリーズ表示
		最大ページ = MAX((出典番号 + 表示人数 - 1) / 表示人数, 1) - 1
		;シリーズの表示（15作品まで）
		START_NO = (現在ページ * 表示人数)
		END_NO   = (現在ページ * 表示人数) + 表示人数 - 1
		FOR LOCAL, START_NO, END_NO + 1
			IF 出典作品配列:LOCAL == ""
				PRINTL
				CONTINUE
				;登録が終わったら表示終了
				BREAK
			ENDIF
			PRINTBUTTON @"[{LOCAL, 3}] %出典作品配列:LOCAL, 41, LEFT%：剩余{出典情報配列:LOCAL, 3}名", LOCAL
			PRINTL 
		NEXT
		SKIPDISP 0
	ENDIF
ENDIF

;- ページ操作ボタン
PRINTSL "-" * LINE_LEN

;ページめくり表示
BTN_NUM   = 9001
BTN_TEXT '= @"[上一页]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
; ページ数
ページ番号文字列  '= @"- {現在ページ + 1, LOG10(MAX(最大ページ +1, 1)) + 1}/{最大ページ + 1, LOG10(MAX(最大ページ +1, 1)) + 1} -"
ページ番号文字列長 =SPACE_LEN - STRLENS(ページ番号文字列)
PRINTS " " * (ページ番号文字列長 / 2)
PRINTFORM %ページ番号文字列%
; 割り切れなかった分のスペースは後半に足す
PRINTS " " * (ページ番号文字列長 / 2 + (ページ番号文字列長 % 2))

; 1ページ次へ
BTN_NUM   = 9011
BTN_TEXT '= @"[下一页]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTL

; 最初のページへ
BTN_NUM   = 9009
BTN_TEXT '= "[|≪]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTS " " * 1
; 5ページ前へ
BTN_NUM   = 9005
BTN_TEXT '= "[－５]"
IF 現在ページ > 0
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
; 余白
PRINTS " " * SPACE_LEN

; 5ページ次へ
BTN_NUM   = 9015
BTN_TEXT '= "[＋５]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTS " " * 1
; 最後のページへ
BTN_NUM   = 9019
BTN_TEXT '= "[≫|]"
IF 現在ページ < 最大ページ
	PRINTBUTTON BTN_TEXT, BTN_NUM
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM %BTN_TEXT%
	RESETCOLOR
ENDIF
PRINTL

;- その他ボタン
;フィルタソートボタン
フィルタボタン番号 = 9020
ソートボタン番号   = 9030
ソート順ボタン番号 = 9031
素質絞込ボタン番号 = 9040
PRINTSL "-" * LINE_LEN
IF 選択モード == 0 && 選択シリーズ >= 0
	; シリーズ選択中はフィルタ設定不可(フィルタを切り替えた際のリスト更新がうまくいかないので)
	SETCOLOR カラーパレット("グレーアウト")
	PRINTL ※系列选择中无法变更筛选/排序设置
	PRINTL
	PRINTL
	RESETCOLOR
ELSE
	CALL フィルタソートボタンセット表示(フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号)
ENDIF
;戻る系ボタン
PRINTSL "-" * LINE_LEN
IF 選択モード == 0 && 選択シリーズ >= 0
	PRINTBUTTON "[9997]解除系列选择", 9997
	PRINT 　　　
	PRINTBUTTON "[9998]召唤该系列所有角色", 9998
	PRINTL
ENDIF

IF 選択シリーズ < 0
	PRINTBUTTONLC "[9999]返回", 9999
	IF 選択モード == 0
		SELECTCASE ソートモード
			CASE 0
				PRINTBUTTON "[9996]按系列名称排序", 9996
			CASE 1
				PRINTBUTTON "[9996]按登记角色数排序", 9996
		ENDSELECT
	ENDIF
ENDIF
REDRAW 0

;入力処理
WHILE 1
	BINPUT
	入力番号 = RESULT
	SELECTCASE 入力番号
		CASE 9100
			;- [单独选择]
			INVERTBIT 選択モード, 0
			選択シリーズ = -1
			現在ページ = 0
			リスタートフラグ = 1
			BREAK
		CASE 9101
			;- [シリーズをまとめる]
			INVERTBIT シリーズモード, 0
			選択シリーズ = -1
			現在ページ = 0
			リスト更新判定 = 1
			リスタートフラグ = 1
			BREAK
		CASE 9001, 9005
			;- 前ページへ
			; ページ番号を更新後、リスタートしてリスト表示へ戻る
			現在ページ = MAX(現在ページ - (入力番号 - 9000), 0)
			リスタートフラグ = 1
			BREAK
		CASE 9011, 9015
			;- 次ページへ
			現在ページ = MIN(現在ページ + (入力番号 - 9010), 最大ページ)
			リスタートフラグ = 1
			BREAK
		CASE 9009
			; 最初のページ
			現在ページ = 0
			リスタートフラグ = 1
			BREAK
		CASE 9019
			; 最後のページ
			現在ページ = 最大ページ
			リスタートフラグ = 1
			BREAK
		CASE フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号
			;- キャラフィルタソート設定
			CALL フィルタソートボタンセット実行部(入力番号, フィルタボタン番号, ソートボタン番号, ソート順ボタン番号, 素質絞込ボタン番号)
			リスト更新判定 = 1
			現在ページ = 0
			リスタートフラグ = 1
			BREAK
		CASE 9999
			;- 戻る
			; リスタートせずにZPショップメニューへ戻る
			リスタートフラグ = 0
			BREAK
		CASE 9997
			;- シリーズ選択解除処理
			選択シリーズ = -1
			現在ページ = ページ数記憶
			ページ数記憶 = 0
			リスタートフラグ = 1
			招待後作品保存 = 
			BREAK
		CASE 9996
			;- 作品ソートモード切替処理
			INVERTBIT ソートモード, 0
			現在ページ = 0
			リスタートフラグ = 1
			BREAK
		CASE 9998
			;- シリーズまとめて招待処理
			PRINTFORML 将引导%出典作品配列:選択シリーズ%系列的{出典情報配列:選択シリーズ}名角色。
			PRINTL 是否确认执行？
			PRINTBUTTONLC "[0]确定", 0 
			PRINTBUTTONLC "[1]取消", 1
			PRINTL
			BINPUT
			IF RESULT == 0
				PRINTL 
				PRINTL 
				FOR 対象キャラ, 0, CHARANUM
					SIF 出典表示候補:対象キャラ == -1
						BREAK

					IF CFLAG:(出典表示候補:対象キャラ):ZPショップ値段 > FLAG:ZP所持量 && ZPキャラ無料回数 < 1
						PRINTW ZP不足，停止引导
						RESTART
					ENDIF

					PRINTFORM %NAME表示(出典表示候補:対象キャラ), 41, LEFT%已被引导。
					IF 滞在キャラ上限 - 滞在キャラ数 - 集客人数 - FLAG:滞在キャラ枠先確保分 > 0
						PRINTL 起床后将造访度假村。
						解放キャラ招待:(FINDELEMENT(解放キャラ招待, 0)) = 出典表示候補:対象キャラ
						FLAG:滞在キャラ枠先確保分 += 1
					ELSE
						PRINTL 因住宿人数已满，将追加至后续招待对象。
					ENDIF
					CFLAG:(出典表示候補:対象キャラ):招待不可フラグ = 0
					IF OPTION変数:ZPキャラ自動例外追加 == 0
						CFLAG:(出典表示候補:対象キャラ):例外招待 = 1
					ENDIF
					IF ZPキャラ無料回数
						ZPキャラ無料回数 = MAX(0, ZPキャラ無料回数 - 1)
					ELSE
						FLAG:ZP所持量 -= CFLAG:(出典表示候補:対象キャラ):ZPショップ値段
					ENDIF
					ARRAYREMOVE 招待不可キャラリスト, FINDELEMENT(招待不可キャラリスト, 出典表示候補:対象キャラ), 1
					TRYCALLFORM ZP_RELEASE_CHARAEVENT_{NO:(出典表示候補:対象キャラ)}(出典表示候補:対象キャラ)
				NEXT
				PRINTL 
				PRINTL 
				PRINTW
				選択シリーズ = -1
				現在ページ = 0
				招待後作品保存 = 
				VARSET 出典表示候補, -1
				候補キャラ数_シリーズ = 0
				; 人数が減るのでリスト更新
				リスト更新判定 = 1
				リスタートフラグ = 1
				BREAK
			ELSE
				リスタートフラグ = 1
				BREAK
			ENDIF
		CASEELSE
			IF 選択モード || 選択シリーズ >= 0
				;- キャラ選択時
				候補番号 = 入力番号
				IF 選択モード
					表示用番号 = 候補キャラ数
				ELSE
					表示用番号 = 候補キャラ数_シリーズ
				ENDIF
				最初から詳細 = 0
				;- キャラ招待ループ
				WHILE 1
					IF 選択モード
						対象キャラ = 表示候補:候補番号
					ELSE
						対象キャラ = 出典表示候補:候補番号
					ENDIF
					CALL ZP_SHOP_EXキャラ_招待確認(対象キャラ, 最初から詳細)
					最初から詳細 = 0
					SELECTCASE RESULT
						CASE -1
							;- キャンセル
							; リスタートしてリスト表示へ戻る
							リスタートフラグ = 1
							BREAK
						CASE 1
							;- キャラ招待実行後
							; リスタートしてリスト表示へ戻る
							IF 選択モード == 0
								;シリーズ表示中なら配列から抜いておく
								ARRAYREMOVE 出典表示候補, 候補番号, 1
								;表示維持用
								招待後作品保存 '= 出典作品配列:選択シリーズ
							ENDIF
							; 招待したキャラを招待不可キャラリストから削除
							ARRAYREMOVE 招待不可キャラリスト, FINDELEMENT(招待不可キャラリスト, 対象キャラ), 1
							; 人数が減るのでリスト更新
							リスト更新判定 = 1
							リスタートフラグ = 1
							BREAK
						CASE 2
							;- 前のキャラ
							; 候補番号を更新、ループして前のキャラを表示
							候補番号 -=  1
							SIF 候補番号 < 0
								候補番号 = 表示用番号 - 1
						CASE 3
							;- 次のキャラ
							; 候補番号を更新、ループして次のキャラを表示
							候補番号 +=  1
							SIF 候補番号 >= 表示用番号
								候補番号 = 0
						CASE 12
							;- 前のキャラ
							; 候補番号を更新、ループして前のキャラを表示
							最初から詳細 = 1
							候補番号 -=  1
							SIF 候補番号 < 0
								候補番号 = 表示用番号 - 1
						CASE 13
							;- 次のキャラ
							; 候補番号を更新、ループして次のキャラを表示
							最初から詳細 = 1
							候補番号 +=  1
							SIF 候補番号 >= 表示用番号
								候補番号 = 0
					ENDSELECT
				WEND
				; 表示していたキャラにあわせてページ位置を更新
				現在ページ = 候補番号 / 表示人数
				BREAK
			ELSE
				;- シリーズ選択時処理
				選択シリーズ = 入力番号
				ページ数記憶 = 現在ページ
				招待後作品保存 = 
				現在ページ = 0
				リスタートフラグ = 1
				VARSET 出典表示候補, -1
				候補キャラ数_シリーズ = 0
				FOR 対象キャラ, 0, CHARANUM
					SIF 表示候補:対象キャラ == -1
						BREAK
					IF シリーズモード && MAP_GET("シリーズ出典", CSVCSTR(NO:(表示候補:対象キャラ), GETNUM(CSTR, "出演作品"))) == 出典作品配列:選択シリーズ
						出典表示候補:候補キャラ数_シリーズ = 表示候補:対象キャラ
						候補キャラ数_シリーズ ++
					ELSEIF CSVCSTR(NO:(表示候補:対象キャラ), GETNUM(CSTR, "出演作品")) == 出典作品配列:選択シリーズ
						出典表示候補:候補キャラ数_シリーズ = 表示候補:対象キャラ
						候補キャラ数_シリーズ ++
					ELSEIF CSVCSTR(NO:(表示候補:対象キャラ), GETNUM(CSTR, "出演作品")) == "" && 出典作品配列:選択シリーズ == "碧蓝幻想"
						出典表示候補:候補キャラ数_シリーズ = 表示候補:対象キャラ
						候補キャラ数_シリーズ ++
					ENDIF
				NEXT
				BREAK
			ENDIF
	ENDSELECT
WEND
IF リスタートフラグ
	CLEARLINE LINECOUNT - 描画開始行
	RESTART
ENDIF
;ソート設定を戻す
CALL ZP_SHOP_EXキャラ_ソート設定入替()
;描画設定を戻す
REDRAW REDRAW_TEMP
RETURN -1

;-------------------------------------------------
;EXキャラ用のソート設定入れ替え
;-------------------------------------------------
@ZP_SHOP_EXキャラ_ソート設定入替()
#DIM L_CNT

FOR L_CNT, 0, VARSIZE("キャラ一覧フィルタ")
	SWAP キャラ一覧フィルタ:L_CNT, キャラ一覧フィルタ_EXキャラ:L_CNT
NEXT
SWAP 弱点コマンド検索タイプ, 弱点コマンド検索タイプ_EXキャラ
FOR L_CNT, 0, VARSIZE("キャラ一覧ソート")
	SWAP キャラ一覧ソート:L_CNT, キャラ一覧ソート_EXキャラ:L_CNT
NEXT
FOR L_CNT, 0, VARSIZE("キャラ一覧ソート")
	SWAP キャラ一覧素質絞り込み:L_CNT, キャラ一覧素質絞り込み_EXキャラ:L_CNT
NEXT

;-------------------------------------------------
;招待不可判定
;-------------------------------------------------
@ZP_SHOP_EXキャラ_招待不可判定(キャラ番号)
#DIM キャラ番号
;- リスト表示対象チェック
; 招待不可フラグのチェック
SELECTCASE CFLAG:キャラ番号:招待不可フラグ
	CASE 0
		;- 招待可能キャラ
		; 表示対象外
		RETURN 0
	CASE 1
		;- 招待不可キャラ
		; 無条件で表示対象
	CASE 2
		;- 招待不可キャラ (ZPショップ対象外)
		; 表示対象外
		RETURN 0
	CASE 3
		;- 招待不可キャラ (他作品キャラ)
		; 前提イベントをクリアしていない場合は不可
		SIF イベント完了フラグ確認("「平行世界」という場所") < 1
			RETURN 0
ENDSELECT
; キャラ固有の条件がある場合、その達成していない場合は不可
TRYCCALLFORM EXキャラ_表示条件_{NO:キャラ番号}
	SIF RESULT == 0
		RETURN 0
CATCH
ENDCATCH
RETURN 1


;-------------------------------------------------
;EXキャラの招待確認
;-------------------------------------------------
@ZP_SHOP_EXキャラ_招待確認(対象キャラ, 最初から詳細)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM 入力番号
#DIM 描画開始行
#DIM 対象画像数
#DIM 最初から詳細
#DIM リロールボタン有効
#DIM 実行結果
#DIM TFLAG保存
#DIM リスタートフラグ
#DIM DYNAMIC 初回フラグ = 1

IF 初回フラグ
	初回フラグ = 0
	; 番号の範囲チェック
	SIF !INRANGE(対象キャラ, 0, CHARANUM - 1)
		RETURN -1
	; 所持ZPチェック
	; ・ZPが足りなくても簡易紹介を表示したいのでZPの確認は後に移動
;	IF CFLAG:対象キャラ:ZPショップ値段 > FLAG:ZP所持量
;		PRINTW ZP不足
;		RETURN
;	ENDIF
	描画開始行 = LINECOUNT
ENDIF

SIF 最初から詳細
	GOTO 最初から詳細_場所

PRINTL
; キャラ確認用の簡易紹介
CALL キャラ簡易紹介(対象キャラ)
対象画像数 = RESULT
リロールボタン有効 = 対象画像数 > 5
PRINTL
; 確認メッセージ
PRINTFORML 引导%GET_NAME(対象キャラ)%。
PRINTL 是否确认执行？
; ボタン表示
PRINTBUTTONLC "[0]确定", 0
PRINTBUTTONLC "[1]取消", 1
PRINTBUTTONLC "[10]详细能力", 10
PRINTL
PRINTBUTTONLC "[2]上一个角色", 2
PRINTBUTTONLC "[3]下一个角色", 3
SIF リロールボタン有効
	PRINTBUTTONLC "[4]重新随机显示画像", 4
PRINTL

; 入力ループ
WHILE 1
	INPUT
	入力番号 = RESULT
	SELECTCASE 入力番号
		CASE 0
			;- [0]はい
			; ZPが足りるかチェックしてキャラ開放
			IF ZPキャラ無料回数 || FLAG:ZP所持量 >= CFLAG:対象キャラ:ZPショップ値段
				; キャラ開放して処理終了
				CALL EXキャラ開放(対象キャラ)
				実行結果 = 1
				リスタートフラグ = 0
			ELSE
				; エラー表示
				PRINTW ZP不足
			ENDIF
			BREAK
		CASE 1
			;- [1]いいえ
			; 何もせず処理終了
			実行結果 = -1
			リスタートフラグ = 0
			BREAK
		CASE 4
			;- [4]画像のランダム表示をリロール
			; リスタートして再表示
			IF リロールボタン有効
				実行結果 = 0
				リスタートフラグ = 1
				BREAK
			ENDIF
		CASE 2
			;- [2]前のキャラ
			; 何もせず処理終了(キャラ変更処理は関数外で実行)
			実行結果 = 2
			リスタートフラグ = 0
			BREAK
		CASE 3
			;- [3]次のキャラ
			; 何もせず処理終了(キャラ変更処理は関数外で実行)
			実行結果 = 3
			リスタートフラグ = 0
			BREAK
		CASE 10
			;- [10]詳細能力
			; 詳細画面を読んでからリスタートして再表示
			$最初から詳細_場所
			TFLAG保存 = TFLAG:調教モード
			TFLAG:調教モード = -2
			CALL PRINT_STATE(対象キャラ, 3)
			TFLAG:調教モード = TFLAG保存
			SELECTCASE RESULT
				CASE 100
					;前のキャラへ
					; 何もせず処理終了(キャラ変更処理は関数外で実行)
					実行結果 = 12
					リスタートフラグ = 0
				CASE 101
					;次のキャラへ
					; 何もせず処理終了(キャラ変更処理は関数外で実行)
					実行結果 = 13
					リスタートフラグ = 0
				CASE 1
					;はい
					; ZPが足りるかチェックしてキャラ開放
					IF ZPキャラ無料回数 || FLAG:ZP所持量 >= CFLAG:対象キャラ:ZPショップ値段
						; キャラ開放して処理終了
						CALL EXキャラ開放(対象キャラ)
						実行結果 = 1
						リスタートフラグ = 0
					ELSE
						; エラー表示
						PRINTW ZP不足
					ENDIF
				CASE 0
					;いいえ
					; 何もせず処理終了
					実行結果 = -1
					リスタートフラグ = 0
			ENDSELECT
			BREAK
		CASEELSE
			; 入力エラー
	ENDSELECT
	; 入力エラー時は消してやり直し
	CLEARLINE 1
	REUSELASTLINE 
WEND

; 表示した内容を消去
CLEARLINE LINECOUNT - 描画開始行

; リスタート or 終了
SIF リスタートフラグ
	RESTART

; 簡易紹介で使用したリソースを解放
CALL ランダム画像表示_リソース解放(1)
RETURN 実行結果

;-------------------------------------------------
;EXキャラの開放
;-------------------------------------------------
@EXキャラ開放(対象キャラ, 解放のみフラグ = 0)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 対象キャラ
#DIM L_CNT
#DIM 滞在フラグ
#DIM 解放のみフラグ

;解放のみは文章を全部出さない
SIF 解放のみフラグ
	SKIPDISP 1

PRINTFORML %GET_NAME(対象キャラ)%已被引导。

;- 招待したEXキャラを今日の招待枠に追加
;・既に上限まで滞在/招待している場合は不可
IF 滞在キャラ上限 - 滞在キャラ数 - 集客人数 > 0 && CFLAG:対象キャラ:滞在期間 < 0
	PRINTW 起床后将造访度假村。
	FOR L_CNT, 0, 100
		IF 解放キャラ招待:L_CNT == 0
			解放キャラ招待:L_CNT = 対象キャラ
			FLAG:滞在キャラ枠先確保分 += 1
			BREAK
		ENDIF
	NEXT
	滞在フラグ = 1
ELSE
	PRINTW 因住宿人数已满，将追加至后续招待对象。
	滞在フラグ = 0
ENDIF
; 招待不可状態の解除
CFLAG:対象キャラ:招待不可フラグ = 0
IF OPTION変数:ZPキャラ自動例外追加 == 0
	CFLAG:対象キャラ:例外招待 = 1
ENDIF
; ZPの支払い
IF 解放のみフラグ
	;何もしない
ELSEIF !ZPキャラ無料回数
	FLAG:ZP所持量 -= CFLAG:対象キャラ:ZPショップ値段
ELSE
	; 無料招待回数を減らす
	ZPキャラ無料回数 = MAX(0, ZPキャラ無料回数 - 1)
ENDIF
SKIPDISP 0

;キャラ固有の解放時処理
TRYCALLFORM キャラZP解放時_固有処理_{NO:対象キャラ}(対象キャラ)

RETURN 滞在フラグ

