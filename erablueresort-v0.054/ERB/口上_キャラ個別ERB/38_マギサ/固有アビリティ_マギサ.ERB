;--------------------------------------------------
;固有アビリティ_マギサ
;--------------------------------------------------

@固有アビ_38_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 威力
#DIM 効果成功率
#DIMS アビ名

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
		アビ名 = 爆发＋＋
		威力 = 280
		効果成功率 = 100
		消費ＭＰ = 80
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
		アビ名 = 爆发＋
		威力 = 250
		効果成功率 = 95
		消費ＭＰ = 70
	ELSE
		アビ名 = 爆发
		威力 = 200
		効果成功率 = 90
		消費ＭＰ = 60
	ENDIF

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>对敌方全体造成［攻击力✕{威力}％］的自身属性伤害。"
			詳細文文字列受け渡し変数 += @"<br>［2％］赋予（常规槽・3回合）的灼热效果。成功率{効果成功率}％。"

			RETURN 消費ＭＰ
		CASE "アビ効果"
			;攻撃部分
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_自属性

			;火傷部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 2
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
	RETURN RESULT


@固有アビ_38_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 防御バフ値

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 36
		アビ名 = 壁障＋＋
		消費ＭＰ = 60
		防御バフ値 = 60
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名 = 壁障＋
		消費ＭＰ = 50
		防御バフ値 = 40
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
		アビ名 = 壁障
		消費ＭＰ = 30
	ELSE
		アビ名 = 
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = 魔力屏障展開！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>为我方全体展开能抵挡1次异常状态与弱化效果的魔法屏障。"
			SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				詳細文文字列受け渡し変数 += @"<br>赋予［{防御バフ値}％］（独立槽・1次）的受伤害减少效果。"

			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>赋予我方全体（1次）弱化无效效果。"
			SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				詳細文文字列受け渡し変数 += @"<br>赋予［{防御バフ値}％］（独立槽・1次）的受伤害减少效果。"
		CASE "アビ効果"
			;弱体無効付与
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 弱体無効
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット

			;防御バフ
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果割合 = 防御バフ値
				アビ汎用変数:持続回数 = 1
				アビ汎用文字列:バフ・デバフ枠 = 壁障_防御
				アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
	RETURN RESULT


@固有アビ_38_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 威力
#DIM 効果成功率
#DIMS アビ名

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 44
		アビ名 = 沃普尔吉斯＋＋
		威力 = 200
		効果成功率 = 90
		消費ＭＰ = 130
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
		アビ名 = 沃普尔吉斯＋
		威力 = 150
		効果成功率 = 85
		消費ＭＰ = 100
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
		アビ名 = 沃普尔吉斯
		威力 = 125
		効果成功率 = 80
		消費ＭＰ = 80
	ELSE
		アビ名 = 
	ENDIF

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>对敌方单体造成［攻击力✕{威力}％］的自身属性伤害。"
			詳細文文字列受け渡し変数 += @"<br>造成［攻击力✕{威力}％］的暗属性伤害。"
			詳細文文字列受け渡し変数 += @"<br>赋予［50％］（独立槽・3回合）的黑暗效果。成功率{効果成功率}％。"
			詳細文文字列受け渡し変数 += @"<br>赋予［50％］（独立槽・3回合）的回避率下降效果。成功率{効果成功率}％。"

			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵単体

			;自属性攻撃
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_自属性

			;闇属性攻撃
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_闇属性

			;暗闇付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ヴァルプ_暗闇
			アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_不利効果_デバフ効果セット

			;回避率デバフ付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ヴァルプ_回避
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
	RETURN RESULT


@固有アビ_38_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(キャラ番号)

	IF 固有38_4_制裁取得条件() == 1
		アビ名 = 制裁
		消費ＭＰ = 120
	ELSE
		アビ名 = 
	ENDIF

	SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 > 0
		アビテンプレート用_アビ封印用フラグ = 1

	アビ汎用文字列:実行時メッセージ１ = 响应召唤，魔神摩拉克斯那巨大的身姿显现了！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>召唤魔神摩拉克斯。"
			詳細文文字列受け渡し変数 += @"<br>此技能每场战斗仅可使用一次。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>赋予自身［累积5％最大99999％］（独立槽・不可驱散）的攻击力提升效果。"
			詳細文文字列受け渡し変数 += @"<br>赋予［100％］（独立槽・不可驱散）的追击效果。"
			詳細文文字列受け渡し変数 += @"<br>赋予［100］（独立槽・不可驱散）的自伤效果。"
			詳細文文字列受け渡し変数 += @"<br>回合结束时，赋予［累积5％最大99999％］（独立槽・不可驱散）攻击力提升效果。"
			詳細文文字列受け渡し変数 += @"<br>此技能每场战斗仅可使用一次。"
		CASE "アビ効果"
			;モラクスの「攻撃」「追撃」「自傷」は3つセットで扱うこと
			CALL アビ対象選択テンプレート_自己のみ
			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):2 = 現在ターン数

			;攻撃バフ
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_攻撃
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果割合 = 5
			アビ汎用変数:累積上限_固定値 = 99999
			CALL アビテンプレート_有利効果_累積バフ効果セット

			;追撃バフ
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_追撃
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_有利効果_バフ効果セット

			;自傷デバフ
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_自傷
			アビ汎用文字列:バフ・デバフ対象能力 = 自傷
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果量 = 100
			アビ汎用変数:弱体耐性貫通オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
	
			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):1 = 1
	ENDSELECT
	RETURN RESULT


;ターン終了時処理関数から呼び出し
@固有ターン終了時処理_キャラ_38
#DIM バフ・デバフ行数
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(GETCHARA(38))

	バフ・デバフ行数 = バフ番号取得_枠("モラクス_攻撃", 隊列番号)
	IF バフ・デバフ行数 > -1
		アビ汎用文字列:実行時メッセージ１ = 魔神摩拉克斯的力量在不断增强……！

		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = モラクス_攻撃
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:効果割合 = 5
		アビ汎用変数:累積上限_固定値 = 99999
		CALL アビテンプレート_有利効果_累積バフ効果セット
	ENDIF


@固有アビ_38_5(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(キャラ番号)

	IF 固有38_4_制裁取得条件() == 1
		アビ名 = 解放
		消費ＭＰ = 120
	ELSE
		アビ名 = 
	ENDIF

	SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 != 1
		アビテンプレート用_アビ封印用フラグ = 1

	アビ汎用文字列:実行時メッセージ１ = 积蓄的魔力被一口气解放了……！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>解放召唤中的摩拉克斯的力量。"
			IF 隊列番号 > -1
				詳細文文字列受け渡し変数 += @"<br>对敌方全体造成［攻击力✕维持摩拉克斯的回合数（{現在ターン数 - 戦闘中カウント:隊列番号:2}）✕40％］的自身属性伤害。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>对敌方全体造成［攻击力✕维持摩拉克斯的回合数✕40％］的自身属性伤害。"
			ENDIF
			詳細文文字列受け渡し変数 += @"<br>消除所有通过技能「制裁」获得的效果。"
			詳細文文字列受け渡し変数 += @"<br>本技能仅在发动「制裁」后可使用，每场战斗仅可使用一次。"
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 40 * (現在ターン数 - 戦闘中カウント:隊列番号:2)
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:消去不可バフデバフ削除オプション = 1
			CALL アビテンプレート_不利効果_バフ消去("モラクス_追撃")
			CALL アビテンプレート_不利効果_バフ消去("モラクス_攻撃")
			CALL アビテンプレート_有利効果_デバフ消去("モラクス_自傷")

			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):1 += 1
	ENDSELECT
	RETURN RESULT


;特殊処理：制裁取得チェック
@固有38_4_制裁取得条件()
#FUNCTION

	;制裁習得条件を突破していたら１を返す
	SIF イベント完了フラグ確認("魔女と晩酌") > 0
		RETURNF 1

	;覚えてなかったらゼロを返す
	RETURNF 0

