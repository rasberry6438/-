
@固有アビ_62_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 剛毅果断
#DIM デバフ行数

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = クリムゾンナイトメア+
	消費ＭＰ = 130
ELSE
	アビ名 = クリムゾンナイトメア
	消費ＭＰ = 150
ENDIF

剛毅果断 = バフ番号取得_枠("剛毅果断", 戦闘行動キャラ)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕400～500％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>［35％］（2ターン）の悪夢への誘い効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に剛毅果断効果が存在する時、攻撃対象に（2ターン）の紅い悪夢効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に剛毅果断効果が存在する時、攻撃対象に［1］のバフ消去を与える。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕400～500％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>［35％］（2ターン）の悪夢への誘い効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（悪夢への誘い："
		詳細文文字列受け渡し変数 += @"<br>　魅了効果互換。）"
		詳細文文字列受け渡し変数 += @"<br>自身に剛毅果断効果が存在する時、攻撃対象に［10％］（2ターン）の紅い悪夢効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（紅い悪夢："
		詳細文文字列受け渡し変数 += @"<br>　被ダメージ上昇効果互換。）"
		詳細文文字列受け渡し変数 += @"<br>自身に剛毅果断効果が存在する時、攻撃対象に［1］のバフ消去を与える。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = RAND(400, 500)
		IF 剛毅果断 > -1
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 悪夢への誘い
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 35
		アビ汎用変数:基礎成功確率 = 200
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:特殊表示オプション = 1
		IF 剛毅果断 > -1
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		IF 剛毅果断 > -1
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 紅い悪夢
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:基礎成功確率 = 200
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_不利効果_バフ消去
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			アビテンプレート用_アビ名 = 悪魔の腕
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 300
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
ENDSELECT



@固有アビ_62_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 剛毅果断
#DIM デバフ行数
#DIM 延長前持続

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = ストレンジアトラクター+
	消費ＭＰ = 130
ELSE
	アビ名 = ストレンジアトラクター
	消費ＭＰ = 150
ENDIF

剛毅果断 = バフ番号取得_枠("剛毅果断", 戦闘行動キャラ)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に火属性［攻撃力✕400～500％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象の弱体効果を［1ターン］延長"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = RAND(400, 500)
		IF 剛毅果断 > -1
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
		CALL アビテンプレート_ダメージ処理_火属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ALL
		IF 剛毅果断 > -1
			アビ汎用変数:効果量 = 2
		ELSE
			アビ汎用変数:効果量 = 1
		ENDIF
		CALL アビテンプレート_不利効果_デバフ効果延長_持続ターン
		
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			アビテンプレート用_アビ名 = 悪魔の腕
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 300
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
ENDSELECT



@固有アビ_62_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 剛毅果断
#DIM 対象一時保存

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
	アビ名 = インドミナス
	消費ＭＰ = 150

剛毅果断 = バフ番号取得_枠("剛毅果断", 戦闘行動キャラ)

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕400～500％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>自身に［累積10％/最大100％］（独自枠・消去不可）の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［累積10％/最大100％］（独自枠・消去不可）の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>味方全体に［200］のＨＰ回復を与える。"
		詳細文文字列受け渡し変数 += @"<br>［1］のデバフ消去を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		対象一時保存 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = RAND(400, 500)
		IF 剛毅果断 > -1
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_インドミナス
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 100
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		IF 剛毅果断 > -1
			CALL アビテンプレート_有利効果_累積バフ効果セット
		ENDIF
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_インドミナス
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 100
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		IF 剛毅果断 > -1
			CALL アビテンプレート_有利効果_累積バフ効果セット
		ENDIF
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 200
		IF 剛毅果断 > -1
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
		CALL アビテンプレート_回復処理_MAXでも回復
		
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			アビテンプレート用_アビ名 = 悪魔の腕
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(対象一時保存)
			アビ汎用変数:効果割合 = 300
			CALL アビテンプレート_ダメージ処理_火属性
		ENDIF
ENDSELECT



@固有アビ_62_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = スプレンドル
	消費ＭＰ = 500
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（3ターン・消去不可）のスプレンドル効果を与える。
		詳細文文字列受け渡し変数 += @"<br>（スプレンドル："
		詳細文文字列受け渡し変数 += @"<br>　通常攻撃後にアビリティ「インドミナス」が発動する効果。）"
		詳細文文字列受け渡し変数 += @"<br>［1500］（通常枠）のバリア効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = スプレンドル
		アビ汎用文字列:バフ・デバフ対象能力 = スプレンドル
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:効果量 = 1500
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有戦闘開始時処理_キャラ_62
#DIM バフ・デバフ行数
#DIM フラウ番号
#DIM 隊列番号

フラウ番号 = GETCHARA(62)
隊列番号 = キャラ隊列検索(フラウ番号)

SIF BATTLE_STATE:隊列番号:ＨＰ < 1
	RETURN 0

IF 陥落チェック(フラウ番号)
	KSTR:(K++) = 「悪い奴は、私が沈めてあげる！」
ELSE
	KSTR:(K++) = 「お待たせ！」
ENDIF

アビテンプレート用_アビ名 = 力の正位置

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 剛毅果断
アビ汎用文字列:バフ・デバフ対象能力 = 剛毅果断
アビ汎用変数:効果割合 = 0
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット

IF 陥落チェック(フラウ番号) && 基礎BATTLE_STATE:フラウ番号:Lv >= 75
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:適用範囲 = 味方全体
	アビ汎用文字列:バフ・デバフ枠 = 力の正位置
	アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
	アビ汎用変数:効果量 = 300
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 孤影悄然_アビ与ダメ
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
アビ汎用文字列:バフ・デバフ名 = 孤影悄然
アビ汎用変数:効果割合 = MIN(現在ターン数, 15)
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット



@固有ターン開始時処理_キャラ_62
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 赤熱判定
#DIM 赤熱ダメージ

隊列番号 = キャラ隊列検索(GETCHARA(62))

SIF BATTLE_STATE:隊列番号:ＨＰ < 1
	RETURN 0

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 孤影悄然_アビ与ダメ
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
アビ汎用文字列:バフ・デバフ名 = 孤影悄然
アビ汎用変数:効果割合 = MIN(現在ターン数, 15)
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

FOR LOCAL, 10, 20
	赤熱判定 = デバフ番号取得_枠("赤熱", LOCAL)
	IF  赤熱判定 > -1
		アビ汎用文字列:バフ・デバフ枠 = 孤影悄然_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用文字列:バフ・デバフ名 = 孤影悄然
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		BREAK
	ENDIF
NEXT

FOR LOCAL, 10, 20
	赤熱判定 = デバフ番号取得_枠("赤熱", LOCAL)
	IF 赤熱判定 > -1
		赤熱ダメージ = DT_CELL_GET("戦闘効果データベース", 赤熱判定, "効果量_固定値")
		IF 赤熱ダメージ == 100
			DT_CELL_SET "戦闘効果データベース", 赤熱判定, "効果量_固定値", 250
		ELSEIF 赤熱ダメージ == 250
			DT_CELL_SET "戦闘効果データベース", 赤熱判定, "効果量_固定値", 500
		ENDIF
	ENDIF
NEXT

;表示を出さないようにリセット
CALL アビテンプレ変数リセット


@固有通常攻撃後処理_バフ_スプレンドル(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数
#DIM 剛毅果断
#DIM 対象一時保存
#DIM フラウ番号

SIF 全滅フラグ
	RETURN 0

フラウ番号 = GETCHARA(62)

剛毅果断 = バフ番号取得_枠("剛毅果断", 戦闘行動キャラ)

IF 陥落チェック(フラウ番号)
	KSTR:(K++) = 「私に任せて！」
ELSE
	KSTR:(K++) = 「さて、どうしようかな？」
ENDIF

アビテンプレート用_アビ名 = インドミナス

対象一時保存 = アビテンプレート用_対象保存:0

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = RAND(400, 500)
IF 剛毅果断 > -1
	CALL アビテンプレート_ダメージ処理_火属性
ENDIF
CALL アビテンプレート_ダメージ処理_火属性

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 攻撃力_別枠累積C
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 10
アビ汎用変数:累積上限_割合 = 100
アビ汎用変数:持続ターン = -1
IF 剛毅果断 > -1
	CALL アビテンプレート_有利効果_累積バフ効果セット
ENDIF
CALL アビテンプレート_有利効果_累積バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 防御力_別枠累積C
アビ汎用文字列:バフ・デバフ対象能力 = 防御力
アビ汎用変数:効果割合 = 10
アビ汎用変数:累積上限_割合 = 100
アビ汎用変数:持続ターン = -1
IF 剛毅果断 > -1
	CALL アビテンプレート_有利効果_累積バフ効果セット
ENDIF
CALL アビテンプレート_有利効果_累積バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果量 = 200
CALL アビ対象選択テンプレート_味方全体_生者のみ
IF 剛毅果断 > -1
	CALL アビテンプレート_回復処理_MAXでも回復
ENDIF
CALL アビテンプレート_回復処理_MAXでも回復

IF 陥落チェック(フラウ番号) && 基礎BATTLE_STATE:フラウ番号:Lv >= 70
	アビテンプレート用_アビ名 = 悪魔の腕
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(対象一時保存)
	アビ汎用変数:効果割合 = 300
	CALL アビテンプレート_ダメージ処理_火属性
ENDIF



@バフ・デバフ特殊表示_剛毅果断(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 剛毅果断
PRINTFORML ダメージアビリティが2回発動する状態［消去不可］
PRINTFORML 持続ターン：永続



@バフ・デバフ特殊表示_スプレンドル(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML スプレンドル
PRINTFORML 通常攻撃時、インドミナスが発動する状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_悪夢への誘い(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 悪夢への誘い
PRINTFORML 行動が稀に制限される状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_紅い悪夢(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 紅い悪夢
PRINTFORML 受けるダメージが上昇している状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_赤熱(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 赤熱
PRINTFORML 毎ターンダメージを受ける状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_62(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『孤影悄然』 □-----------------------------------------------------------------
KSTR:(K++) = 　経過ターンに応じて自身のアビリティ与ダメージ上昇
KSTR:(K++) = 　赤熱状態の敵がいる時、火属性追撃効果
KSTR:(K++) = 　
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　
	KSTR:(K++) = □ 特性：『悪魔の腕』 □-----------------------------------------------------------------
	KSTR:(K++) = 　ダメージアビリティ発動時、火属性追加ダメージ
	KSTR:(K++) = 　
ENDIF
KSTR:(K++) = □ 特性：『力の正位置』 □----------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 75
	KSTR:(K++) = 　メインメンバー編成時、戦闘開始時に自身に剛毅果断効果［永続/消去不可］/味方全体にアビリティ与ダメージ上昇効果［永続/消去不可］
ELSE
	KSTR:(K++) = 　メインメンバー編成時、戦闘開始時に自身に剛毅果断効果［永続/消去不可］
ENDIF
KSTR:(K++) = 　◆再発動不可
KSTR:(K++) = 　
IF 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = □ 特性：『至賢の領域・力と悪魔』 □--------------------------------------------------
	KSTR:(K++) = 　奥義発動時、自身のMPを回復
	KSTR:(K++) = 　
ENDIF

CALL 口上変数表示(対象キャラ, 1)



