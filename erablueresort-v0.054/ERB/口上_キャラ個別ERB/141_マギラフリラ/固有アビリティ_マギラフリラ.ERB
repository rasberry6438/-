
@固有アビ_141_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 剑刃歌剧+
	消費ＭＰ = 150
ELSE
	アビ名 = 剑刃歌剧
	消費ＭＰ = 150
ENDIF

隊列番号 = キャラ隊列検索(GETCHARA(141))

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成5次［攻击力✕100％］的光属性伤害。
			詳細文文字列受け渡し変数 += @"<br>赋予自身［500］（常规槽）的屏障效果。"
			詳細文文字列受け渡し変数 += @"<br>当自身持有魔剑时，此技能的攻击次数增加［魔剑数量÷2］（向下取整）次。"
		ELSE
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成5次［攻击力✕100％］的光属性伤害。
			詳細文文字列受け渡し変数 += @"<br>赋予自身［500］（常规槽）的屏障效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			FOR LOCAL, 0, (5 + (戦闘中カウント:隊列番号:0 / 2))
				CALL アビテンプレート_ダメージ処理_光属性
			NEXT
		ELSE
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			FOR LOCAL, 0, 5
				CALL アビテンプレート_ダメージ処理_光属性
			NEXT
		ENDIF
		
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 500
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@固有アビ_141_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = 银色华尔兹+
	消費ＭＰ = 125
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	アビ名 = 银色华尔兹
	消費ＭＰ = 125
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成10次［攻击力✕75％］的光属性伤害。
			詳細文文字列受け渡し変数 += @"赋予［每层10％/最大30％］（叠加槽・5回合）的攻击力下降效果。<br>"
			詳細文文字列受け渡し変数 += @"赋予［每层10％/最大30％］（叠加槽・5回合）的防御力下降效果。<br>"
			詳細文文字列受け渡し変数 += @"赋予［1］层（常规槽・5回合）的最大CT增加效果。<br>"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
			詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成10次［攻击力✕75％］的光属性伤害。
			詳細文文字列受け渡し変数 += @"赋予［每层10％/最大30％］（叠加槽・5回合）的攻击力下降效果。<br>"
			詳細文文字列受け渡し変数 += @"赋予［每层10％/最大30％］（叠加槽・5回合）的防御力下降效果。<br>"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 75
		FOR LOCAL, 0, 10
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力累積
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用文字列:バフ・デバフ枠 = 防御力累積
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 1
			アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
			アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
			アビ汎用変数:持続ターン = 5
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@固有アビ_141_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ名 = 刃之意志+
	消費ＭＰ = 200
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 刃之意志
	消費ＭＰ = 200
ENDIF

SIF 現在ターン数 < 10
	アビテンプレート用_アビ封印用フラグ = 1

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:2 == 1
	アビテンプレート用_アビ封印用フラグ = 1
	

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>◆赌上我刃重的一切…必将尔等…击溃！！
		詳細文文字列受け渡し変数 += @"<br>此技能需在第10回合后可使用。"
		詳細文文字列受け渡し変数 += @"<br>此技能每场战斗仅可使用一次。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>赋予自身魔剑数量最大化的效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予武器爆发效果。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 += @"<br>赋予［50％］（独立槽・不可驱散）的奥义值上升量提升效果。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>此技能需在第10回合后可使用。"
		詳細文文字列受け渡し変数 += @"<br>此技能每场战斗仅可使用一次。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
	CASE "アビ効果"
		戦闘中カウント:隊列番号:0 = 10
		
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 刃之意志
			アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
			アビ汎用変数:持続ターン = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		
		戦闘中カウント:隊列番号:2 = 1
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有通常攻撃後処理_バフ_スペリオルシンフォニー(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数

SIF 全滅フラグ
	RETURN 0

SELECTCASE RAND:4
	CASE 0
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「雷神之锤…米约尔尼尔！」
		CASE 1
			WSTR:(K++) = 「寄宿着神圣雷电的巨锤啊！将万物粉碎殆尽！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 天雷之锤
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
		アビ汎用変数:効果割合 = 700
		CALL アビテンプレート_ダメージ処理_光属性
	CASE 1
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「必杀魔枪…魔弹射手！」
		CASE 1
			WSTR:(K++) = 「远古邪法铸造的必杀魔枪啊！贯穿森罗万象！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 夺命弹丸
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(3)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加_フライクーゲル
		アビ汎用文字列:バフ・デバフ対象能力 = 伤害提升
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
	CASE 2
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「奇迹权杖…墨丘利的节杖！」
		CASE 1
			WSTR:(K++) = 「带来福音的奇迹权杖啊！讨伐那些宵小之徒！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 救赎之杖
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = RAND(100, 300)
		CALL アビテンプレート_ＭＰ回復処理_MAXでも回復
	CASE 3
		SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「神威秘枪…千棘刺之枪！」
		CASE 1
			WSTR:(K++) = 「影之女王赐予的神威秘枪啊！歼灭所有敌人！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = 爆裂长枪
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
		アビ汎用変数:効果割合 = 300
		CALL アビテンプレート_ダメージ処理_光属性
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率_ゲイボルグ
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = クリティカルダメージ率_ゲイボルグ
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@バフ・デバフ特殊表示_魔剣(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

隊列番号 = キャラ隊列検索(GETCHARA(141))

PRINTFORM 魔剑增益
PRINTFORML ：%DT_CELL_GETS("戦闘効果データベース", バフ・デバフ行数, "効果名")%
PRINTFORML 魔剑数量：{戦闘中カウント:隊列番号:0}
PRINTFORML 持续回合：永久

@探索欄表示_キャラ_141(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "魔剣:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
IF 戦闘中カウント:隊列番号:0 > 0
	FOR LOCAL, 0, 戦闘中カウント:隊列番号:0
		GDRAWTEXT レイヤー番号, "†", 165 + LOCAL * 11, 90
	NEXT
ENDIF

@固有戦闘開始時処理_キャラ_141
;戦闘開始時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

戦闘中カウント:隊列番号:0 = 10

バフ・デバフ行数 = バフ番号取得_枠("魔剣", 隊列番号)
IF バフ・デバフ行数 < 0
	WSTR:(K++) = 「让你见识刃重众之精髓」
	アビテンプレート用_アビ名 = 刃重的精髓
	
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 魔剣
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 追击_魔剑
	アビ汎用文字列:バフ・デバフ対象能力 = 追击
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 25
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 造成伤害增加_魔剑
	アビ汎用文字列:バフ・デバフ対象能力 = 伤害提升
	アビ汎用変数:持続ターン = 1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 10
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 受到伤害减少_魔剑
	アビ汎用文字列:バフ・デバフ対象能力 = 被伤害
	アビ汎用変数:持続ターン = 1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF


@固有行動開始時処理_キャラ_141
;行動開始時処理関数から呼び出し
#DIM 魔剣粉砕
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

魔剣粉砕 = デバフ番号取得_枠("魔剣粉砕", 隊列番号)
IF 魔剣粉砕 < 0 && 戦闘中カウント:隊列番号:0 > 0
	戦闘中カウント:隊列番号:0 = MIN(10, 戦闘中カウント:隊列番号:0 + 1)
	
	バフ・デバフ行数 = バフ番号取得_枠("魔剣", 隊列番号)
	IF バフ・デバフ行数 > -1
		WSTR:(K++) = 「请跳得更好一些」
		
		アビテンプレート用_アビ名 = 刃重的精髓
	
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 25
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 造成伤害增加_魔剑
		アビ汎用文字列:バフ・デバフ対象能力 = 伤害提升
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
	
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 戦闘中カウント:隊列番号:0 * 10
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 受到伤害减少_魔剑
		アビ汎用文字列:バフ・デバフ対象能力 = 被伤害
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
	ENDIF
	
	WSTR:(K++) = 成功收回了一把魔剑！
ELSEIF 魔剣粉砕 > -1
	IF 戦闘中カウント:隊列番号:0 == 0
		WSTR:(K++) = 玛琪拉菲菈正在将失去的魔剑牵引回来…
		行動不能フラグ = 1
	ENDIF
ENDIF



@固有ターン終了時処理_キャラ_141
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(141))

IF 戦闘中カウント:隊列番号:0 == 0
	バフ・デバフ行数 = バフ番号取得_枠("魔剣粉砕", 隊列番号)
	IF バフ・デバフ行数 < 0
		WSTR:(K++) = 魔剑回到了玛琪拉菲菈身边！
		
		戦闘中カウント:隊列番号:0 = 5
	ENDIF
ENDIF



@特殊反応_被ダメ時_魔剣(バフ番号, 隊列番号, 被ダメ記録順)
#DIM 被ダメ回数
#DIM 被ダメ記録順
#DIM スペリオルシンフォニー行数
#DIM 隊列番号
#DIM バフ番号

被ダメ回数 = 被ダメ反応回数:被ダメ記録順
隊列番号 = キャラ隊列検索(GETCHARA(141))

スペリオルシンフォニー行数 = バフ番号取得_枠("スペリオルシンフォニー", 隊列番号)
IF スペリオルシンフォニー行数 > -1
	SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「那些武器可是被称为超凡系列的珍品哦！？」
	CASE 1
		WSTR:(K++) = 「竟敢弄伤我珍贵的收藏品…！？」
	ENDSELECT
ELSE
	WSTR:(K++) = 「竟敢对我的魔剑…！」
ENDIF

アビテンプレート用_アビ名 = 魔剑之盾

WSTR:(K++) = 失去了{MIN(戦闘中カウント:隊列番号:0, 被ダメ回数)}把魔剑…
戦闘中カウント:隊列番号:0 = MAX(0, 戦闘中カウント:隊列番号:0 - 被ダメ回数)

IF 戦闘中カウント:隊列番号:0 == 0
	SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「呜…抵挡不住了…！」
	CASE 1
		WSTR:(K++) = 「呃…！？节奏被打乱了…」
	ENDSELECT
	
	WSTR:(K++) = 因失去魔剑而无法行动…
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 魔剣粉砕
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:持続ターン = 4
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_141(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『刃重的精髓』 □--------------------------------------------------------------
KSTR:(K++) = 　最大HP较低且奥义槽难以积累，但必定在TA/追击效果/战斗开始时获得10把魔剑
KSTR:(K++) = 　魔剑会根据数量提供对应的伤害提升与伤害减免效果
KSTR:(K++) = 　魔剑会在行动开始时恢复1把/发动奥义时恢复3把
KSTR:(K++) = 　魔剑在受到攻击时会根据受击次数减少/当降为0时，将陷入3回合无法行动状态

CALL 口上変数表示(対象キャラ, 1)


