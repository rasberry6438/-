
@固有アビ_167_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
	アビ名 = 正鹄冲++
	消費ＭＰ = 125
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 正鹄冲+
	消費ＭＰ = 125
ELSE
	アビ名 = 正鹄冲
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成［攻击力✕400％］的火属性伤害。
		詳細文文字列受け渡し変数 += @"<br>赋予［300］（常规槽・3回合）的承受伤害上升效果。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			詳細文文字列受け渡し変数 += @"<br>赋予自身［1000％］（Ａ槽・2回合）的TA率提升效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 400
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 300
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ上昇
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 1000
			アビ汎用文字列:バフ・デバフ枠 = TA率A
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT



@固有アビ_167_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
	アビ名 = 石榴乱舞++
	消費ＭＰ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 石榴乱舞+
	消費ＭＰ = 80
ELSE
	アビ名 = 石榴乱舞
	消費ＭＰ = 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成8次［攻击力✕100％］的火属性伤害。
		詳細文文字列受け渡し変数 += @"<br>赋予［1］层减速效果。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			詳細文文字列受け渡し変数 += @"<br>赋予我方全体［10％］的奥义槽增加。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 8
			CALL アビテンプレート_ダメージ処理_火属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
		ENDIF
ENDSELECT



@固有アビ_167_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 镇守之威光+
	消費ＭＰ = 300
ELSE
	アビ名 = 镇守之威光
	消費ＭＰ = 300
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消耗ＭＰ：{消費ＭＰ}<br>赋予敌方单体［1］层（常规槽・5回合）的最大CT增加效果。
		詳細文文字列受け渡し変数 += @"<br>赋予［100％］（1回合・不可驱散）的压伏效果。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 += @"<br>赋予（2回合）的恐怖效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 压伏
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 恐怖
			アビ汎用文字列:バフ・デバフ対象能力 = 恐怖
			アビ汎用変数:効果量 = 0
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT



@固有戦闘開始時処理_キャラ_167
;戦闘開始時処理関数から呼び出し
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(167))

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果割合 = 10
アビ汎用文字列:バフ・デバフ枠 = 赤炎之钺
アビ汎用文字列:バフ・デバフ名 = 赤炎之钺
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビテンプレ変数リセット

@固有ターン終了時処理_キャラ_167
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 被ダメ判定

隊列番号 = キャラ隊列検索(GETCHARA(167))

被ダメ判定 = 0
IF 戦闘中カウント:隊列番号:被ダメ合計 > 戦闘中カウント:隊列番号:0
	被ダメ判定 = 1
ENDIF

IF 被ダメ判定 == 1
	WSTR:(K++) = 「杂～鱼_H_」
	アビテンプレート用_アビ名 = 赤炎之钺

	CALL アビ対象選択テンプレート_敵ランダムＸ体(4)
	アビ汎用変数:効果割合 = 200
	CALL アビテンプレート_ダメージ処理_火属性
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 15
	CALL アビテンプレート_有利効果_奥義ゲージUP
ENDIF

戦闘中カウント:隊列番号:0 = 戦闘中カウント:隊列番号:被ダメ合計



@バフ・デバフ特殊表示_赤き火炎の鉞(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 赤炎之钺
PRINTFORML 更容易成为敌人目标，但提升技能造成的伤害/
PRINTFORML 受到伤害后，在回合结束时对敌方随机目标造成［攻击力✕200％］的4次火属性伤害/自身奥义槽上升（永久/不可驱散）

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_167(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『赤炎之钺』 □------------------------------------------------------------
KSTR:(K++) = 　更容易成为敌人目标，技能伤害提升
KSTR:(K++) = 　受到伤害后，在回合结束时对敌方随机目标造成的4次火属性伤害/自身奥义槽上升

CALL 口上変数表示(対象キャラ, 1)



