;--------------------------------------------------
;●パチュリー・ノーレッジ口上＠erablue_resort用
;　――妊娠時の確率や可不可の処理に使う特殊関数
;--------------------------------------------------

;--------------------------------------------------
;●ライセンス/更新履歴/概要/使用フラグ
;--------------------------------------------------
;　いずれもパチュリー口上_readme.txt記載に従う
;--------------------------------------------------

;--------------------------------------------------
;ここにおいてある関数は母親のキャラ番号で記述すること
;--------------------------------------------------

;--------------------------------------------------
;種親の制御
;--------------------------------------------------
;複数の精子が子宮にある場合、どれで妊娠判定をするかを選ぶタイミングで呼ばれる
;種親に男のキャラ番号が入っているので、NAMEや素質などを見て結果を返す
;/
;RETURN 1 でこの種親で確定し、妊娠判定を行う
;RETURN -1 でこの種親では妊娠判定を行わない、この種親しか子宮にない場合は妊娠処理自体が終了する
;RETURN 0 で通常の種親ランダム判定に戻す
;/
;※ここで１を返すようにしても、そもそも精液が入ってなければ意味がない。
;　「オーナーの種があればオーナーの種を最優先する」としても、別キャラの精液のみが入ってる場合とかね。
;--------------------------------------------------
@個別妊娠処理_種親制御_1147(対象キャラ, 種親)
#DIM 対象キャラ
#DIM 種親

	;イベントを達成していなければそもそも妊娠しない……という構想はあるが、やるにしても妊娠率制御のほうでええなあ。
	;こっちでは、「複数名に犯されても、MASTERの種以外では妊娠しない」とかができる。
	;もし実装するなら、紅魔館メンバー優先で妊娠、とかかなー。

	;オーナーに恋慕状態かつ、判定の種親がオーナーでないなら、その種は弾く（オーナーの種でのみ孕む）。かわいいね。
	;……というのは、妊娠率制御のほうでやったほうがいいのは、上述。
;	SIF GETBIT(陥落チェック(対象キャラ),0) && 種親 != MASTER
;		RETURN -1

	;縁結び対象と恋慕の場合の判定文メモ
	;絶対入らない分岐の中にしまっておくね
	IF 0
		;種親がオーナーで恋慕ならオーナーの種が最優先
		SIF 種親 == MASTER && 関係性チェック(対象キャラ, 種親, "恋慕")
			RETURN 1
		;種親が特定キャラなら優先のケース
		;（この判定文の場合、恋慕オーナーの種があればそっちが優先で
		;　オーナーが非恋慕vsレミリアの場合、レミリアの種が優先）
		SIF NAME:種親 == "[永远鲜红的幼月]蕾米莉亚"
			RETURN 1
		;種親がオーナー
		;これも優先はする（たぶんera様の御力）
		SIF 種親 == MASTER
			RETURN 1
		;その他、関係性を結んでいるなら妊娠してもいいとするケース
		SIF 関係性チェック(対象キャラ, 種親, "恋慕")
			RETURN 0
		;種親が特定のキャラ以外では妊娠しないケース
		;この例文では「オーナーでもレミリアでもないならダメ」
		SIF 種親 != MASTER || NAME:種親 != "[永远鲜红的幼月]蕾米莉亚"
			RETURN -1
	ENDIF

	RETURN 0

;--------------------------------------------------
;妊娠率の制御
;--------------------------------------------------
;男親が決定後、妊娠するかどうかのタイミングで呼ばれる
;種親に男のキャラ番号が入っているので、NAMEや素質などを見て結果を返す
;※この時点で種親の精液は子宮に入ってるのは確実だよ
;/
;RETURN 1 で100%妊娠させる　※この関数で１を返すと、種族違いも無視して絶対に孕む。ご注意。
;RETURN -1 で絶対に妊娠させない
;RETURN 0 で通常の妊娠判定に戻す
;※１、-1を返したとき、下記メッセージは表示されない。独自処理ができる
;　「星晶獣eraの影響を受けた母体が、異種の子種を受け入れようとしている……！」「しかし、異種族の間では妊娠には至らないようだ。」
;/
;・妊娠解禁イベントの雰囲気
;「あなた、私に子供を産ませたいの？」
;――言われて、頷きかねた。
;孕め、孕めとばかりに膣内射精しているが、彼女は避妊魔法をかけているので……そのうえ、精液を収集しているとのことなので、
;今のところ妊娠の兆候はない。
;パチュリーは、うーん、と唸り、ややあって、息を吐いた。
;「まあいいわ……産ませたいなら、ご勝手に。避妊魔法は解いておくわ」
;「子供が欲しいと思ったことはないけれど、あなたが欲しいなら一回くらいは構わない」
;「とはいえ、この世界の人間が、シャショクの術を使った魔法使いを、孕ませられるかは知らないけど。……実験してみる？」
;/
;・イベント終了まで一切妊娠しない
;・イベント終了後、最初の妊娠は一撃。１００％孕む。
;　・ただし、干渉がなければ、異種の壁は越えられない
;　　・ただし、種族：魔法使い×ヒューマンなら可。種族：魔法使い、「人間に最も近い妖怪」であるため。
;・その後はちょっと高めの設定にする
;　・もちろん、異種の壁はそのままである。さすがにね。
;--------------------------------------------------
@個別妊娠処理_妊娠率制御_1147(対象キャラ, 種親)
#DIM 対象キャラ
#DIM 種親
#DIM 妊娠率上昇補正

	;異種判定
	;/
	;通常通り、異種ならダメ
	;ただし、通常に加えて、「種族が魔法使いで、種親がヒューマンの場合」でもオーケーにする。
	IF 0
	;種親がいない場合は確定で異種……というか、通常処理に戻す
	;妊娠.ERB側で弾いてくれるはずだが、おまじない。
	ELSEIF 種親 < 0
		RETURN 0
	;干渉されてるなら種族に関わらずオーケー
	ELSEIF 干渉強化変数:対象キャラ:干渉種類 != 0
		;ここではなんもしない
	;パッチェさんの種族が『その他』、かつ、種親と種族名が一致するなら異種ではない
	ELSEIF TALENT:対象キャラ:種族 == 種族_その他 && CSTR:対象キャラ:特殊種族 == CSTR:種親:特殊種族
		;ここではなんもしない
	;パッチェさんの種族が『その他（魔法使い）』、かつ、種親の種族が『ヒューマン』なら異種ではない
	;種族：魔法使いは、「人間に最も近い妖怪」である。……まあお空のヒューマンが幻想郷の人間と近いのかは分からないが……
	ELSEIF TALENT:対象キャラ:種族 == 種族_その他 && CSTR:対象キャラ:特殊種族 == "魔法使い" && TALENT:種親:種族 == 種族_ヒューマン
		;ここではなんもしない
	;上記パターン以外で種族が違う場合はダメ
	;（種族がその他からエルーンなどに変更されていて＋オーナーとパッチェさんの種族が異なる場合）
	;これも通常処理に戻して、妊娠.ERBで弾いてもらう
	ELSEIF TALENT:対象キャラ:種族 != 種族_その他 && TALENT:対象キャラ:種族 != TALENT:種親:種族
		RETURN 0
	ELSE
		;ここではなんもしない
	ENDIF

	;妊娠許可イベントを発生させるまでは妊娠しない想定。避妊魔法使ってる感じで。
	IF !イベント完了フラグ確認("パッチェさんの疑問")
		;簡易で受精したかしてないかを取る
		;この辺は「パッチェさんの子宮の中では、（種親）の精が卵子めがけて必死に泳いでいる……」に続くテキスト。
		;「星晶獣eraの影響を受けた母体が、異種の子種を受け入れようとしている……！」……または
		;「しかし、異種族の間では妊娠には至らないようだ。」……の代替になる。
		IF 0
		;補正100％オーバー、異種とかでなければ絶対妊娠する
		;受精して着床まで行ったの消し去るのもそれはそれでえっちだよな……
		ELSEIF TCVAR:対象キャラ:妊娠率補正 >= 100
			CSTR:対象キャラ:妊娠報告_記録文字列 += "精子は避妊魔法を食い破り、受精卵は母胎に根を張ったが、着床した結果を消し去られてしまったようだ……<br>"
		;補正がRAND:100を上回ったら「受精はした」とする
		;（本当は妊娠率はもっと高い。危険日補正などが入るため）
		ELSEIF TCVAR:対象キャラ:妊娠率補正 > RAND:100
			CSTR:対象キャラ:妊娠報告_記録文字列 += "精子は避妊魔法を食い破ったが、着床は防がれてしまったようだ……<br>"
		;さすがに受精しなかった
		ELSE
			CSTR:対象キャラ:妊娠報告_記録文字列 += "しかし、避妊魔法が卵子を守りきり、受精を防いだようだ……<br>"
		ENDIF
		;マイナス１は絶対受精しない返り値な
		RETURN -1
	ENDIF

	;イベント終了後、初回妊娠までは100％妊娠。一撃死。雑魚……
	;（とはいえさすがに異種は突破できない。↑で弾いている）
	;ちなみにこの関数で１を返すと、上記の種族違いも無視して絶対に孕む。ご注意。
	IF EXP:対象キャラ:出産経験 == 0
		;この辺は「パッチェさんの子宮の中では、（種親）の精が卵子めがけて必死に泳いでいる……」に続くテキスト。
		CSTR:対象キャラ:妊娠報告_記録文字列 += "避妊魔法によって幾度も幾度も防がれていた受精着床が、とうとう果たされる……！<br>"
		RETURN 1
	ENDIF

	;妊娠率特殊補正
	;これ独自にやろうとしてたけどシステム側に組み込まれた。感謝……
	;/
	;TCVAR:対象キャラ:妊娠率補正、で妊娠率を加算し、100％にできる。
	;妊娠率補正は、ポルチオマッサージなどで上昇させることができる。
	;ここでは、それとは別に、「たまに100％妊娠」させることで、疑似的に「妊娠しやすい」素質にする
	;これ、「す～ぐ孕む雑魚雌」扱いするか、「きちんと孕む優秀な母胎」扱いするか、迷うところある。
	;/
	;妊娠率特殊補正はデフォルトで＋10。ちょっと妊娠しやすい。屈服させられてるともうちょっと高い。
	;さらに、高めた妊娠率補正の一部が加算される。
	IF 0
	ELSEIF パッチェさん_マゾメス判定(対象キャラ, "中")
		妊娠率上昇補正 = 20
	ELSE
		妊娠率上昇補正 = 10
	ENDIF
	;妊娠率補正の一部をランダムに加算する
	;SQRT(RAND:100)は「RANDで出た値の平方根」を取る命令
	;だ～いたい、４５６+1あたりが出やすいので、補正の20％弱くらいが加算される、んじゃないかな～（この辺、正規分布になるんだっけ？）
	妊娠率上昇補正 += TCVAR:対象キャラ:妊娠率補正 / (SQRT(RAND:100) + 1)

	;で、たまに絶対孕む。
	IF RAND:100 < 妊娠率上昇補正
		;この辺は「パッチェさんの子宮の中では、（種親）の精が卵子めがけて必死に泳いでいる……」に続くテキスト。
		IF 0
		;畜生胎ならさらにRAND追加
		ELSEIF !RAND:4 && TALENT:対象キャラ:受胎体質 >= 3
			CSTR:対象キャラ:妊娠報告_記録文字列 += "精液に酔った子宮が、確実な受精着床のため、卵子を大量に吐き出す……！<br>"
		;多胎以上ならRAND追加
		ELSEIF !RAND:3 && TALENT:対象キャラ:受胎体質 >= 2
			CSTR:対象キャラ:妊娠報告_記録文字列 += "受胎の味を覚えた子宮が、占有権を明け渡す……！<br>"
		ELSEIF !RAND:2
			CSTR:対象キャラ:妊娠報告_記録文字列 += "貪欲な子宮が精液を飲み干し、精子と卵子を引き合わせる……！<br>"
		ELSE
			CSTR:対象キャラ:妊娠報告_記録文字列 += "雄に屈服させられた子宮が、貢物として卵子を差し出した……！<br>"
		ENDIF
		RETURN 1
	ENDIF

	;ここまで来たら通常判定な。
	RETURN 0






