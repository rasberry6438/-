
@敵BATTLE_STATE_Lv14_哥布林将军(対象エネミー)
#DIM 対象エネミー
敵BATTLE_STATE:対象エネミー:Lv = 14
敵BATTLE_STATE:対象エネミー:最大ＨＰ = 10000
敵BATTLE_STATE:対象エネミー:ＨＰ = 10000
敵BATTLE_STATE:対象エネミー:攻撃力 = 560
敵BATTLE_STATE:対象エネミー:防御力 = 610
敵BATTLE_STATE:対象エネミー:回復力 = 0
敵BATTLE_STATE:対象エネミー:行動速度 = 210
敵BATTLE_STATE:対象エネミー:DA率 = 30
敵BATTLE_STATE:対象エネミー:TA率 = 20
敵BATTLE_STATE:対象エネミー:命中率 = 95
敵BATTLE_STATE:対象エネミー:回避率 = 0
敵BATTLE_STATE:対象エネミー:属性 = 3
敵BATTLE_STATE:対象エネミー:所持EXP = 800
敵BATTLE_STATE:対象エネミー:所持ルピ = 850
敵BATTLE_STATE:対象エネミー:最大チャージターン = 2

敵BATTLE_STATE:対象エネミー:ボスフラグ = 1

敵BATTLE_STATE_STR:対象エネミー:種族 = 人型_ゴブリン

@敵討伐後処理_Lv14_哥布林将军(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")

ドロップ確率 = 30 + トレハンLv * 2
ドロップアイテム = 白银古纹
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 40 + トレハンLv * 5
ドロップアイテム = 怨念肉块
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

@敵トリガー行動_Lv14_哥布林将军
#DIM 自己回復量
IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	;HPが半分以下になったら強攻撃
	アビ汎用文字列:実行時メッセージ１ = 「妈的，你们这些人类真是烦死了！！」
	アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的哥布林斩击！
	アビテンプレート用_アビ名 = 哥布林斩击

	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
	アビ汎用変数:効果割合 = 200
	CALL アビテンプレート_ダメージ処理_自属性
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	RETURN 1
ELSEIF バフ番号取得_枠("再行動", 戦闘行動キャラ) < 0
	;初手で攻撃バフ
	IF GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
		アビ汎用文字列:実行時メッセージ１ = 「就让你们成为我剑上的铁锈吧！！」
		アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的哥布林之力！
		アビ汎用文字列:実行時メッセージ３ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的充能回合达到最大值！
	ELSE
		アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的哥布林之力！
		アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的充能回合达到最大值！
	ENDIF
	アビテンプレート用_アビ名 = 哥布林之力
	
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 再行動
	アビ汎用文字列:バフ・デバフ対象能力 = 再行動
	アビ汎用変数:効果量 = 1
	アビ汎用変数:持続ターン = 5
	CALL アビテンプレート_有利効果_バフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 30
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:持続ターン = 5
	CALL アビテンプレート_有利効果_バフ効果セット
	
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 2

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	RETURN 1
ENDIF

@敵チャージ行動_Lv14_哥布林将军
#DIM 自己回復量

アビ汎用文字列:実行時メッセージ１ = 「把你们一口气全轰飞！！」
アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%的战吼！
	アビテンプレート用_アビ名 = 战吼

CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用変数:効果割合 = 100
CALL アビテンプレート_ダメージ処理_自属性
アビ汎用変数:効果割合 = 15
アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = 3
アビ汎用変数:基礎成功確率 = 100
CALL アビテンプレート_不利効果_デバフ効果セット

