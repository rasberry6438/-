
@敵BATTLE_STATE_Lv12_生有冰翼的少女(ARG)
敵BATTLE_STATE:ARG:Lv = 12
敵BATTLE_STATE:ARG:最大ＨＰ = 13000
敵BATTLE_STATE:ARG:ＨＰ = 13000
敵BATTLE_STATE:ARG:攻撃力 = 550
敵BATTLE_STATE:ARG:防御力 = 490
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 460
敵BATTLE_STATE:ARG:DA率 = 0
敵BATTLE_STATE:ARG:TA率 = 0
敵BATTLE_STATE:ARG:命中率 = 100
敵BATTLE_STATE:ARG:回避率 = 0
敵BATTLE_STATE:ARG:属性 = 1
敵BATTLE_STATE:ARG:所持EXP = 685
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_妖精

SIF EXIST画像FILE(@"%CSTR:(キャラ検索("[湖上的冰精]琪露诺")):画像フォルダ%/顔_デフォルト")
	敵BATTLE_STATE_STR:ARG:ボス画像 = %CSTR:(キャラ検索("[湖上的冰精]琪露诺")):画像フォルダ%/顔_デフォルト

@敵討伐後処理_Lv12_生有冰翼的少女(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET("戦闘効果データベース", データベース行数, "効果量_固定値")


ドロップ確率 = 10 + トレハンLv * 5
IF RAND:100 < ドロップ確率
	リザルト後特殊処理_処理キー = チルノ大ちゃん招待フラグ解放
ENDIF

@リザルト後特殊処理_チルノ大ちゃん招待フラグ解放()
#DIMS 立ち絵_チルノ
#DIMS 立ち絵_大ちゃん
#DIM 対象キャラ
#DIM 対象キャラ２

対象キャラ = キャラ検索("[湖上的冰精]琪露诺")
対象キャラ２ = キャラ検索("[现身于雾中的妖精]大妖精")
;既に解放済みなら戻す
SIF CFLAG:対象キャラ:招待不可フラグ == 0 && CFLAG:対象キャラ２:招待不可フラグ == 0
	RETURN 0

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(対象キャラ,100,,,1,1)
立ち絵_チルノ = %立ち絵リソース記録%
CALLF 顔グラ表示用文字列関数(対象キャラ２,101,,,1,1)
立ち絵_大ちゃん = %立ち絵リソース記録%

;会話部分

CALL 口上変数初期化
WSTR:(K++) = ……难以置信！蓝发幼女爬起身，用毫无敌意的眼神望过来！
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「能和本小姐势均力敌，挺有能耐嘛。
WSTR:(K++) = 　作为勇战本小姐这个强敌的证明，这个送给你」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「琪露诺酱，琪露诺酱。
WSTR:(K++) = 　普通人收到冰冻青蛙会困扰的啦」
CALL メッセージ欄表示用関数(立ち絵_大ちゃん,"宛如妖精的少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 「诶——？不可能吧。
WSTR:(K++) = 　本小姐的常识是『收到礼物就该开心』哦」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 那位绿发少女（不知不觉间已与我同行）说得没错，就算收到冻僵的青蛙……
WSTR:(K++) = ……那只被封在冰块里的青蛙……也未免有点令人困扰。
WSTR:(K++) = 若是即将返程也罢，但眼下还有任务在身。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「任务？……呃？
WSTR:(K++) = 　说是从异世界来消除红雾？
WSTR:(K++) = 　这家伙在说什么啊」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
CALL 口上変数初期化
WSTR:(K++) = 唔，虽然确实是事实。
WSTR:(K++) = 蓝发幼女（似乎叫琪露诺）用怀疑的眼神瞪着%CALLNAME:MASTER%。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「真说是异世界来的话，拿出证据啊。
WSTR:(K++) = 　拿得出来？肯定拿不出吧！异世界的东西怎么可能带过来！」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 实物确实无法携带，但可以带你们过去。
WSTR:(K++) = 所谓百闻不如一见，这才是最好的证据。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「…………？？？」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 面对满脸困惑的琪露诺，解释说就是邀请她们去玩。
WSTR:(K++) = 要不要来我们那边做客？
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「什么嘛，说话拐弯抹角的家伙！
WSTR:(K++) = 　不过邀请本小姐就接受啦！大酱也一起去吧！」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 「啊，我也可以叨扰……吗？
WSTR:(K++) = 　好想去看看异世界呢」
CALL メッセージ欄表示用関数(立ち絵_大ちゃん,"大ちゃん",,0)
CALL 口上変数初期化
WSTR:(K++) = %CALLNAME:MASTER%点头同意。
WSTR:(K++) = 但要等事情办完哦，冰冻青蛙也到时候再说吧。
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = 「冰冻鲤鱼也能做哦」
CALL メッセージ欄表示用関数(立ち絵_チルノ,"チルノ",,0)
CALL 口上変数初期化
WSTR:(K++) = 那个就不必啦——
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = [湖上的冰精]琪露诺开始造访度假村了！
WSTR:(K++) = [现身于雾中的妖精]大妖精开始造访度假村了！
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化

CFLAG:対象キャラ:招待不可フラグ = 0
CFLAG:対象キャラ２:招待不可フラグ = 0


@敵トリガー行動_Lv12_生有冰翼的少女
#DIM 攻撃回数
#DIM 大ちゃん生存３回死亡５回
#DIMS 立ち絵_チルノ

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[湖上的冰精]琪露诺"),100,,,1,1)
立ち絵_チルノ = %立ち絵リソース記録%


IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("再也不让你上岸啦！/把你冷冻保存起来！/不管是谁都冻成冰棍哦！")%」
	CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
	アビ汎用文字列:実行時メッセージ２ = ──冰符「冰瀑」──

	FOR 攻撃回数, 0, 3
		CALL アビ対象選択テンプレート_味方ランダムＸ体(3, 1)
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 氷結
	アビ汎用文字列:バフ・デバフ対象能力 = 氷結
	アビ汎用変数:効果量 = 50
	アビ汎用変数:累積上限_固定値 = 500
	アビ汎用変数:持続ターン = 5
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_累積デバフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「%TEXTR("再也不让你上岸啦！/把你冷冻保存起来！/不管是谁都冻成冰棍哦！")%」
	CALL メッセージ欄表示用関数(立ち絵_チルノ,"氷の羽を持つ少女",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
	アビ汎用文字列:実行時メッセージ２ = ──冻符「冷冻之神」──

	IF 敵BATTLE_STATE:4:ＨＰ < 1
		大ちゃん生存３回死亡５回 = 5
	ELSE
		大ちゃん生存３回死亡５回 = 3
	ENDIF


	FOR 攻撃回数, 0, 大ちゃん生存３回死亡５回
		SIF 攻撃回数 > 0
			CALL アビ汎用変数文字列リセット

		アビ汎用変数:敵対心無視オプション = 1
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 凍符_行動速度
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_累積デバフ効果セット
	NEXT

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	;通常攻撃は複数回攻撃
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の通常弾幕！

	FOR 攻撃回数, 0, RAND:2 + 2
		CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
		アビ汎用変数:効果割合 = 80
		アビ汎用文字列:ダメージ種類 = 通常攻撃ダメージ
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT
	IF デバフ番号取得("恐怖", 戦闘行動キャラ) < 0
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン += 1
	ELSE
		;回数消費
		CALL デバフ回数経過処理(戦闘行動キャラ, "恐怖")
	ENDIF
ENDIF

RETURN 1

@敵チャージ行動_Lv12_生有冰翼的少女
#DIM 攻撃回数
アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%做出了符卡宣言！
アビ汎用文字列:実行時メッセージ２ = ──冰符「冰瀑」──

FOR 攻撃回数, 0, 3
	CALL アビ対象選択テンプレート_味方ランダムＸ体(3, 1)
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
NEXT

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 氷結
アビ汎用文字列:バフ・デバフ対象能力 = 氷結
アビ汎用変数:効果量 = 50
アビ汎用変数:累積上限_固定値 = 500
アビ汎用変数:持続ターン = 5
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_不利効果_累積デバフ効果セット

