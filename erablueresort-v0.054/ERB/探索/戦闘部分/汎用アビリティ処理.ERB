@汎用固有アビ_1(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1
		;攻撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_炎爆(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_冻结(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_风压(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_地裂震(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_光辉闪耀(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_暗潮涌(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_阵型变换(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_治愈(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0, 3
				;火, 土
				JUMP 汎用アビリティ処理_阵型变换(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 1, 4
				;水, 光
				JUMP 汎用アビリティ処理_治愈(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_疾速提升(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_暗影迷雾(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_疾速提升(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 6
		;回避
		JUMP 汎用アビリティ処理_诱敌之舞(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_士气高涨(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_炎爆(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_治愈(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_疾速提升(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_士气高涨(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_光辉闪耀(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_暗影迷雾(ARGS, GET_SKILL_LEVEL(1, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_2(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1, 7
		;攻撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_爆裂轰击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_激流射击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_坠空冲击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_碎岩击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_闪耀射线(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_延迟诅咒(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_爆裂轰击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_激流射击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_追风(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_碎岩击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_闪耀射线(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_延迟诅咒(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2, 3
		;防御, 回復
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_炽焰(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_寒霜侵脑(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_追风(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_闪光爆(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3, 5
				;土, 闇
				JUMP 汎用アビリティ処理_逆流波动(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_逆流波动(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_寒霜侵脑(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_追风(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_碎岩击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_闪光爆(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_延迟诅咒(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 2
				;風
				JUMP 汎用アビリティ処理_追风(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
			CASEELSE
				;風以外
				JUMP 汎用アビリティ処理_迅捷步(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_炽焰(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_激流射击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_坠空冲击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_碎岩击(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_闪光爆(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_延迟诅咒(ARGS, GET_SKILL_LEVEL(2, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_3(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1, 7
		;攻撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_灼热增幅(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_极寒暴雪(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_毒云瘴(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_地脉突刺(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_吸收射线(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_复仇裁决者(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 5
		;連撃
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_灼热增幅(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_极寒暴雪(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_迅影强袭(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_地脉突刺(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_天命加护(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_复仇裁决者(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 2
		;防御
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_炎狱火墙(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_山岳怒吼(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_毒云瘴(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_复仇壁垒(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_吸收射线(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_魅惑(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 3
		;回復
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_炎狱火墙(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_山岳怒吼(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_迅影强袭(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_复仇壁垒(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_吸收射线(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_魅惑(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_灼热增幅(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_山岳怒吼(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_迅影强袭(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_地脉突刺(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_吸收射线(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_终焉审判(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_炎狱火墙(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_魅惑(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
			CASEELSE
				;火, 闇以外
				JUMP 汎用アビリティ処理_天命加护(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:対象キャラ:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_灼热增幅(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_山岳怒吼(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_毒云瘴(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_复仇壁垒(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_吸收射线(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_终焉审判(ARGS, GET_SKILL_LEVEL(3, 対象キャラ), 対象キャラ)
ENDSELECT


@汎用固有アビ_4(ARGS, 対象キャラ)
#DIM 対象キャラ
SELECTCASE 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存
	CASE 1
		;攻撃
		JUMP 汎用アビリティ処理_渴望迸发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_守护爆发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_救赎冲击(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 4
		;サポート
		JUMP 汎用アビリティ処理_献祭爆发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_执念爆发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 6
		;回避
		JUMP 汎用アビリティ処理_适性爆发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_终焉爆发(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
	CASEELSE
		;バランス
		JUMP 汎用アビリティ処理_完美爆裂(ARGS, GET_SKILL_LEVEL(4, 対象キャラ), 対象キャラ)
ENDSELECT


@GET_SKILL_LEVEL(アビ番号, 対象キャラ)
#FUNCTION
#DIM アビ番号
#DIM 対象キャラ
SELECTCASE アビ番号
	CASE 1
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 20
				RETURNF 3
			CASE IS >= 8
				RETURNF 2
			CASEELSE
				RETURNF 1
		ENDSELECT
	CASE 2
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 28
				RETURNF 3
			CASE IS >= 16
				RETURNF 2
			CASE IS >= 4
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	CASE 3
		SELECTCASE 基礎BATTLE_STATE:対象キャラ:Lv
			CASE IS >= 32
				RETURNF 3
			CASE IS >= 24
				RETURNF 2
			CASE IS >= 12
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	[SKIPSTART]
	CASE 4
		;Lvではなく、イベントで潜在能力を解放することで習得するイメージ
		;※この文は実装イメージです 実際の挙動とは異なる場合があります
		RETURNF LIMIT(CFLAG:対象キャラ:潜在能力解放レベル, 0, 3)
	[SKIPEND]
	CASEELSE
		RETURNF 0
ENDSELECT


@汎用アビリティ処理_炎爆(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 炎爆＋＋
	消費ＭＰ = 25
	効果数値 = 300
ELSEIF LEVEL >= 2
	アビ名 = 炎爆＋
	消費ＭＰ = 17
	効果数値 = 250
ELSEIF LEVEL >= 1
	アビ名 = 炎爆
	消費ＭＰ = 15
	効果数値 = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方横列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += @"造成［攻击力✕{効果数値}％］的火属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT


@汎用アビリティ処理_炽焰(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 炽焰＋＋
	消費ＭＰ = 35
	効果数値１ = 20
	効果数値２ = 150
ELSEIF LEVEL >= 2
	アビ名 = 炽焰＋
	消費ＭＰ = 25
	効果数値１ = 15
	効果数値２ = 100
ELSEIF LEVEL >= 1
	アビ名 = 炽焰
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕100％］的火属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値１}％］（Ｂ槽・3回合）的防御力下降效果。成功率80％。"
		IF LEVEL >= 2
		詳細文文字列受け渡し変数 += @"<br>赋予［回复力{効果数値２}％］（3回合）的烧伤效果。成功率70％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_火属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 効果数値２ / 100
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_灼热增幅(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = 灼热增幅＋＋
	消費ＭＰ = 35
ELSEIF LEVEL >= 2
	アビ名 = 灼热增幅＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = 灼热增幅
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［30％］（Ａ槽・3回合）的攻击力提升效果。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>赋予［20％］（Ａ槽・3回合）的DA率提升效果。"
		ENDIF
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予［100％］（Ａ槽・3回合）的伤害提升效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = DA率A
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加A
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
			アビ汎用変数:効果量 = 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_治愈(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 治愈＋＋
	消費ＭＰ = 25
	効果数値 = 100
ELSEIF LEVEL >= 2
	アビ名 = 治愈＋
	消費ＭＰ = 15
	効果数値 = 75
ELSEIF LEVEL >= 1
	アビ名 = 治愈
	消費ＭＰ = 10
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>为我方单体回复［回复力✕{効果数値}％］的ＨＰ。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予［1］个减益消除。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			CALL アビテンプレート_回復処理_MAXでも回復
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_有利効果_デバフ消去
		ELSE
			CALL アビテンプレート_回復処理
		ENDIF
ENDSELECT


@汎用アビリティ処理_激流射击(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = 激流射击＋＋
	消費ＭＰ = 30
	効果回数 = 4
ELSEIF LEVEL >= 2
	アビ名 = 激流射击＋
	消費ＭＰ = 20
	効果回数 = 3
ELSEIF LEVEL >= 1
	アビ名 = 激流射击
	消費ＭＰ = 15
	効果回数 = 2
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成{効果回数}次［攻击力✕130％］的水属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 130
		FOR LOCAL, 0, 効果回数
			CALL アビテンプレート_ダメージ処理_水属性
		NEXT
ENDSELECT


@汎用アビリティ処理_山岳怒吼(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = 山岳怒吼＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = 山岳怒吼＋
	消費ＭＰ = 10
ELSEIF LEVEL >= 1
	アビ名 = 山岳怒吼
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体（1次）弱化无效效果。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予［回复力✕30％］（Ａ槽・3回合）的再生效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 再生A
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 30 / 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_疾速提升(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 疾速提升＋＋
	消費ＭＰ = 25
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 2
	アビ名 = 疾速提升＋
	消費ＭＰ = 15
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 1
	アビ名 = 疾速提升
	消費ＭＰ = 10
	効果数値１ = 20
	効果数値２ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［{効果数値１}％］（Ａ槽・3回合）的行动速度提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ａ槽・3回合）的连续攻击率提升效果。"
		IF LEVEL >= 3
		詳細文文字列受け渡し変数 += @"<br>赋予［造成伤害✕10％］（Ａ槽・3回合）的追击效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 行動速度A
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 追撃A
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_追风(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 追风＋＋
	消費ＭＰ = 30
	効果数値１ = 20
	効果数値２ = 250
ELSEIF LEVEL >= 2
	アビ名 = 追风＋
	消費ＭＰ = 20
	効果数値１ = 10
	効果数値２ = 150
ELSEIF LEVEL >= 1
	アビ名 = 追风
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［+{効果数値１}％］（Ａ槽・3回合）的回避率提升效果。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>赋予自身［攻击力✕{効果数値２}％］（常规槽・7回合）的回避时反击效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 回避反撃
			アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_毒云瘴(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 毒云瘴＋＋
	消費ＭＰ = 50
	効果ターン = 5
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 毒云瘴＋
	消費ＭＰ = 40
	効果ターン = 3
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = 毒云瘴
	消費ＭＰ = 25
	効果ターン = 3
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予敌方全体［攻击力✕{効果数値}％］（常规槽・{効果ターン}回合）的中毒效果。成功率60％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 60
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_士气高涨(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 士气高涨＋＋
	消費ＭＰ = 15
	効果数値 = 25
ELSEIF LEVEL >= 2
	アビ名 = 士气高涨＋
	消費ＭＰ = 10
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = 士气高涨
	消費ＭＰ = 5
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予我方全体［20％］（Ａ槽・3回合）的奥义值上升量UP效果。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値}％］的奥义槽增加。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = ゲージ上昇効率A
			アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

			CALL アビ汎用変数文字列リセット
		ENDIF
		アビ汎用変数:効果量 = 効果数値
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT


@汎用アビリティ処理_碎岩击(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 碎岩击＋＋
	消費ＭＰ = 30
	効果数値１ = 200
	効果数値２ = 20
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = 碎岩击＋
	消費ＭＰ = 20
	効果数値１ = 150
	効果数値２ = 15
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = 碎岩击
	消費ＭＰ = 15
	効果数値１ = 100
	効果数値２ = 10
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値１}％］的土属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ｂ槽・3回合）的防御力下降效果。成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_ダメージ処理_土属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_复仇壁垒(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 复仇壁垒＋＋
	消費ＭＰ = 30
	効果回数 = 4
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 复仇壁垒＋
	消費ＭＰ = 25
	効果回数 = 3
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = 复仇壁垒
	消費ＭＰ = 20
	効果回数 = 2
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予自身［能力值✕{効果数値}％］（独立槽・{効果回数}次）的特殊反击效果。"
		詳細文文字列受け渡し変数 += "<br>该反击会根据攻击力・防御力・回复力中最高能力值造成伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 复仇壁垒
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@特殊反応_被ダメ時_复仇壁垒(バフ番号, 攻撃者隊列, 被ダメ記録順)
#DIMS CONST 能力候補 = "攻撃力", "防御力", "回復力"
#DIM 被ダメ記録順
#DIM 攻撃者隊列
#DIM バフ番号

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = 反击

アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")

LOCAL:1 = 0
FOR LOCAL, 0, 3
	CALL バフ込み数値算出(能力候補:LOCAL)
	IF LOCAL:1 < RESULT
		LOCAL:1 = RESULT
		アビ汎用文字列:攻撃側使用能力値 '= 能力候補:LOCAL
	ENDIF
NEXT
CALL アビテンプレート_ダメージ処理_自属性

@バフ固有説明文_复仇壁垒(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL 受到伤害时，会发动反击。
PRINTL 该反击会根据攻击力・防御力・回复力中最高能力值造成伤害。
RETURN 0


@汎用アビリティ処理_光辉闪耀(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 光辉闪耀＋＋
	消費ＭＰ = 25
	効果数値 = 350
ELSEIF LEVEL >= 2
	アビ名 = 光辉闪耀＋
	消費ＭＰ = 15
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = 光辉闪耀
	消費ＭＰ = 10
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的光属性伤害。"
		詳細文文字列受け渡し変数 += "<br>该技能无视对方防御力。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
ENDSELECT


@汎用アビリティ処理_闪光爆(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 闪光爆＋＋
	消費ＭＰ = 30
	効果数値 = 80
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = 闪光爆＋
	消費ＭＰ = 20
	効果数値 = 50
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = 闪光爆
	消費ＭＰ = 15
	効果数値 = 50
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予敌方纵列［{効果数値}％］（常规槽・3回合）的致盲效果。成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_吸收射线(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 与ダメージ量
IF LEVEL >= 3
	アビ名 = 吸收射线＋＋
	消費ＭＰ = 30
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 吸收射线＋
	消費ＭＰ = 25
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = 吸收射线
	消費ＭＰ = 20
	効果数値 = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［回复力✕{効果数値}％］的光属性伤害。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予［1］个增益消除。"
		ENDIF
		詳細文文字列受け渡し変数 += "<br>赋予自身［造成伤害✕50％］的ＨＰ回复。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:攻撃側使用能力値 = 回復力
		CALL アビテンプレート_ダメージ処理_光属性
		;RESULTに与えたダメージ量が入っている
		与ダメージ量 = RESULT
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		IF 与ダメージ量 > 0
			;ダメージが発生した場合のみ処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 与ダメージ量 / 2
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_暗影迷雾(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 暗影迷雾＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = 暗影迷雾＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = 暗影迷雾
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方纵列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += @"赋予［{効果数値}％］（Ａ槽・3回合）的攻击力下降效果。成功率100％。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方纵列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += @"赋予［{効果数値}％］（Ａ槽・3回合）的防御力下降效果。成功率100％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_延迟诅咒(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 延迟诅咒＋＋
	消費ＭＰ = 30
	効果数値 = 200
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = 延迟诅咒＋
	消費ＭＰ = 20
	効果数値 = 150
	効果成功率 = 100
ELSEIF LEVEL >= 1
	アビ名 = 延迟诅咒
	消費ＭＰ = 15
	効果数値 = 100
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的暗属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予［1］层减速效果。成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_スロウ
ENDSELECT


@汎用アビリティ処理_魅惑(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 魅惑＋＋
	消費ＭＰ = 30
	効果数値 = 65
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = 魅惑＋
	消費ＭＰ = 25
	効果数値 = 35
	効果成功率 = 60
ELSEIF LEVEL >= 1
	アビ名 = 魅惑
	消費ＭＰ = 20
	効果数値 = 35
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>对敌方横列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += @"赋予［{効果数値}％］（常规槽・3回合）的魅惑效果。成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 2
		CALL アビ対象選択テンプレート_敵横列
		ELSE
		CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_冻结(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 冻结＋＋
	消費ＭＰ = 25
	効果数値１ = 300
	効果数値２ = 30
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = 冻结＋
	消費ＭＰ = 15
	効果数値１ = 250
	効果数値２ = 20
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = 冻结
	消費ＭＰ = 10
	効果数値１ = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値１}％］的水属性伤害。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ｂ槽・3回合）的行动速度下降效果。成功率{効果成功率}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 行動速度B
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_风压(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 风压＋＋
	消費ＭＰ = 25
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 风压＋
	消費ＭＰ = 15
	効果数値 = 160
ELSEIF LEVEL >= 1
	アビ名 = 风压
	消費ＭＰ = 10
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方全体"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方横列"
		ENDIF
		詳細文文字列受け渡し変数 += @"造成［攻击力✕{効果数値}％］的风属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_敵横列
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_地裂震(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 地裂震＋＋
	消費ＭＰ = 35
	効果数値 = 150
	効果回数 = 3
ELSEIF LEVEL >= 2
	アビ名 = 地裂震＋
	消費ＭＰ = 25
	効果数値 = 140
	効果回数 = 2
ELSEIF LEVEL >= 1
	アビ名 = 地裂震
	消費ＭＰ = 15
	効果数値 = 100
	効果回数 = 2
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对随机3名敌人造成{効果回数}次［攻击力✕{効果数値}％］的土属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR LOCAL, 0, 効果回数
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体_重複無し(3)
			アビ汎用変数:効果割合 = 効果数値
			CALL アビテンプレート_ダメージ処理_土属性
		NEXT
ENDSELECT


@汎用アビリティ処理_暗潮涌(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 暗潮涌＋＋
	消費ＭＰ = 25
	効果数値 = 280
ELSEIF LEVEL >= 2
	アビ名 = 暗潮涌＋
	消費ＭＰ = 15
	効果数値 = 170
ELSEIF LEVEL >= 1
	アビ名 = 暗潮涌
	消費ＭＰ = 10
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方纵列造成［攻击力✕{効果数値}％］的暗属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
ENDSELECT


@汎用アビリティ処理_阵型变换(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = 阵型变换＋＋
	消費ＭＰ = 40
	効果回数 = 4
ELSEIF LEVEL >= 2
	アビ名 = 阵型变换＋
	消費ＭＰ = 30
	効果回数 = 2
ELSEIF LEVEL >= 1
	アビ名 = 阵型变换
	消費ＭＰ = 20
	効果回数 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［25％］（常规槽・{効果回数}次）的伤害减少效果。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>赋予［10％］（Ａ槽・3回合）的防御力提升效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 25
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 防御力A
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_诱敌之舞(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 诱敌之舞＋＋
	消費ＭＰ = 30
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 诱敌之舞＋
	消費ＭＰ = 10
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = 诱敌之舞
	消費ＭＰ = 10
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予自身［{効果数値}％］（Ｃ槽・2次行动）的仇恨值提升效果。"
		詳細文文字列受け渡し変数 += "<br>赋予［60％］（Ｃ槽・2次行动）的回避率提升效果。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 敵対心C
		アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回避率C
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 60
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF LEVEL >= 3
			アビテンプレート用_行動消費無しフラグ = 1
		ENDIF
ENDSELECT


@汎用アビリティ処理_迅捷步(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 迅捷步＋＋
	消費ＭＰ = 30
	効果数値１ = 300
	効果数値２ = 15
ELSEIF LEVEL >= 2
	アビ名 = 迅捷步＋
	消費ＭＰ = 20
	効果数値１ = 200
	効果数値２ = 10
ELSEIF LEVEL >= 1
	アビ名 = 迅捷步
	消費ＭＰ = 15
	効果数値１ = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予自身［攻击力✕{効果数値１}％］（常规槽・7回合）的回避时反击效果。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（Ａ槽・7回合）的回避率提升效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避反撃
		アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 回避率A
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 効果数値２
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_爆裂轰击(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 対象数
IF LEVEL >= 3
	アビ名 = 爆裂轰击＋＋
	消費ＭＰ = 45
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = 爆裂轰击＋
	消費ＭＰ = 35
	効果数値 = 35
ELSEIF LEVEL >= 1
	アビ名 = 爆裂轰击
	消費ＭＰ = 30
	効果数値 = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成［攻击力✕敌人数✕{効果数値}％］的火属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値 * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT


@汎用アビリティ処理_寒霜侵脑(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = 寒霜侵脑＋＋
	消費ＭＰ = 50
	効果数値 = 200
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = 寒霜侵脑＋
	消費ＭＰ = 35
	効果数値 = 100
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = 寒霜侵脑
	消費ＭＰ = 25
	効果数値 = 100
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予敌方纵列［回复力✕{効果数値}％］（常规槽・3回合）的烧伤效果。成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 火傷
		アビ汎用文字列:バフ・デバフ対象能力 = 火傷
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_坠空冲击(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 坠空冲击＋＋
	消費ＭＰ = 30
	効果数値 = 400
ELSEIF LEVEL >= 2
	アビ名 = 坠空冲击＋
	消費ＭＰ = 20
	効果数値 = 300
ELSEIF LEVEL >= 1
	アビ名 = 坠空冲击
	消費ＭＰ = 15
	効果数値 = 220
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［攻击力✕{効果数値}％］的风属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_闪耀射线(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数_最小
#DIM 効果回数_最大
#DIM 効果回数
IF LEVEL >= 3
	アビ名 = 闪耀射线＋＋
	消費ＭＰ = 40
	効果回数_最小 = 4
	効果回数_最大 = 9
ELSEIF LEVEL >= 2
	アビ名 = 闪耀射线＋
	消費ＭＰ = 25
	効果回数_最小 = 2
	効果回数_最大 = 6
ELSEIF LEVEL >= 1
	アビ名 = 闪耀射线
	消費ＭＰ = 15
	効果回数_最小 = 1
	効果回数_最大 = 4
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对随机敌人造成{効果回数_最小}次～{効果回数_最大}次［攻击力✕120％］的光属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		効果回数 = RAND(効果回数_最小, 効果回数_最大 + 1)
		FOR LOCAL, 0, 効果回数
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 120
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
ENDSELECT


@汎用アビリティ処理_逆流波动(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 逆流波动＋＋
	消費ＭＰ = 40
	効果数値 = 3
ELSEIF LEVEL >= 2
	アビ名 = 逆流波动＋
	消費ＭＰ = 20
	効果数値 = 2
ELSEIF LEVEL >= 1
	アビ名 = 逆流波动
	消費ＭＰ = 10
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方横列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += @"赋予［{効果数値}］个增益消除。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		FOR LOCAL, 0, 効果数値
			CALL アビテンプレート_不利効果_バフ消去
		NEXT
ENDSELECT


@汎用アビリティ処理_炎狱火墙(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 炎狱火墙＋＋
	消費ＭＰ = 30
	効果回数 = 2
	効果数値 = 170
ELSEIF LEVEL >= 2
	アビ名 = 炎狱火墙＋
	消費ＭＰ = 25
	効果回数 = 1
	効果数値 = 170
ELSEIF LEVEL >= 1
	アビ名 = 炎狱火墙
	消費ＭＰ = 20
	効果回数 = 1
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［各自的攻击力✕{効果数値}％］（常规槽・{効果回数}次）的反击效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 反击
		アビ汎用文字列:バフ・デバフ対象能力 = 反击
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_极寒暴雪(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 极寒暴雪＋＋
	消費ＭＰ = 30
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = 极寒暴雪＋
	消費ＭＰ = 20
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = 极寒暴雪
	消費ＭＰ = 15
	効果数値 = 75
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方全体造成［攻击力✕{効果数値}％］的水属性伤害。"
		詳細文文字列受け渡し変数 += "<br>赋予［20％］（Ｂ槽・3回合）的防御力下降效果。成功率80％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@汎用アビリティ処理_地脉突刺(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 地脉突刺＋＋
	消費ＭＰ = 30
	効果数値 = 240
ELSEIF LEVEL >= 2
	アビ名 = 地脉突刺＋
	消費ＭＰ = 25
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = 地脉突刺
	消費ＭＰ = 20
	効果数値 = 130
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方横列造成［攻击力✕{効果数値}％］的土属性伤害。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>该技能无视对方防御力。"
		ELSE
			詳細文文字列受け渡し変数 += "<br>该技能无视对方50％防御力。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵横列
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			アビ汎用変数:防御無視オプション_割合 = 100
		ELSE
			アビ汎用変数:防御無視オプション_割合 = 50
		ENDIF
		CALL アビテンプレート_ダメージ処理_土属性
ENDSELECT


@汎用アビリティ処理_迅影强袭(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 迅影强袭＋＋
	消費ＭＰ = 25
	効果数値１ = 200
	効果数値２ = 2300
ELSEIF LEVEL >= 2
	アビ名 = 迅影强袭＋
	消費ＭＰ = 22
	効果数値１ = 150
	効果数値２ = 1400
ELSEIF LEVEL >= 1
	アビ名 = 迅影强袭
	消費ＭＰ = 20
	効果数値１ = 100
	効果数値２ = 750
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>对敌方单体造成［行动速度✕{効果数値１}％］的风属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>战斗开始后的首回合使用时，伤害提升至［行动速度✕{効果数値２}％］。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		IF 現在ターン数 > 1
			アビ汎用変数:効果割合 = 効果数値１
		ELSE
			アビ汎用変数:効果割合 = 効果数値２
		ENDIF
		アビ汎用文字列:攻撃側使用能力値 = 行動速度
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT


@汎用アビリティ処理_复仇裁决者(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
IF LEVEL >= 3
	アビ名 = 复仇裁决者＋＋
	消費ＭＰ = 30
	効果ターン = 7
ELSEIF LEVEL >= 2
	アビ名 = 复仇裁决者＋
	消費ＭＰ = 25
	効果ターン = 3
ELSEIF LEVEL >= 1
	アビ名 = 复仇裁决者
	消費ＭＰ = 20
	効果ターン = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予自身［攻击力✕200％］（常规槽・{効果ターン}回合）的反击效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 反击
		アビ汎用文字列:バフ・デバフ対象能力 = 反击
		アビ汎用変数:効果割合 = 200
		アビ汎用変数:持続ターン = 効果ターン
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_天命加护(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 天命加护＋＋
	消費ＭＰ = 30
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = 天命加护＋
	消費ＭＰ = 25
	効果数値 = 25
ELSEIF LEVEL >= 1
	アビ名 = 天命加护
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}<br>赋予我方全体［40％］（Ａ槽・7回合）的暴击率提升效果。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>赋予我方全体［{効果数値}％］（Ａ槽・7回合）的暴击伤害提升效果。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 40
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = クリティカルダメージ率A
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
			アビ汎用変数:効果量 = 効果数値
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_终焉审判(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = 终焉审判＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = 终焉审判＋
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = 终焉审判
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>对敌方全体"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>对敌方纵列"
		ELSE
			詳細文文字列受け渡し変数 += "<br>对敌方单体"
		ENDIF
		詳細文文字列受け渡し変数 += "赋予即死效果。成功率30％。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>赋予［30％］（Ｂ槽・2回合）的回避率下降效果。成功率90％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSEIF LEVEL >= 2
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:基礎成功確率 = 50
		CALL アビテンプレート_不利効果_即死
		IF LEVEL >= 3
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 回避率B
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 30
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT


@汎用アビリティ処理_完美爆裂(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 対象数
#DIM 与ダメージ量
IF LEVEL >= 3
	アビ名 = 完美爆裂＋＋
	消費ＭＰ = 50
	効果数値１ = 650
	効果数値２ = 30
ELSEIF LEVEL >= 2
	アビ名 = 完美爆裂＋
	消費ＭＰ = 50
	効果数値１ = 550
	効果数値２ = 25
ELSEIF LEVEL >= 1
	アビ名 = 完美爆裂
	消費ＭＰ = 50
	効果数値１ = 450
	効果数値２ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>对敌方全体造成［攻击力✕{効果数値１}÷对象数％］的无属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（独立槽・5回合）的攻击力下降效果。成功率100％。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（独立槽・5回合）的防御力下降效果。成功率100％。"
		詳細文文字列受け渡し変数 += "<br>赋予我方全体［造成伤害✕50％］的ＨＰ回复。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ < 1
				CONTINUE
			対象数 += 1
		NEXT

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１ / MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_無属性
		;RESULTに与えたダメージ量が入っている
		与ダメージ量 = RESULT

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_完美爆裂
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_完美爆裂
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット

		IF 与ダメージ量 > 0
			;ダメージが発生した場合のみ処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 与ダメージ量 / 2
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_渴望迸发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM バフ番号
#DIM 対象数
IF LEVEL >= 3
	アビ名 = 渴望迸发＋＋
	消費ＭＰ = 50
	効果数値１ = 120
	効果数値２ = 100
ELSEIF LEVEL >= 2
	アビ名 = 渴望迸发＋
	消費ＭＰ = 50
	効果数値１ = 100
	効果数値２ = 75
ELSEIF LEVEL >= 1
	アビ名 = 渴望迸发
	消費ＭＰ = 50
	効果数値１ = 80
	効果数値２ = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>对敌方全体造成［攻击力✕敌人数✕{効果数値１}％］的自身属性伤害。"
		詳細文文字列受け渡し変数 += @"<br>赋予自身［{効果数値２}％］（独立槽・3回合）的技能伤害提升效果。"
		詳細文文字列受け渡し変数 += "<br>通过此能力获得的技能伤害提升效果不适用于本能力。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		バフ番号 = バフ番号取得_枠("渴望迸发", 戦闘行動キャラ)
		IF バフ番号 > -1
			CALL 戦闘効果削除(バフ番号)
		ENDIF

		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT

		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１ * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 渴望迸发
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_守护爆发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 守护爆发＋＋
	消費ＭＰ = 50
	効果数値 = 95
ELSEIF LEVEL >= 2
	アビ名 = 守护爆发＋
	消費ＭＰ = 50
	効果数値 = 85
ELSEIF LEVEL >= 1
	アビ名 = 守护爆发
	消費ＭＰ = 50
	効果数値 = 75
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予自身（2次行动）全体掩护效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値}％］（2次行动）的伤害减免效果。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 全体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 全体かばう
		;1にするとかばう使った直後に消えてしまうので2で設定
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_救赎冲击(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 救赎冲击＋＋
	消費ＭＰ = 50
	効果数値１ = 150
	効果数値２ = 600
ELSEIF LEVEL >= 2
	アビ名 = 救赎冲击＋
	消費ＭＰ = 50
	効果数値１ = 110
	効果数値２ = 300
ELSEIF LEVEL >= 1
	アビ名 = 救赎冲击
	消費ＭＰ = 50
	効果数値１ = 75
	効果数値２ = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>为我方全体回复［回复力✕{効果数値１}％］的ＨＰ。"
		詳細文文字列受け渡し変数 += @"<br>为我方全体无法战斗的队友恢复［{効果数値２}］（可复活）的ＨＰ。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_回復処理_MAXでも回復

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_死者のみ
		IF RESULT != -1
			アビ汎用変数:効果量 = 効果数値２
			アビ汎用変数:蘇生可能オプション = 1
			CALL アビテンプレート_回復処理_MAXでも回復
		ENDIF
ENDSELECT


@汎用アビリティ処理_献祭爆发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = 献祭爆发＋＋
	消費ＭＰ = 50
	効果数値１ = 200
	効果数値２ = 50
ELSEIF LEVEL >= 2
	アビ名 = 献祭爆发＋
	消費ＭＰ = 50
	効果数値１ = 170
	効果数値２ = 35
ELSEIF LEVEL >= 1
	アビ名 = 献祭爆发
	消費ＭＰ = 50
	効果数値１ = 140
	効果数値２ = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予我方全体［{効果数値１}％］（独立槽・1次行动）的攻击力・防御力・回复力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（独立槽・1次行动）的弱化成功率提升效果。"
		詳細文文字列受け渡し変数 += "<br>使用此技能不消耗行动次数。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予我方全体［{効果数値１}％］（独立槽・1次行动）的攻击力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値１}％］（独立槽・1次行动）的防御力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値１}％］（独立槽・1次行动）的回复力提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値２}％］（独立槽・1次行动）的弱化成功率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_献祭爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力_献祭爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 回復力_献祭爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 回復力
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 弱体成功率_献祭爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体成功率
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@汎用アビリティ処理_执念爆发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 执念爆发＋＋
	消費ＭＰ = 50
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = 执念爆发＋
	消費ＭＰ = 50
	効果数値 = 125
ELSEIF LEVEL >= 1
	アビ名 = 执念爆发
	消費ＭＰ = 50
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予自身（常规槽・1次）的再行动效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［造成伤害✕{効果数値}％］（独立槽・1回合）的追击效果。"
		詳細文文字列受け渡し変数 += @"<br>使用此技能不消耗行动次数。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 追撃_执念爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@汎用アビリティ処理_适性爆发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 适性爆发＋＋
	消費ＭＰ = 50
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = 适性爆发＋
	消費ＭＰ = 50
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = 适性爆发
	消費ＭＰ = 50
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消耗ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予自身［1000％］（独立槽・3回合）的回避率提升效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予自身［攻击力✕{効果数値}％］（独立槽・7回合）的特殊闪避时反击效果。"
		詳細文文字列受け渡し変数 += "此闪避反击会与除自身外其他可战斗的队友共同执行。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率_适性爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 适性爆发
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_回避時
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 7
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@特殊反応_回避時_适性爆发(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号
#DIM 戦闘行動キャラ一時保存
#DIM CONST 対象番号 = 0

CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = 回避反撃

アビ汎用変数:効果割合 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_割合")
アビ汎用変数:効果量 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
CALL アビテンプレート_ダメージ処理_自属性

戦闘行動キャラ一時保存 = 戦闘行動キャラ
FOR LOCAL, 0, 4
	SIF LOCAL == 戦闘行動キャラ一時保存
		CONTINUE
	SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	戦闘行動キャラ = LOCAL
	アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%の追撃！
	CALL アビテンプレート_ダメージ処理_自属性
NEXT
戦闘行動キャラ = 戦闘行動キャラ一時保存

@バフ固有説明文_适性爆发(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL 当闪避攻击时，会发动反击。
PRINTL 该反击会与除自身外其他可战斗的队友共同执行。
RETURN 0


@汎用アビリティ処理_终焉爆发(ARGS, LEVEL, 対象キャラ)
#DIM LEVEL
#DIM 対象キャラ
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 终焉爆发＋＋
	消費ＭＰ = 50
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = 终焉爆发＋
	消費ＭＰ = 50
	効果数値 = 30
ELSEIF LEVEL >= 1
	アビ名 = 终焉爆发
	消費ＭＰ = 50
	効果数値 = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：最大ＭＰ的{消費ＭＰ}％<br>赋予我方全体武器爆发效果。"
		詳細文文字列受け渡し変数 += @"<br>赋予［{効果数値}％］（独立槽・2回合）的奥义伤害提升效果。"
		RETURN (基礎BATTLE_STATE:対象キャラ:最大ＭＰ * 消費ＭＰ + 99) / 100
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL アビテンプレート_有利効果_ウェポンバースト

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 终焉爆发
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
