@バフ込み数値算出(対象能力, 対象キャラ = -1, 補正前数値)
#DIMS 対象能力
#DIM 対象キャラ
#DIM 補正前数値

IF 対象キャラ == -1
	対象キャラ = 戦闘行動キャラ
ENDIF
CALL バフ・デバフ測定(対象キャラ, 対象能力, 補正前数値, 1)

RETURN RESULT

@バフ番号取得(対象バフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象バフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

RETURNF バフ・デバフ効果検索("バフ", "能力", 対象バフ, 対象キャラ)

@バフ存在判定(対象バフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象バフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

IF バフ・デバフ効果検索("バフ", "能力", 対象バフ, 対象キャラ) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@バフ番号取得_枠(対象バフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象バフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

RETURNF バフ・デバフ効果検索("バフ", "枠", 対象バフ, 対象キャラ)

@バフ存在判定_枠(対象バフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象バフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

IF バフ・デバフ効果検索("バフ", "枠", 対象バフ, 対象キャラ) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@バフ所持者検索(対象バフ)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 所持者数
#DIM 所持番号
#DIMS 対象バフ

VARSET RESULT
VARSET 効果所持者検索結果
DT_SELECT "戦闘効果データベース", @"対象能力 = '%対象バフ%' And バフ・デバフフラグ = 'バフ'"
所持者数 = RESULT
IF 所持者数
	;RESULTに入ってる行IDを所持者に変換する
	FOR 所持番号, 1, 所持者数 + 1
		効果所持者検索結果:(所持番号 - 1) '= DT_CELL_GETS("戦闘効果データベース", RESULT:所持番号, "適用範囲", 1)
	NEXT
ENDIF
RETURNF 所持者数

@バフ所持者検索_枠(対象バフ)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 所持者数
#DIM 所持番号
#DIMS 対象バフ

VARSET RESULT
VARSET 効果所持者検索結果
DT_SELECT "戦闘効果データベース", @"効果枠 = '%対象バフ%' And バフ・デバフフラグ = 'バフ'"
所持者数 = RESULT
IF 所持者数
	;RESULTに入ってる行IDを所持者に変換する
	FOR 所持番号, 1, 所持者数 + 1
		効果所持者検索結果:(所持番号 - 1) '= DT_CELL_GETS("戦闘効果データベース", RESULT:所持番号, "適用範囲", 1)
	NEXT
ENDIF
RETURNF 所持者数

@デバフ番号取得(対象デバフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象デバフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

RETURNF バフ・デバフ効果検索("デバフ", "能力", 対象デバフ, 対象キャラ)

@デバフ存在判定(対象デバフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象デバフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

IF バフ・デバフ効果検索("デバフ", "能力", 対象デバフ, 対象キャラ) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@デバフ番号取得_枠(対象デバフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象デバフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

RETURNF バフ・デバフ効果検索("デバフ", "枠", 対象デバフ, 対象キャラ)

@デバフ存在判定_枠(対象デバフ, 対象キャラ = -1)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 対象キャラ
#DIMS 対象デバフ

IF 対象キャラ == -1
	対象キャラ = 戦闘行動対象
ENDIF

IF バフ・デバフ効果検索("デバフ", "枠", 対象デバフ, 対象キャラ) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@デバフ所持者検索(対象デバフ)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 所持者数
#DIM 所持番号
#DIMS 対象デバフ

VARSET RESULT
VARSET 効果所持者検索結果
DT_SELECT "戦闘効果データベース", @"対象能力 = '%対象デバフ%' And バフ・デバフフラグ = 'デバフ'"
所持者数 = RESULT
IF 所持者数
	;RESULTに入ってる行IDを所持者に変換する
	FOR 所持番号, 1, 所持者数 + 1
		効果所持者検索結果:(所持番号 - 1) '= DT_CELL_GETS("戦闘効果データベース", RESULT:所持番号, "適用範囲", 1)
	NEXT
ENDIF
RETURNF 所持者数

@デバフ所持者検索_枠(対象デバフ)
#FUNCTION
;あるなら行数を、ないなら-1を返す

#DIM 所持者数
#DIM 所持番号
#DIMS 対象デバフ

VARSET RESULT
VARSET 効果所持者検索結果
DT_SELECT "戦闘効果データベース", @"効果枠 = '%対象デバフ%' And バフ・デバフフラグ = 'デバフ'"
所持者数 = RESULT
IF 所持者数
	;RESULTに入ってる行IDを所持者に変換する
	FOR 所持番号, 1, 所持者数 + 1
		効果所持者検索結果:(所持番号 - 1) '= DT_CELL_GETS("戦闘効果データベース", RESULT:所持番号, "適用範囲", 1)
	NEXT
ENDIF
RETURNF 所持者数


@隊列算出_味方(対象キャラ)
#FUNCTION
;味方の中で対象キャラが隊列の何番目にいるかを返す。いないなら-1

#DIM 対象キャラ
#DIM 隊列番号

FOR 隊列番号, 0, 14
	IF BATTLE_STATE:隊列番号:キャラ登録番号 == 対象キャラ
		RETURNF 隊列番号
	ENDIF
NEXT

RETURNF -1

@バフ・デバフ効果適用判定_行指定(判定行数, バフ・デバフ = "ALL", 判定対象能力 = "ALL", 判定隊列)
#FUNCTION
#DIM 判定行数
#DIMS バフ・デバフ
#DIMS 判定対象能力
#DIM 判定隊列
#DIM 要素保存

SIF バフ・デバフ != "ALL" && DT_CELL_GETS("戦闘効果データベース", 判定行数, "バフ・デバフフラグ") != バフ・デバフ
	RETURNF 0
SIF 判定対象能力 != "ALL" && DT_CELL_GETS("戦闘効果データベース", 判定行数, "対象能力") != 判定対象能力
	RETURNF 0


IF DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠") == "パッシブ"
	;パッシブ効果の場合、対象隊列が存在するだけで効果を発揮する
	;パッシブである以上、必ず付随先は設定されているはず
	要素保存 = DT_CELL_GET("戦闘効果データベース", 判定行数, "パッシブ付随先NO")
	IF キャラNO生存_存在判定(要素保存) == 0
		;隊列0～3(戦闘不能パッシブ反応は隊列+1)でパッシブ付随先が本人なら発動
		SIF !戦闘不能パッシブ反応 || 戦闘不能パッシブ反応 > 4 || NO:(BATTLE_STATE:(戦闘不能パッシブ反応 - 1):0) != 要素保存
			RETURNF 0
	ENDIF
ELSEIF DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠") != ""
	;付随枠が設定されている時、該当の効果が存在するかどうかを見る
	;基本的に効果なし効果か固有効果のはずなので、バフ・デバフの分岐は見ない
	IF DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠_対象隊列") != ""
		;付随先の隊列が設定されている場合、そっちを見る
		IF ISNUMERIC(DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠_対象隊列"))
			要素保存 = TOINT(DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠_対象隊列"))
			SIF バフ・デバフ効果検索("ALL", "枠", DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠"), 要素保存) == -1
				RETURNF 0
		ENDIF
	ELSE
		;設定されてないなら自分を基準に見る
		SIF バフ・デバフ効果検索("ALL", "枠", DT_CELL_GETS("戦闘効果データベース", 判定行数, "付随効果枠"), 判定隊列) == -1
			RETURNF 0
	ENDIF
ENDIF

LOCALS '= DT_CELL_GETS("戦闘効果データベース", 判定行数, "適用範囲")
IF LOCALS == @"{判定隊列}" || LOCALS == @"NO{NO:(BATTLE_STATE:判定隊列:0)}"
	RETURNF 1
ELSE
	;特殊範囲の分を判定
	SELECTCASE LOCALS
		CASE "フィールド"
			;当然OK
			RETURNF 1
		CASE "敵味方全体"
			;当然OK
			RETURNF 1
		CASE "味方全体"
			SIF 判定隊列 <= 3 && 判定隊列 >= 0
				RETURNF 1
		CASE "敵全体"
			SIF 判定隊列 <= 19 && 判定隊列 >= 10
				RETURNF 1
		CASEELSE
			IF STRFIND(LOCALS, "_") >= 0
				;隊列複数指定を想定
				VARSET RESULTS
				SPLIT LOCALS, "_", RESULTS
				FOR LOCAL, 0, RESULT
					;引っかかったらOKを返す
					IF STRFIND(RESULTS:LOCAL, "NO") >= 0
						;特定キャラ指定の場合
						SIF REPLACE(RESULTS:LOCAL, "NO", "") == @"{NO:(BATTLE_STATE:判定隊列:0)}"
							RETURNF 1
					ELSEIF RESULTS:LOCAL == @"{判定隊列}"
						RETURNF 1
					ENDIF
				NEXT
				;引っかからなかったら0
				RETURNF 0
			ELSE
				RETURNF 0
			ENDIF
	ENDSELECT
ENDIF

RETURNF 0

@バフ・デバフ効果検索(バフ・デバフ, 枠・能力, 判定対象能力, 判定隊列)
#FUNCTION
#DIMS DYNAMIC バフ・デバフ
#DIMS DYNAMIC 枠・能力
#DIMS DYNAMIC 判定対象能力
#DIM DYNAMIC 判定隊列
#DIM DYNAMIC 検索番号
#DIM DYNAMIC 要素保存

FOR 検索番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	IF バフ・デバフ != "ALL"
		SIF DT_CELL_GETS("戦闘効果データベース", 検索番号, "バフ・デバフフラグ") != バフ・デバフ
			CONTINUE
	ENDIF
	IF 判定対象能力 != "ALL"
		IF 枠・能力 == "能力"
			SIF DT_CELL_GETS("戦闘効果データベース", 検索番号, "対象能力") != 判定対象能力
				CONTINUE
		ELSEIF 枠・能力 == "枠"
			;枠
			SIF DT_CELL_GETS("戦闘効果データベース", 検索番号, "効果枠") != 判定対象能力
				CONTINUE
		ENDIF
	ENDIF
	IF DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠") == "パッシブ"
		;パッシブ効果の場合、対象隊列が存在するだけで効果を発揮する
		;パッシブである以上、必ず付随先は設定されているはず
		要素保存 = DT_CELL_GET("戦闘効果データベース", 検索番号, "パッシブ付随先NO")
		IF キャラNO生存_存在判定(要素保存) == 0
			;隊列0～3(戦闘不能パッシブ反応は隊列+1)でパッシブ付随先が本人なら発動
			SIF !戦闘不能パッシブ反応 || 戦闘不能パッシブ反応 > 4 || NO:(BATTLE_STATE:(戦闘不能パッシブ反応 - 1):0) != 要素保存
				RETURNF 0
		ENDIF
	ELSEIF DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠") != ""
		;付随枠が設定されている時、該当の効果が存在するかどうかを見る
		;基本的に効果なし効果か固有効果のはずなので、バフ・デバフの分岐は見ない
		IF DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠_対象隊列") != ""
			;付随先の隊列が設定されている場合、そっちを見る
			IF ISNUMERIC(DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠_対象隊列"))
				要素保存 = TOINT(DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠_対象隊列"))
				SIF バフ・デバフ効果検索("ALL", "枠", DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠"), 要素保存) == -1
					CONTINUE
			ENDIF
		ELSE
			;設定されてないなら自分を基準に見る
			SIF バフ・デバフ効果検索("ALL", "枠", DT_CELL_GETS("戦闘効果データベース", 検索番号, "付随効果枠"), 判定隊列) == -1
				CONTINUE
		ENDIF
	ENDIF

	LOCALS '= DT_CELL_GETS("戦闘効果データベース", 検索番号, "適用範囲")
	IF LOCALS == @"{判定隊列}" || LOCALS == @"NO{NO:(BATTLE_STATE:判定隊列:0)}"
		RETURNF 検索番号
	ELSE
		;特殊範囲の分を判定
		SELECTCASE LOCALS
			CASE "フィールド"
				;当然OK
				RETURNF 検索番号
			CASE "敵味方全体"
				;当然OK
				RETURNF 検索番号
			CASE "味方全体"
				SIF 判定隊列 <= 3 && 判定隊列 >= 0
					RETURNF 検索番号
			CASE "敵全体"
				SIF 判定隊列 <= 19 && 判定隊列 >= 10
					RETURNF 検索番号
			CASEELSE
				IF STRFIND(LOCALS, "_") >= 0
					;隊列複数指定を想定
					VARSET RESULTS
					SPLIT LOCALS, "_", RESULTS
					FOR LOCAL, 0, RESULT
						;引っかかったらOKを返す
						IF STRFIND(RESULTS:LOCAL, "NO") >= 0
							;特定キャラ指定の場合
							SIF REPLACE(RESULTS:LOCAL, "NO", "") == @"{NO:(BATTLE_STATE:判定隊列:0)}"
								RETURNF 検索番号
						ELSEIF RESULTS:LOCAL == @"{判定隊列}"
							RETURNF 検索番号
						ENDIF
					NEXT
					;引っかからなかったら次へ
					CONTINUE
				ELSE
					CONTINUE
				ENDIF
		ENDSELECT
	ENDIF
NEXT

RETURNF -1


@強化存在判定(判定隊列)
;なんでもいいから強化が存在するかどうかをチェック
#FUNCTION
#DIM 判定隊列

IF バフ・デバフ効果検索("バフ", "枠", "ALL", 判定隊列) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


@強化数算出(判定隊列, 非表示分カウント = 0)
;なんでもいいから強化がいくつ存在するかどうかを算出
#FUNCTION
#DIM 判定隊列
#DIM 計算値
#DIM 検索番号
#DIM 非表示分カウント

計算値 = 0
FOR 検索番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	SIF 非表示分カウント == 0 && DT_CELL_GET("戦闘効果データベース", 検索番号, "特殊表示オプション") == -1
		CONTINUE
	SIF バフ・デバフ効果適用判定_行指定(検索番号, "バフ", "ALL", 判定隊列)
		計算値 ++
NEXT

RETURNF 計算値


@弱体存在判定(判定隊列)
;なんでもいいから弱体が存在するかどうかをチェック
#FUNCTION
#DIM 判定隊列

IF バフ・デバフ効果検索("デバフ", "枠", "ALL", 判定隊列) >= 0
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


@弱体数算出(判定隊列, 非表示分カウント = 0)
;なんでもいいから弱体がいくつ存在するかどうかを算出
#FUNCTION
#DIM 判定隊列
#DIM 計算値
#DIM 検索番号
#DIM 非表示分カウント

計算値 = 0
FOR 検索番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	SIF 非表示分カウント == 0 && DT_CELL_GET("戦闘効果データベース", 検索番号, "特殊表示オプション") == -1
		CONTINUE
	SIF バフ・デバフ効果適用判定_行指定(検索番号, "デバフ", "ALL", 判定隊列)
		計算値 ++
NEXT

RETURNF 計算値


@付随依存変動算出(判定行, 判定キャラ, 判定種別)
;付随元の数値に依存する効果を見る
#FUNCTION
#DIM 判定行
#DIMS 判定種別
#DIM 判定キャラ
#DIM 付随元行
#DIM 付随元隊列
#DIM 計算値
#DIMS 参照効果名
VARSET LOCAL

;ないはずだけど一応枠があるかどうかを見る
SIF DT_CELL_GETS("戦闘効果データベース", 判定行, "付随効果枠") == ""
	RETURNF 0
;パッシブ付随はもとのバフ・デバフが存在しないので無視
SIF DT_CELL_GETS("戦闘効果データベース", 判定行, "付随効果枠") == "パッシブ"
	RETURNF 0

;付随バフが誰のとこにあるのかを取得
IF ISNUMERIC(DT_CELL_GETS("戦闘効果データベース", 判定行, "付随効果枠_対象隊列"))
	付随元隊列 = TOINT(DT_CELL_GETS("戦闘効果データベース", 判定行, "付随効果枠_対象隊列"))
ELSE
	付随元隊列 = 判定キャラ
ENDIF
;付随元隊列から見て、付随元の効果がどこにあるか？
付随元行 = バフ・デバフ効果検索("ALL", "枠", DT_CELL_GETS("戦闘効果データベース", 判定行, "付随効果枠"), 付随元隊列)
;付随元効果が無いなら効果は発揮しない
SIF 付随元行 == -1
	RETURNF 0

参照効果名 '= DT_CELL_GETS("戦闘効果データベース", 判定行, "参照効果枠")
IF 参照効果名 == ""
	;付随元の固定値を持ってくる。割合は見ない
	計算値 = DT_CELL_GET("戦闘効果データベース", 付随元行, "効果量_固定値")

	;計算値と付随割合（固定値）の乗数を返す
	IF 判定種別 == "割合"
		RETURNF 計算値 * DT_CELL_GET("戦闘効果データベース", 判定行, "付随効果量_割合")
	ELSEIF 判定種別 == "固定値"
		RETURNF 計算値 * DT_CELL_GET("戦闘効果データベース", 判定行, "付随効果量_固定値")
	ENDIF
ELSE
	;この下から、参照効果枠があるならその効果量を引っ張ってくるように改造する。
	;参照効果枠が"属性刻印"の場合、参照効果枠_対象隊列の属性に合わせて効果値が増減するようにする
	;参照効果枠が"自属性刻印限定"の場合、参照効果枠_対象隊列の属性の属性刻印のみで計算する
	;参照効果枠が"付随元属性刻印"の場合、付随効果枠_対象隊列の属性に合わせて効果値が増減するようにする
	;"付随元属性刻印限定"は…アビリティで参照効果枠指定時に、アビリティ使用キャラの自属性刻印を直接入れたらいいかなと
	IF 参照効果名 != "付随元属性刻印"
		;参照効果枠が誰のとこにあるのかを取得。"付随元属性刻印"の場合、付随バフ対象と同じキャラなのでスルー
		IF ISNUMERIC(DT_CELL_GETS("戦闘効果データベース", 判定行, "参照効果枠_対象隊列"))
			付随元隊列 = TOINT(DT_CELL_GETS("戦闘効果データベース", 判定行, "参照効果枠_対象隊列"))
		ELSE
			付随元隊列 = 判定キャラ
		ENDIF
	ENDIF
	;付随元隊列から見て、参照元の効果がどこにあるか？
	IF !GROUPMATCH(参照効果名, "属性刻印", "付随元属性刻印")
		IF 参照効果名 == "自属性刻印限定"
			付随元行 = バフ・デバフ効果検索("ALL", "枠", 属性刻印名(BATTLE_STATE:判定キャラ:属性), 付随元隊列)
		ELSE
			付随元行 = バフ・デバフ効果検索("ALL", "枠", 参照効果名, 付随元隊列)
		ENDIF
		;参照元効果が無いなら効果は発揮しない
		SIF 付随元行 == -1
			RETURNF 0

		;参照元の固定値を持ってくる。割合は見ない
		計算値 = DT_CELL_GET("戦闘効果データベース", 付随元行, "効果量_固定値")
	ELSE
		計算値 = 0
		FOR LOCAL, 0, 6
			;属性刻印の最大値を算出する。参照効果枠対象と同一属性刻印は2倍換算する。
			IF 参照効果名 == "付随元属性刻印" && LOCAL == BATTLE_STATE:付随元隊列:属性
				計算値 = MAX(計算値, 刻印現在数確認(属性刻印:LOCAL, 付随元隊列)*2)
			ELSEIF 参照効果名 == "属性刻印" && LOCAL == BATTLE_STATE:判定キャラ:属性
				計算値 = MAX(計算値, 刻印現在数確認(属性刻印:LOCAL, 付随元隊列)*2)
			ELSE
				計算値 = MAX(計算値, 刻印現在数確認(属性刻印:LOCAL, 付随元隊列))
			ENDIF
		NEXT
		;1/2（端数切り上げ）に。同属性は1～5、他属性は1～3
		計算値 = (計算値 + 1) / 2
	ENDIF


	;計算値と付随割合（固定値）の乗数を返す
	IF 判定種別 == "割合"
		RETURNF 計算値 * DT_CELL_GET("戦闘効果データベース", 判定行, "付随効果量_割合")
	ELSEIF 判定種別 == "固定値"
		RETURNF 計算値 * DT_CELL_GET("戦闘効果データベース", 判定行, "付随効果量_固定値")
	ENDIF
ENDIF

@キャラNO生存_存在判定(対象キャラNO)
#FUNCTION
;特定のNOのキャラが生存し、隊列内にいるかを判定
#DIM 対象キャラNO
#DIM 対象キャラ
#DIM LCOUNT

;あなたは指定不可
SIF 対象キャラNO <= 0
	RETURNF 0

対象キャラ = GETCHARA(対象キャラNO)

FOR LCOUNT, 0, 4
	SIF BATTLE_STATE:LCOUNT:0 != 対象キャラ
		CONTINUE
	SIF BATTLE_STATE:LCOUNT:ＨＰ < 1
		CONTINUE
	RETURNF 1
NEXT

RETURNF 0



