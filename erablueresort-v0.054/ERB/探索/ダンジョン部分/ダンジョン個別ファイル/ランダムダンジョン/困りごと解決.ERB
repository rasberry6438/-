
@ダンジョン初期処理_困りごと解決
;ダンジョン構造の読み込み
;0でない時、そのマスは存在する
;
#DIM 深度
#DIM 分岐
#DIMS 接続先配列,2

VARSET ダンジョン構造配列_困りごと解決
VARSET 分割ブロックリスト
VARSET ランダムダンジョン配列
ランダムダンジョン中フラグ = 1
; CALL ブロック生成(8)
; CALL ブロック内部屋生成(20)
CALL 穴掘り方式()
CALL 部屋タイプ割り当て_困りごと解決


ダンジョンサブタイトル = CONVENIENCE AGENCY
ダンジョンマス画像URL_伏せ状態 = 壁_街
ダンジョン現在位置:0 = ランダムダンジョン初期位置:0
ダンジョン現在位置:1 = ランダムダンジョン初期位置:1
ダンジョン背景画像 = 背景_街

;スタート地点は必ず存在
;初期からオープン済みなので3を入れる
;ビット0：存在するか　ビット1：移動できるか　ビット2：そこから通路を伸ばすか　ビット3：入れないが表示する場合に使用
;ビット4以降：自由枠フラグ

IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}") == 0
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}", 3
ENDIF


FOR 深度, 1, 100
	LOCAL = 0
	FOR 分岐, 0, 9
		IF ダンジョン構造配列_困りごと解決:深度:分岐
			;既にマス情報が入ってるなら飛ばす
			LOCAL = 1
			CONTINUE
		ELSEIF ENUMFUNCBEGINSWITH(@"ダンジョンマス情報_困りごと解決_{深度}_{分岐}")
			LOCAL = 1
			ダンジョン構造配列_困りごと解決:深度:分岐 = 1
		ENDIF
	NEXT
	IF LOCAL
		ダンジョン最大深度 = 深度
	ENDIF
NEXT

@ダンジョン解説文_困りごと解決

詳細文文字列受け渡し変数 = 
詳細文文字列受け渡し変数 += "在度假岛各处解决小麻烦。<br>"
詳細文文字列受け渡し変数 += "虽属日常工作范畴，解决后不会获得卢比报酬，<br>"
詳細文文字列受け渡し変数 += "但作为谢礼能得到各式赠品。<br>"
詳細文文字列受け渡し変数 += "每次进入场景结构都会变化。<br>"

ランダムダンジョンフラグ = 1

;推奨レベルを返す
RETURN 5

@敵討伐後処理_ダンジョン固有_困りごと解決(討伐エネミー数)
;ダンジョン内で戦闘した後に呼ばれる関数、主にダンジョンごとのドロップアイテム取得に使用
;まだ敵データはリセットしていないので敵BATTLE_STATEで色々情報は取得可能
#DIM 討伐エネミー数
#DIM ドロップ確率
#DIMS ドロップアイテム

ドロップ確率 = 20 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 15 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 20 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 15 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("礼物类道具", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = 古旧戒指・铜
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = 古旧耳饰・铜
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 1 * 討伐エネミー数 / 2
ドロップアイテム = 古旧戒指・银
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 1 * 討伐エネミー数 / 2
ドロップアイテム = 古旧耳饰・银
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

;----------
;部屋の内容を割り当てる
;----------
@部屋タイプ割り当て_困りごと解決
#DIM キャライベ数
#DIM 対象キャラ
;部屋の属性を調べて対応する条件の部屋からランダムに割り当てる
;ランダムダンジョン配列:深度:分岐:イベント名 = イベントの名前
#DIM 深度
#DIM 分岐

;固有イベントをチェック
VARSET RESULTS
VARSET LOCALS
LOCAL:1 = 0
キャライベ数 = ENUMFUNCBEGINSWITH("イベント_困りごと解決_キャラ依頼_")
FOR LOCAL, 0, キャライベ数
	;依頼のキャラを引っ張ってくる
	対象キャラ = TOINT(SUBSTRING(RESULTS:LOCAL, 33))
	SIF 対象キャラ < 1
		CONTINUE
	;キャラが島にいないとダメ
	SIF CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
	;ある程度仲良くないとダメ
	SIF CFLAG:対象キャラ:好感度レベル < 関係閾値:2
		CONTINUE
	;キャラがPTメンバーにいるとダメ
	SIF GROUPMATCH(対象キャラ, BATTLE_STATE:0:キャラ登録番号, BATTLE_STATE:1:キャラ登録番号, BATTLE_STATE:2:キャラ登録番号, BATTLE_STATE:3:キャラ登録番号, BATTLE_STATE:10:キャラ登録番号, BATTLE_STATE:11:キャラ登録番号, BATTLE_STATE:12:キャラ登録番号, BATTLE_STATE:13:キャラ登録番号)
		CONTINUE
	LOCALS:(LOCAL:1) = %RESULTS:LOCAL%
	LOCAL:1 += 1
NEXT
キャライベ数 = LOCAL:1

FOR 深度, 0, 10
	FOR 分岐, 0, 10
		SIF ランダムダンジョン配列:深度:分岐:イベント状態 == ""
			CONTINUE
		IF 深度 == ランダムダンジョン初期位置:0 && 分岐 == ランダムダンジョン初期位置:1
			;初期位置
			ランダムダンジョン配列:深度:分岐:イベント名 = 初期マス
		ELSE
			;イベントがあるのは半分のマス
			IF RAND:2
				ランダムダンジョン配列:深度:分岐:イベント名 = 何もなし
				CONTINUE
			ENDIF
			;更に1/2でキャライベを格納
			IF キャライベ数 > 0 && RAND:2
				LOCAL = RAND:キャライベ数
				ランダムダンジョン配列:深度:分岐:イベント名 = %SUBSTRING(LOCALS:LOCAL, 22)%
				ランダムダンジョン配列:深度:分岐:マス画像名 = 依頼
				ARRAYSHIFT LOCALS, -1, "", LOCAL
				キャライベ数 -= 1
				CONTINUE
			ENDIF
			;残りは汎用イベ
			SELECTCASE RAND:6
				CASE 5
					ランダムダンジョン配列:深度:分岐:イベント名 = 疲れたおじいさん
				CASE 4
					ランダムダンジョン配列:深度:分岐:イベント名 = 遊戯場修理
				CASE 3
					ランダムダンジョン配列:深度:分岐:イベント名 = ナンパに困る女性
				CASE 2
					ランダムダンジョン配列:深度:分岐:イベント名 = 忘れっぽい青年
				CASE 1
					ランダムダンジョン配列:深度:分岐:イベント名 = 迷子の男の子
				CASE 0
					ランダムダンジョン配列:深度:分岐:イベント名 = ペット探し
			ENDSELECT
			ランダムダンジョン配列:深度:分岐:マス画像名 = 依頼
		ENDIF
	NEXT
NEXT


;----------
;マスタイプ
;----------
@マスタイプ_困りごと解決(深度,分岐)
#DIM 深度
#DIM 分岐
SELECTCASE ランダムダンジョン配列:深度:分岐:マス画像名
	;マスタイプで分岐
	CASE IS == "依頼"
		ダンジョンマス画像URL = 床_街
		IF ランダムダンジョン配列:深度:分岐:イベント状態 != "依頼完了"
			ダンジョンマス画像URL_重ね表示 = 会話アイコン
		ENDIF
	CASE IS == ""
		ダンジョンマス画像URL = 床_街
	CASEELSE
		ダンジョンマス画像URL = 床_街
ENDSELECT



;----------
;イベント内容
;----------
@イベント_困りごと解決_初期マス
ダンジョンクリアフラグ_困りごと解決 = 1
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 请巡视周边解决各类问题。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 有人生活的地方就会产生各种困扰。
	KSTR:(K++) = 请四处巡视并解决问题。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_何もなし
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 当前区域暂无需要帮助者。
	KSTR:(K++) = 去别的地方吧。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_ペット探し
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 发现啜泣着呼唤某物名字的少女。
	WSTR:(K++) = 似乎是带来的宠物走失了。
	KSTR:(K++) = 帮助她一起寻找。_PAGE
	WSTR:(K++) = …
	WSTR:(K++) = ……
	WSTR:(K++) = ………找到了！
	LOCALS = %ランダム素材名("礼物类道具", 1)%
	WSTR:(K++) = 将戴着项圈的猫归还少女，少女赠送「%LOCALS%」作为谢礼。
	KSTR:(K++) = %CALLNAME:PLAYER%一行告别了笑容满面的女孩，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_迷子の男の子
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 发现慌张四处张望的小男孩。
	WSTR:(K++) = 似乎是与家人走散的迷路儿童。
	KSTR:(K++) = 将男孩带回家人身边。_PAGE
	WSTR:(K++) = …
	WSTR:(K++) = ……
	WSTR:(K++) = ………找到了！
	LOCALS = %ランダム素材名("礼物类道具", 1)%
	WSTR:(K++) = 当迷路的男孩被带到他母亲身边时，母亲赠送「%LOCALS%」作为谢礼。
	KSTR:(K++) = %CALLNAME:PLAYER%一行告别被母亲宠坏的男孩，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_忘れっぽい青年
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 青年在泳池前发愁。
	WSTR:(K++) = 忘记带泳裤无法游泳。
	LOCALS = %ランダム素材名("礼物类道具", 2)%
	WSTR:(K++) = 带他去泳装租借处，青年开心地赠送「%LOCALS%」作为谢礼。
	KSTR:(K++) = %CALLNAME:PLAYER%一行告别与其他人兴奋地玩耍的年轻人，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_ナンパに困る女性
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 年轻女性被两名男性纠缠。
	WSTR:(K++) = 显然她很难拒绝搭讪。
	KSTR:(K++) = %CALLNAME:PLAYER%以度假村管理员身份制止骚扰行为。
	WSTR:(K++) = 两名男子道歉说他们不是故意打扰这名女子，然后走开了。
	LOCALS = %ランダム素材名("礼物类道具", 2)%
	WSTR:(K++) = 女性向%CALLNAME:PLAYER%鞠躬致谢，赠送「%LOCALS%」作为谢礼。
	KSTR:(K++) = %CALLNAME:PLAYER%一行告别了安心在度假村玩耍的女性，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_遊戯場修理
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 进入游戏场时，发现两位男士正手足无措地站着。
	WSTR:(K++) = 看起来他们不小心弄坏了游乐设备。
	KSTR:(K++) = 稍微检查后发现，这种程度的损坏换个零件就能修好。_PAGE
	IF RAND:2
		LOCALS = 古旧戒指・铜
	ELSE
		LOCALS = %ランダム素材名("礼物类道具", 3)%
	ENDIF
	WSTR:(K++) = %CALLNAME:PLAYER%三下五除二修好设备，男士们如释重负地送上「%LOCALS%」作为谢礼。
	KSTR:(K++) = %CALLNAME:PLAYER%一行告别了小心使用设备的男士们，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_疲れたおじいさん
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 当前区域暂无需要帮助者。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	IF 施設改造度:3:0
		WSTR:(K++) = 在露营区发现树荫下瘫坐着一位老爷爷。
	ELSE
		WSTR:(K++) = 在度假屋庭院的树荫下发现瘫坐着的老爷爷。
	ENDIF
	WSTR:(K++) = 似乎散步途中突然身体不适。
	KSTR:(K++) = %CALLNAME:PLAYER%递上饮用水，用毛巾轻轻擦拭老人额头的汗水……_PAGE
	IF RAND:2
		LOCALS = 古旧耳饰・铜
	ELSE
		LOCALS = %ランダム素材名("礼物类道具", 3)%
	ENDIF
	WSTR:(K++) = 片刻后老人恢复精神，反复道谢并硬塞给%CALLNAME:PLAYER%「%LOCALS%」。
	KSTR:(K++) = %CALLNAME:PLAYER%叮嘱老爷爷要注意身体后，继续寻找其他需要帮助的人。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

