@証関数_巫师之证(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = 巫师之证
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = 隐者
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = 秘法师
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = 巫师
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "擅长群体攻击的魔导士凭证。<br>"
		詳細文文字列受け渡し変数 += "精通全体攻击与纵列攻击等范围魔法，亦可操控毒素。<br>"
		詳細文文字列受け渡し変数 += "装备后仿佛智力提升——虽然只是错觉。<br>"
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「人食い鳥の島」のイベントで入手）
;@証出現率_巫师之证(入手タイプ, 装備ランク)

@証アビ関数_巫师之证_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "以太爆发"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 185
	CASE 2
		効果数値:0 = 150
	CASEELSE
		効果数値:0 = 100
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：对敌方全体造成［攻击力✕{効果数値:0}％］的自身属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_巫师之证_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIMS アビ名
#DIM 効果数値, 3
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "短剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		アビ名 '= "猛毒"
		効果数値:0 = 250
		効果数値:1 = 80
		効果数値:2 = 30
	CASE 2
		アビ名 '= "毒素"
		効果数値:0 = 210
		効果数値:1 = 80
		効果数値:2 = 25
	CASEELSE
		アビ名 '= "毒素"
		効果数値:0 = 180
		効果数値:1 = 70
		効果数値:2 = 25
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者擅长短剑或辅助类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：对敌方单体造成［攻击力✕{効果数値:0}％］的暗属性伤害。<br>"
		詳細文文字列受け渡し変数 += @"　　　　附加3回合中毒［自身等级✕{効果数値:2}］状态，基础成功率{効果数値:1}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_闇属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:効果量 = BATTLE_STATE:戦闘行動キャラ:Lv * 効果数値:2
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果数値:1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_巫师之证_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "超新星焰"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "杖")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
	CASE 2
		効果数値:0 = 200
	CASEELSE
		効果数値:0 = 150
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者擅长法杖或攻击类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：对敌方纵列造成［攻击力✕{効果数値:0}％］的火属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@証アビ関数_巫师之证_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "追击附魔"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 1
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	; CASE IS >= 2
	; 	効果数値:0 = 20
	CASEELSE
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：赋予我方全体3回合追击［造成伤害×{効果数値:0}％/A槽］效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
