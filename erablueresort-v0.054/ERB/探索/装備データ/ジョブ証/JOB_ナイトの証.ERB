@証関数_骑士之证(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = 骑士之证
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = 圣剑士
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = 堡垒
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = 骑士
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "重视防御的坦克向职业证。<br>"
		詳細文文字列受け渡し変数 += "擅长守护同伴并抓住敌人破绽施展技巧。<br>"
		詳細文文字列受け渡し変数 += "虽然人气不高但能进行关键的走位操作。<br>"
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「人食い鳥の島」のイベントで入手）
;@証出現率_骑士之证(入手タイプ, 装備ランク)

@証アビ関数_骑士之证_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "盾墙"
#DIM CONST 消費ＭＰ = 45
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 70
	CASE 2
		効果数値:0 = 60
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：赋予我方全体1次［{効果数値:0}％］的伤害减少效果。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_骑士之证_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 攻撃対象
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "过载涌动"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("バランス")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 200
		効果数値:1 = 70
	CASE 2
		効果数値:0 = 150
		効果数値:1 = 50
	CASEELSE
		効果数値:0 = 100
		効果数値:1 = 30
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者为剑专精或平衡类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　效果：对敌方横列造成［攻击力✕{効果数値:0}％］的自身属性伤害。<br>"
		詳細文文字列受け渡し変数 += @"　　　　目标每存在1个CT威力便提升［{効果数値:1}％］。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:選択時メッセージ１ = 要选择哪一横列使用？
		CALL アビ対象選択テンプレート_敵単体
		;本来「CALL アビ対象選択テンプレート_敵横列」を使うべきだが、相手によって威力が変動するため
		;手動で処理を行う
		SIF アビテンプレート用_キャンセルフラグ
			RETURN -1
		攻撃対象 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = 効果数値:0 + (敵BATTLE_STATE:(攻撃対象 - 10):チャージターン * 効果数値:1)
		CALL アビテンプレート_ダメージ処理_自属性
		IF 攻撃対象 % 2
			攻撃対象 -= 1
		ELSE
			攻撃対象 += 1
		ENDIF
		IF 敵BATTLE_STATE_STR:(攻撃対象 - 10):エネミー名 != "" && 敵BATTLE_STATE:(攻撃対象 - 10):ＨＰ > 0
			CALL アビ対象選択テンプレート_指定(攻撃対象)
			アビ汎用変数:効果割合 = 効果数値:0 + (敵BATTLE_STATE:(攻撃対象 - 10):チャージターン * 効果数値:1)
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF
ENDSELECT

@証アビ関数_骑士之证_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "援护"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "槍")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("防御")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 35
	CASE 2
		効果数値:0 = 25
	CASEELSE
		効果数値:0 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装备者为长枪专精或防御类型时）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：赋予自身至下次行动前，代替队友承受所有单体攻击的效果。"
		IF 効果数値:0 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　使自身防御力至下次行动前提升［{効果数値:0}％/独立槽］。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 単体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 単体かばう
		;1にするとかばう使った直後に消えてしまうので2で設定
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット
		IF 効果数値:0 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = かばう_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 効果数値:0
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_骑士之证_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS ガード属性
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "元素护盾"
#DIM CONST 消費ＭＰ = 40
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
	IF 装備ステータス補正保存:対象キャラ:属性
		ガード属性 '= 属性数値文字列変換(有利属性変換(装備ステータス補正保存:対象キャラ:属性 - 10))
	ELSE
		ガード属性 '= 属性数値文字列変換(有利属性変換(基礎BATTLE_STATE:対象キャラ:属性))
	ENDIF
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += @"　效果：针对能力使用者的优势属性，3回合内提升我方全体抗性［+{効果数値:0}/A槽］。"
		ELSE
			詳細文文字列受け渡し変数 += @"　效果：3回合内我方全体%ガード属性%抗性提升［+{効果数値:0}/A槽］。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = %ガード属性%耐性A
		アビ汎用文字列:バフ・デバフ対象能力 = %ガード属性%耐性
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_骑士之证_10(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS CONST アビ名 = "盾牌猛击"
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消耗ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　效果：对敌方单体造成［防御力✕100％］的自身属性伤害。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 防御力
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT
