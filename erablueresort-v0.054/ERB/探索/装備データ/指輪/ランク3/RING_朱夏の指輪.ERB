
@指輪個別文章表示_朱夏之戒
LOCALS = 
LOCALS += @"■朱夏之戒<br><br>"
LOCALS += "某国巫女灌注火行之气打造的戒指。<br>"
LOCALS += "夏季装备时可发挥高攻击性能。<br>"
LOCALS += "亦适合全力讴歌人生的壮年者装备。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_朱夏之戒(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基础效果】<br>"
LOCALS += "最大ＨＰ+450 最大ＭＰ+90 攻击力+400 防御力+60 行动速度+200 ※需符合条件<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【选项效果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_朱夏之戒(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_朱夏之戒(対象キャラ)
#DIM 対象キャラ
;夏なら無条件でOK
SIF (DAY / 30) % 4 == 1
	RETURN 1
;耳飾りとのセット効果
SIF GROUPMATCH(DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:対象キャラ:装備耳飾り番号, "耳飾り名", 1), "樱花耳饰", "牵牛花耳饰", "红叶耳饰", "水仙耳饰", "莲华耳饰", "菊花耳饰") > 0
	RETURN 1
;大人・壮年
RETURN (TALENT:対象キャラ:年齢層 == 年齢層_大人 || TALENT:対象キャラ:年齢層 == 年齢層_壮年)

@指輪ステータス決定_朱夏之戒(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_450_最大ＭＰ_90_攻撃力_400_防御力_60_行動速度_200

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "朱夏之戒", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
