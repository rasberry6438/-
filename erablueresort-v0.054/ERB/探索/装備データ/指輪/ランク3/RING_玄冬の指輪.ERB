
@指輪個別文章表示_玄冬之戒
LOCALS = 
LOCALS += @"■玄冬之戒<br><br>"
LOCALS += "某国巫女灌注水行之气打造的戒指。<br>"
LOCALS += "冬季装备时可发挥高防御性能。<br>"
LOCALS += "亦适合等待萌芽的年幼者与安度晚年的老者装备。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_玄冬之戒(指輪番号 = 0, 効果番号 = -1)
#DIM 効果番号
#DIM 指輪番号
IF 効果番号 > -1
	;効果を指定されているのでランダム部分固定
	LOCALS = 
	LOCALS += "【基础效果】<br>"
	LOCALS += @"最大ＨＰ+800 防御力+400 恢复力+150 行动速度+150 %属性数値文字列変換(効果番号 - 1)%耐性+20 ※需符合条件<br>"
ELSEIF 指輪番号 < 1
	;所持してない指輪なのでランダム表記
	LOCALS = 
	LOCALS += "【基础效果】<br>"
	LOCALS += "最大ＨＰ+800 防御力+400 恢复力+150 行动速度+150 随机属性耐性+20 ※需符合条件<br>"
ELSE
	;所持してる指輪なのでランダム部分を引っ張ってくる
	LOCALS = 
	LOCALS += "【基础效果】<br>"
	LOCALS += "最大ＨＰ+800 防御力+400 恢复力+150 行动速度+150 "
	LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "基礎ステータス補正")%
	LOCALS:1 = %SUBSTRING(LOCALS:1, 58, 2)%
	LOCALS += @"%LOCALS:1%耐性+20 ※需符合条件<br>"
ENDIF

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【选项效果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%
;ランダム効果数を返す
RETURN 6

@指輪出現率_玄冬之戒(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_玄冬之戒(対象キャラ)
#DIM 対象キャラ
;冬なら無条件でOK
SIF (DAY / 30) % 4 == 3
	RETURN 1
;耳飾りとのセット効果
SIF GROUPMATCH(DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:対象キャラ:装備耳飾り番号, "耳飾り名", 1), "樱花耳饰", "牵牛花耳饰", "红叶耳饰", "水仙耳饰", "莲华耳饰", "菊花耳饰") > 0
	RETURN 1
;幼い・老年
RETURN (TALENT:対象キャラ:年齢層 == 年齢層_幼い || TALENT:対象キャラ:年齢層 >= 年齢層_人外)

@指輪ステータス決定_玄冬之戒(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_800_防御力_400_回復力_150_行動速度_150
IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF
IF 効果指定 > -1
	LOCAL = 効果指定
ELSE
	LOCAL = RAND:6
ENDIF
基本補正文字列 += @"_%属性数値文字列変換(LOCAL)%耐性_20"

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "玄冬之戒", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
