@乗機関数_科罗萨斯复制品(処理モード)
#DIMS 処理モード
#DIMS 武装登録, 6
#DIM 乗機攻撃力
#DIM サブパイロット人数
#DIM 乗機命中率
#DIMS 乗機名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "乗機登録"
		乗機名一時保存 = 科罗萨斯复制品
		;武装が無い場合は「不可」を入れる
		;固有武装の場合は「固有」を入れる
		;装備付け替え可能枠は何入れてもいいけど、わかりやすいように「装備可能」を入れてる
		武装登録:0 = 固有
		武装登録:1 = 固有
		武装登録:2 = 装備可能
		武装登録:3 = 不可
		武装登録:4 = 不可
		武装登録:5 = 不可
		;攻撃系武装で使用する基礎攻撃力を設定する
		乗機攻撃力 = 700
		乗機命中率 = 95
		;何人サブメンバーを載せられるか？
		サブパイロット人数 = 2

		{
		DT_ROW_ADD "所持乗機データベース", "乗機名", 乗機名一時保存, "乗機攻撃力", 乗機攻撃力, "乗機攻撃力_強化後", 乗機攻撃力, 
			"乗機命中率", 乗機命中率, "乗機命中率_強化後", 乗機命中率, 
			"武装0", 武装登録:0, "武装1", 武装登録:1, "武装2", 武装登録:2, "武装3", 武装登録:3, "武装4", 武装登録:4, "武装5", 武装登録:5,
			"サブ搭乗数", サブパイロット人数, "サブ搭乗数_強化後", サブパイロット人数
		}
	CASE "フレーバー"
		LOCALS += "根据巴鲁慈地下废弃工厂发现的图纸制造的机体。<br>"
		LOCALS += "仅重现了科罗萨斯武装与装甲的量产机型。<br>"
		LOCALS += "因未进行星晶兽化改造，性能上处于下位替代。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 铁矿石_10
		作成素材登録:1 = 赤热矿_10
		作成素材登録:2 = 日珥反应炉_1
ENDSELECT

;兵装開発では出現しない（入手法：ダンジョン「バルツ地下廃工場」のイベントで入手）
;@乗機開発対象_科罗萨斯复制品(処理モード)


@乗機兵装関数_科罗萨斯复制品_0(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM Lv記録
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 防御力场
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%限制：剩余{乗機残弾数:武装番号, 2}次<br>"
		LOCALS += "　效果：3回合内我方全体防御力上升[50％/机体槽]。<br>"
		LOCALS += "　　　　赋予我方全体3回合再生效果[队友最大等级✕10/机体槽]。<br>"
		LOCALS += "　　　　对敌方全体造成[队友最大等级×25]火属性固定伤害。<br>"
		LOCALS += "　　　　当替补成员存在科罗萨斯或科罗玛格酱时，再生效果数值翻倍。"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		Lv記録 = 0
		FOR LOCAL, 0, 14
			SIF LOCAL > 3 && LOCAL < 10
				CONTINUE
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			SIF Lv記録 < BATTLE_STATE:LOCAL:Lv
				Lv記録 = BATTLE_STATE:LOCAL:Lv
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 10
		FOR LOCAL, 10, 14
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			IF キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[钢铁巨人]科罗萨斯") || キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[钢铁小巨人]科罗玛格酱")
				アビ汎用変数:効果量 = Lv記録 * 20
				BREAK
			ENDIF
		NEXT
		アビ汎用文字列:バフ・デバフ枠 = 再生_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 25
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@乗機兵装関数_科罗萨斯复制品_1(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 1
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 日珥反应炉
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%限制：剩余{乗機残弾数:武装番号, 2}次<br>"
		LOCALS += "　效果：3回合内我方全体攻击力上升[20%/机体槽]。<br>"
		LOCALS += "　　　　对敌方全体造成［攻击力✕150％］的火属性伤害。<br>"
		LOCALS += "　　　　3回合内敌方全体防御力下降[15%/机体槽]，基础成功率80%。<br>"
		LOCALS += "　　　　当替补成员存在科罗萨斯或科罗玛格酱时，伤害部分会触发两次。"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_火属性
		FOR LOCAL, 10, 14
			SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
				CONTINUE
			IF キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[钢铁巨人]科罗萨斯") || キャラ一致チェック(BATTLE_STATE:LOCAL:キャラ登録番号, "[钢铁小巨人]科罗玛格酱")
				CALL アビテンプレート_ダメージ処理_火属性
				BREAK
			ENDIF
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 15
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@乗機兵装関数_科罗萨斯复制品_2(処理モード, 兵器id = 0)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
#DIM 兵器id
;武装番号は武装登録の番号と対応させること
武装番号 = 2
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名", "アビ効果"
		;兵装処理系は装備してる兵装の関数に投げる
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
		CATCH
			PRINTW 处理无效
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%限制：剩余{乗機残弾数:武装番号, 2}次<br>"
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
			詳細文文字列受け渡し変数 = %LOCALS%%詳細文文字列受け渡し変数%
		CATCH
			PRINTW 处理无效
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "残弾セット"
		乗機残弾種別:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾種別", 1)
		乗機残弾数:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾数_強化後", 1)
;---------------------------------------------------------------------------------------------------------
;編集するのはここから
	CASE "装備条件説明文"
		LOCALS += @"　可装备类别：机械系or魔法系<br>"
		LOCALS += "　可装备距离：近距离<br>"
		LOCALS += "　其他条件：无<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "装備条件判定"
		;装備不可だと0、可能だと1を返す
		SIF 乗機装備条件判定(兵器id, "機械系_魔法系", "近距離")
			RETURN 1
		RETURN 0
;編集するのはここまで
;---------------------------------------------------------------------------------------------------------
ENDSELECT
