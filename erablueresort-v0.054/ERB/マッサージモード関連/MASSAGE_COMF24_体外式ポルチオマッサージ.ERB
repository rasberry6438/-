;-------------------------------------------------
;体外式子宫口按摩
;-------------------------------------------------
@MASSAGE_COMNAME24
#FUNCTIONS
TSTR:コマンド名受渡 = 体外式子宫口按摩


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_24
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_24
LOCALS = <br>■体外式子宫口按摩<br>
LOCALS += "通过腹部体表对子宫口附近性感带进行指压刺激"
LOCALS += "<br>指令类型：情色按摩<br>获取来源：快Ｖ・接触・痛苦・欢乐"
LOCALS += "<br>专属经验值：子宫口开发(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM24
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


RCVAR:ポルチオ開発フラグ = 2
IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 80
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -60)
	BASE:TARGET:性欲 += 80
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -50)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSE
;局部に直接触れるわけではないので、警戒は少し上がるが理性は削れる。警戒で理性が10上がり、ポルチオへの刺激で理性が50下がるイメージでこの数値。
;性欲も大きく煽られる。だいぶ強コマンド。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -40)
	BASE:TARGET:性欲 += 80
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 20)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
ENDIF

;ポルチオ責めよりは弱い。お腹を押し込まれるため苦痛がちょっとある
CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 300)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 50)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)

;苦痛コマンドフラグだと苦痛快楽じゃなくて苦痛絶頂ばっかり溜まりがちだから快楽固定でマゾ性感上げの緩和を図る
EXP:TARGET:苦痛快楽経験 += 1

;妊娠率が10%上昇する
RCVAR:TARGET:妊娠率補正_加算値 += 10

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 100)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE24
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(24)
	RETURN RESULT

;Ｖがないとダメ
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;実行難易度もかなり緩い。ちょっと強すぎる気もしなくもないけど……えっちだからヨシ！
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1400
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM24
PRINTFORM %CALLNAME:PLAYER%对%CALLNAME:TARGET%
	IF TALENT:TARGET:体型 >= 1
	;肉感的以上
		PRINTFORM 丰腴饱满的
	ELSEIF TALENT:TARGET:体型 <= -2
	;細身以下
		PRINTFORM 纤细骨感的
	ENDIF
PRINTFORML 腹部施压，用指腹以画圆手法%TEXTR("刺激/揉捏")%子宫口区域。

;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 900
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%%TEXTR("双眸迷离地/满脸陶醉地张着樱唇/神情恍惚地")%
	CASE 1
		PRINTFORM %CALLNAME:TARGET%从微张的唇间%TEXTR("溢出甜腻呻吟/发出愉悦娇喘/漏出淫靡声响")%
	ENDSELECT
	PRINTFORML 彻底沉溺于%CALLNAME:PLAYER%%TEXTR("的『按摩』/的技法/带来的快感")%%TEXTR("之中/的支配/的冲击")%…
ENDIF
