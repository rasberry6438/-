@SHOW_USERCOM

SIF SAVESTR:ゲームフェイズ管理 == ""
	RETURN 0

TRYCCALLFORM SHOW_USERCOM_%SAVESTR:ゲームフェイズ管理%
CATCH
	CALL SHOW_USERCOM_通常モード
ENDCATCH
RETURN RESULT


@SHOW_USERCOM_通常モード
#DIM コマンド数
#DIM 同行者フラグ
VARSET LOCAL

;コマンド表示
CALL SHOW_COMMAND
IF OPTION変数:サイド領域非表示 == 0
	CALL サイド領域_改行処理
ENDIF
DRAWLINE

RESETCOLOR
PRINTC 能力显示(MASTER)[804]
IF CFLAG:うふふ && TARGET > 0
	SIF TALENT:PLAYER:脱衣不可 == 0
		PRINTC 脱衣[820]
	IF CFLAG:うふふ == 2
		PRINTC 脱身[999]
	ELSE
		PRINTC 结束幽会[999]
	ENDIF
ENDIF
IF CFLAG:飲み会 && TARGET > 0
	PRINTC 结束酒会[998]
ENDIF
PRINTC 探索情报总览[830]

同行者フラグ = 0
FOR LOCAL, 1, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	SIF CFLAG:(TARGET:LOCAL):同行 || CFLAG:(TARGET:LOCAL):うふふ
		同行者フラグ = 1
NEXT
SIF !同行者フラグ && !CFLAG:MASTER:隠密
	PRINTC 隐藏于暗处[840]
SIF 同行者フラグ && !CFLAG:MASTER:隠密
	PRINTC 一起隐匿[840]
SIF CFLAG:MASTER:隠密 && (!CFLAG:うふふ || GET_TARGETNUM() == 同行者フラグ)
	PRINTC 离开暗处[841]
IF TARGET
	PRINTC 使用物品[850]
ELSE
	PRINTC 对自己使用物品[850]
ENDIF
PRINTL
PRINTC 照片列表[856]
PRINTC OPTION[809]
IF OPTION変数:連れ込み制御
	SETCOLOR カラーパレット("黄")
	PRINTBUTTONC "禁止带人:ON[892]", 892
	RESETCOLOR
ELSE
	PRINTBUTTONC "禁止带人:OFF[892]", 892
ENDIF
IF OPTION変数:休憩制御
	SETCOLOR カラーパレット("黄")
	PRINTBUTTONC "休息时禁止访问:ON[893]", 893
	RESETCOLOR
ELSE
	PRINTBUTTONC "休息时禁止访问:OFF[893]", 893
ENDIF
SIF あなた特殊能力:居場所感知
	PRINTC 角色位置感知[860]
PRINTC 雇佣报告[861]
RESETCOLOR

PRINTL 
PRINT ＜指令记录:
FOR LOCAL, 0, 5
	IF コマンド履歴:LOCAL >= 370 && コマンド履歴:LOCAL <= 374
		SIF LOCAL > 0
			PRINT  - 
		;現在位置が同じかつCOMABLEが通るならボタン化
		IF TARGET == コマンド履歴:(LOCAL + 5)
			TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
			IF RESULT
				PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
			ELSE
				PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			ENDIF
		ELSE
			;一旦TARGETが違うなら一旦相手を保存してから固有コマンド表示
			SWAP TARGET, コマンド履歴:(LOCAL + 5)
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			SWAP TARGET, コマンド履歴:(LOCAL + 5)
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 380 && コマンド履歴:LOCAL <= 384
		SIF LOCAL > 0
			PRINT  - 
		;現在位置が同じかつCOMABLEが通るならボタン化
		IF CFLAG:MASTER:現在位置 == コマンド履歴:(LOCAL + 10) && CFLAG:MASTER:現在マップ種別 == コマンド履歴:(LOCAL + 15)
			TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
			IF RESULT
				PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
			ELSE
				PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			ENDIF
		ELSE
			;一旦現在位置が違うなら一旦位置を保存してから固有コマンド表示
			LOCAL:1 = CFLAG:MASTER:現在位置
			LOCAL:2 = CFLAG:MASTER:現在マップ種別
			CFLAG:MASTER:現在位置 = コマンド履歴:(LOCAL + 10)
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
			CFLAG:MASTER:現在位置 = LOCAL:1
			CFLAG:MASTER:現在マップ種別 = LOCAL:2
		ENDIF
	ELSEIF コマンド履歴:LOCAL >= 0
		SIF LOCAL > 0
			PRINT  - 
		;COMABLEが通るならボタン化
		TRYCALLFORM COM_ABLE{コマンド履歴:LOCAL}
		IF RESULT
			PRINTBUTTON @"[%TRAIN_COMNAME(コマンド履歴:LOCAL)%]", コマンド履歴:LOCAL
		ELSE
			PRINTFORM [%TRAIN_COMNAME(コマンド履歴:LOCAL)%]
		ENDIF
	ELSEIF コマンド履歴:LOCAL == -1
		SIF LOCAL > 0
			PRINT  - 
		;COMABLEが通るならボタン化
		TRYCALLFORM COM_ABLE_S%派生コマンド履歴:LOCAL%
		IF RESULT
			PRINTBUTTON @"[%TRAIN_COMNAME_S(派生コマンド履歴:LOCAL)%]", @"%派生コマンド履歴:LOCAL%"
		ELSE
			PRINTFORM [%TRAIN_COMNAME_S(派生コマンド履歴:LOCAL)%]
		ENDIF
	ENDIF
NEXT
PRINTL ＞

@USERCOM
#DIMS COM変換
RFLAG:入替えコマンドフラグ = 0

SIF SAVESTR:ゲームフェイズ管理 == ""
	RETURN 0

;逆奉仕時の処理を仕込む
IF STRFIND(RSTR:SELECTCOM_S受け渡し, "R") == 0
	COM変換 = %REPLACE(RSTR:SELECTCOM_S受け渡し, "R", "")%
	IF ISNUMERIC(COM変換)
		RESULT = TOINT(COM変換)
		RSTR:SELECTCOM_S受け渡し = 
	ELSE
		RSTR:SELECTCOM_S受け渡し = %COM変換%
	ENDIF
	RFLAG:入替えコマンドフラグ = 1
	SWAP TARGET, PLAYER
ENDIF

CALLFORM USERCOM_%SAVESTR:ゲームフェイズ管理%

RETURN RESULT

@USERCOM_通常モード
;#DIM ページ番号
#DIM 数値変換用
#DIM TARGET何人目？

;ページ番号 = 0
;RETURN 0でSOURCE_CHECK以降に進む
;NEXTCOMを仕込む
IF NEXTCOM >= 0
	RESULT = NEXTCOM
	NEXTCOM = -1
ENDIF

IF RSTR:SELECTCOM_S受け渡し != ""
	;受け渡しがある場合はMODE設定の変更かどうか確認
	CALL MODE設定変更処理(RSTR:SELECTCOM_S受け渡し)
	SIF RESULT
		RETURN 1

	;それ以外の場合はRSTR:SELECTCOM_S受け渡しに派生コマンドが入っているはず
	SELECTCOM = -1
	SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%
	SIF TRAIN_COMNAME_S(SELECTCOM_S) == ""
		RETURN 1
	RSTR:SELECTCOM_S受け渡し = %SELECTCOM_S%
	RESULT = 1
	LOCAL:2 = 0
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		LOCAL:10 = TARGET
		FOR LOCAL, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:LOCAL < 0
				BREAK
			RESULT = 0
			TARGET = 同時モード_選択キャラ:LOCAL
			TRYCALLFORM COM_ABLE_S%SELECTCOM_S%
			IF RESULT == 0
				TARGET = LOCAL:10
				RETURN 1
			ENDIF
			RESULT = 1
			IF TCVAR:TARGET:食いしばりフラグ
			ELSEIF TARGET == MASTER
				TARGET = PLAYER
				PLAYER = MASTER
				RFLAG:指示モード対象あなたフラグ = 1
				TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
				RFLAG:指示モード対象あなたフラグ = 0
				LOCAL:2 = RESULT
				IF !LOCAL:2
					TRYCALLFORM CAN_COM_S%SELECTCOM_S%
					;失敗時口上
					;これプレイヤー側時いる？　一旦封印
					;TRYCALLFORM KOJO_CANCOMPLAYER失敗時_{NO:TARGET}_%SELECTCOM_S%(TARGET)
				ENDIF
				PLAYER = TARGET
				TARGET = MASTER
				SIF !LOCAL:2
					RETURN 1
			ELSE
				TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
				IF !RESULT
					TRYCALLFORM CAN_COM_S%SELECTCOM_S%
					;失敗時口上
					PRINTL
					TRYCALLFORM KOJO_CANCOM失敗時_{NO:TARGET}_%SELECTCOM_S%(TARGET)
					RETURN 1
				ENDIF
			ENDIF
		NEXT
		RSTR:SELECTCOM_S受け渡し = %SELECTCOM_S%
		TARGET = LOCAL:10
	ELSE
		TRYCALLFORM COM_ABLE_S%SELECTCOM_S%
		SIF RESULT == 0 || RESULT == -1
			RETURN 1
		IF TCVAR:TARGET:食いしばりフラグ
		ELSEIF TARGET == MASTER
			TARGET = PLAYER
			PLAYER = MASTER
			RFLAG:指示モード対象あなたフラグ = 1
			TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
			RFLAG:指示モード対象あなたフラグ = 0
			LOCAL:2 = RESULT
			IF !LOCAL:2
				TRYCALLFORM CAN_COM_S%SELECTCOM_S%
				;失敗時口上
				;これプレイヤー側時いる？　一旦封印
				;TRYCALLFORM KOJO_CANCOMPLAYER失敗時_{NO:TARGET}_%SELECTCOM_S%(TARGET)
			ENDIF
			PLAYER = TARGET
			TARGET = MASTER
			SIF !LOCAL:2
				RETURN 1
		ELSE
			TRYCALLFORM CAN_COM_S%SELECTCOM_S%(1)
			IF !RESULT
				TRYCALLFORM CAN_COM_S%SELECTCOM_S%
				;失敗時口上
				PRINTL
				TRYCALLFORM KOJO_CANCOM失敗時_{NO:TARGET}_%SELECTCOM_S%(TARGET)
				RETURN 1
			ENDIF
		ENDIF
	ENDIF
ELSEIF RESULT == 9101
	;通常モード
	FLAG:モード管理 = 0
	TFLAG:あなたターゲット用 = 0
	VARSET 同時モード_選択キャラ, -1
	同時モード_選択数 = 0
	RETURN 1
ELSEIF RESULT == 9102
	;指示モード
	SIF !CFLAG:うふふ
		RETURN 1
	IF TARGET == MASTER
		FLAG:モード管理 = 0
		TFLAG:あなたターゲット用 = 0
		RETURN 1
	ENDIF
	IF TCVAR:TARGET:食いしばりフラグ || CFLAG:TARGET:体力限界
		PRINTFORMW %CALLNAME:TARGET%已无自主行动体力…
		RETURN 1
	ENDIF
	SETBIT FLAG:モード管理, 0
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		FINDELEMENT 同時モード_選択キャラ, TARGET
		SIF RESULT >= 0
			同時モード_選択キャラ:RESULT = PLAYER
	ENDIF
	SIF CFLAG:MASTER:隠密 && CFLAG:TARGET:隠密
		TFLAG:あなたターゲット用 = 1
	PLAYER = TARGET
	RETURN 1
ELSEIF RESULT == 9103
	;同時モード
	SIF !CFLAG:うふふ
		RETURN 1
	CALL 同時モード設定()
	SELECTCASE RESULT
		CASE 1
			; 選択人数が1人の場合は同時モードオフ
			CLEARBIT FLAG:モード管理, 1
			TFLAG:あなたターゲット用 = 0
		CASE IS > 1
			; 選択人数が2人以上の場合は同時モードに設定
			SETBIT FLAG:モード管理, 1
	ENDSELECT
	RETURN 1
ELSEIF RESULT == 9104
	;オートコマンドオプション
	FLAG:オートコマンド切り替えオプション += 1
	SIF FLAG:オートコマンド切り替えオプション > 15
		FLAG:オートコマンド切り替えオプション = 0
	RETURN 1
ELSEIF RESULT == 9100
;9100でINFO格納フィルタ
	INVERTBIT INFO格納フィルタ:(RESULT - 9100), 0
	RETURN 1
ELSEIF RESULT >=9050
	RETURN 1
ELSEIF RESULT >=9000
;9000番代は簡易コマンドフィルタに使う
	INVERTBIT 簡易コマンドフィルタ:(RESULT - 9000), 0
	RETURN 1
ELSEIF RESULT == 2000
	IF GETBIT(FLAG:モード管理, モードビット_指示)
		;指示モード時のみ、あなたをターゲットに出来る
		TFLAG:あなたターゲット用 = 1
	ENDIF
	RETURN 1
ELSEIF RESULT > 2000
;2000番以降は相手変えのショートカットに使う
	数値変換用 = RESULT - 2000
	SIF 数値変換用 <= 0 || 数値変換用 >= CHARANUM
		RETURN 1

	IF CFLAG:MASTER:隠密 && CFLAG:数値変換用:隠密
		;隠密同士なら変更可能
	ELSEIF CFLAG:MASTER:隠密 && CFLAG:TARGET:隠密
		PRINTW 一起隐匿时中无法更换同伴
		RETURN 1
	ELSEIF GETBIT(FLAG:モード管理, モードビット_指示) && CFLAG:MASTER:隠密 && CFLAG:PLAYER:隠密
		PRINTW 一起隐匿时中无法更换同伴
		RETURN 1
	ENDIF

	IF CFLAG:数値変換用:同室
		IF GETBIT(FLAG:モード管理, モードビット_同時) && !MATCH(同時モード_選択キャラ, 数値変換用)
			FLAG:モード管理 = 0
			VARSET 同時モード_選択キャラ, -1
			同時モード_選択数 = 0
		ENDIF
		TFLAG:TARGET変更用 = 数値変換用
		TFLAG:あなたターゲット用 = 0
		PREVCOM = RESULT
	ENDIF
	RETURN 1
ELSEIF RESULT >= 0 && RESULT <= 700
	SELECTCOM = RESULT
	IF GROUPMATCH(SELECTCOM, 351, 356, 355) || (GROUPMATCH(SELECTCOM, 320, 323) && RFLAG:コマンド結果受渡し変数３)
		CALLFORM COM_ABLE{SELECTCOM}
		SIF !RESULT
			RETURN 0
		STR:0 = 連れ出す
		CALLFORM COM{SELECTCOM}
		RETURN RESULT
	ENDIF
	SIF TRAIN_COMNAME(SELECTCOM) == ""
		RETURN 1
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		LOCAL:10 = TARGET
		FOR LOCAL, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:LOCAL < 0
				BREAK
			RESULT = 0
			TARGET = 同時モード_選択キャラ:LOCAL
			TRYCALLFORM COM_ABLE{SELECTCOM}
			IF RESULT == 0
				TARGET = LOCAL:10
				RETURN 1
			ENDIF
			RESULT = 1
			IF TCVAR:TARGET:食いしばりフラグ
			ELSEIF TARGET == MASTER
				TARGET = PLAYER
				PLAYER = MASTER
				RFLAG:指示モード対象あなたフラグ = 1
				TRYCALLFORM CAN_COM{SELECTCOM}(1)
				RFLAG:指示モード対象あなたフラグ = 0
				LOCAL:2 = RESULT
				IF !LOCAL:2
					TRYCALLFORM CAN_COM{SELECTCOM}
					;失敗時口上
					;これプレイヤー側時いる？　一旦封印
					;TRYCALLFORM KOJO_CANCOMPLAYER失敗時_{NO:TARGET}_{SELECTCOM}(TARGET)
				ENDIF
				PLAYER = TARGET
				TARGET = MASTER
				SIF !LOCAL:2
					RETURN 1
			ELSE
				TRYCALLFORM CAN_COM{SELECTCOM}(1)
				IF !RESULT
					TRYCALLFORM CAN_COM{SELECTCOM}
					;失敗時口上
					PRINTL
					TRYCALLFORM KOJO_CANCOM失敗時_{NO:TARGET}_{SELECTCOM}(TARGET)
					RETURN 1
				ENDIF
			ENDIF
		NEXT
		TFLAG:SELECTCOM受け渡し = SELECTCOM
		TARGET = LOCAL:10
	ELSE
		RESULT = 0
		TRYCALLFORM COM_ABLE{SELECTCOM}
		SIF RESULT == 0
			RETURN 1
		TFLAG:SELECTCOM受け渡し = SELECTCOM
		RESULT = 1
		IF TCVAR:TARGET:食いしばりフラグ
		ELSEIF TARGET == MASTER
			TARGET = PLAYER
			PLAYER = MASTER
			RFLAG:指示モード対象あなたフラグ = 1
			TRYCALLFORM CAN_COM{SELECTCOM}(1)
			RFLAG:指示モード対象あなたフラグ = 0
			LOCAL:2 = RESULT
			IF !LOCAL:2
				TRYCALLFORM CAN_COM{SELECTCOM}
				;失敗時口上
				;これプレイヤー側時いる？　一旦封印
				;TRYCALLFORM KOJO_CANCOMPLAYER失敗時_{NO:TARGET}_{SELECTCOM}(TARGET)
			ENDIF
			PLAYER = TARGET
			TARGET = MASTER
			SIF !LOCAL:2
				RETURN 1
		ELSE
			TRYCALLFORM CAN_COM{SELECTCOM}(1)
			IF !RESULT
				TRYCALLFORM CAN_COM{SELECTCOM}
				;失敗時口上
				PRINTL
				TRYCALLFORM KOJO_CANCOM失敗時_{NO:TARGET}_{SELECTCOM}(TARGET)
				RETURN 1
			ENDIF
		ENDIF
	ENDIF

ELSEIF RESULT == 803
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:MASTER:飲み会
			IF 飲み会TARGET:LOCAL == TARGET
				TARGET何人目？ = LOCAL
				BREAK
			ENDIF
		ELSE
			IF TARGET:LOCAL == TARGET
				TARGET何人目？ = LOCAL
				BREAK
			ENDIF
		ENDIF
	NEXT
	$能力表示_LOOP
	LOCAL:1 = 0
	SIF TARGET何人目？ != 1
		SETBIT LOCAL:1, 0
	IF CFLAG:MASTER:飲み会
		SIF 飲み会TARGET:(TARGET何人目？ + 1) > 0
			SETBIT LOCAL:1, 1
		CALL PRINT_STATE(飲み会TARGET:TARGET何人目？, LOCAL:1)
	ELSE
		SIF TARGET:(TARGET何人目？ + 1) > 0
			SETBIT LOCAL:1, 1
		CALL PRINT_STATE(TARGET:TARGET何人目？, LOCAL:1)
	ENDIF
	IF RESULT == 100
		TARGET何人目？ -= 1
		GOTO 能力表示_LOOP
	ELSEIF RESULT == 101
		TARGET何人目？ += 1
		GOTO 能力表示_LOOP
	ENDIF
	RETURN 1
ELSEIF RESULT == 804
	CALL PRINT_STATE(MASTER)
	RETURN 1
ELSEIF RESULT == 805
	IF あなた追跡キャラ
		あなた追跡キャラ = 0
	ELSE
		あなた追跡キャラ = TARGET
		CFLAG:PLAYER:前ターン位置 = CFLAG:PLAYER:現在位置
		CFLAG:PLAYER:前マップ種別 = CFLAG:PLAYER:現在マップ種別
	ENDIF
	RETURN 1
ELSEIF RESULT == 806
	CALL うふふチュートリアル()
	RETURN 1
ELSEIF RESULT == 809
	CALL OPTION
	RETURN 1
ELSEIF RESULT == 820 && CFLAG:うふふ
	CALL CLOTHES_SETTING_TRAIN(PLAYER)
	RETURN 1
ELSEIF RESULT == 830
	表示切り替え文字列 = 探索集約_探索メンバー
	CALL 探索系集約表示
	RETURN 1
ELSEIF RESULT == 840
	CFLAG:MASTER:隠密 = 1
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF CFLAG:(TARGET:LOCAL):同行 || CFLAG:(TARGET:LOCAL):うふふ
			CFLAG:(TARGET:LOCAL):隠密 = 1
	NEXT
	RETURN 1
ELSEIF RESULT == 841
	IF 連れ込みパターン != ""
		PRINTFORMW %CALLNAME:TARGET%不愿松手…
		CALL KOJO_EVENT, 13, TARGET, 1
	ELSE
		CFLAG:MASTER:隠密 = 0
		FOR LOCAL, 1, CHARANUM
			SIF TARGET:LOCAL < 1
				BREAK
			SIF CFLAG:(TARGET:LOCAL):同行 || CFLAG:(TARGET:LOCAL):うふふ
				CFLAG:(TARGET:LOCAL):隠密 = 0
		NEXT
		PREVCOM = 841
	ENDIF
	RETURN 1
ELSEIF RESULT == 850
	CALL ITEM_PROTO(TARGET)
	RETURN 1
ELSEIF RESULT == 856
	CALL 所持写真一覧表示
	RETURN 1
ELSEIF RESULT == 860
	CALL キャラ居場所感知処理
	RETURN 1
ELSEIF RESULT == 861
	CALL 雇用レポート表示
	RETURN 1
ELSEIF RESULT == 889
; 	IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
; 		INVERTBIT 画像サイズ切り替えOPTION記憶, 0
; 		IF 二人目画像表示用()
; 			SETBIT 画像サイズ切り替えOPTION記憶, 4
; 		ELSE
; 			CLEARBIT 画像サイズ切り替えOPTION記憶, 4
; 		ENDIF
; 	ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用()
; 		CLEARBIT 画像サイズ切り替えOPTION記憶, 4
; 	ELSE
		INVERTBIT 画像サイズ切り替えOPTION記憶, 0
; 	ENDIF
 	RETURN 1
ELSEIF RESULT == 890
	INVERTBIT あなた射精我慢フラグ, 0
	RETURN 1
ELSEIF RESULT == 891
	CALL オート射精先設定
	RETURN 1
ELSEIF RESULT == 892
	INVERTBIT OPTION変数:連れ込み制御, 0
	RETURN 1
ELSEIF RESULT == 893
	INVERTBIT OPTION変数:休憩制御, 0
	RETURN 1
ELSEIF RESULT == 999 && CFLAG:うふふ
	IF 連れ込みパターン != ""
		PRINTFORMW %CALLNAME:(TARGET)%拒绝放开…
		CALL KOJO_EVENT, 13, TARGET, 1
		RETURN 1
	ELSE
		CALL うふふ終了処理("通常終了")
		RETURN 1
	ENDIF
ELSEIF RESULT == 998 && CFLAG:飲み会
	CALL 飲み会終了処理()
	RETURN 1
ELSE
	RETURN 1
ENDIF
RETURN 0

@SHOW_COMMAND
CALL SHOW_MODE()

IF TFLAG:調教モード == 2
	SETCOLOR 0xC0C0C0
	SIF 簡易コマンドフィルタ:0
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[常规]", 9000
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:0
		CALL SHOW_COMMAND_実処理("その他")
;PRINTL
	SETCOLOR 0xC0C0C0
	SIF 簡易コマンドフィルタ:17
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[侍奉系]", 9017
	PRINTL -----------------------------------------------------------------------------------------------
	IF !簡易コマンドフィルタ:17
		CALL SHOW_COMMAND_実処理("奉仕")
		SIF GETBIT(FLAG:モード管理, モードビット_同時) == 0
			CALL SHOW_COMMAND_実処理("奉仕", 1)
	ENDIF
;PRINTL
	SETCOLOR 0xFF66CC
	SIF 簡易コマンドフィルタ:6
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[Ｖ系]", 9006
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:6
		CALL SHOW_COMMAND_実処理("Ｖ系")
;PRINTL
	SETCOLOR 0xDDFFDD
	SIF 簡易コマンドフィルタ:7
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[Ａ系]", 9007
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:7
		CALL SHOW_COMMAND_実処理("Ａ系")
;PRINTL
	SETCOLOR 0xD3DEF1
	SIF 簡易コマンドフィルタ:15
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[触手系]", 9015
	PRINTL -----------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:15
		CALL SHOW_COMMAND_実処理("触手系")
;PRINTL
	SETCOLOR 0xD3DEF1
	SIF 簡易コマンドフィルタ:12
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[特殊play]", 9012
	PRINTL -----------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:12
		CALL SHOW_COMMAND_実処理("特殊プレイ")
ELSEIF TFLAG:調教モード == 3
	SETCOLOR 0xC0C0C0
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTPLAIN [酒会]
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:49
		CALL SHOW_COMMAND_実処理("その他")
;PRINTL
	SETCOLOR 0xFF66CC
	SIF 簡易コマンドフィルタ:6
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[性骚扰]", 9006
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:1
		CALL SHOW_COMMAND_実処理("セクハラ")
ELSE
	SETCOLOR 0xC0C0C0
	SIF 簡易コマンドフィルタ:0
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[日常]", 9000
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:0
		CALL SHOW_COMMAND_実処理("日常")
;PRINTL
	SETCOLOR 0xFF99EE
	SIF 簡易コマンドフィルタ:1
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[性骚扰]", 9001
	PRINTL ---------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:1
		CALL SHOW_COMMAND_実処理("セクハラ")
;PRINTL
	SETCOLOR 0xAADDAA
	SIF 簡易コマンドフィルタ:4
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[探索]", 9004
	PRINTL -------------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:4
		CALL SHOW_COMMAND_実処理("探索")
;PRINTL
	SETCOLOR 0xB0BBE0
	SIF 簡易コマンドフィルタ:49
		SETCOLOR 0x666666
	文字色保存 = GETCOLOR()
	PRINT -----
	PRINTBUTTON "[其他]", 9049
	PRINTL -----------------------------------------------------------------------------------------------
	SIF !簡易コマンドフィルタ:49
		CALL SHOW_COMMAND_実処理("その他")
ENDIF
RESETCOLOR
;PRINTL
文字色保存 = 0

@SHOW_COMMAND_実処理(ARGS, TARGET入替 = 0)
#DIMS 複合条件保存, 10
#DIM スキップフラグ
#DIM TARGET入替
#DIM 配列番号
#DIMS コマンドキー
#DIM 派生カテゴリ表示フラグ

SIF TARGET入替
	SWAP TARGET, PLAYER

TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000

VARSET LOCALS
LOCAL:1 = 0
LOCAL:2 = 0
LOCAL:3 = COMTYPE(ARGS)
;セクハラは全部まとめてセクハラ扱い
SIF GROUPMATCH(LOCAL:3, 1, 2, 3)
	LOCAL:3 = 1
SIF LOCAL:3 == 16
	LOCAL:3 = 0
SIF ARGS == "その他"
	LOCAL:3 = 49

FOR LOCAL,0,700
	SIF EXISTFUNCTION(@"COM_ABLE{LOCAL}") == 0
		CONTINUE
	IF GETBIT(FLAG:モード管理, モードビット_同時)
		LOCAL:10 = TARGET
		スキップフラグ = 0
		FOR 配列番号, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:配列番号 < 0
				BREAK
			RESULT = 0
			TARGET = 同時モード_選択キャラ:配列番号
			TRYCALLFORM COM_ABLE{LOCAL}
			IF RESULT == 0
				スキップフラグ = 1
				BREAK
			ENDIF
		NEXT
		TARGET = LOCAL:10
		SIF スキップフラグ
			CONTINUE
	ELSE
		TRYCALLFORM COM_ABLE{LOCAL}
		SIF RESULT == 0
			CONTINUE
	ENDIF
	弱点コマンドカテゴリ = 
	TFLAG:派生コマンド指定 = -1
	TRYCALLFORMF COMTYPE_{LOCAL}
	SIF TFLAG:派生コマンド指定 > -1 && LOCAL >= 370 && LOCAL <= 389
		CONTINUE
	;複合条件処理
	IF TFLAG:コマンドタイプ受渡 > 99
		IF TFLAG:コマンドタイプ受渡 == 100
			SPLIT TSTR:複合コマンドタイプ受け渡し, "&", 複合条件保存
		ELSEIF TFLAG:コマンドタイプ受渡 == 101
			SPLIT TSTR:複合コマンドタイプ受け渡し, "_", 複合条件保存
		ENDIF
		FOR LOCAL:1, 0, RESULT
			IF TFLAG:調教モード == 2
				IF 複合条件保存:(LOCAL:1) == "特殊プレイ"
					TFLAG:コマンドタイプ受渡 = 12
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "触手系"
					TFLAG:コマンドタイプ受渡 = 15
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "Ｖ系"
					TFLAG:コマンドタイプ受渡 = 6
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "Ａ系"
					TFLAG:コマンドタイプ受渡 = 7
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "奉仕"
					TFLAG:コマンドタイプ受渡 = 17
					BREAK
				ENDIF
			ELSEIF TFLAG:調教モード == 3
				IF GROUPMATCH(複合条件保存:(LOCAL:1), "セクハラ弱", "セクハラ", "セクハラ強")
					TFLAG:コマンドタイプ受渡 = 1
					BREAK
				ENDIF
			ELSE
				IF 複合条件保存:(LOCAL:1) == "日常"
					TFLAG:コマンドタイプ受渡 = 0
					BREAK
				ELSEIF GROUPMATCH(複合条件保存:(LOCAL:1), "セクハラ弱", "セクハラ", "セクハラ強")
					TFLAG:コマンドタイプ受渡 = 1
					BREAK
				ELSEIF 複合条件保存:(LOCAL:1) == "探索"
					TFLAG:コマンドタイプ受渡 = 4
					BREAK
				ENDIF
			ENDIF
		NEXT
	ENDIF
	;指定カテゴリと同じコマンドだけ表示
	SELECTCASE LOCAL:3
		CASE 0 TO 48
			IF GROUPMATCH(TFLAG:コマンドタイプ受渡, 1, 2, 3)
				SIF LOCAL:3 != 1
					CONTINUE
			ELSEIF TFLAG:コマンドタイプ受渡 != LOCAL:3
				CONTINUE
			ENDIF
		CASE 49
			SIF GROUPMATCH(TFLAG:コマンドタイプ受渡, 0, 1, 2, 3, 4, 6, 7, 15, 12, 17)
				CONTINUE
			SIF 簡易コマンドフィルタ:49
				CONTINUE
	ENDSELECT
	SIF 簡易コマンドフィルタ処理(LOCAL) == 1
		CONTINUE
	SELECTCASE LOCAL
		CASE 370 TO 371
			LOCALS:1 = {LOCAL}_{NO:TARGET}
		CASE 380 TO 384
			LOCALS:1 = {LOCAL}_{CFLAG:MASTER:現在マップ種別}_{CFLAG:MASTER:現在位置}
		CASE 385 TO 389
			LOCALS:1 = {LOCAL}_%開催予定祭り名%
		CASEELSE
			LOCALS:1 = {LOCAL}
	ENDSELECT
	IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
		LOCALS:1 = %SUBSTRING(弱点コマンドカテゴリ, 12)%
	ENDIF
	CALLFORMF TRAIN_COMNAME(LOCAL)
	IF TSTR:コマンドカラー受渡 == ""
		IF 判明済み弱点チェック(TARGET, LOCALS:1)
			LOCALS += "<font color='#FF3399'>"
			LOCAL:4 = 1
		ENDIF
		IF 判明済み弱点チェック(PLAYER, @"%LOCALS:1%_PLAYER側")
			LOCALS += "<font color='#FF3399'>"
			LOCAL:4 = 1
		ENDIF
	ELSE
		LOCALS += @"<font color='#%TSTR:コマンドカラー受渡%'>"
		LOCAL:4 = 1
	ENDIF
	IF STRLENS(TRAIN_COMNAME(LOCAL))
		IF TARGET入替
			LOCALS += @"<button value = 'R{LOCAL}'"
		ELSE
			LOCALS += @"<button value = '{LOCAL}'"
		ENDIF
		TSTR:ツールチップ受渡 = 
		RFLAG:奉仕逆転時ツールチップフラグ = TARGET入替
		TRYCCALLFORM COM_TOOLTIP_{LOCAL}
			IF OPTION変数:ツールチップ切り替え == 0
				LOCALS += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS += @">"
			ENDIF
		CATCH
			LOCALS += @">"
		ENDCATCH
		IF TARGET入替
			LOCALS += @"%TRAIN_COMNAME(LOCAL) + "する", 22%[%@"R{LOCAL}", 3%]</button>"
		ELSE
			LOCALS += @"%TRAIN_COMNAME(LOCAL), 22%[{LOCAL, 3}]</button>"
		ENDIF
		IF LOCAL:4
			LOCALS += "</font>"
			LOCAL:4 = 0
		ENDIF
		LOCAL:2 += 1
	ENDIF
	IF LOCAL:2 >= 4
		LOCAL:2 = 0
		LOCALS += "<br>" 
	ENDIF
NEXT
IF LOCAL:2
	LOCAL:2 = 0
	LOCALS += "<br>" 
ENDIF
;LOCALS += "<br>" 

FOR LOCAL, 0, 500
	LOCAL:2 = 0
	派生カテゴリ表示フラグ = 0
	SIF LOCAL >= 370 && LOCAL <= 390
		CONTINUE
	;派生コマンドを見る
	FOR LOCAL:1, 1, 50
		TRYCCALLFORM COM_ABLE_S{LOCAL}_{LOCAL:1}
			SIF RESULT == 0
				CONTINUE
		CATCH
			CONTINUE
		ENDCATCH
		RESULT = 0
		弱点コマンドカテゴリ = 
		TRYCALLFORMF COMTYPE_S_{LOCAL}_{LOCAL:1}
		;複合条件処理
		IF TFLAG:コマンドタイプ受渡 > 99
			IF TFLAG:コマンドタイプ受渡 == 100
				SPLIT TSTR:複合コマンドタイプ受け渡し, "&", 複合条件保存
			ELSEIF TFLAG:コマンドタイプ受渡 == 101
				SPLIT TSTR:複合コマンドタイプ受け渡し, "_", 複合条件保存
			ENDIF
			FOR LOCAL:10, 0, RESULT
				IF TFLAG:調教モード == 2
					IF 複合条件保存:(LOCAL:10) == "特殊プレイ"
						TFLAG:コマンドタイプ受渡 = 12
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "触手系"
						TFLAG:コマンドタイプ受渡 = 15
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ｖ系"
						TFLAG:コマンドタイプ受渡 = 6
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ａ系"
						TFLAG:コマンドタイプ受渡 = 7
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "奉仕"
						TFLAG:コマンドタイプ受渡 = 17
						BREAK
					ENDIF
				ELSE
					IF 複合条件保存:(LOCAL:10) == "日常"
						TFLAG:コマンドタイプ受渡 = 0
						BREAK
					ELSEIF GROUPMATCH(複合条件保存:(LOCAL:10), "セクハラ弱", "セクハラ", "セクハラ強")
						TFLAG:コマンドタイプ受渡 = 1
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "探索"
						TFLAG:コマンドタイプ受渡 = 4
						BREAK
					ENDIF
				ENDIF
			NEXT
		ENDIF
		;指定カテゴリと同じコマンドだけ表示
		SELECTCASE LOCAL:3
			CASE 0 TO 48
				IF GROUPMATCH(TFLAG:コマンドタイプ受渡, 1, 2, 3)
					SIF LOCAL:3 != 1
						CONTINUE
				ELSEIF TFLAG:コマンドタイプ受渡 != LOCAL:3
					CONTINUE
				ENDIF
			CASE 49
				SIF GROUPMATCH(TFLAG:コマンドタイプ受渡, 0, 1, 2, 3, 4, 6, 7, 15, 12, 17)
					CONTINUE
				SIF 簡易コマンドフィルタ:49
					CONTINUE
		ENDSELECT
		IF 派生カテゴリ表示フラグ == 0
			派生名フラグ = 1
			LOCALS += @"▼%TRAIN_COMNAME(LOCAL)%衍生<br>"
			派生名フラグ = 0
			派生カテゴリ表示フラグ = 1
		ENDIF
		コマンドキー = {LOCAL}_{LOCAL:1}
		SIF 簡易コマンドフィルタ処理(LOCAL) == 1
			CONTINUE
		IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
			コマンドキー = %SUBSTRING(弱点コマンドカテゴリ, 12)%
		ENDIF
		CALLFORMF TRAIN_COMNAME_S(コマンドキー)
		IF TSTR:コマンドカラー受渡 == ""
			IF 判明済み弱点チェック(TARGET, コマンドキー)
				LOCALS += "<font color='#FF3399'>"
				LOCAL:4 = 1
			ENDIF
			IF 判明済み弱点チェック(PLAYER, @"%コマンドキー%_PLAYER側")
				LOCALS += "<font color='#FF3399'>"
				LOCAL:4 = 1
			ENDIF
		ELSE
			LOCALS += @"<font color='#%TSTR:コマンドカラー受渡%'>"
			LOCAL:4 = 1
		ENDIF
		IF TARGET入替
			LOCALS += @"<button value = 'R%コマンドキー%'"
		ELSE
			LOCALS += @"<button value = '%コマンドキー%'"
		ENDIF
		TRYCCALLFORM COM_TOOLTIP_S%コマンドキー%
			IF OPTION変数:ツールチップ切り替え == 0
				LOCALS += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS += @">"
			ENDIF
		CATCH
			LOCALS += @">"
		ENDCATCH
		IF TARGET入替
			LOCALS += @"%TRAIN_COMNAME_S(コマンドキー) + "侍奉", 20%[%"R" + コマンドキー,5%]</button>"
		ELSE
			LOCALS += @"%TRAIN_COMNAME_S(コマンドキー), 20%[%コマンドキー,5%]</button>"
		ENDIF
		IF LOCAL:4
			LOCALS += "</font>"
			LOCAL:4 = 0
		ENDIF
		LOCAL:2 += 1
		SETCOLOR 文字色保存
		IF LOCAL:2 >= 4
			LOCAL:2 = 0
			LOCALS += "<br>"
		ENDIF
		; PRINTBUTTONC @"%TRAIN_COMNAME_S(LOCALS)%[%LOCALS,5%]", @"%LOCALS%"
		; SETCOLOR 文字色保存
		; LOCAL:2 = 1
	NEXT
	;固有コマンドが派生かどうかを見る

	
	FOR LOCAL:1, 370, 390
		TRYCCALLFORM COM_ABLE{LOCAL:1}
			SIF RESULT == 0
				CONTINUE
		CATCH
			CONTINUE
		ENDCATCH
		RESULT = 0
		弱点コマンドカテゴリ = 
		TFLAG:派生コマンド指定 = -1
		TRYCALLFORMF COMTYPE_{LOCAL:1}
		SIF TFLAG:派生コマンド指定 != LOCAL
			CONTINUE
		;複合条件処理
		IF TFLAG:コマンドタイプ受渡 > 99
			IF TFLAG:コマンドタイプ受渡 == 100
				SPLIT TSTR:複合コマンドタイプ受け渡し, "&", 複合条件保存
			ELSEIF TFLAG:コマンドタイプ受渡 == 101
				SPLIT TSTR:複合コマンドタイプ受け渡し, "_", 複合条件保存
			ENDIF
			FOR LOCAL:10, 0, RESULT
				IF TFLAG:調教モード == 2
					IF 複合条件保存:(LOCAL:10) == "特殊プレイ"
						TFLAG:コマンドタイプ受渡 = 12
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "触手系"
						TFLAG:コマンドタイプ受渡 = 15
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ｖ系"
						TFLAG:コマンドタイプ受渡 = 6
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "Ａ系"
						TFLAG:コマンドタイプ受渡 = 7
						BREAK
					ENDIF
				ELSE
					IF 複合条件保存:(LOCAL:10) == "日常"
						TFLAG:コマンドタイプ受渡 = 0
						BREAK
					ELSEIF GROUPMATCH(複合条件保存:(LOCAL:10), "セクハラ弱", "セクハラ", "セクハラ強")
						TFLAG:コマンドタイプ受渡 = 1
						BREAK
					ELSEIF 複合条件保存:(LOCAL:10) == "探索"
						TFLAG:コマンドタイプ受渡 = 4
						BREAK
					ENDIF
				ENDIF
			NEXT
		ENDIF
		;指定カテゴリと同じコマンドだけ表示
		SELECTCASE LOCAL:3
			CASE 0 TO 48
				IF GROUPMATCH(TFLAG:コマンドタイプ受渡, 1, 2, 3)
					SIF LOCAL:3 != 1
						CONTINUE
				ELSEIF TFLAG:コマンドタイプ受渡 != LOCAL:3
					CONTINUE
				ENDIF
			CASE 49
				SIF GROUPMATCH(TFLAG:コマンドタイプ受渡, 0, 1, 2, 3, 4, 6, 7, 15, 12, 17)
					CONTINUE
				SIF 簡易コマンドフィルタ:49
					CONTINUE
		ENDSELECT
		IF 派生カテゴリ表示フラグ == 0
			派生名フラグ = 1
			LOCALS += @"▼%TRAIN_COMNAME(LOCAL)%衍生<br>"
			派生名フラグ = 0
			派生カテゴリ表示フラグ = 1
		ENDIF
		コマンドキー = {LOCAL:1}
		SIF 簡易コマンドフィルタ処理(LOCAL:1) == 1
			CONTINUE
		IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
			コマンドキー = %SUBSTRING(弱点コマンドカテゴリ, 12)%
		ENDIF
		CALLFORMF TRAIN_COMNAME(TOINT(コマンドキー))
		IF TSTR:コマンドカラー受渡 == ""
			IF 判明済み弱点チェック(TARGET, コマンドキー)
				LOCALS += "<font color='#FF3399'>"
				LOCAL:4 = 1
			ENDIF
			IF 判明済み弱点チェック(PLAYER, @"%コマンドキー%_PLAYER側")
				LOCALS += "<font color='#FF3399'>"
				LOCAL:4 = 1
			ENDIF
		ELSE
			LOCALS += @"<font color='#%TSTR:コマンドカラー受渡%'>"
			LOCAL:4 = 1
		ENDIF
		IF TARGET入替
			LOCALS += @"<button value = 'R%コマンドキー%'"
		ELSE
			LOCALS += @"<button value = '%コマンドキー%'"
		ENDIF
		TRYCCALLFORM COM_TOOLTIP_%コマンドキー%
			IF OPTION変数:ツールチップ切り替え == 0
				LOCALS += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS += @">"
			ENDIF
		CATCH
			LOCALS += @">"
		ENDCATCH
		IF TARGET入替
			LOCALS += @"%TRAIN_COMNAME(TOINT(コマンドキー)) + "侍奉", 20%[%"R" + コマンドキー,5%]</button>"
		ELSE
			LOCALS += @"%TRAIN_COMNAME(TOINT(コマンドキー)), 20%[%コマンドキー,5%]</button>"
		ENDIF
		IF LOCAL:4
			LOCALS += "</font>"
			LOCAL:4 = 0
		ENDIF
		LOCAL:2 += 1
		SETCOLOR 文字色保存
		IF LOCAL:2 >= 4
			LOCAL:2 = 0
			LOCALS += "<br>"
		ENDIF
	NEXT

	IF LOCAL:2
		LOCAL:2 = 0
		LOCALS += "<br>"
	ENDIF
	;SIF 派生カテゴリ表示フラグ
	;	LOCALS += "<br>"
NEXT

HTML_PRINT LOCALS, 1

SIF TARGET入替
	SWAP TARGET, PLAYER

@ITEM_PROTO(対象キャラ)
#DIM 対象キャラ
$選択肢ループラベル
VARSET LOCAL
DRAWLINE

IF ITEM:危险期调节剂 < 1
	SETCOLOR 0x666666
	PRINTFORML ■未持有危险期调节剂。
	RESETCOLOR
ELSEIF RISKY_DAY(対象キャラ) == -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%不会怀孕。
	RESETCOLOR
ELSEIF 陥落チェック(対象キャラ) == 0 && 知識素質:対象キャラ:性知識 != -2 && 対象キャラ != MASTER
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%既非无知状态也未被攻略■无法使用危险期调节剂。
	RESETCOLOR
ELSEIF RISKY_DAY(対象キャラ) == 2
	SETCOLOR 0x666666
	PRINTFORML 当前已是%CALLNAME:対象キャラ%的最佳受孕期■无需使用危险期调节剂。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■危险期调节剂 × {ITEM:危险期调节剂}", 104
	PRINTL 
ENDIF
DRAWLINE

IF ITEM:避孕药 < 1
	SETCOLOR 0x666666
	PRINTFORML ■未持有避孕药。
	RESETCOLOR
ELSEIF RISKY_DAY(対象キャラ) == -2 || GETBIT(TALENT:対象キャラ:性別, 0) == 0 || TALENT:対象キャラ:Ｖ感度 == -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%不会怀孕。
	RESETCOLOR
ELSEIF TCVAR:対象キャラ:避妊薬
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%已处于■避孕药生效中。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■避孕药 × {ITEM:避孕药}", 92
	PRINTL 
ENDIF
DRAWLINE
IF ITEM:排卵诱导剂 < 1
	SETCOLOR 0x666666
	PRINTFORML ■未持有排卵诱导剂。
	RESETCOLOR
ELSEIF RISKY_DAY(対象キャラ) == -2
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%不会怀孕。
	RESETCOLOR
ELSEIF 陥落チェック(対象キャラ) == 0 && 知識素質:対象キャラ:性知識 != -2 && 対象キャラ != MASTER
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%既非无知状态也未被攻略■无法使用排卵诱导剂。 
	RESETCOLOR
ELSEIF TCVAR:対象キャラ:避妊薬
	SETCOLOR 0x666666
	PRINTFORML 避孕药生效期间■禁止使用排卵诱导剂。
	RESETCOLOR
ELSEIF TCVAR:対象キャラ:排卵誘発薬
	SETCOLOR 0x666666
	PRINTFORML %CALLNAME:対象キャラ%已处于■排卵诱导剂生效中。
	RESETCOLOR
ELSE
	PRINTBUTTON @"■排卵诱导剂 × {ITEM:排卵诱导剂}", 91
	PRINTL 
ENDIF
DRAWLINE

SIF 対象キャラ == MASTER
	GOTO INPUT部分

IF ITEM:艾拉的友情魂晶 > 0
	PRINTBUTTON @"■艾拉的友情魂晶 × {ITEM:艾拉的友情魂晶}", 100
	PRINTL 
ELSE
	SETCOLOR 0x666666
	PRINTFORML ■未持有艾拉的友情魂晶。
	RESETCOLOR
ENDIF
IF ITEM:艾拉的信赖魂晶 > 0
	PRINTBUTTON @"■艾拉的信赖魂晶 × {ITEM:艾拉的信赖魂晶}", 109
	PRINTL 
ELSE
	SETCOLOR 0x666666
	PRINTFORML ■未持有艾拉的信赖魂晶。
	RESETCOLOR
ENDIF
LOCAL:2 = 恋慕枠判定()
LOCAL:1 = -1
FOR LOCAL, 0, LOCAL:2
	IF 恋慕対象フラグ:LOCAL == 対象キャラ
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF ITEM:艾拉的恋心魂晶 > 0
	IF GETBIT(TALENT:対象キャラ:陥落不可, 0)
		SETCOLOR 0x666666
		PRINTFORML ■[无法爱慕]特质限制，无法使用艾拉的恋心魂晶
		RESETCOLOR
	ELSEIF 恋愛対象外チェック(対象キャラ)
		SETCOLOR 0x666666
		PRINTFORML ■非恋爱对象，无法使用艾拉的恋心魂晶
		PRINTFORML 　原因：非恋爱对象且无同性/异性恋倾向、持有无性欲特质、
		PRINTL 　　　　性知识为负值且未通过您达成高潮等
		RESETCOLOR
	ELSE
		PRINTBUTTON @"■艾拉的恋心魂晶 × {ITEM:艾拉的恋心魂晶}", 110
		PRINTL 
	ENDIF
ELSE
	SETCOLOR 0x666666
	PRINTFORML ■未持有艾拉的恋心魂晶。
	RESETCOLOR
ENDIF
IF ITEM:艾拉的性爱魂晶 > 0
	IF 恋愛対象外チェック(対象キャラ)
		SETCOLOR 0x666666
		PRINTFORML ■非恋爱对象，无法使用艾拉的性爱魂晶
		PRINTFORML 　原因：非恋爱对象且无同性/异性恋倾向、持有无性欲特质、
		PRINTL 　　　　性知识为负值且未通过您达成高潮等
		RESETCOLOR
	ELSE
		PRINTBUTTON @"■艾拉的性爱魂晶 × {ITEM:艾拉的性爱魂晶}", 111
		PRINTL 
	ENDIF
ELSE
	SETCOLOR 0x666666
	PRINTFORML ■未持有艾拉的性爱魂晶。
	RESETCOLOR
ENDIF
IF LOCAL:1 > 0
	IF ITEM:艾拉的爱慕魂晶 > 0
		IF GETBIT(TALENT:対象キャラ:陥落不可, 0)
			SETCOLOR 0x666666
			PRINTFORML ■[无法爱慕]特质限制，无法使用艾拉的爱慕魂晶
			RESETCOLOR
		ELSEIF 恋愛対象外チェック(対象キャラ)
			SETCOLOR 0x666666
			PRINTFORML ■非恋爱对象，无法使用艾拉的爱慕魂晶
			PRINTFORML 　原因：非恋爱对象且无同性/异性恋倾向、持有无性欲特质、
			PRINTL 　　　　性知识为负值且未通过您达成高潮等
			RESETCOLOR
		ELSE
			PRINTBUTTON @"■艾拉的爱慕魂晶 × {ITEM:艾拉的爱慕魂晶}", 101
			PRINTL 
		ENDIF
	ELSE
		SETCOLOR 0x666666
		PRINTFORML ■未持有艾拉的爱慕魂晶。
		RESETCOLOR
	ENDIF
ENDIF
PRINTL ※原型开发阶段数值未平衡，临时实装直接提升好感度与爱慕值的道具。

$INPUT部分
PRINTL 
PRINTBUTTON "[999]返回", 999
BINPUT

SELECTCASE RESULT:0
	CASE 91
		PRINTFORMW %CALLNAME:対象キャラ%已服用排卵诱导剂。
		TCVAR:対象キャラ:排卵誘発薬 = 1
		ITEM:排卵诱导剂 -= 1
	CASE 92
		PRINTFORMW %CALLNAME:対象キャラ%已服用避孕药。
		TCVAR:対象キャラ:避妊薬 = 1
		TCVAR:対象キャラ:排卵誘発薬 = 0
		ITEM:避孕药 -= 1
	CASE 100
		IF ITEM:100 > 0
			PRINTFORML %CALLNAME:対象キャラ%的好感度要素：友情已提升。
			PRINTL 好感度要素：友情＋10000
			CFLAG:対象キャラ:友情度 += 10000
			CALL FAVOR_LvUPCheck(対象キャラ)
			ITEM:100 -= 1
		ELSE
			PRINTW 目标物品不存在。
		ENDIF
		GOTO 選択肢ループラベル
	CASE 109
		IF ITEM:100 > 0
			PRINTFORML %CALLNAME:対象キャラ%的好感度要素：信赖已提升。
			PRINTL 好感度要素：信赖＋10000
			CFLAG:対象キャラ:信頼度 += 10000
			CALL FAVOR_LvUPCheck(対象キャラ)
			ITEM:100 -= 1
		ELSE
			PRINTW 目标物品不存在。
		ENDIF
		GOTO 選択肢ループラベル
	CASE 110
		IF ITEM:100 > 0
			PRINTFORML %CALLNAME:対象キャラ%的好感度要素：恋心已提升。
			PRINTL 好感度要素：恋心＋10000
			CFLAG:対象キャラ:恋心度 += 10000
			CALL FAVOR_LvUPCheck(対象キャラ)
			ITEM:100 -= 1
		ELSE
			PRINTW 目标物品不存在。
		ENDIF
		GOTO 選択肢ループラベル
	CASE 111
		IF ITEM:100 > 0
			PRINTFORML %CALLNAME:対象キャラ%的好感度要素：性爱已提升。
			PRINTL 好感度要素：性爱＋10000
			CFLAG:対象キャラ:性愛度 += 10000
			CALL FAVOR_LvUPCheck(対象キャラ)
			ITEM:100 -= 1
		ELSE
			PRINTW 目标物品不存在。
		ENDIF
		GOTO 選択肢ループラベル
	CASE 101
		IF LOCAL:1 > 0
			IF ITEM:101 > 0
				PRINTFORML %CALLNAME:対象キャラ%的爱慕度增加了。
				PRINTL 爱慕度＋5000
				CFLAG:対象キャラ:恋慕度 += 5000
				CALL Love_LvUPCheck(対象キャラ)
				ITEM:101 -= 1
			ELSE
				PRINTW 目标物品不存在。
			ENDIF
		GOTO 選択肢ループラベル
		ENDIF
	; CASE 103
	; 	PRINTFORMW %CALLNAME:TARGET%が発情期に入りました。
	; 	CFLAG:TARGET:発情期フラグ = -1
	; 	ITEM:103 -= 1
	; 	IF (TALENT:種族 == 3 || NO:TARGET == 11) && 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
	; 		SIF TALENT:母乳体質 == 0
	; 			TALENT:母乳体質 = 2
	; 	ENDIF
	CASE 104
		IF 発情期判定(対象キャラ)
			PRINTFORML %CALLNAME:対象キャラ%的发情周期已调整。
		ENDIF
		PRINTFORMW %CALLNAME:対象キャラ%的最佳受孕日已变更为今日。
		CFLAG:対象キャラ:危険日 = DAY % PREG_CYCLE(対象キャラ)
		SIF CFLAG:対象キャラ:危険日 == 0
			CFLAG:対象キャラ:危険日 = PREG_CYCLE(対象キャラ)
		ITEM:104 -= 1
	CASE 999
		RETURN
	CASEELSE
		GOTO 選択肢ループラベル
ENDSELECT


